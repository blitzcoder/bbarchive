<!DOCTYPE html><html lang="en" ><head ><title >MessageBox with OpenUrl in Blitz3D?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >MessageBox with OpenUrl in Blitz3D?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >MessageBox with OpenUrl in Blitz3D?</a><br><br>
<a name="1315947"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RustyKristi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Is there a way to just include a MessageBox with a clickable Ok button that launches a program or webpage?<br><br>I would assume this is related to WinBlitz3D but I just need this functionality. <br><br></td></tr></table><br>
<a name="1315954"></a>

<a name="1315955"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> It depends if you want a pop up (another window than the blitz3d graphics window) or not.<br>If not, you can create a window and buttons (or any other GUI element) by using images and color rect line text...<br><br>(there are already several examples in the code archives) <br><br></td></tr></table><br>
<a name="1315986"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Const UI_COLOR_BG_R = 120 
Const UI_COLOR_BG_G = 137 
Const UI_COLOR_BG_B = 149 
 
Const UI_COLOR_FG_R = 37 
Const UI_COLOR_FG_G = 41 
Const UI_COLOR_FG_B = 51 
 
Const UI_COLOR_FGD_R = 0 
Const UI_COLOR_FGD_G = 0 
Const UI_COLOR_FGD_B = 0 
 
Const UI_COLOR_LIGHTEN_R = 189 
Const UI_COLOR_LIGHTEN_G = 202 
Const UI_COLOR_LIGHTEN_B = 211 
 
Const UI_COLOR_DLIGHTEN_R = 153 
Const UI_COLOR_DLIGHTEN_G = 174 
Const UI_COLOR_DLIGHTEN_B = 189 
 
Const UI_COLOR_DARKEN_R = 37 
Const UI_COLOR_DARKEN_G = 41 
Const UI_COLOR_DARKEN_B = 51 
 
Global UI_FONT,UI_FONT_BOLD,mx,my 
 
 
;UI - Label 
Type UI_Label 
 Field Name$ 
 Field Caption$ 
 Field X,Y,W,H 
 Field Owner.UI_Form 
End Type 
 
;UI - Button 
Type UI_Button 
 Field Name$ 
 Field Caption$ 
 Field X,Y,W,H 
 Field State, Enabled 
 Field Owner.UI_Form 
End Type 
 
;UI - Picture 
Type UI_Picture 
 Field Name$ 
 Field PicturePath$, Picture$ 
 Field X,Y,W,H 
 Field Owner.UI_Form 
End Type 
 
;UI - Form 
Type UI_Form 
 Field Name$ 
 Field Caption$ 
 Field X,Y,W,H 
 Field Icon$ 
 Field MoveMode 
End Type 
 
 
;Function UIDrawButton(Caption$,x,y,w,h,state,font) 
Function UIDrawButton(btn.UI_Button) 
 
 ;owner.UI_Form=New UI_Form 
 state=btn\State 
 x=btn\X+btn\Owner\X 
 y=btn\Y+btn\Owner\Y+20 
 w=btn\W 
 h=btn\H 
 caption$=btn\Caption$ 
 ; 
 ; State: 
 ; 1-Normal 2-Hover 3-Pressed 4-Disabled 
 ; 
 
 ;Drawing Button BG 
 Color UI_COLOR_BG_R,UI_COLOR_BG_G,UI_COLOR_BG_B 
 Rect x,y,w,h,True 
 
 SetFont UI_FONT 
  
 While StringWidth(caption$)&gt;w-8 
  caption$=Mid$(caption$,1,Len(caption$)-1) 
  oc=1 
 Wend 
  
 If oc=1 Then 
  caption$=Mid$(caption$,1,Len(caption$)-1) 
  caption$=caption$+"..." 
 EndIf 
  
 ;Drawing Lighten and Darken 
 Select state 
  
 Case 1 
  Color UI_COLOR_DLIGHTEN_R,UI_COLOR_DLIGHTEN_G,UI_COLOR_DLIGHTEN_B 
  Line x,y,x+w,y 
  Line x,y,x,y+h 
  Line x,y+h,x+w,y+h 
  Line x+w,y,x+w,y+h 
  Color UI_COLOR_FG_R,UI_COLOR_FG_G,UI_COLOR_FG_B 
  Text x+w/2,y+h/2,caption$,True,True 
 Case 2 
  Color UI_COLOR_DARKEN_R,UI_COLOR_DARKEN_G,UI_COLOR_DARKEN_B 
  Line x,y+h,x+w,y+h 
  Line x+w,y,x+w,y+h 
  Color UI_COLOR_LIGHTEN_R,UI_COLOR_LIGHTEN_G,UI_COLOR_LIGHTEN_B 
  Line x,y,x+w,y 
  Line x,y,x,y+h 
  Color UI_COLOR_FG_R,UI_COLOR_FG_G,UI_COLOR_FG_B 
  Text x+w/2,y+h/2,caption$,True,True 
 Case 3 
  Color UI_COLOR_DARKEN_R,UI_COLOR_DARKEN_G,UI_COLOR_DARKEN_B 
  Line x,y,x+w,y 
  Line x,y,x,y+h 
  Color UI_COLOR_LIGHTEN_R,UI_COLOR_LIGHTEN_G,UI_COLOR_LIGHTEN_B 
  Line x,y+h,x+w,y+h 
  Line x+w,y,x+w,y+h 
  Color UI_COLOR_FG_R,UI_COLOR_FG_G,UI_COLOR_FG_B 
  Text x+w/2+1,y+h/2+1,caption$,True,True 
 Case 4 
  Color UI_COLOR_DLIGHTEN_R,UI_COLOR_DLIGHTEN_G,UI_COLOR_DLIGHTEN_B 
  Line x,y,x+w,y 
  Line x,y,x,y+h 
  Line x,y+h,x+w,y+h 
  Line x+w,y,x+w,y+h 
  Text x+w/2,y+h/2,caption$,True,True 
 End Select 
  
  
   
End Function 
 
Function UIDrawLabel(lbl.UI_Label) 
 
 SetFont UI_FONT 
 x=lbl\X+lbl\Owner\X 
 y=lbl\Y+lbl\Owner\Y+20 
 w=StringWidth(lbl\Caption$)  
 h=StringHeight(lbl\Caption$)  
  
 Color UI_COLOR_BG_R,UI_COLOR_BG_G,UI_COLOR_BG_B 
 Rect x,y,w,h,True 
 Color UI_COLOR_FG_R,UI_COLOR_FG_G,UI_COLOR_FG_B 
 Text x,y,lbl\Caption$ 
  
End Function 
 
Function UIDrawForm(frm.UI_Form) 
 x=frm\X 
 y=frm\Y 
 w=frm\W 
 h=frm\H 
 caption$=frm\Caption$ 
  
 Color UI_COLOR_BG_R,UI_COLOR_BG_G,UI_COLOR_BG_B 
 Rect x,y,w,h,True 
 Color UI_COLOR_DLIGHTEN_R,UI_COLOR_DLIGHTEN_G,UI_COLOR_DLIGHTEN_B 
 Rect x,y,w,h,False 
 Rect x+1,y+1,w-2,h-2,False 
 Rect x+1,y+1,w-2,20,True 
 Color UI_COLOR_FG_R,UI_COLOR_FG_G,UI_COLOR_FG_B 
 SetFont UI_FONT_BOLD 
 Text x+8,y+11,Caption$,False,True 
 
 For lbl.UI_Label = Each UI_Label 
  If lbl\owner=frm Then UIDrawLabel lbl 
 Next 
 
 For btn.UI_Button = Each UI_Button 
  If btn\owner=frm Then UIDrawButton btn 
 Next 
  
End Function 
 
Function UIDrawImage(img,x,y) 
  
End Function  
 
Function UI_Setup() 
 UI_FONT=LoadFont("Arial Cyr",14,False) 
 UI_FONT_BOLD=LoadFont ("Arial Cyr",14,True) 
End Function  
 
Function UpdateButtons$(form.UI_Form) ;returns result code 
 
 For btn.UI_Button=Each UI_Button 
 
 If btn\owner=form Then 
   
  If btn\enabled=True Then  
   If MouseX()&gt;=btn\x+btn\owner\x And MouseX()&lt;=btn\x+btn\owner\x+btn\w And MouseY()&gt;=btn\y+btn\owner\y+20 And MouseY()&lt;=btn\y+btn\owner\y+btn\h+20 Then 
 
    If MouseHit(1) Return btn\Name$ 
         
    If MouseDown(1) Then 
     btn\state=3 
    Else 
     btn\state=2 
    EndIf 
   Else 
    btn\state=1 
   EndIf 
  Else 
   btn\state=4 
  EndIf 
   
 EndIf  
  
 Next 
End Function 
 
Function UpdateForms() 
 For frm.UI_Form=Each UI_Form 
  If MouseX()=&gt;frm\x+1 And MouseX()&lt;=frm\x+frm\w-2 And MouseY()&gt;frm\y+1 And MouseY()&lt;=frm\y+20   Then 
   If MouseHit(1) Then 
    mx=MouseX()-frm\X 
    my=MouseY()-frm\Y 
    frm\movemode=1 
   EndIf 
  Else 
   If Not MouseDown(1) Then frm\movemode=0 
  EndIf 
   
  If MouseDown(1) And frm\movemode=1 Then 
    frm\x=MouseX()-mx 
    frm\y=MouseY()-my 
    
  EndIf 
   
 Next 
End Function 
 
Function UICreateButton.UI_Button(name$,Caption$,x,y,w,h,Owner.UI_Form,enabled=True) 
bt.UI_Button=New UI_Button 
bt\x=x 
bt\y=y 
bt\w=w 
bt\h=h 
bt\caption$=Caption$ 
bt\name$=name$ 
bt\enabled=enabled 
bt\owner=Owner 
Return bt 
End Function 
 
Function UICreateForm.UI_Form(name$,Caption$,x,y,w,h) 
 frm.UI_Form = New UI_Form 
 frm\name$=name$ 
 frm\caption$=Caption$ 
 frm\x=x 
 frm\y=y 
 frm\w=w 
 frm\h=h 
 Return frm.UI_Form 
End Function 
 
Function UICreateLabel.UI_Label(name$,Caption$,x,y,Owner.UI_Form) 
 lbl.UI_Label = New UI_Label 
 lbl\name$=name$ 
 lbl\caption$=Caption$ 
 lbl\x=x 
 lbl\y=y 
 lbl\owner=Owner 
 Return lbl.UI_Label 
End Function  
 
; 
; 
; TEST 
; 
; 
Graphics3D 800,600,32,2 
;ClsColor UI_COLOR_BG_R,UI_COLOR_BG_G,UI_COLOR_BG_B 
Cls  
UI_Setup 
 
fr.UI_form=UICreateForm.UI_Form("Form1","hello",200,200,352,140) 
lb.UI_Label=UICreateLabel.UI_Label("Label1","OK",8,8,fr) 
bt1.UI_Button=UICreateButton.UI_Button("Exit","Enter",352-80-10,8,80,24, fr.UI_Form) 
bt2.UI_Button=UICreateButton.UI_Button("Rm","Exit",352-80-10,40 ,80,24,fr.UI_Form) 
 
While Not KeyHit(1) 
 Cls 
 UpdateForms 
 Select UpdateButtons$(fr) 
  Case "Exit": End 
  Case "Rm" 
   bt1\caption$="Exit" 
   bt2\Caption$="Rename" 
   fr\caption$="Test Dialog" 
   lb\caption$="Sample of dialog drawing" 
 End Select 
 UIDrawForm fr 
 ;UIDrawLabel lb 
 ;UIdrawButton bt1 
 ;UIdrawButton bt2 
 Flip  
Wend 
 
End</textarea><br><br><img src="http://i64.tinypic.com/2rho6tx.png"> <br><br></td></tr></table><br>
<a name="1315993"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RustyKristi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Yue, I am really looking for the native Windows GUI solution. <br><br></td></tr></table><br>
<a name="1316023"></a>

<a name="1316104"></a>

<a name="1316105"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dan</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> use the WinB3d addon then ? iv uploaded the archive <a href="http://s000.tinyupload.com/?file_id=43545779375489953203" target="_blank"> here</a>. <br>edit: WinBlitz3d <br><br></td></tr></table><br>
<a name="1316079"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RustyKristi</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Dan. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
