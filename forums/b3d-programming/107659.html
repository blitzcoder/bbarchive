<!DOCTYPE html><html lang="en" ><head ><title >Select Object Arrows</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Select Object Arrows</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Select Object Arrows</a><br><br>
<a name="1334535"></a>

<a name="1334536"></a>

<a name="1335045"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Caton</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm trying to select and move the object for my level editor with the mouse anyway I could do that?<br>&lt;Image Removed&gt; <br><br></td></tr></table><br>
<a name="1334547"></a>

<a name="1334548"></a>

<a name="1334549"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> A CameraPick for the colored parts on first detected MouseDown, then move the entity with MouseXSpeed/MouseYSpeed depending on picked direction.<br><br>My code for moving with the mouse usually follows this pattern:<br><pre class=code>
Global down

If MouseDown(1) Then
	If Not down Then
		; enter move state
		down = True
		MouseXSpeed()
		MouseYSpeed()
	EndIf
	; inside move state
	; move it here using MouseXSpeed() and MouseYSpeed())
ElseIf down Then
	; leaving move state
	down = False
EndIf
</pre> <br><br></td></tr></table><br>
<a name="1334557"></a>

<a name="1334558"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Caton</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have two objects, Now when I select one object to move, the other one goes missing<br><br><pre class=code>
If MouseDown(1) Then
CameraPick(Maincam, MouseX(), MouseY())
For obj.tobj = Each tobj
If PickedEntity() Then PositionEntity obj\mesh,PickedX#(),0,PickedZ#()
Next
EndIf
</pre> <br><br></td></tr></table><br>
<a name="1334561"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I assumed from your picture you wanted a movable thingy, like what 3D editors have, but looking at your code you are doing placement instead?<br><br>In any case, heres some code to move a thingy with the mouse:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Graphics3D 800,600

Global cam = CreateCamera()
MoveEntity cam, 0,5, -10

Global thing = MakeThingy()

PointEntity cam, thing

Const MOVE_COEF# = 0.10

Global ismoving
Global movingent

Repeat
	
	If MouseDown(1) Then
		If Not ismoving Then
			If CameraPick( cam, MouseX(), MouseY()) Then
				Local e = PickedEntity()
				Local t = GetEntityType(e)
				If t &gt;= 1 And t &lt;= 3 Then
					ismoving = t
					movingent = thing
					MouseXSpeed()
					MouseYSpeed()
				EndIf
			EndIf
		EndIf
		Select ismoving
			Case 1	MoveEntity movingent, 0, -MouseYSpeed() * MOVE_COEF, 0
			Case 2	MoveEntity movingent, MouseXSpeed() * MOVE_COEF , 0, 0
			Case 3	MoveEntity movingent, 0, 0, -MouseYSpeed() * MOVE_COEF
		End Select
	ElseIf ismoving Then
		ismoving = 0
		movingent = 0
	EndIf

	RenderWorld
	Flip
	Cls

Until KeyHit(1)
End


Function MakeThingy()
	Local thing = CreateCube()
	
	Local y = CreateCube()
	ScaleEntity y, 0.25, 1, 0.25
	EntityColor y, 255,0,0
	MoveEntity y, 0,1,0
	EntityPickMode y, 3
	EntityType y, 1
	EntityParent y, thing
	
	Local x = CreateCube()
	ScaleEntity x, 1, 0.25, 0.25
	EntityColor x, 0,255,0
	MoveEntity x, 1,0,0
	EntityPickMode x, 3
	EntityType x, 2
	EntityParent x, thing
		
	Local z = CreateCube()
	ScaleEntity z, 0.25, 0.25, 1
	EntityColor z, 0,0,255
	MoveEntity z, 0,0,-1
	EntityPickMode z, 3
	EntityType z, 3
	EntityParent z, thing
		
	Return thing
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1335010"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Caton</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> but what about object rotation? <br><br></td></tr></table><br>
<a name="1335014"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Replace MoveEntity with TurnEntity? <br><br></td></tr></table><br>
<a name="1335041"></a>

<a name="1335042"></a>

<a name="1335043"></a>

<a name="1335044"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Caton</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I know this off topic but I need to fix this error.<br>Trying to make level list<br><br><pre class=code>
ListDir=ReadDir("c:\test") 
Repeat
lvfile$=NextFile$(ListDir)
If lvfile$="" Then Exit
If FileType("c:\test"+"\"+lvfile$) = 2 Then
DebugLog("c:\test"+"\"+lvfile$)
list.levlist = New levlist
y=y+1
list\y=y
EndIf
Forever
CloseDir ListDir

Type levlist
field name$,y
End Type
</pre><br><br>I only have one folder in test but it says three how can I fix this? <br><br></td></tr></table><br>
<a name="1335050"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> You need to check for "." and ".." too and skip them. <br><br></td></tr></table><br>
<a name="1335051"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Caton</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> I did but it still doesn't work.<br><pre class=code>
ListDir=ReadDir("c:\test") 
Repeat
lvfile$=NextFile$(ListDir)
If lvfile$="" or lvfile$="." or lvfile$=".." Then Exit
If FileType("c:\test"+"\"+lvfile$) = 2 Then
DebugLog("c:\test"+"\"+lvfile$)
list.levlist = New levlist
y=y+1
list\y=y
EndIf
Forever
CloseDir ListDir

Type levlist
field name$,y
End Type
</pre> <br><br></td></tr></table><br>
<a name="1335053"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> "." and ".." are the two first elements of a directory, your loop never reaches any further because you exit to early. <br><br></td></tr></table><br>
<a name="1335055"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Caton</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Still didn't work<br><pre class=code>
ListDir=ReadDir("c:\test") 
Repeat
lvfile$=NextFile$(ListDir)
If lvfile$="" Then Exit
If FileType("c:\test"+"\"+lvfile$) = 2 Then
list.levlist = New levlist
y=y+1
list\y=y
DebugLog("c:\test"+"\"+lvfile$)
EndIf
If lvfile$="." or lvfile$=".." Then Exit
Forever
CloseDir ListDir

Type levlist
field name$,y
End Type
</pre> <br><br></td></tr></table><br>
<a name="1335059"></a>

<a name="1335060"></a>

<a name="1335061"></a>

<a name="1335062"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chalky</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's because you're still exiting as soon as you find them.<br><br>You should only exit if lvfile$ is empty (untested):<br><pre class=code>
ListDir=ReadDir("c:\test") 
Repeat
  lvfile$=NextFile$(ListDir)
  If lvfile$="" Then Exit
  If FileType("c:\test\"+lvfile$) = 2 Then
    If lvfile$&lt;&gt;"." And lvfile$&lt;&gt;".." Then
      list.levlist = New levlist
      y=y+1
      list\y=y
      DebugLog("c:\test\"+lvfile$)
    EndIf
  EndIf
Forever
CloseDir ListDir

Type levlist
  field name$,y
End Type
</pre> <br><br></td></tr></table><br>
<a name="1335094"></a>

<a name="1335095"></a>

<a name="1335124"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Caton</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Only will do one object but not other objects<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Global controlermode=1
Global LineX,LineY,LineZ,AxlX,AxlY,AxlZ,objectselected
Global ismoving
Const AXLOFFSET# = 5.0
Const MOVE_COEF# = 0.12
Const MOVE_COEF2# = 1.0
AppTitle("Editor")
Graphics3D 800,600,32,2
SetBuffer BackBuffer()
While Not KeyDown( 1 )
Cls
UpdateEditor()
Renderworld()
Updateworld()
Flip
Wend
ClearWorld(True,True,True)
EndGraphics()
End

Function SetupEditor()
CreateEditorCamera()
CreateAxl()
PlaceObject()
End Function

Function PlaceObject()
obj.iobject = new iobject
CreateObject(obj)
End Function

Function ControlEditorCamera()
If KeyDown(205) Then TranslateEntity MainCam,0.3,0,0
If KeyDown(200) Then TranslateEntity MainCam,0,0,0.3
If KeyDown(203) Then TranslateEntity MainCam,-0.3,0,0
If KeyDown(208) Then TranslateEntity MainCam,0,0,-0.3
End Function

Function CreateEditorCamera()
MainCam=CreateCamera()
PositionEntity MainCam,0,20,-15
RotateEntity MainCam,68,0,0
End Function

Function CreateObject(obj.iobject,x#=0,y#=0,z#=0)
obj\mesh=CreateCube()
If obj\mesh Then
EntityPickMode obj\mesh,2
PositionEntity obj\mesh,x#,y#,z#
FlipMesh obj\mesh
EndIf
End Function

Function UpdateEditor()
If controlermode=2 Then controlermode=0
If KeyHit(2) Then controlermode=controlermode+1
ControlEditorCamera()
ControlAxl()
End Function

Function ControlAxl()
If MouseDown(1) Then
		If Not ismoving Then
		objectselected=0
			If CameraPick( Maincam, MouseX(), MouseY()) Then
If PickedEntity() Then objectselected=1
				Local e = PickedEntity()
				Local t = GetEntityType(e)
				If t &gt;= 1 And t &lt;= 3 Then
					ismoving = t
					MouseXSpeed()
					MouseYSpeed()
				EndIf
			EndIf
		EndIf
		Select ismoving
			Case 1	
            For obj.iobject = Each iobject
         Select controlermode
            Case 1
			MoveEntity obj\mesh, 0, -MouseYSpeed() * MOVE_COEF, 0
			Case 2
			TurnEntity obj\mesh, 0, -MouseYSpeed() * MOVE_COEF2, 0
			End Select
			PositionEntity AxlX,AXLOFFSET#+EntityX(obj\mesh),EntityY(obj\mesh),EntityZ(obj\mesh)
			PositionEntity AxlY,EntityX(obj\mesh),AXLOFFSET#+EntityY(obj\mesh),EntityZ(obj\mesh)
			PositionEntity AxlZ,EntityX(obj\mesh),EntityY(obj\mesh),-AXLOFFSET#+EntityZ(obj\mesh)
			Next
			Case 2
			For obj.iobject = Each sobject
			Select controlermode
            Case 1
			MoveEntity obj\mesh, MouseXSpeed() * MOVE_COEF , 0, 0
			Case 2
			TurnEntity obj\mesh, MouseXSpeed() * MOVE_COEF2 , 0, 0
			End Select
			PositionEntity AxlX,AXLOFFSET#+EntityX(obj\mesh),EntityY(obj\mesh),EntityZ(obj\mesh)
			PositionEntity AxlY,EntityX(obj\mesh),AXLOFFSET#+EntityY(obj\mesh),EntityZ(obj\mesh)
			PositionEntity AxlZ,EntityX(obj\mesh),EntityY(obj\mesh),-AXLOFFSET#+EntityZ(obj\mesh)
			Next
			Case 3
			For obj.iobject = Each sobject
			Select controlermode
            Case 1
			MoveEntity obj\mesh, 0, 0, -MouseYSpeed() * MOVE_COEF
			Case 2
			TurnEntity obj\mesh, 0, 0, -MouseYSpeed() * MOVE_COEF2
			End Select
			PositionEntity AxlX,AXLOFFSET#+EntityX(obj\mesh),EntityY(obj\mesh),EntityZ(obj\mesh)
			PositionEntity AxlY,EntityX(obj\mesh),AXLOFFSET#+EntityY(obj\mesh),EntityZ(obj\mesh)
			PositionEntity AxlZ,EntityX(obj\mesh),EntityY(obj\mesh),-AXLOFFSET#+EntityZ(obj\mesh)
			Next
		End Select
	ElseIf ismoving Then
		ismoving = 0
	EndIf

Select objectselected
Case 0
PositionEntity AxlX,0,0,0
PositionEntity AxlY,0,0,0
PositionEntity AxlZ,0,0,0
HideEntity AxlX
HideEntity AxlY
HideEntity AxlZ
Case 1
ShowEntity AxlX
ShowEntity AxlY
ShowEntity AxlZ
For obj.iobject = Each sobject
PositionEntity AxlX,AXLOFFSET#+EntityX(obj\mesh),EntityY(obj\mesh),EntityZ(obj\mesh)
PositionEntity AxlY,EntityX(obj\mesh),AXLOFFSET#+EntityY(obj\mesh),EntityZ(obj\mesh)
PositionEntity AxlZ,EntityX(obj\mesh),EntityY(obj\mesh),-AXLOFFSET#+EntityZ(obj\mesh)
Next
End Select
End Function

Type iobject
Field pitch#,yaw#,roll#,x#,y#,z#
Field mesh,entity,texture
End Type

Function CreateAxl()	
	AxlY = CreateCone()
	ScaleEntity AxlY, 0.5, 0.7, 0.5
	EntityColor AxlY, 255,0,0
	EntityPickMode AxlY, 3
	EntityType AxlY, 1
	HideEntity AxlY
	
	LineY = CreateCylinder()
	PositionEntity LineY,0,-2,0
	ScaleEntity LineY, 0.1, 1.5, 0.1
	EntityColor LineY, 255,0,0
	EntityPickMode LineY, 3
	EntityType LineY, 2
	EntityParent LineY,AxlY
		
	AxlX = CreateCone()
	ScaleEntity AxlX, 0.5, 0.7, 0.5
	RotateEntity AxlX,-90,90,0
	EntityColor AxlX, 0,255,0
	EntityPickMode AxlX, 3
	EntityType AxlX, 2
	HideEntity AxlX
	
	LineX = CreateCylinder()
	PositionEntity LineX,-2,0,0
	ScaleEntity LineX, 0.1, 1.5, 0.1
	RotateEntity LineX,90,90,0
	EntityColor LineX, 0,255,0
	EntityPickMode LineX, 3
	EntityType LineX, 2
	EntityParent LineX,AxlX
		
	AxlZ = CreateCone()
	ScaleEntity AxlZ, 0.5, 0.7, 0.5
	EntityColor AxlZ, 0,0,255
	RotateEntity AxlZ,90,180,0
	EntityPickMode AxlZ, 3
	EntityType AxlZ, 3
	HideEntity AxlZ
	
	LineZ = CreateCylinder()
	PositionEntity LineZ,0,0,2
	ScaleEntity LineZ, 0.1, 1.5, 0.1
	RotateEntity LineZ,90,0,0
	EntityColor LineZ, 0,0,255
	EntityPickMode LineZ, 3
	EntityType LineZ, 2
	EntityParent LineZ,AxlZ
	
	ObjBox = CreateCube()
	PositionEntity ObjBox,0,0,5
	ScaleEntity ObjBox,0.7,0.7,0.7
	EntityColor ObjBox,111,55,38
	EntityAlpha ObjBox,0.5
	EntityPickMode ObjBox, 3
	EntityType ObjBox, 2
	EntityParent ObjBox,AxlZ
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1335116"></a>

<a name="1335121"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chalky</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> 1.<br>Did the code I posted for you in #12 work?<br><br>2.<br>If you're posting as much code as you have in #13 it's probably better to use [codebox][/codebox] than [code][/code].<br><br>3.<br>Did you write the code in #13 yourself - or are you trying to adapt someone else's? I assume you want to know why only one object is getting processed in the For/Next loop? Could you please highlight where in your code are you creating multiple objects?<br><br>Repeatedly posting chunks of code with a vague question/statement is not likely to get much response - particularly if you do not acknowledge people's efforts to help. <br><br></td></tr></table><br>
<a name="1335123"></a>

<a name="1335125"></a>

<a name="1335126"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Caton</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yep the level list works now.<br><br>grable's code<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600

Global cam = CreateCamera()
MoveEntity cam, 0,5, -10

Global thing = MakeThingy()

PointEntity cam, thing

Const MOVE_COEF# = 0.10

Global ismoving
Global movingent

Repeat
	
	If MouseDown(1) Then
		If Not ismoving Then
			If CameraPick( cam, MouseX(), MouseY()) Then
				Local e = PickedEntity()
				Local t = GetEntityType(e)
				If t &gt;= 1 And t &lt;= 3 Then
					ismoving = t
					movingent = thing
					MouseXSpeed()
					MouseYSpeed()
				EndIf
			EndIf
		EndIf
		Select ismoving
			Case 1	MoveEntity movingent, 0, -MouseYSpeed() * MOVE_COEF, 0
			Case 2	MoveEntity movingent, MouseXSpeed() * MOVE_COEF , 0, 0
			Case 3	MoveEntity movingent, 0, 0, -MouseYSpeed() * MOVE_COEF
		End Select
	ElseIf ismoving Then
		ismoving = 0
		movingent = 0
	EndIf

	RenderWorld
	Flip
	Cls

Until KeyHit(1)
End


Function MakeThingy()
	Local thing = CreateCube()
	
	Local y = CreateCube()
	ScaleEntity y, 0.25, 1, 0.25
	EntityColor y, 255,0,0
	MoveEntity y, 0,1,0
	EntityPickMode y, 3
	EntityType y, 1
	EntityParent y, thing
	
	Local x = CreateCube()
	ScaleEntity x, 1, 0.25, 0.25
	EntityColor x, 0,255,0
	MoveEntity x, 1,0,0
	EntityPickMode x, 3
	EntityType x, 2
	EntityParent x, thing
		
	Local z = CreateCube()
	ScaleEntity z, 0.25, 0.25, 1
	EntityColor z, 0,0,255
	MoveEntity z, 0,0,-1
	EntityPickMode z, 3
	EntityType z, 3
	EntityParent z, thing
		
	Return thing
End Function
</textarea><br><br>My code<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Global controlermode=1
Global LineX,LineY,LineZ,AxlX,AxlY,AxlZ,objectselected
Global ismoving
Const AXLOFFSET# = 5.0
Const MOVE_COEF# = 0.12
Const MOVE_COEF2# = 1.0
AppTitle("Editor")
Graphics3D 800,600,32,2
SetBuffer BackBuffer()
While Not KeyDown( 1 )
Cls
UpdateEditor()
Renderworld()
Updateworld()
Flip
Wend
ClearWorld(True,True,True)
EndGraphics()
End

Function SetupEditor()
CreateEditorCamera()
CreateAxl()
PlaceObject()
End Function

Function PlaceObject()
obj.iobject = new iobject
CreateObject(obj)
End Function

Function ControlEditorCamera()
If KeyDown(205) Then TranslateEntity MainCam,0.3,0,0
If KeyDown(200) Then TranslateEntity MainCam,0,0,0.3
If KeyDown(203) Then TranslateEntity MainCam,-0.3,0,0
If KeyDown(208) Then TranslateEntity MainCam,0,0,-0.3
End Function

Function CreateEditorCamera()
MainCam=CreateCamera()
PositionEntity MainCam,0,20,-15
RotateEntity MainCam,68,0,0
End Function

Function CreateObject(obj.iobject,x#=0,y#=0,z#=0)
obj\mesh=CreateCube()
If obj\mesh Then
EntityPickMode obj\mesh,2
PositionEntity obj\mesh,x#,y#,z#
FlipMesh obj\mesh
EndIf
End Function

Function UpdateEditor()
If controlermode=2 Then controlermode=0
If KeyHit(2) Then controlermode=controlermode+1
ControlEditorCamera()
ControlAxl()
End Function

Function ControlAxl()
If MouseDown(1) Then
		If Not ismoving Then
		objectselected=0
			If CameraPick( Maincam, MouseX(), MouseY()) Then
If PickedEntity() Then objectselected=1
				Local e = PickedEntity()
				Local t = GetEntityType(e)
				If t &gt;= 1 And t &lt;= 3 Then
					ismoving = t
					MouseXSpeed()
					MouseYSpeed()
				EndIf
			EndIf
		EndIf
		Select ismoving
			Case 1	
            For obj.iobject = Each sobject
         Select controlermode
            Case 1
			MoveEntity obj\mesh, 0, -MouseYSpeed() * MOVE_COEF, 0
			Case 2
			TurnEntity obj\mesh, 0, -MouseYSpeed() * MOVE_COEF2, 0
			End Select
			PositionEntity AxlX,AXLOFFSET#+EntityX(obj\mesh),EntityY(obj\mesh),EntityZ(obj\mesh)
			PositionEntity AxlY,EntityX(obj\mesh),AXLOFFSET#+EntityY(obj\mesh),EntityZ(obj\mesh)
			PositionEntity AxlZ,EntityX(obj\mesh),EntityY(obj\mesh),-AXLOFFSET#+EntityZ(obj\mesh)
			Next
			Case 2
			For obj.iobject = Each sobject
			Select controlermode
            Case 1
			MoveEntity obj\mesh, MouseXSpeed() * MOVE_COEF , 0, 0
			Case 2
			TurnEntity obj\mesh, MouseXSpeed() * MOVE_COEF2 , 0, 0
			End Select
			PositionEntity AxlX,AXLOFFSET#+EntityX(obj\mesh),EntityY(obj\mesh),EntityZ(obj\mesh)
			PositionEntity AxlY,EntityX(obj\mesh),AXLOFFSET#+EntityY(obj\mesh),EntityZ(obj\mesh)
			PositionEntity AxlZ,EntityX(obj\mesh),EntityY(obj\mesh),-AXLOFFSET#+EntityZ(obj\mesh)
			Next
			Case 3
			For obj.iobject = Each sobject
			Select controlermode
            Case 1
			MoveEntity obj\mesh, 0, 0, -MouseYSpeed() * MOVE_COEF
			Case 2
			TurnEntity obj\mesh, 0, 0, -MouseYSpeed() * MOVE_COEF2
			End Select
			PositionEntity AxlX,AXLOFFSET#+EntityX(obj\mesh),EntityY(obj\mesh),EntityZ(obj\mesh)
			PositionEntity AxlY,EntityX(obj\mesh),AXLOFFSET#+EntityY(obj\mesh),EntityZ(obj\mesh)
			PositionEntity AxlZ,EntityX(obj\mesh),EntityY(obj\mesh),-AXLOFFSET#+EntityZ(obj\mesh)
			Next
		End Select
	ElseIf ismoving Then
		ismoving = 0
	EndIf

Select objectselected
Case 0
PositionEntity AxlX,0,0,0
PositionEntity AxlY,0,0,0
PositionEntity AxlZ,0,0,0
HideEntity AxlX
HideEntity AxlY
HideEntity AxlZ
Case 1
ShowEntity AxlX
ShowEntity AxlY
ShowEntity AxlZ
For obj.iobject = Each iobject
PositionEntity AxlX,AXLOFFSET#+EntityX(obj\mesh),EntityY(obj\mesh),EntityZ(obj\mesh)
PositionEntity AxlY,EntityX(obj\mesh),AXLOFFSET#+EntityY(obj\mesh),EntityZ(obj\mesh)
PositionEntity AxlZ,EntityX(obj\mesh),EntityY(obj\mesh),-AXLOFFSET#+EntityZ(obj\mesh)
Next
End Select
End Function

Type iobject
Field pitch#,yaw#,roll#,x#,y#,z#
Field mesh,entity,texture
End Type

Function CreateAxl()	
	AxlY = CreateCone()
	ScaleEntity AxlY, 0.5, 0.7, 0.5
	EntityColor AxlY, 255,0,0
	EntityPickMode AxlY, 3
	EntityType AxlY, 1
	HideEntity AxlY
	
	LineY = CreateCylinder()
	PositionEntity LineY,0,-2,0
	ScaleEntity LineY, 0.1, 1.5, 0.1
	EntityColor LineY, 255,0,0
	EntityPickMode LineY, 3
	EntityType LineY, 2
	EntityParent LineY,AxlY
		
	AxlX = CreateCone()
	ScaleEntity AxlX, 0.5, 0.7, 0.5
	RotateEntity AxlX,-90,90,0
	EntityColor AxlX, 0,255,0
	EntityPickMode AxlX, 3
	EntityType AxlX, 2
	HideEntity AxlX
	
	LineX = CreateCylinder()
	PositionEntity LineX,-2,0,0
	ScaleEntity LineX, 0.1, 1.5, 0.1
	RotateEntity LineX,90,90,0
	EntityColor LineX, 0,255,0
	EntityPickMode LineX, 3
	EntityType LineX, 2
	EntityParent LineX,AxlX
		
	AxlZ = CreateCone()
	ScaleEntity AxlZ, 0.5, 0.7, 0.5
	EntityColor AxlZ, 0,0,255
	RotateEntity AxlZ,90,180,0
	EntityPickMode AxlZ, 3
	EntityType AxlZ, 3
	HideEntity AxlZ
	
	LineZ = CreateCylinder()
	PositionEntity LineZ,0,0,2
	ScaleEntity LineZ, 0.1, 1.5, 0.1
	RotateEntity LineZ,90,0,0
	EntityColor LineZ, 0,0,255
	EntityPickMode LineZ, 3
	EntityType LineZ, 2
	EntityParent LineZ,AxlZ
	
	ObjBox = CreateCube()
	PositionEntity ObjBox,0,0,5
	ScaleEntity ObjBox,0.7,0.7,0.7
	EntityColor ObjBox,111,55,38
	EntityAlpha ObjBox,0.5
	EntityPickMode ObjBox, 3
	EntityType ObjBox, 2
	EntityParent ObjBox,AxlZ
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1335142"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Caton</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> umm... so why is it doing it wrong moving only one object? <br><br></td></tr></table><br>
<a name="1335143"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >dna</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> I get a memory access violation <br><br></td></tr></table><br>
<a name="1335144"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Caton</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> your debugger is off. <br><br></td></tr></table><br>
<a name="1335146"></a>

<a name="1335147"></a>

<a name="1335148"></a>

<a name="1335149"></a>

<a name="1335150"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >grable</td><td align="right"><font class=tiny>(Posted 2017)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> I tried your latest code, but it doesnt even compile, and even after making it compile its not setup correctly.<br><br>Anyway...<br>You are confusing the various objects you are selecting and moving, i assume you dont want to move ALL objects like you are attempting to do (with all those For loops), lots of redundant code.<br><br>Heres what i got after some fixing, i added creation of new objects with space. Also i added some indentation, as i could not read it without.<br>Notice that the first mousedown has 2 states now, using entity type 0 for the real objects and still 1,2,3 for the axl.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Global controlermode=1
Global LineX,LineY,LineZ,AxlX,AxlY,AxlZ,objectselected
Global ismoving
Global MainCam
Global selectedobj.iobject

Const AXLOFFSET# = 5.0
Const MOVE_COEF# = 0.12
Const MOVE_COEF2# = 1.0

AppTitle("Editor")
Graphics3D 800,600,32,2

SetupEditor()

SetBuffer BackBuffer()
While Not KeyDown( 1 )
	Cls
	
	If KeyHit(57) Then PlaceObject()
	UpdateEditor()
	
	UpdateWorld()
	RenderWorld()
	Flip
Wend
;ClearWorld(True,True,True)
;EndGraphics()
End

Function SetupEditor()
	CreateEditorCamera()
	CreateAxl()
	PlaceObject()
End Function

Function PlaceObject()
	obj.iobject = New iobject
	CreateObject(obj)
End Function

Function ControlEditorCamera()
	If KeyDown(205) Then TranslateEntity MainCam,0.3,0,0
	If KeyDown(200) Then TranslateEntity MainCam,0,0,0.3
	If KeyDown(203) Then TranslateEntity MainCam,-0.3,0,0
	If KeyDown(208) Then TranslateEntity MainCam,0,0,-0.3
End Function

Function CreateEditorCamera()
	MainCam=CreateCamera()
	PositionEntity MainCam,0,20,-15
	RotateEntity MainCam,68,0,0
End Function

Function CreateObject(obj.iobject,x#=0,y#=0,z#=0)
	obj\mesh=CreateCube()
	If obj\mesh Then
		EntityPickMode obj\mesh,2
		PositionEntity obj\mesh,x#,y#,z#
		FlipMesh obj\mesh
	EndIf
End Function

Function UpdateEditor()
	If controlermode=2 Then controlermode=0
	If KeyHit(2) Then controlermode=controlermode+1
	ControlEditorCamera()
	ControlAxl()
End Function

Function ControlAxl()
	If MouseDown(1) Then
		If Not ismoving Then
			If CameraPick( Maincam, MouseX(), MouseY()) Then
				Local e = PickedEntity()
				Local t = GetEntityType(e)
				If t = 0 Then
					; find real object
					For obj.iobject = Each iObject
						If obj\mesh = e Then
							selectedobj = obj
							Goto done
						EndIf
					Next
					.done
					objectselected = selectedobj &lt;&gt; Null
				ElseIf t &gt;= 1 And t &lt;= 3 Then
					If selectedobj &lt;&gt; Null Then						
						ismoving = t
						MouseXSpeed()
						MouseYSpeed()
					EndIf
				EndIf
			Else
				objectselected = 0
				selectedobj = Null
			EndIf
		EndIf
		Select ismoving
			Case 1	
	        	Select controlermode
		            Case 1	MoveEntity selectedobj\mesh, 0, -MouseYSpeed() * MOVE_COEF, 0
					Case 2	TurnEntity selectedobj\mesh, 0, -MouseYSpeed() * MOVE_COEF2, 0
				End Select
			Case 2
				Select controlermode
		            Case 1	MoveEntity selectedobj\mesh, MouseXSpeed() * MOVE_COEF , 0, 0
					Case 2	TurnEntity selectedobj\mesh, MouseXSpeed() * MOVE_COEF2 , 0, 0
				End Select
			Case 3
				Select controlermode
		            Case 1	MoveEntity selectedobj\mesh, 0, 0, -MouseYSpeed() * MOVE_COEF
					Case 2	TurnEntity selectedobj\mesh, 0, 0, -MouseYSpeed() * MOVE_COEF2
				End Select
		End Select
	ElseIf ismoving Then
		ismoving = 0
	EndIf

	Select objectselected
		Case 0
			PositionEntity AxlX,0,0,0
			PositionEntity AxlY,0,0,0
			PositionEntity AxlZ,0,0,0
			HideEntity AxlX
			HideEntity AxlY
			HideEntity AxlZ
		Case 1
			ShowEntity AxlX
			ShowEntity AxlY
			ShowEntity AxlZ
			PositionEntity AxlX,AXLOFFSET#+EntityX(selectedobj\mesh),EntityY(selectedobj\mesh),EntityZ(selectedobj\mesh)
			PositionEntity AxlY,EntityX(selectedobj\mesh),AXLOFFSET#+EntityY(selectedobj\mesh),EntityZ(selectedobj\mesh)
			PositionEntity AxlZ,EntityX(selectedobj\mesh),EntityY(selectedobj\mesh),-AXLOFFSET#+EntityZ(selectedobj\mesh)
	End Select
End Function

Type iobject
	Field pitch#,yaw#,roll#,x#,y#,z#
	Field mesh,entity,texture
End Type

Function CreateAxl()	
	AxlY = CreateCone()
	ScaleEntity AxlY, 0.5, 0.7, 0.5
	EntityColor AxlY, 255,0,0
	EntityPickMode AxlY, 3
	EntityType AxlY, 1
	HideEntity AxlY
	
	LineY = CreateCylinder()
	PositionEntity LineY,0,-2,0
	ScaleEntity LineY, 0.1, 1.5, 0.1
	EntityColor LineY, 255,0,0
	EntityPickMode LineY, 3
	EntityType LineY, 2
	EntityParent LineY,AxlY
		
	AxlX = CreateCone()
	ScaleEntity AxlX, 0.5, 0.7, 0.5
	RotateEntity AxlX,-90,90,0
	EntityColor AxlX, 0,255,0
	EntityPickMode AxlX, 3
	EntityType AxlX, 2
	HideEntity AxlX
	
	LineX = CreateCylinder()
	PositionEntity LineX,-2,0,0
	ScaleEntity LineX, 0.1, 1.5, 0.1
	RotateEntity LineX,90,90,0
	EntityColor LineX, 0,255,0
	EntityPickMode LineX, 3
	EntityType LineX, 2
	EntityParent LineX,AxlX
		
	AxlZ = CreateCone()
	ScaleEntity AxlZ, 0.5, 0.7, 0.5
	EntityColor AxlZ, 0,0,255
	RotateEntity AxlZ,90,180,0
	EntityPickMode AxlZ, 3
	EntityType AxlZ, 3
	HideEntity AxlZ
	
	LineZ = CreateCylinder()
	PositionEntity LineZ,0,0,2
	ScaleEntity LineZ, 0.1, 1.5, 0.1
	RotateEntity LineZ,90,0,0
	EntityColor LineZ, 0,0,255
	EntityPickMode LineZ, 3
	EntityType LineZ, 2
	EntityParent LineZ,AxlZ
	
	ObjBox = CreateCube()
	PositionEntity ObjBox,0,0,5
	ScaleEntity ObjBox,0.7,0.7,0.7
	EntityColor ObjBox,111,55,38
	EntityAlpha ObjBox,0.5
	EntityPickMode ObjBox, 3
	EntityType ObjBox, 2
	EntityParent ObjBox,AxlZ
End Function

</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
