<!DOCTYPE html><html lang="en" ><head ><title >Physics..</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Physics..</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Physics..</a><br><br>
<a name="1053288"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kenshin Yurihara</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Alright so, I'm now working with 3D and what not for the first time and I was wondering if anyone had some ideas on which Physics Wrapper to use. I'd prefer free, but not to much in this world is free...so I don't really expect that.<br><br>Anyway, Peace. <br><br></td></tr></table><br>
<a name="1053303"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Blitzplotter</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I like JV-ODE, there is a 'trial' version which is free.... if you like it I'd give a +1 vote for buying JV-ODE, very reasonable for the functionality within. Lots of example code as well to play with! <br><br></td></tr></table><br>
<a name="1053329"></a>

<a name="1053331"></a>

<a name="1053332"></a>

<a name="1053337"></a>

<a name="1053339"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's a free ODE wrapper I made earlier:<br><br><a href="http://www.blitzbasic.com/Community/posts.php?topic=89067" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=89067</a><br><br>It's extremely minimalist though, although I might add one or two things in the future. If you need any more features than that I too would just recommend JV-ODE (also, VIP3R is really good with support).<br><br>Other things... PhysX is probably the best as long as you have the hardware for it, althought it can also be a pain to install. Tokamak is wonderful <i>if you don't use trimeshes</i>, as they still have a memory leak. There's also the old free BlitzODE wrapper floating around somewhere, but it's based on an older, less stable ODE version and is inexplicably missing certain important functions... not really recommended.<br><br>Alternatively, for amusing silliness, you could consider a homebuilt point-and-spring engine such as this one:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Const VP_MAXPOINTS = 19		;Extend these as far as necessary
Const VP_MAXLINKS = 52

Type verlet		;Verlet points
	Field cx#,cy#,cz#	;Current position
	Field ox#,oy#,oz#	;Previous position
	Field radius#		;Collision radius
	Field pivot			;3D entity (necessary for collisions with non-VPhysics entities)
	Field collided		;True if verlet is in contact with another verlet or B3D entity
End Type

Type vlink		;VLinks
	Field p1.verlet
	Field p2.verlet
	Field minlength#
	Field maxlength#
	Field offset#		;The actual distance between p1 and p2, to pass on to child link
	Field parent.vlink	;Copy the length of another link (prevents rotation of hinges)
End Type

Type vpe		;Physics entities
	Field state			;0=Inactive, no collisions; 1=Active, VPhysics interactions
	Field ptype			;Type of primitive. 1=single point, 2=cube, 3=ragdoll, 4=fabric
	Field points		;Number of VPoints
	Field p.verlet[VP_MAXPOINTS]	;List of VPoints
	Field links			;Numver of VLinks
	Field l.vlink[VP_MAXLINKS]	;List of VLinks
	Field radius#
	Field mesh			;The mesh itself
End Type


Global gwidth=1024,gheight=768
Global VP_FPS#=50		;Fixed timestep for physics

Global airres#=0.985	;Air resistance (ie. slowdown when ver\collided=False)
Global colres#=0.88		;Ground resistance (ie. slowdown when ver\collided=True)
Global gravity#=0.15
Global physicstype=1	;B3D Collsion Type of physics objects


Graphics3D gwidth,gheight,32,2
SetBuffer BackBuffer()
;Include "Includes\Draw3Dz.bb"


;Scene
camera=CreateCamera();:PositionEntity camera,0,30,-100
PositionEntity camera,0,30,-60
light = CreateLight()
;PositionEntity light,0,100,-50
RotateEntity light,90,0,0
ground=CreateMesh():PointEntity camera, ground
parquet=CreateSurface(ground)
v1=AddVertex(parquet,-125,0,150):v2=AddVertex(parquet,125,0,150):v3=AddVertex(parquet,125,0,-100)
AddTriangle(parquet,v1,v2,v3):v2=AddVertex(parquet,-125,0,-100):AddTriangle(parquet,v1,v3,v2)
EntityColor ground,0,0,255:EntityType ground,2
block=CreateCube():ScaleEntity block,20,5,20
EntityType block,2

;DrawInit3D(camera)
;Origin3D(gwidth,gheight)
;font=LoadFont3D("Fonts\Phyton1.png")


dmesh=CreateCube()
PositionEntity dmesh,0,15,0
ScaleEntity dmesh,7,7,7
EntityColor dmesh,0,255,0
;EntityAlpha dmesh,0.5

cmesh=CreateCube()
PositionEntity cmesh,20,30,0
ScaleEntity cmesh,7,7,7
EntityColor cmesh,0,255,0
;EntityAlpha cmesh,0.5

t1.vpe=CreateVCube(dmesh,4,4,4,3.25)
t2.vpe=CreateVCube(cmesh,5,5,5,2.25)

;ragdoll=CreatePivot()
;ragtest.vpe=CreateRagdoll(ragdoll)


phystime#=1000.0/VP_FPS
ctime=MilliSecs()
accum#=0


SC_FPS=60	;Desired framerate
rtime=Floor(1000.0/SC_FPS)
limited=True

Collisions physicstype,2,2,2

While Not KeyDown(1)
	dtime=(MilliSecs()-ctime)
	accum=accum+(MilliSecs()-ctime)
	ctime=MilliSecs()
	
	While accum&gt;=phystime	;This is the update loop for ALL time-dependent functions, not just physics
		CaptureWorld	;The most important thing! Instant-movement should happen before this
		
		UpdateVP
	
		;Forces need to stay within this loop
		ApplyForce(t2,(KeyDown(205)-KeyDown(203))/8.0,4*KeyHit(57),(KeyDown(200)-KeyDown(208))/8.0)
		
	;	ragtest\p[1]\cx=0:ragtest\p[1]\cy=35:ragtest\p[1]\cz=0
	;	ragtest\p[1]\cx=0:ragtest\p[1]\oy=35:ragtest\p[1]\cz=0
	
		IntegrateVP 5
		UpdateWorld
		
		accum=accum-phystime
	Wend

	If KeyHit(38) Then limited=Not limited

	If MilliSecs()-render_time=&gt;1000 Then fps=frames:frames=0:render_time=MilliSecs():Else frames=frames+1
	
	RenderWorld accum/phystime
	Text 0,30,"FPS: "+fps

	Delay (rtime-(MilliSecs()-ctime))-(limited+1)
	;Insert VWait Parameter Here
	Flip limited
Wend

;DrawFree3D
End



Function AddVerlet.verlet(ent%,x#,y#,z#,r#)		;Places the VPoint at x,y,z relative to entity
	ver.verlet=New verlet
;	ver\vbody=ent
	
	ver\cx#=EntityX(ent,1)+x:ver\ox#=ver\cx#
	ver\cy#=EntityY(ent,1)+y:ver\oy#=ver\cy#
	ver\cz#=EntityZ(ent,1)+z:ver\oz#=ver\cz#
	ver\radius#=r
	
;	ver\pivot=CreateSphere(8)
;	ScaleEntity ver\pivot,r,r,r		;Remove this later
	ver\pivot=CreatePivot()
	PositionEntity ver\pivot,ver\cx,ver\cy,ver\cz,1
	ResetEntity ver\pivot	;Make sure it doesn't collide with anything before things begin
	EntityRadius ver\pivot,r
	EntityType ver\pivot,physicstype
	
	Return ver
End Function

Function FreeVerlet(ver.verlet)
	For link.vlink=Each vlink
		If link\p1=ver Or link\p2=ver Then Delete link
	Next
	Delete ver
End Function

Function LinkVerlets.vlink(v1.verlet,v2.verlet,forced#=0,maxlen#=0,p=0)	;p=Handle(parent.vlink), because Null isn't a constant
	Local link.vlink=New vlink
	link\p1=v1
	link\p2=v2
	
	Local dx#=v1\cx-v2\cx
	Local dy#=v1\cy-v2\cy
	Local dz#=v1\cz-v2\cz
	Local length#=Sqr(dx*dx+dy*dy+dz*dz)
	
	If forced=0
		link\minlength=length
	Else
		link\minlength=forced
	EndIf
	
	link\maxlength=link\minlength
	If maxlen&gt;0 Then link\maxlength=maxlen
	If maxlen&lt;0 Then link\maxlength=length	;In case the starting positions are the max length
	
	If p=0
		link\parent=Null
	Else
		link\parent=Object.vlink(p)
	EndIf
	Return link
End Function

Function UnlinkVerlets(v1.verlet,v2.verlet)
	For link.vlink=Each vlink
		If (link\p1=v1 And link\p2=v2) Or (link\p1=v2 And link\p2=v1)
			Delete link
		EndIf
	Next
End Function

Function CreateVPoint.vpe(mesh,x#=0,y#=0,z#=0,radius#=1,yradius#=0)
	point.vpe=New vpe
	point\state=1
	point\mesh=mesh
	point\ptype=1
	point\points=1
	point\links=0
	
	point\p[1]=AddVerlet(mesh,x,y,z,radius)
	
	;Adjust the Y radius here if desired - for characters
	
	Return point
End Function

Function CreateVCube.vpe(mesh,xs#=1,ys#=1,zs#=1,rs#=1)
	cube.vpe=New vpe
	cube\state=1
	cube\mesh=mesh
	cube\ptype=2
	cube\points=9
	cube\links=30
	
	cube\p[1]=AddVerlet(mesh, xs, ys, zs,rs)
	cube\p[2]=AddVerlet(mesh,-xs, ys, zs,rs)
	cube\p[3]=AddVerlet(mesh,-xs, ys,-zs,rs)
	cube\p[4]=AddVerlet(mesh, xs, ys,-zs,rs)
	cube\p[5]=AddVerlet(mesh, xs,-ys, zs,rs)
	cube\p[6]=AddVerlet(mesh,-xs,-ys, zs,rs)
	cube\p[7]=AddVerlet(mesh,-xs,-ys,-zs,rs)
	cube\p[8]=AddVerlet(mesh, xs,-ys,-zs,rs)

	cube\l[1]=LinkVerlets(cube\p[1],cube\p[2]):cube\l[2]=LinkVerlets(cube\p[2],cube\p[3])
	cube\l[3]=LinkVerlets(cube\p[3],cube\p[4]):cube\l[4]=LinkVerlets(cube\p[4],cube\p[1])
	cube\l[5]=LinkVerlets(cube\p[5],cube\p[6]):cube\l[6]=LinkVerlets(cube\p[6],cube\p[7])
	cube\l[7]=LinkVerlets(cube\p[7],cube\p[8]):cube\l[8]=LinkVerlets(cube\p[8],cube\p[5])
	cube\l[9]=LinkVerlets(cube\p[1],cube\p[5]):cube\l[10]=LinkVerlets(cube\p[2],cube\p[6])
	cube\l[11]=LinkVerlets(cube\p[3],cube\p[7]):cube\l[12]=LinkVerlets(cube\p[4],cube\p[8])

	cube\l[13]=LinkVerlets(cube\p[2],cube\p[4]):cube\l[14]=LinkVerlets(cube\p[2],cube\p[5])
	cube\l[15]=LinkVerlets(cube\p[3],cube\p[6]):cube\l[16]=LinkVerlets(cube\p[3],cube\p[8])
	cube\l[17]=LinkVerlets(cube\p[4],cube\p[5]):cube\l[18]=LinkVerlets(cube\p[6],cube\p[8])
	cube\l[19]=LinkVerlets(cube\p[2],cube\p[8]):cube\l[20]=LinkVerlets(cube\p[4],cube\p[6])
	cube\l[21]=LinkVerlets(cube\p[1],cube\p[7]):cube\l[22]=LinkVerlets(cube\p[3],cube\p[5])
	
	cs#=xs
	If cs&lt;ys Then cs=ys		;Fit the centre point to the smallest dimension
	If cs&lt;zs Then cs=zs
	cs=cs+rs				;Then add corner radius to fill out the sides
	cube\p[9]=AddVerlet(mesh,0,0,0,cs)
	
	cube\l[23]=LinkVerlets(cube\p[1],cube\p[9]):cube\l[24]=LinkVerlets(cube\p[7],cube\p[9])
	cube\l[25]=LinkVerlets(cube\p[3],cube\p[9]):cube\l[26]=LinkVerlets(cube\p[5],cube\p[9])
	cube\l[27]=LinkVerlets(cube\p[2],cube\p[9]):cube\l[28]=LinkVerlets(cube\p[8],cube\p[9])
	cube\l[29]=LinkVerlets(cube\p[4],cube\p[9]):cube\l[30]=LinkVerlets(cube\p[6],cube\p[9])
	
	Return cube
End Function

;Function CreateVCloth.vpe()
;End Function

Function CreateRagdoll.vpe(body)
	rd.vpe=New vpe
	rd\state=1
	rd\mesh=body
	rd\ptype=3
	rd\points=19
	rd\links=52

	rd\p[1]=AddVerlet(body,0,27,0,1.5)		;Head
	rd\p[2]=AddVerlet(body,-3.32,23.5,0,1.1)	;Shoulders
	rd\p[3]=AddVerlet(body,3.32,23.5,0,1)
	rd\p[4]=AddVerlet(body,-1.64,15.8,1,1.2)	;Hips
	rd\p[5]=AddVerlet(body,1.64,15.8,1,1.2)
	rd\p[6]=AddVerlet(body,-1.64,15.8,-1,1.2)
	rd\p[7]=AddVerlet(body,1.64,15.8,-1,1.2)

	rd\p[8]=AddVerlet(body,-8.6,23,0,0.8)	;Arms
	rd\p[9]=AddVerlet(body,-8.6,24,0,0.8)
	rd\p[10]=AddVerlet(body,-13.6,23.5,0,0.6)
	rd\p[11]=AddVerlet(body,8.6,23,0,0.8)
	rd\p[12]=AddVerlet(body,8.6,24,0,0.8)
	rd\p[13]=AddVerlet(body,13.6,23.5,0,0.6)

	rd\p[14]=AddVerlet(body,-2,7.8,0,1.1)	;Legs
	rd\p[15]=AddVerlet(body,-1.2,7.8,0,1.1)
	rd\p[16]=AddVerlet(body,-1.6,0,0,1)
	rd\p[17]=AddVerlet(body,2,7.8,0,1.1)
	rd\p[18]=AddVerlet(body,1.2,7.8,0,1.1)
	rd\p[19]=AddVerlet(body,1.6,0,0,1)

	rd\l[1]=LinkVerlets(rd\p[1],rd\p[2],3,-1):rd\l[2]=LinkVerlets(rd\p[1],rd\p[3],3,-1)
	rd\l[3]=LinkVerlets(rd\p[2],rd\p[3])
	rd\l[4]=LinkVerlets(rd\p[2],rd\p[4]):rd\l[5]=LinkVerlets(rd\p[3],rd\p[5])
	rd\l[6]=LinkVerlets(rd\p[2],rd\p[6]):rd\l[7]=LinkVerlets(rd\p[3],rd\p[7])
	rd\l[8]=LinkVerlets(rd\p[4],rd\p[6]):rd\l[9]=LinkVerlets(rd\p[5],rd\p[7])
	rd\l[10]=LinkVerlets(rd\p[4],rd\p[5]):rd\l[11]=LinkVerlets(rd\p[6],rd\p[7])
	rd\l[12]=LinkVerlets(rd\p[4],rd\p[7]):rd\l[13]=LinkVerlets(rd\p[5],rd\p[6])
	rd\l[14]=LinkVerlets(rd\p[2],rd\p[5]):rd\l[15]=LinkVerlets(rd\p[2],rd\p[7])
	rd\l[16]=LinkVerlets(rd\p[3],rd\p[4]):rd\l[17]=LinkVerlets(rd\p[3],rd\p[6])
	rd\l[18]=LinkVerlets(rd\p[1],rd\p[6],11.5,-1):rd\l[19]=LinkVerlets(rd\p[1],rd\p[7],11.5,-1)

	rd\l[20]=LinkVerlets(rd\p[2],rd\p[8]):rd\l[21]=LinkVerlets(rd\p[2],rd\p[9])
	rd\l[22]=LinkVerlets(rd\p[8],rd\p[10]):rd\l[23]=LinkVerlets(rd\p[9],rd\p[10])
	rd\l[24]=LinkVerlets(rd\p[3],rd\p[9],6,-1):rd\l[25]=LinkVerlets(rd\p[2],rd\p[10],5,-1)
	rd\l[26]=LinkVerlets(rd\p[8],rd\p[9]):rd\l[27]=LinkVerlets(rd\p[11],rd\p[12])
	rd\l[28]=LinkVerlets(rd\p[3],rd\p[11]):rd\l[29]=LinkVerlets(rd\p[3],rd\p[12])
	rd\l[30]=LinkVerlets(rd\p[11],rd\p[13]):rd\l[31]=LinkVerlets(rd\p[12],rd\p[13])
	rd\l[32]=LinkVerlets(rd\p[2],rd\p[12],6,-1):rd\l[33]=LinkVerlets(rd\p[3],rd\p[13],5,-1)

	rd\l[34]=LinkVerlets(rd\p[4],rd\p[14]):rd\l[35]=LinkVerlets(rd\p[4],rd\p[15])
	rd\l[36]=LinkVerlets(rd\p[14],rd\p[16]):rd\l[37]=LinkVerlets(rd\p[15],rd\p[16])
	rd\l[38]=LinkVerlets(rd\p[14],rd\p[15]):rd\l[39]=LinkVerlets(rd\p[6],rd\p[16],7,15.6)
	rd\l[40]=LinkVerlets(rd\p[6],rd\p[14],8,10):rd\l[41]=LinkVerlets(rd\p[6],rd\p[15],8,10,Handle(rd\l[40]))
	rd\l[42]=LinkVerlets(rd\p[5],rd\p[17]):rd\l[43]=LinkVerlets(rd\p[5],rd\p[18])
	rd\l[44]=LinkVerlets(rd\p[17],rd\p[19]):rd\l[45]=LinkVerlets(rd\p[18],rd\p[19])
	rd\l[46]=LinkVerlets(rd\p[17],rd\p[18]):rd\l[47]=LinkVerlets(rd\p[7],rd\p[19],7,15.6)
	rd\l[48]=LinkVerlets(rd\p[7],rd\p[17],8,10):rd\l[49]=LinkVerlets(rd\p[7],rd\p[18],8,10,Handle(rd\l[48]))
	rd\l[50]=LinkVerlets(rd\p[15],rd\p[18],2,12)
	rd\l[51]=LinkVerlets(rd\p[5],rd\p[15],7,14):rd\l[52]=LinkVerlets(rd\p[4],rd\p[18],8,14)
	
	Return rd
End Function

Function FreeVEntity(ent.vpe)
	For v=1 To ent\points
		FreeVerlet(ent\p[v])
	Next
	Delete ent
End Function

Function HideVEntity(ent.vpe,hidemesh=False)
	For v=1 To ent\points
		HideEntity ent\p[v]\pivot
	Next
	ent\state=False
	If hidemesh Then HideEntity ent\mesh
End Function

Function ShowVEntity(ent.vpe)
	For v=1 To ent\points
		ShowEntity ent\p[v]\pivot
	Next
	ent\state=True
	ShowEntity ent\mesh
End Function

Function SetColType(ent.vpe,ctype)
	For v=1 To ent\points
		EntityType ent\p[v]\pivot,ctype
	Next
End Function

Function ApplyForce(ent.vpe,x#,y#,z#)
	If ent\state
		For v=1 To ent\points
			ver.verlet=ent\p[v]
			ver\cx=ver\cx+x
			ver\cy=ver\cy+y
			ver\cz=ver\cz+z
		Next
	EndIf
End Function

Function SetForce(ent.vpe,x#,y#,z#)
	If ent\state
		For v=1 To ent\points
			ver.verlet=ent\p[v]
			ver\ox=ver\cx
			ver\oy=ver\cy
			ver\oz=ver\cz
			ver\cx=ver\cx+x
			ver\cy=ver\cy+y
			ver\cz=ver\cz+z
		Next
	EndIf
End Function

Function ApplyPointForce(ver.verlet,x#,y#,z#)
	ver\cx=ver\cx+x
	ver\cy=ver\cy+y
	ver\cz=ver\cz+z
End Function

Function SetPointForce(ver.verlet,x#,y#,z#)
	ver\ox=ver\cx
	ver\oy=ver\cy
	ver\oz=ver\cz
	ver\cx=ver\cx+x
	ver\cy=ver\cy+y
	ver\cz=ver\cz+z
End Function

Function MoveVerlet(ver.verlet,x#,y#,z#,n=1,r=1)
	ver\cx=ver\cx+x
	ver\cy=ver\cy+y
	ver\cz=ver\cz+z
	If n
		ver\ox=ver\cx
		ver\oy=ver\cy
		ver\oz=ver\cz
	Else
		ver\ox=ver\cx+x
		ver\oy=ver\cy+y
		ver\oz=ver\cz+z
	EndIf
	PositionEntity ver\pivot,ver\cx,ver\cy,ver\cz
	If r Then ResetEntity ver\pivot
End Function

Function MoveVEntity(ent.vpe,x#,y#,z#,n=1,r=1)
	For v=1 To ent\points
		ver.verlet=ent\p[v]
		ver\cx=ver\cx+x
		ver\cy=ver\cy+y
		ver\cz=ver\cz+z
		If n
			ver\ox=ver\cx
			ver\oy=ver\cy
			ver\oz=ver\cz
		Else
			ver\ox=ver\cx+x
			ver\oy=ver\cy+y
			ver\oz=ver\cz+z
		EndIf
		PositionEntity ver\pivot,ver\cx,ver\cy,ver\cz
		If r Then ResetEntity ver\pivot
	Next
	TranslateEntity ent\mesh,x,y,z,1
End Function

Function PositionVerlet(ver.verlet,x#,y#,z#,n=1)
	If n
		ver\ox=x
		ver\oy=y
		ver\oz=z
	Else
		ver\ox=x-(ver\cx-ver\ox)
		ver\oy=y-(ver\cy-ver\oy)
		ver\oz=z-(ver\cz-ver\oz)
	EndIf
	ver\cx=x
	ver\cy=y
	ver\cz=z
	PositionEntity ver\pivot,ver\cx,ver\cy,ver\cz
	ResetEntity ver\pivot
End Function

Function PositionVEntity(ent.vpe,x#,y#,z#,n=1)
	For v=1 To ent\points
		ver.verlet=ent\p[v]
		If n
			ver\ox=x
			ver\oy=y
			ver\oz=z
		Else
			ver\ox=x-(ver\cx-ver\ox)
			ver\oy=y-(ver\cy-ver\oy)
			ver\oz=z-(ver\cz-ver\oz)
		EndIf
		ver\cx=x+(ver\cx-EntityX(ent\mesh,1))
		ver\cy=y+(ver\cy-EntityY(ent\mesh,1))
		ver\cz=z+(ver\cz-EntityZ(ent\mesh,1))
		PositionEntity ver\pivot,ver\cx,ver\cy,ver\cz
		ResetEntity ver\pivot
	Next
	PositionEntity ent\mesh,x,y,z,1
End Function

Function IntegrateVP(itns%)
	Local dx#,dy#,dz#,length#,diff#

	For vb.vpe=Each vpe
	If vb\state		;Proceed if vb is activated
	For v=1 To vb\points
		ver.verlet=vb\p[v]
		drag#=airres
		If ver\collided Then drag=colres
		
		xvel#=(ver\cx-ver\ox)*drag
		If Abs(xvel)&lt;0.002 Then xvel=0	;Remove tiny errors that cause things to spin without forces...
		ver\ox=ver\cx
		ver\cx=ver\cx+xvel

		yvel#=(ver\cy-ver\oy)*drag
		If Abs(yvel)&lt;0.002 Then yvel=0
		ver\oy=ver\cy
		ver\cy=ver\cy+yvel-gravity
		
		zvel#=(ver\cz-ver\oz)*drag
		If Abs(zvel)&lt;0.002 Then zvel=0	;...which actually speeds up simulation
		ver\oz=ver\cz
		ver\cz=ver\cz+zvel
		
		;Collisions
		ver\collided=False
		For vt.vpe=Each vpe
			If vt&lt;&gt;vb And vt\state		;Check that vt is also activated
				For v2=1 To vt\points
					vet.verlet=vt\p[v2]
					dx#=ver\cx-vet\cx
					dy#=ver\cy-vet\cy
					dz#=ver\cz-vet\cz
					dd#=Sqr(dx*dx+dy*dy+dz*dz)
					tr#=ver\radius+vet\radius
					If dd&lt;tr	;If the verlets are nearer together than their combined radii
						ddt#=dd-tr
						ddx#=ddt*(dx/dd)
						ddy#=ddt*(dy/dd)
						ddz#=ddt*(dz/dd)
						
						ver\cx=ver\cx-ddx
						ver\cy=ver\cy-ddy
						ver\cz=ver\cz-ddz
						
						vet\cx=vet\cx+ddx
						vet\cy=vet\cy+ddy
						vet\cz=vet\cz+ddz
						
						ver\collided=True
					EndIf
				Next
			EndIf
		Next
		
		;Screen boundaries
		If ver\cx&lt;-100 Then ver\cx=-100
	;	If ver\cy&lt;ver\radius Then ver\cy=ver\radius
		If ver\cz&lt;-75 Then ver\cz=-75
		If ver\cx&gt;100 Then ver\cx=100
		If ver\cy&gt;100 Then ver\cy=100
		If ver\cz&gt;125 Then ver\cz=125
	Next
	EndIf	;State
	Next	;vpe

	For vb.vpe=Each vpe
	If vb\state		;Proceed if vb is activated
	For loop = 1 To itns	;iterations
	For link=1 To vb\links
		c.vlink=vb\l[link]
		dx=c\p2\cx-c\p1\cx
		dy=c\p2\cy-c\p1\cy
		dz=c\p2\cz-c\p1\cz
		
		length=Sqr(dx*dx+dy*dy+dz*dz)	;distance between p1 and p2

		If length&gt;0
			If c\parent=Null
				diff=0
				c\offset=length
				If length&lt;c\minlength Then diff=(length-c\minlength)/length:c\offset=c\minlength	;vector length minus vlink length
				If length&gt;c\maxlength Then diff=(length-c\maxlength)/length:c\offset=c\maxlength
			Else
				diff=(length-c\parent\offset)/length	;Snap to length of parent
			EndIf
		EndIf

		dx=dx/2:dy=dy/2:dz=dz/2	;find the midpoint

		c\p1\cx=c\p1\cx+diff*dx
		c\p1\cy=c\p1\cy+diff*dy
		c\p1\cz=c\p1\cz+diff*dz

		c\p2\cx=c\p2\cx-diff*dx
		c\p2\cy=c\p2\cy-diff*dy
		c\p2\cz=c\p2\cz-diff*dz
	Next	;VLinks
	Next	;Iterations
	EndIf	;State
	Next	;vpe
	
	For ver.verlet=Each verlet
		PositionEntity ver\pivot,ver\cx,ver\cy,ver\cz
	Next
End Function

Function UpdateVP()
	For ver.verlet=Each verlet
		If CountCollisions(ver\pivot)
			ver\cx=EntityX(ver\pivot)
			ver\cy=EntityY(ver\pivot)
			ver\cz=EntityZ(ver\pivot)
			ver\collided=True
		EndIf
	Next
;	For c.vlink = Each vlink
;		PositionEntity c\mesh,c\p1\cx,c\p1\cy,c\p1\cz,1
;		PointEntity c\mesh,c\p2\pivot
;	Next
	For body.vpe=Each vpe
		If body\state
			If body\ptype=1
				If body\mesh&lt;&gt;0 Then PositionEntity body\mesh,body\p[1]\cx,body\p[1]\cy,body\p[1]\cz
			EndIf
			If body\ptype=2
				mx#=(body\p[3]\cx+body\p[5]\cx)/2
				my#=(body\p[3]\cy+body\p[5]\cy)/2
				mz#=(body\p[3]\cz+body\p[5]\cz)/2
				PositionEntity body\mesh,mx,my,mz	
				AlignToVector body\mesh,(body\p[1]\cx+body\p[3]\cx)/2-mx,(body\p[1]\cy+body\p[3]\cy)/2-my,(body\p[1]\cz+body\p[3]\cz)/2-mz,2,1
				AlignToVector body\mesh,(body\p[4]\cx+body\p[5]\cx)/2-mx,(body\p[4]\cy+body\p[5]\cy)/2-my,(body\p[4]\cz+body\p[5]\cz)/2-mz,3,1
			EndIf
		EndIf
	Next
End Function

</textarea><br><br>(You'll commonly find these called "Verlet" engines although that actually describes the integration model, not point-and-spring.) Such a system <i>can</i> be a good fit for a sufficiently low-demand game, and (last time I checked) was behind the physics in Stevie's <a href="/posts.php?topic=86093" target="_blank">Polymaniacs</a>, as well as the older Hitman games - on the other hand it can also look awful, so...<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1053372"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Xaron</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I use the Tokamak Wrapper. Even though it's not developed any further it's rocksolid and very easy to use with Blitz3D. <br><br></td></tr></table><br>
<a name="1053450"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kenshin Yurihara</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was going to use something close to what, Yasha said and make my own, but I'm not looking for anything messy. On the other hand I was thinking about using JV-ODE..we'll have to see how things go.<br><br>Thanks guys. <br><br></td></tr></table><br>
<a name="1053479"></a>

<a name="1053487"></a>

<a name="1053488"></a>

<a name="1053492"></a>

<a name="1053493"></a>

<a name="1053501"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kenshin Yurihara</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmm, just removing this post thats all.<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1053852"></a>

<a name="1053895"></a>

<a name="1053897"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kenshin Yurihara</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay so back on the subject of "physics". This "Verlet" Engine idea...what is the "concept" of it?<br><br>From looking at a tutorial by "Nate the Great" to me it looks like the "verlet" engine idea works like so: Assign Verlets like "points" on the model then "constrains" pull/move/push other verlets depending on XYZ<br>because they are all linked together? &lt;-probably a little hard to understand that had to summerize it all up atm.<br><br>A better example..For a "Ragdoll" I'd need to create a "point" on the Hands, Forearm(lower Arm from hand to elbow), Elbow, Brachium(shoulder to elbow), thigh(hip to knee) ,Leg(knee to foot), Foot, Neck, Head.<br>Correct?<br><br><font class="tiny">Last edited 2010</font><br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1053969"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> There are two indirectly related concepts here (the code snippet above contains examples of both):<br><br>1) Point/Constraint. Your object is primarily represented by a group of individual points or particles. Particles are held in place by links or constraints that limit their movement with regard to other particles. An iterative process of simulating each particle's movement, then correcting based on the constraints, gives the illusion of movement for a complete rigid body as the particles move together.<br><br>2) Verlet integration. Note: many people refer to the points in the above as "verlets", because it's catchy, but try not to let this confuse you. Loup Verlet is a mathematician, and Verlet integration actually refers to the method of calculating the position of the particles. It's stable and time-reversible and most importantly, if you filter out all of the unnecessary stuff (from our point of view), very simple and very fast. It works by calculating the next position of a particle based on the difference between the current position and the previous position.<br><br>The two do work well together, although they don't have to. There are however three collossal holes in "Verlet physics" for Blitz3D:<br><br>1) The system makes no provision for collision detection, and therefore no way for objects to interact with each other. This isn't a total show-stopper: see the cubes demo above for a way around it, one which has worked well for Polymaniacs. Essentially it just applies spherical collision detection to the particles, of suitable sizes so that the spheres overlap and fill all the space of the object. This looks OK for highly-dynamic games like Polymaniacs, but doesn't let you do things like stack cubes as there is no support for flat surfaces. The ideal way to deal with this is to have a third class of object, the collision body, and pin these to the constraints, but I don't know if anyone has done this (you can't really do it in standard Blitz3D as you need nonspherical collisions).<br><br>2) The system makes no provision for joints and joint constraints; a flexible joint is one where there aren't constraint pins forcing something to be rigid, in which case it can flail around wildly, or have an unrestricted linear movement if you pinned it with two constraints. This makes ragdolls very difficult to design as there's no inherent way to stop limbs flying all over the place. I did some poking about with "sliding constraints" that had some give, controlling the freedom of hinge joints, but the results still weren't very good. The reason why there is no simple solution to this problem is an extension of problem 3:<br><br>3) The points are just points. They have position but not orientation. This may sound ridiculous when you first see the demo and the solid shapes are clearly falling over one another, but it just means that the angle of connection between points has changed. In turn, this makes it more difficult to calculate the orientation of the mesh that is attached to the physics object; it's not too hard with a couple of AlignToVectors for cubes and similar rigid shapes, but ragdolls are a whole different story as there simply aren't enough points to get an accurate orientation for each bone (e.g. a forearm is, simplistically, the pin between wrist and elbow points. There's no third point to use to get its roll value). Solving this algorithmically is possible but you'd end up making an inefficient duplicate of most of ODE. It's also possible to simply throw more points at a problem, to give more reference points, but that obviously slows the simulation down. This also means most structures will need to be known by the engine, so no dynamic meshes created at runtime.<br><br>Anyway, the method isn't impossible to use - it worked for Hitman, (see here: <a href="http://www.gamasutra.com/resource_guide/20030121/jacobson_01.shtml" target="_blank">http://www.gamasutra.com/resource_guide/20030121/jacobson_01.shtml</a> ), but most of the solutions to the above presented in that article aren't feasible for B3D, and aren't actually that efficient either.<br><br><br>Basically... Verlet physics is good for vehicles, great for liquids... totally inappropriate for ragdolls, I would say. Really it depends what your goals are: do you want to learn about physics engine design, or just implement it for an existing project?<br><br>(Incidentally, if you were put off miniODE by the lack of ragdoll examples... just ask, I'll take the excuse to add them.) <br><br></td></tr></table><br>
<a name="1053977"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> there is a ragdoll example using miniODE inside of my awc, take a look if you need it, follow my signature<br><br>(yasha, great post)<br><br>Juan <br><br></td></tr></table><br>
<a name="1054045"></a>

<a name="1054046"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kenshin Yurihara</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> My "Goal" is Ragdolls and primitive meshes like Cubes, Cylinders, and etc.<br><br>Nothing about, MiniODE, threw me off, but you posted "no collision detection information" and if you mean like I cannot tell if the bullet collides with the "arm" then that might be a problem. Then again it might not be if I stepped down and went a little simpler.<br><br>I'm messing around currently and if I get something going I'll use that.<br><br>If I don't I'll consider JV-ODE or Tokamak Physics(Donno if I spelt that right)<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1054052"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> posted "no collision detection information" <br></div><br><br>Ah, that. Yeah... at the time I was already covering collision detection in my own project with ColDet and didn't think anything of the fact that 1) the functionality is right there in ODE and 2) building a full second set of mesh data for a full second library is incredibly wasteful. Rather selfish of me, sorry.<br><br>I keep meaning to fix this - I'll get on that and add some. I'm only thinking very basic stuff - check whether two geoms collided, get the collision normals and any other information like forces (can't remember now what's included in an ODE collision), and the ability to raypick. Anything else would be the province of a more advanced library like JV-ODE.<br><br>Charrua - thanks for beating me to it with the example! <br><br></td></tr></table><br>
<a name="1054053"></a>

<a name="1054070"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kenshin Yurihara</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Not selfish at all, we all do what we have to do to get by. In all honesty its selfish of me asking you to do it when I probably should myself.<br><br>Honestly, I see why ODE is used quite alot now.<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1054067"></a>

<a name="1054068"></a>

<a name="1054069"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kenshin Yurihara</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> -Another Post I posted without thinking God I can be a idiot.<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
