<!DOCTYPE html><html lang="en" ><head ><title >Perlin Clouds</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Perlin Clouds</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Perlin Clouds</a><br><br>
<a name="1023415"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have made a bit of code for Perlin noise generated Clouds, but I want it to be faster.   I'm not sure it can be done but here is the code to help.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,32,2
SetBuffer BackBuffer()
size = 128

Global ImgType = 0	;Change this to 1 for a textured sphere

;the image or texture must have the same height as width.
If ImgType = 1 Then img = CreateTexture(size,size)
If ImgType = 0 Then img = CreateImage(size,size)

Dim random#(1,1,1)
sc = 10
If ImgType = 1 Then sphere = CreateSphere(32):ScaleEntity sphere ,sc,sc,sc
If ImgType = 1 Then EntityTexture sphere,img


camera = CreateCamera()
PositionEntity camera,0,0,-20

AmbientLight 255,255,255

While Not KeyHit(1)
	Cls
	m1 = MilliSecs()
	Perlin(img,ImgType,16,32,3,rs#,0,.5,.5)
	Smoothen(img,ImgType,2,0)
	m2 = MilliSecs()
	
	RenderWorld
	
	Text size,0,"Millisecs to render: " + (m2-m1)
	Text 0,size,"Random seed: " + rs
	rs# = rs# + 1
	If ImgType = 0 Then DrawImage img,0,0
	Flip
	
Wend
End

Function Perlin(Image,ImageType,MinOctaves,MaxOctaves,Multiplier#,RandomSeed%,InterpolateType%=0,Tension#=0,Bias#=0) 
	;Image		: the handle of the image to use
	;ImageType	: 0 for Image, 1 for Texture
	;MinOctaves : the starting octaves
	;MaxOctaves : the Maximum octaves
	;Multiplier : multiplies the output
	;RandomSeed : seed the random generator
	;InterpolateType	: What kind of interpolation should be used
						; 0 = linear: 1 = cosine : 2 = hermite
		;For use with hermite interpolation!
	;Tension#	: 1 is high, 0 normal, -1 is low (Random between the tension you set)
	;Bias#		: 0 is =, + is towards first segment, - towards the other (Random between the bias you set)
	
	If ImageType = 0 Then size = ImageWidth(Image)
	If ImageType = 1 Then size = TextureWidth(Image)
	
	Dim random#(size,size,MaxOctaves)
	
	If ImageType = 0 Then LockBuffer ImageBuffer(Image)
	If ImageType = 1 Then LockBuffer TextureBuffer(Image)
	
	For oc# = MinOctaves To MaxOctaves
		For i# = 0 To size - 1
			For j# = 0 To size - 1
				x1# = Float i/ Float oc
				y1# = Float j/ Float oc
				tc = oc
				Select True
					Case InterpolateType = 2
						n0# = Noise#(x1-1,y1  ,RandomSeed,size,tc)*(1.0/oc)
						n1# = Noise#(x1  ,y1  ,RandomSeed,size,tc)*(1.0/oc)
						n2# = Noise#(x1+1,y1  ,RandomSeed,size,tc)*(1.0/oc)
						n3# = Noise#(x1+2,y1  ,RandomSeed,size,tc)*(1.0/oc)
						n4# = Noise#(x1+3,y1  ,RandomSeed,size,tc)*(1.0/oc)
						n5# = Noise#(x1  ,y1+1,RandomSeed,size,tc)*(1.0/oc)
						n6# = Noise#(x1  ,y1+2,RandomSeed,size,tc)*(1.0/oc)
						n7# = Noise#(x1  ,y1+3,RandomSeed,size,tc)*(1.0/oc)
						n8# = Noise#(x1+1,y1+1,RandomSeed,size,tc)*(1.0/oc)
						n9# = Noise#(x1+2,y1+2,RandomSeed,size,tc)*(1.0/oc)
						n10# = Noise#(x1+3,y1+3,RandomSeed,size,tc)*(1.0/oc)
						v1# = HInterpolate#(n1,n2,n3,n4,x1#-Int(x1#),Rnd(-Tension,Tension),Rnd(- Bias,Bias))
						v2# = HInterpolate#(n1,n5,n6,n7,x1#-Int(x1#),Rnd(-Tension,Tension),Rnd(- Bias,Bias))
						v3# = HInterpolate#(n1,n8,n9,n10,x1#-Int(x1#),Rnd(-Tension,Tension),Rnd(- Bias,Bias))
						out# = HInterpolate#(n1,v1,v2,v3,y1#-Int(y1#),Rnd(-Tension,Tension),Rnd(- Bias,Bias))
					Case InterpolateType = 1
						n1# = Noise#(x1,y1,RandomSeed,size,tc)*(1.0/oc)
						n2# = Noise#(x1+1,y1,RandomSeed,size,tc)*(1.0/oc)
						n3# = Noise#(x1,y1+1,RandomSeed,size,tc)*(1.0/oc)
						n4# = Noise#(x1+1,y1+1,RandomSeed,size,tc)*(1.0/oc)
						v1# = CoInterpolate#(n1,n2,x1#-Int(x1#))
						v2# = CoInterpolate#(n3,n4,x1#-Int(x1#))
						out# = CoInterpolate#(v1,v2,y1#-Int(y1))
					Default
						n1# = Noise#(x1,y1,RandomSeed,size,tc)*(1.0/oc)
						n2# = Noise#(x1+1,y1,RandomSeed,size,tc)*(1.0/oc)
						n3# = Noise#(x1,y1+1,RandomSeed,size,tc)*(1.0/oc)
						n4# = Noise#(x1+1,y1+1,RandomSeed,size,tc)*(1.0/oc)
						v1# = LInterpolate#(n1,n2,x1#-Int(x1#))
						v2# = LInterpolate#(n3,n4,x1#-Int(x1#))
						out# = LInterpolate#(v1,v2,y1#-Int(y1))
				End Select
				random#(i,j,oc) = out#*Multiplier#
				If oc = MaxOctaves Then
					t# = 0.0
					For oct = MinOctaves To MaxOctaves
						t# = t# + random(i,j,oct)
					Next
					t = t *(255/1)
					If t &lt; 0 Then t# = 0
					If t &gt; 255 Then t = 255
					
					If ImageType = 0 Then WritePixelFast i,j,Get_ARGB_From(t,t,t),ImageBuffer(Image)
					If ImageType = 1 Then WritePixelFast i,j,Get_ARGB_From(t,t,t),TextureBuffer(Image)
					
				EndIf
			Next
		Next
	Next
	
	If ImageType = 0 Then UnlockBuffer ImageBuffer(Image)
	If ImageType = 1 Then UnlockBuffer TextureBuffer(Image)
	
End Function

;/*
;   Tension: 1 is high, 0 normal, -1 is low
;   Bias: 0 is even,
;         positive is towards first segment,
;         negative towards the other
;*/
Function HInterpolate#(y0#,y1#,y2#,y3#,mu#,tension#=0,bias#=0)

	Local m0#,m1#,mu2#,mu3#;
	Local a0#,a1#,a2#,a3#;

	mu2 = mu * mu;
	mu3 = mu2 * mu;
	m0  = (y1-y0)*(1+bias)*(1-tension)/2;
	m0  = m0+(y2-y1)*(1-bias)*(1-tension)/2;
	m1  = (y2-y1)*(1+bias)*(1-tension)/2;
	m1  = m1+(y3-y2)*(1-bias)*(1-tension)/2;
	a0  =  2*mu3 - 3*mu2 + 1;
	a1  =    mu3 - 2*mu2 + mu;
	a2  =    mu3 -   mu2;
	a3  = -2*mu3 + 3*mu2;

	Return(a0*y1+a1*m0+a2*m1+a3*y2);
	
End Function 

Function LInterpolate#(a#, b#, x#)
	Return  a*(1-x) + b*x
End Function

Function CoInterpolate#(a#, b#, x#)
	ft# = x * 3.1415927
	f# = (1 - Cos(ft)) * .5
	Return  a*(1-f) + b*f
End Function

Function Noise#( x%, y%,RandomSeed%=1,ImageSize%=1,ImageIndex%=0)
    n = x + y * 57 + ((ImageSize*ImageSize)*ImageIndex) + RandomSeed;
    n = (n Shl (13^n));
    ES# = ( 1.0 - ( (n * (n * n * 15731 + 789221) + 1376312589) And $7fffffff) / 1073741824.0); 
    Return es#   
  End Function

;========================================
;		     Color Functions
;#Region  ;========================================

Function Get_ARGB_From(r,g,b):		Return b Or(g Shl 8)Or(r Shl 16):	End Function

Function Get_ALPHA_From(ARGB):		Return ARGB Shr 32 And %11111111 :	End Function

Function Get_RED_From(ARGB):	    Return ARGB Shr 16 And %11111111 :	End Function

Function Get_GREEN_From(ARGB):		Return ARGB Shr 8 And %11111111 :	End Function

Function Get_BLUE_From(ARGB):		Return ARGB And %11111111 :			End Function

;#End Region

Function Smoothen(Image,ImageType,Passes,roughness)
	
	If ImageType = 0 Then IB = ImageBuffer(Image):LockBuffer IB:IW = ImageWidth(Image)-1
	If ImageType = 1 Then TB = TextureBuffer(Image):LockBuffer TB:IW = TextureWidth(Image)-1
	For p = 1 To Passes
		For x = 0 To IW
			For y = 0 To IW
				tx = x:ty = y
				x = x - 1
				If x &lt; 0 Then x = IW
				If ImageType = 0 Then argb = ReadPixelFast (x,y,IB)
				If ImageType = 1 Then argb = ReadPixelFast (x,y,TB)
				avg# = avg# + Get_RED_From(argb)
				x = tx:y = ty
				y = y - 1
				If y &lt; 0 Then y = iw
				If ImageType = 0 Then argb = ReadPixelFast (x,y,IB)
				If ImageType = 1 Then argb = ReadPixelFast (x,y,TB)
				avg# = avg#+Get_RED_From(argb)
				x = tx:y = ty
				x = x + 1
				If x &gt; iw Then x = 0
				If ImageType = 0 Then argb = ReadPixelFast (x,y,IB)
				If ImageType = 1 Then argb = ReadPixelFast (x,y,TB)
				avg# = avg#+Get_RED_From(argb)
				x = tx:y = ty
				y = y + 1
				If y &gt; iw Then y = 0
				If ImageType = 0 Then argb = ReadPixelFast (x,y,IB)
				If ImageType = 1 Then argb = ReadPixelFast (x,y,TB)
				avg# = avg#+Get_RED_From(argb)
				x = tx:y = ty
				If ImageType = 0 Then argb = ReadPixelFast (x,y,IB)
				If ImageType = 1 Then argb = ReadPixelFast (x,y,TB)
				avg# = avg#+Get_RED_From(argb)
				avg# = (avg#/5) + Rand(-Roughness,Roughness)
				If avg# &lt; 0 Then avg# = 0
				If avg# &gt; 255 Then avg# = 255
				If ImageType = 0 Then WritePixelFast x,y,Get_ARGB_From(avg,avg,avg),IB
				If ImageType = 1 Then WritePixelFast x,y,Get_ARGB_From(avg,avg,255),TB
				avg# = 0
			Next
		Next
	Next
	If ImageType = 0 Then UnlockBuffer IB
	If ImageType = 1 Then UnlockBuffer TB
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1023466"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Only thing I can suggest is reducing the function calls within the 'Perlin' function - on a large bitmap maybe slower than a week in the jail for what you're doing per pixel.<br><br>Do some research - there a much better ways of doing this!! <br><br></td></tr></table><br>
<a name="1023505"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DareDevil</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> the system is good ! :)<br>is slow :|<br><br>Good Work <br><br></td></tr></table><br>
<a name="1023524"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks DareDevil.  I'm trying to figure out how to speed it up, and how to apply it to a Geo-sphere seamlessly.   Having problems doing both. <br><br></td></tr></table><br>
<a name="1023556"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Take one half, of the picture. Flip it, then apply it to the opposite side with a transparent fading-transition-gradient. Do that for each side and maybe it will look seamless. It's just an idea from the top of my head from how I think Paint Shop Pro's seamless tiling system works. Sorry if it's confusing. <br><br></td></tr></table><br>
<a name="1023589"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> trying something new.  so to make the clouds slide i create a bank to store all the random data, then i slide the bank so that a new column of<br>random data can be entered.  Then write it to the screen. but I must not be using copyrect the right way. cause the image isn't moving when copied.    The problem is in the cycleperlin function.  <br><br>edit.   Never mind.  copyrect does not work in a locked buffer.<br>Oh crap, I got to redo the whole thing, i Just realized I'm not scaling each octave correctly which changes everything.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,32,2
SetBuffer BackBuffer()
size = 256

Global ImgType = 0	;Change this to 1 for a textured sphere

;the image or texture must have the same height as width.
If ImgType = 1 Then img = CreateTexture(size,size)
If ImgType = 0 Then img = CreateImage(size,size)

Dim RandomBank(1)

sc = 10
If ImgType = 1 Then sphere = CreateSphere(32):ScaleEntity sphere ,sc,sc,sc
If ImgType = 1 Then EntityTexture sphere,img


camera = CreateCamera()
PositionEntity camera,0,0,-20

AmbientLight 255,255,255

MaxOctaves = 20
MinOctaves = 5
Multiplier# = 100
InitPerlin(img,ImgType,MinOctaves,MaxOctaves,Multiplier#)

While Not KeyHit(1)
	Cls
	m1 = MilliSecs()
	CyclePerlin(img,ImgType,MinOctaves,MaxOctaves,Multiplier#,1)
	;Smoothen(img,ImgType,2,0)
	m2 = MilliSecs()
	
	RenderWorld
	
	Text size,0,"Millisecs to render: " + (m2-m1)
	If ImgType = 0 Then DrawImage img,0,0
	Flip
	
Wend
End

Function InitPerlin(Image,ImageType,MinOctaves,MaxOctaves,Multiplier#)
	If ImageType = 0 Then size = ImageWidth(Image)
	If ImageType = 1 Then size = TextureWidth(Image)
	
	If ImageType = 0 Then LockBuffer ImageBuffer(Image)
	If ImageType = 1 Then LockBuffer TextureBuffer(Image)
	
	Dim RandomBank ( ( MaxOctaves - MinOctaves ) )
	
	For i = MinOctaves To MaxOctaves-1
		ti = (i - MinOctaves)+1
		RandomBank( ti ) = CreateBank( (((size)*(size))*4)/(MaxOctaves-i) )
		FillBank(RandomBank(ti),-1,1,MaxOctaves-i)
	Next
	
	For x = 0 To size-1 
		For y = 0 To size-1
			For oc = MinOctaves To MaxOctaves-1
				z = MaxOctaves - (oc- MinOctaves)
				o = (oc - MinOctaves) + 1
				bs = BankSize(RandomBank(o))
				x1 = Int(x/z)
				y1 = Int(y/z)
				x2 = ( Int(x/z) + 1 )
				y2 = ( Int(y/z) + 1 )
				If x2 &gt; (size-1)/z Then x2 = 0
				If y2 &gt; (size-1)/z Then y2 = 0 
				offset1 = ( y1 + ( x1 * (size-1) ) ) * 4
				offset2 = ( y2 + ( x1 * (size-1) ) ) * 4
				offset3 = ( y1 + ( x2 * (size-1) ) ) * 4
				offset4 = ( y2 + ( x2 * (size-1) ) ) * 4
				n1# = PeekFloat(RandomBank(o),offset1)
				n2# = PeekFloat(RandomBank(o),offset2)
				n3# = PeekFloat(RandomBank(o),offset3)
				n4# = PeekFloat(RandomBank(o),offset4)
				v1# = CoInterpolate#(n1#,n2#,(x/z)-Int(x/z))
				v2# = CoInterpolate#(n3#,n4#,(x/z)-Int(x/z))
				h# = h# + CoInterpolate#(v1#,v2#,(y/z)-Int(y/z))*Multiplier#
			Next
			If h &gt; 255 Then h = 255
			If h &lt; 0 Then h = 0
			argb = Get_ARGB_From(h,h,h)
			WritePixelFast x,y,argb,ImageBuffer(Image)
			h# = 0.0
		Next
	Next
	
	If ImageType = 0 Then UnlockBuffer ImageBuffer(Image)
	If ImageType = 1 Then UnlockBuffer TextureBuffer(Image)
	
End Function

Function CyclePerlin(Image,ImageType,MinOctaves,MaxOctaves,Multiplier#,Offset)
	If ImageType = 0 Then LockBuffer ImageBuffer(Image):size = ImageWidth(Image)
	If ImageType = 1 Then LockBuffer TextureBuffer(Image):size = TextureWidth(Image)
	
	SlideRandomBank(size,MinOctaves,MaxOctaves)
	
	If ImageType = 0 Then CopyRect(offset,0,ImageWidth(Image)- offset,ImageHeight(Image),0,0,ImageBuffer(Image),ImageBuffer(Image))
	If ImageType = 1 Then CopyRect(offset,0,ImageWidth(Image)- offset,ImageHeight(Image),0,0,TextureBuffer(Image),TextureBuffer(Image))
	x = size-1
	For y = 0 To size-1
		For oc = MinOctaves To MaxOctaves-1
			z = MaxOctaves - (oc- MinOctaves)
			o = (oc - MinOctaves) + 1
			bs = BankSize(RandomBank(o))
			x1 = Int(x/z)
			y1 = Int(y/z)
			x2 = ( Int(x/z) + 1 )
			y2 = ( Int(y/z) + 1 )
			If x2 &gt; (size-1)/z Then x2 = 0
			If y2 &gt; (size-1)/z Then y2 = 0 
			offset1 = ( y1 + ( x1 * (size-1) ) ) * 4
			offset2 = ( y2 + ( x1 * (size-1) ) ) * 4
			offset3 = ( y1 + ( x2 * (size-1) ) ) * 4
			offset4 = ( y2 + ( x2 * (size-1) ) ) * 4
			n1# = PeekFloat(RandomBank(o),offset1)
			n2# = PeekFloat(RandomBank(o),offset2)
			n3# = PeekFloat(RandomBank(o),offset3)
			n4# = PeekFloat(RandomBank(o),offset4)
			v1# = CoInterpolate#(n1#,n2#,(x/z)-Int(x/z))
			v2# = CoInterpolate#(n3#,n4#,(x/z)-Int(x/z))
			h# = h# + CoInterpolate#(v1#,v2#,(y/z)-Int(y/z))*Multiplier#
		Next
		If h &gt; 255 Then h = 255
		If h &lt; 0 Then h = 0
		argb = Get_ARGB_From(h,h,h)
		WritePixelFast x,y,argb,ImageBuffer(Image)
		h# = 0.0
	Next
	If ImageType = 0 Then UnlockBuffer ImageBuffer(Image)
	If ImageType = 1 Then UnlockBuffer TextureBuffer(Image)
End Function

;#Region Interpolation Methods
Function CoInterpolate#(a#, b#, x#)
	ft# = x * 3.1415927
	f# = (1 - Cos(ft)) * .5
	Return  a*(1-f) + b*f
End Function
;#End Region

;========================================
;		     Color Functions
;#Region  ;========================================

Function Get_ARGB_From(r,g,b):		Return b Or(g Shl 8)Or(r Shl 16):	End Function

Function Get_RED_From(ARGB):	    Return ARGB Shr 16 And %11111111 :	End Function

;#End Region

Function Smoothen(Image,ImageType,Passes,roughness)
	
	If ImageType = 0 Then IB = ImageBuffer(Image):LockBuffer IB:IW = ImageWidth(Image)-1
	If ImageType = 1 Then TB = TextureBuffer(Image):LockBuffer TB:IW = TextureWidth(Image)-1
	For p = 1 To Passes
		For x = 0 To IW
			For y = 0 To IW
				tx = x:ty = y
				x = x - 1
				If x &lt; 0 Then x = IW
				If ImageType = 0 Then argb = ReadPixelFast (x,y,IB)
				If ImageType = 1 Then argb = ReadPixelFast (x,y,TB)
				avg# = avg# + Get_RED_From(argb)
				x = tx:y = ty
				y = y - 1
				If y &lt; 0 Then y = iw
				If ImageType = 0 Then argb = ReadPixelFast (x,y,IB)
				If ImageType = 1 Then argb = ReadPixelFast (x,y,TB)
				avg# = avg#+Get_RED_From(argb)
				x = tx:y = ty
				x = x + 1
				If x &gt; iw Then x = 0
				If ImageType = 0 Then argb = ReadPixelFast (x,y,IB)
				If ImageType = 1 Then argb = ReadPixelFast (x,y,TB)
				avg# = avg#+Get_RED_From(argb)
				x = tx:y = ty
				y = y + 1
				If y &gt; iw Then y = 0
				If ImageType = 0 Then argb = ReadPixelFast (x,y,IB)
				If ImageType = 1 Then argb = ReadPixelFast (x,y,TB)
				avg# = avg#+Get_RED_From(argb)
				x = tx:y = ty
				If ImageType = 0 Then argb = ReadPixelFast (x,y,IB)
				If ImageType = 1 Then argb = ReadPixelFast (x,y,TB)
				avg# = avg#+Get_RED_From(argb)
				avg# = (avg#/5) + Rand(-Roughness,Roughness)
				If avg# &lt; 0 Then avg# = 0
				If avg# &gt; 255 Then avg# = 255
				If ImageType = 0 Then WritePixelFast x,y,Get_ARGB_From(avg,avg,avg),IB
				If ImageType = 1 Then WritePixelFast x,y,Get_ARGB_From(avg,avg,255),TB
				avg# = 0
			Next
		Next
	Next
	If ImageType = 0 Then UnlockBuffer IB
	If ImageType = 1 Then UnlockBuffer TB
End Function

Function FillBank(Bank,min# = 0, max# = 1,oc% = 1)
	bs = BankSize(Bank)
	For i = 0 To (bs/4)-1
		PokeFloat Bank,i*4,(Rnd(min,max)/oc)
	Next
End Function

Function SlideRandomBank(size,MinOctaves,MaxOctaves)
	For i = MinOctaves To MaxOctaves-1
		ti = (i - MinOctaves)+1
		bs = BankSize(RandomBank(ti))
		CopyBank (RandomBank(ti),(size/i)*4,RandomBank(ti),0,bs-(size/i)*4)
		For j = (bs/4)-size/i To (bs/4)-1
			PokeFloat(RandomBank(ti),j*4,(Rnd(min,max)/i))
		Next
	Next
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1023672"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ian Thompson</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> What Perlin article is this based on, if any, it looks quite interesting? <br><br></td></tr></table><br>
<a name="1023709"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm using <a href="http://freespace.virgin.net/hugo.elias/models/m_perlin.htm" target="_blank">http://freespace.virgin.net/hugo.elias/models/m_perlin.htm</a> as a loose reference. <br><br></td></tr></table><br>
<a name="1023766"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well I got most of it figured out.  Still some graphical glitches, When shiftMax# + MinOctaves = MaxOctaves  .  But overall looks quite good. <br>If the Image is blank or the sphere is completely blue then re-run the code.  not sure why it blacks out some times and not others,  something to do with the random numbers it gets from SeedRnd MilliSecs() command.   The  Shift# variable needs to be in a clock format so that the clouds move smoothly, but I'm not sure how that is done.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,32,2
SetBuffer BackBuffer()
SeedRnd MilliSecs()

size = 128

Global ImgType = 1	;Change this to 1 for a textured sphere

;the image or texture must have the same height as width.
If ImgType = 1 Then img = CreateTexture(size,size)
If ImgType = 0 Then img = CreateImage(size,size)

Dim RandomBank(1)

sc = 10
If ImgType = 1 Then sphere = CreateSphere(32):ScaleEntity sphere ,sc,sc,sc
If ImgType = 1 Then EntityTexture sphere,img


camera = CreateCamera()
PositionEntity camera,0,0,-20

AmbientLight 255,255,255

MaxOctaves = 4
MinOctaves = 0
Multiplier# = 255
RandomMin# = -1
RandomMax# = 1
MaxShift# = (MaxOctaves)
InitPerlin(img,ImgType,MinOctaves,MaxOctaves,Multiplier#,RandomMin#,RandomMax#)

While Not KeyHit(1)
	Cls
	m1 = MilliSecs()
	CyclePerlin(img,ImgType,MinOctaves,MaxOctaves,Multiplier#,Floor(shift#),RandomMin#,RandomMax#)
	m2 = MilliSecs()
	shift# = (shift# + 1/(2^(Maxoctaves-shift#))) Mod MaxShift#
	RenderWorld
	
	Text size,0,"Millisecs to render: " + (m2-m1)
	Text size,13,"Shift: " + shift#
	If ImgType = 0 Then DrawImage img,0,0
	Flip
	
Wend
FreePerlin(Img,ImgType,MinOctaves,MaxOctaves)
End

Function InitPerlin(Image,ImageType,MinOctaves,MaxOctaves,Multiplier#,Low#,High#)

	If ImageType = 0 Then size = ImageWidth(Image)
	If ImageType = 1 Then size = TextureWidth(Image)
	
	If ImageType = 0 Then LockBuffer ImageBuffer(Image)
	If ImageType = 1 Then LockBuffer TextureBuffer(Image)
	
	Dim RandomBank ( ( MaxOctaves - MinOctaves ) )
	
	For i = MinOctaves To MaxOctaves
		ti = (i - MinOctaves)
		OL = 2 ^ i
		BS = ( ( size * size ) / OL ) * 4
		RandomBank( ti ) = CreateBank( BS )
		FillBank( RandomBank(ti) , Low# , High# ,(MaxOctaves - ti) )
	Next

	For x = 0 To size-1 
		For y = 0 To size-1
			For oc = MinOctaves To MaxOctaves
				h# = h# + SmoothHeight#(x,y,oc,MinOctaves,MaxOctaves,Size)*Multiplier#
			Next
			If h &gt; 255 Then h = 255
			If h &lt; 0 Then h = 0
			If ImageType = 0 Then argb = Get_ARGB_From(Floor(h),Floor(h),Floor(h))
			If ImageType = 1 Then argb = Get_ARGB_From(Floor(h),Floor(h),255)
			If ImageType = 0 Then WritePixelFast x,y,argb,ImageBuffer(Image)
			If ImageType = 1 Then WritePixelFast x,y,argb,TextureBuffer(Image)
			h# = 0.0
		Next
	Next
	
	If ImageType = 0 Then UnlockBuffer ImageBuffer(Image)
	If ImageType = 1 Then UnlockBuffer TextureBuffer(Image)
	
End Function

Function CyclePerlin(Image,ImageType,MinOctaves,MaxOctaves,Multiplier#,Shift=0,min#,max#)

	If ImageType = 0 Then LockBuffer ImageBuffer(Image):size = ImageWidth(Image)
	If ImageType = 1 Then LockBuffer TextureBuffer(Image):size = TextureWidth(Image)
	
	SlideRandomBank(size,MinOctaves,MaxOctaves,Shift,min#,max#)
	
	For x = 0 To size-1
		For y = 0 To size-1
			For oc = MinOctaves To MaxOctaves
				h# = h# + SmoothHeight(x,y,oc,MinOctaves,MaxOctaves,Size) *Multiplier#
			Next
			If h &gt; 255 Then h = 255
			If h &lt; 0 Then h = 0
			If ImageType = 0 Then argb = Get_ARGB_From(Floor(h),Floor(h),Floor(h))
			If ImageType = 1 Then argb = Get_ARGB_From(Floor(h),Floor(h),255)
			If ImageType = 0 Then WritePixelFast x,y,argb,ImageBuffer(Image)
			If ImageType = 1 Then WritePixelFast x,y,argb,TextureBuffer(Image)
			h# = 0.0
		Next
	Next
	If ImageType = 0 Then UnlockBuffer ImageBuffer(Image)
	If ImageType = 1 Then UnlockBuffer TextureBuffer(Image)
End Function

;#Region Interpolation Methods

Function LInterpolate#(a#, b#, x#)
	Return  a*(1-x) + b*x
End Function

;#End Region

;========================================
;		     Color Functions
;#Region  ;========================================

Function Get_ARGB_From(r,g,b):		Return b Or(g Shl 8)Or(r Shl 16):	End Function

Function Get_RED_From(ARGB):	    Return ARGB Shr 16 And %11111111 :	End Function

;#End Region

Function FillBank(Bank,min# = 0, max# = 1,oc% = 1)
	bs = BankSize(Bank)
	For i = 0 To (bs/4)-1
		r# = (Rnd(min,max)/(oc+1))
		PokeFloat Bank,i*4,r#
	Next
End Function

Function SlideRandomBank(size,MinOctaves,MaxOctaves,shift,min#,max#)
	If shift+ MinOctaves &gt; MaxOctaves  Then RuntimeError " Shift &gt; MaxOctaves "
	For i = MinOctaves To MinOctaves+shift
		ti = (i - MinOctaves)
		OL = 2^i
		bs = BankSize(RandomBank(ti))
		Source_Offset = (size/(OL))*4
		Count = bs-(size/(OL))*4
		CopyBank (RandomBank(ti),Source_Offset,RandomBank(ti),0,Count)
		Starts  = ((bs/4)-(size/OL))
		Ends	= (bs/4)-1
		For j = Starts To Ends
			r# = (Rnd(min,max)/(MaxOctaves - ti))
			PokeFloat(RandomBank(ti),j*4,r#)
		Next
	Next
End Function

Function SmoothHeight#(x,y,oc,MinOctaves,MaxOctaves,Size)
	z = 2^oc
	o = (oc - MinOctaves)
	bs = BankSize(RandomBank(o))
	
	;Get the coordinates the array bank should be at
	x0 = ( Floor(x/z)- 1 )
	y0 = ( Floor(y/z)- 1 )
	x1 = Floor(x/z)
	y1 = Floor(y/z)
	x2 = ( Floor(x/z)+ 1 )
	y2 = ( Floor(y/z)+ 1 )
	x3 = ( Floor(x/z)+ 2 )
	y3 = ( Floor(y/z)+ 2 )
	
	;limit the Coordinates so that they are wrapping and within range
	If x0 &lt; 0 Then x0 = (size-1)/z
	If y0 &lt; 0 Then y0 = (size-1)/z
	If x2 &gt; (size-1)/z Then x2 = 0
	If y2 &gt; (size-1)/z Then y2 = 0 
	If x3 &gt; ((size-1)/z)+1 Then x3 = 1
	If x3 &gt; ((size-1)/z) Then x3 = 0
	If y3 &gt; ((size-1)/z)+1 Then y3 = 1
	If y3 &gt; ((size-1)/z) Then y3 = 0
	
	;Get the offset within the bank making it like a 2d array
	offset0 = ( y0 + ( x0 * (size-1) ) ) * 4
	offset1 = ( y0 + ( x1 * (size-1) ) ) * 4
	offset2 = ( y0 + ( x2 * (size-1) ) ) * 4
	offset3 = ( y0 + ( x3 * (size-1) ) ) * 4
	offset4 = ( y1 + ( x0 * (size-1) ) ) * 4
	offset5 = ( y1 + ( x1 * (size-1) ) ) * 4
	offset6 = ( y1 + ( x2 * (size-1) ) ) * 4
	offset7 = ( y1 + ( x3 * (size-1) ) ) * 4
	offset8 = ( y2 + ( x0 * (size-1) ) ) * 4
	offset9 = ( y2 + ( x1 * (size-1) ) ) * 4
	offset10 = ( y2 + ( x2 * (size-1) ) ) * 4
	offset11 = ( y2 + ( x3 * (size-1) ) ) * 4
	offset12 = ( y3 + ( x0 * (size-1) ) ) * 4
	offset13 = ( y3 + ( x1 * (size-1) ) ) * 4
	offset14 = ( y3 + ( x2 * (size-1) ) ) * 4
	offset14 = ( y3 + ( x3 * (size-1) ) ) * 4
	
	;get the values at the above offset within the bank
	n0# = PeekFloat(RandomBank(o),offset0)
	n1# = PeekFloat(RandomBank(o),offset1)
	n2# = PeekFloat(RandomBank(o),offset2)
	n3# = PeekFloat(RandomBank(o),offset3)
	n4# = PeekFloat(RandomBank(o),offset4)
	n5# = PeekFloat(RandomBank(o),offset5)
	n6# = PeekFloat(RandomBank(o),offset6)
	n7# = PeekFloat(RandomBank(o),offset7)
	n8# = PeekFloat(RandomBank(o),offset8)
	n9# = PeekFloat(RandomBank(o),offset9)
	n10# = PeekFloat(RandomBank(o),offset10)
	n11# = PeekFloat(RandomBank(o),offset11)
	n12# = PeekFloat(RandomBank(o),offset12)
	n13# = PeekFloat(RandomBank(o),offset13)
	n14# = PeekFloat(RandomBank(o),offset14)
	n15# = PeekFloat(RandomBank(o),offset15)
	
	;Smoothen the values from above
	s1# = ((n0# + n2# + n8# + n10#)/16)+((n1# + n4# + n6# + n9#)/8)+(n5#/4)
	s2# = ((n1# + n3# + n9# + n11#)/16)+((n2# + n5# + n7# + n10#)/8)+(n6#/4)
	s3# = ((n4# + n6# + n12#+ n14#)/16)+((n5# + n8# + n10#+ n13#)/8)+(n9#/4)
	s4# = ((n5# + n7# + n13#+ n15#)/16)+((n6# + n9# + n11#+ n14#)/8)+(n10#/4)
	
	;apply linear interpolation to the smooth values
	v1# = LInterpolate#(s1#,s2#,(Float x/Float z)-Floor(Float x/Float z))
	v2# = LInterpolate#(s3#,s4#,(Float x/Float z)-Floor(Float x/Float z))
	;return the Linear interpolation between the above two values
	Return LInterpolate#(v1#,v2#,(Float y/Float z)-Floor(Float y/Float z))
End Function

Function FreePerlin(Image,ImageType,MinOctaves,MaxOctaves)
	For i = MinOctaves To MaxOctaves
		ti = (i - MinOctaves)
		FreeBank RandomBank(ti)
	Next
	If ImageType = 0 Then FreeImage Image
	If ImageType = 1 Then FreeTexture Image
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1023795"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Search for "Fast Perlin Noise" or "Improved Perlin Noise" in Google.<br><br><a href="http://mrl.nyu.edu/~perlin/noise/" target="_blank">http://mrl.nyu.edu/~perlin/noise/</a><br><br><a href="http://www.gamedev.net/community/forums/mod/journal/journal.asp?jn=263350&amp;reply_id=2889484" target="_blank">http://www.gamedev.net/community/forums/mod/journal/journal.asp?jn=263350&amp;reply_id=2889484</a><br><br>I never got it running in B3D but perhaps you're more successful... <br><br></td></tr></table><br>
<a name="1023818"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Christian,  I got the improved perlin kind of working.  not sure if i'm doing it right but it draws something.<br><br>Edit:  Got it to work a little better.<br><br>Edit: Improved Perlin noise Fully working<br><br>Edit: Added a z cycle so that you can see morphing noise<br>Edit: Small speed increase with gradient lookup table<br>Edit: Added more options to the Perlin2D function<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,32,2
SetBuffer BackBuffer()
Dim p(512)
Dim grayd#(512)
;ms = MilliSecs()
Restore permutation
For i=0 To 256-1
	Read perm
	p(i) = ( perm )
	p(256+i)= ( perm )
	grayd#(i) = Rnd(-.7,.7)
	grayd#(256+i) = Rnd(-.7,.7)
Next
seed = 1200
size = 512
Img = CreateImage(size,size)
While Not KeyHit(1)
	Cls
	ms = MilliSecs()
	Perlin2D(img,0,seed,z,0,11)
	z = z + 1
	DrawImage Img,0,0
	Color 255,255,255
	Text GraphicsWidth()/2,GraphicsHeight()/2,"Done at (ms): "+(MilliSecs()-ms)
	Flip
Wend

Flip
WaitKey()


Function Perlin2D( Image , ImageType , Seed , z# , MinOctaves , MaxOctaves )
	IB = ImageBuffer(Image)
	size = ImageWidth(Image)
	LockBuffer IB
	For x = 0 To size-1
		For y = 0 To size-1
			;col# = random2D#( x, y,size,129241)*255
			col# = noise#( Float x, Float y , Float z , size , Seed, MinOctaves , MaxOctaves ) * 255
			If col &lt; 0 Then col = 0
			If col &gt; 255 Then col = 255
			rgb = Get_ARGB_From(col,col,col)
			WritePixelFast x,y,rgb,IB
		Next
	Next
	UnlockBuffer IB
End Function

Function SmoothNoise#( x#, y#, z#,Seed = 0) 
	x = x + Seed: y = y + Seed: z = z + Seed
	Local x1#,y1#,z1#,u#,v#,w#,a#,aa#,ab#,b#,ba#,bb#
	Local g1#,g2#,g3#,g4#,g5#,g6#,g7#,g8#
	Local l1#,l2#,l3#,l4#,l5#,l6#,l7#
	x1 = ( Floor(x) And 255 );,                  // FIND UNIT CUBE THAT
	y1 = ( Floor(y) And 255 );,                  // CONTAINS POINT.
	z1 = ( Floor(z) And 255 );,
	x = x - Floor( x );                                // FIND RELATIVE X,Y,Z
	y = y - Floor( y );                                // OF POINT IN CUBE.
	z = z - Floor( z );
	u# = fade#(x);,                                // COMPUTE FADE CURVES
	v# = fade#(y);,                                // FOR EACH OF x,y,z.
	w# = fade#(z);
	a# = p(x1)+y1
	aa# = p(a)+z1
	ab# = p(a+1)+z1;,      // HASH COORDINATES OF
	b# = p(x1+1)+y1
	ba# = p(b)+z1
	bb# = p(b+1)+z1;      // THE 8 CUBE CORNERS,
	
	g1# = grayd#(bb+1)
	g2# = grayd#(ab+1)
	g3# = grayd#(ba+1)
	g4# = grayd#(aa+1)
	g5# = grayd#(bb)
	g6# = grayd#(ab)
	g7# = grayd#(ba)
	g8# = grayd#(aa)
	l1# = lerp#(u, g2#, g1#)
	l2# = lerp#(u, g4#, g3#)
	l3# = lerp#(v, l2#, l1#)
	l4# = lerp#(u, g6#, g5#)
	l5# = lerp#(u, g8#, g7#)
	l6# = lerp#(v, l5#, l4#)
	l7# = lerp#(w, l6#, l3#)
	Return l7#
End Function
Function fade#( t# ) : s# = t * t * t * (t * (t * 6 - 15) + 10):Return s#:End Function
Function lerp#( t#, a#, b#): z# = a + t * (b - a): Return z#:End Function
Function grad#(hash%, x#, y#, z#):
	h% = hash And 15
	If h &lt; 8
		u# = x#
	Else
		u# = y#
	EndIf
	If h &lt; 4
		v# = y#
	ElseIf h = 12 Or h = 14 Then
		v# = x#
 	Else
		v# = z#
	EndIf
	If h &lt;&gt; 0 And 1 &lt;&gt; 0
		u# = -u#
	EndIf
	If h &lt;&gt; 0 And 2 &lt;&gt; 0
		v# = -v#
	EndIf
	uv# = u# + v#
	Return uv#
End Function 

.permutation 
Data 151,160,137,91,90,15
Data 131,13,201,95,96,53,194,233,7,225,140,36,103,30,69,142,8,99,37,240,21,10,23
Data 190, 6,148,247,120,234,75,0,26,197,62,94,252,219,203,117,35,11,32,57,177,33
Data 88,237,149,56,87,174,20,125,136,171,168, 68,175,74,165,71,134,139,48,27,166
Data 77,146,158,231,83,111,229,122,60,211,133,230,220,105,92,41,55,46,245,40,244
Data 102,143,54, 65,25,63,161, 1,216,80,73,209,76,132,187,208, 89,18,169,200,196
Data 135,130,116,188,159,86,164,100,109,198,173,186, 3,64,52,217,226,250,124,123
Data 5,202,38,147,118,126,255,82,85,212,207,206,59,227,47,16,58,17,182,189,28,42
Data 223,183,170,213,119,248,152, 2,44,154,163, 70,221,153,101,155,167, 43,172,9
Data 129,22,39,253, 19,98,108,110,79,113,224,232,178,185, 112,104,218,246,97,228
Data 251,34,242,193,238,210,144,12,191,179,162,241, 81,51,145,235,249,14,239,107
Data 49,192,214, 31,181,199,106,157,184, 84,204,176,115,121,50,45,127, 4,150,254
Data 138,236,205,93,222,114,67,29,24,72,243,141,128,195,78,66,215,61,156,180

 Function random2D#( x# , y# , Size% = 64, Seed%)
   	
		x = x + Seed;
		y = y + Seed;
		
		Local value# = 0.0: initialSize = Size;
		
		While( size &gt;= 1) 
			value = value + SmoothNoise( x*3 / Size, y*3 / Size, 100 / Size, Seed );
			Size = Size - 1;
		Wend
		
		If (value &lt; -1) Then value = -1;
		If (value &gt; 1) Then value = 1;
		
		Return value;
   
End Function

Function noise#(x#,y#,z#,size#=64,seed% = 0, MinOctaves = 0 , MaxOctaves = 9999) 
		If seed = 0 Then seed = MilliSecs()
		x# = x# + seed
		y# = y# + seed
		z# = z# + seed
		;//Set the initial value and initial size
		value# = 0.0: initialSize# = size#;
		
		For i = 1 To MinOctaves
			size = size/2
		Next
		;//Add finer and finer hues of smoothed noise together
		While(size &gt;= 1.0) And MaxOctaves &gt; MinOctaves
	
			value# = value# + SmoothNoise#(x / size, y / size, z / size, seed) * size
			size = size / 2.0;
			MaxOctaves = MaxOctaves - 1
		Wend
		
		;//Return the result over the initial size
		Return  (value# / Float initialSize);
	
End Function

Function Get_ARGB_From(r,g,b):		Return b Or(g Shl 8)Or(r Shl 16):	End Function
 </textarea> <br><br></td></tr></table><br>
<a name="1023854"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> <a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=2706" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=2706</a><br><br>Improved Perlin noise now in the Code archives. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
