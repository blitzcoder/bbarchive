<!DOCTYPE html><html lang="en" ><head ><title >Bilinear Resampling</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Bilinear Resampling</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Bilinear Resampling</a><br><br>
<a name="940448"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> For my current project I need a bilinear resampling function which is NOT based on Tformfilter and Resizeimage. The goal is to stretch a 64x64 bank to a 256x256 or higher dimensioned bank with bilinear interpolated values. I've setup a quick test with a 4x4 "data image" but there is an error I cant identify. The target size is shown in the green rectangle and you'll notice that the blob is distorted a little bit (it will get even worse if you increase the s# scale variable)<br><br>Where is the error? I added some kind of debug logging and the correct results should be:<br><br>0, 32, 96,128,128, 96, 32, 0<br>32, 64,128,160,160,128, 64, 32<br>96,128,191,223,223,191,128, 96<br>128,160,223,255,255,223,160,128<br>128,160,223,255,255,223,160,128<br>96,128,191,223,223,191,128, 96<br>32, 64,128,160,160,128, 64, 32<br>0, 32, 96,128,128, 96, 32, 0<br><br>but they are<br><br>0, 32, 64, 64, 64, 32,  0,  0<br>32, 96,160,160,160, 96, 32, 16<br>64,160,255,255,255,160, 64, 32<br>64,160,255,255,255,160, 64, 32<br>64,160,255,255,255,160, 64, 32<br>32, 96,160,160,160, 96, 32, 16<br>0, 32, 64, 64, 64, 32,  0,  0<br>0, 16, 32, 32, 32, 16,  0,  0<br><br>Please help, I'm stuck there :-/<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 400,300,32,2

width=4
Height=4
s#=2.0

Restore Image
Dim Heightmap(width,Height)

For Y=0 To width-1
	For X=0 To Height-1
		Read Heightmap(x,y)
	Next
Next

Scale(s,width,Height)
Color 0,128,0 : Rect 16,16,64*s,64*s,0

While Not KeyHit(1)
	
	rgb=(ReadPixel(MouseX(),MouseY(),FrontBuffer()) And $ff0000) / $10000
	
	Color   0,  0,  0 : Rect 0,0,24,16,1
	Color 255,255,255 : Text 0,0,rgb
	
	Flip
	
Wend

End

Function Bilinear#(x#,y#)
	
	Local x0#=Floor(x)
	Local y0#=Floor(y)
	Local dx#=x-x0
	Local dy#=y-y0
	
	Local r0=Heightmap(x0,y0)
	Local r1=Heightmap(x0+1,y0)
	Local r2=Heightmap(x0+1,y0+1)
	Local r3=Heightmap(x0,y0+1)
	
	Return LinearInterpolate(LinearInterpolate(r0,r1,dx),LinearInterpolate(r3,r2,dx),dy)
	
End Function

Function Scale(ratio#,width%,height%)
	
	Local x%,y%,c%
	
	For y=0 To (height*ratio)-1
		
		out$=""
		
		For x=0 To (width*ratio)-1
			
			Local u# = x*1.0/ratio
			Local v# = y*1.0/ratio
			
			c=Bilinear(u,v)
			
			Color c,c,c
			Rect (x+1)*16,(y+1)*16,16,16,1
			
			out$=out$+Fill(c,3)
			If x&lt;(width*ratio)-1 Then out$=out$+","
			
		Next
		
		DebugLog out
		
	Next
	
End Function

Function LinearInterpolate#(x1#,x2#,mu#=0.5)
	
	Return (x1*(1.0-mu)+x2*mu)
	
End Function

Function Fill$(number%,lenght%=2)
	
	Local r$=""
	
	For i=1 To lenght-Len(Str(number))
		
		r$=r$+" "
		
	Next
	
	Return r$+Str(number)
	
End Function

.Image
Data   0, 64, 64,  0
Data  64,255,255, 64
Data  64,255,255, 64
Data   0, 64, 64,  0</textarea> <br><br></td></tr></table><br>
<a name="940500"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Subirenihil</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> First off, your target values are a little off (on the edges, how can {0,64,64,0} expand into {0,32,96,128,128,96,32,0} - linear interpolation is about {0,32,64,64,64,64,32,0} while spline interpolation is about {0,36,64,80,80,64,36,0}).<br><br>What it looks like you are trying to do is expand the image from<br>0.0 _ 1.0 _ 2.0 _ 3.0<br>to<br>0.0 _ 0.4 _ 0.9 _ 1.3 _ 1.7 _ 2.1 _ 2.6 _ 3.0<br>but you are coding it to expand to<br>0.0 _ 0.5 _ 1.0 _ 1.5 _ 2.0 _ 2.5 _ 3.0 _ 3.5<br>which shifts it off center.<br><br>The following is what you (inadvertently) are doing (the xx's are the interpolated values, the numbers are what you have in your array):<br><pre class=code>
 00  xx  64  xx  64  xx  00  xx (00)
 XX  xx  XX  xx  XX  xx  XX  xx (00)
 64  xx 255  xx 255  xx  64  xx (00)
 XX  xx  XX  xx  XX  xx  XX  xx (00)
 64  xx 255  xx 255  xx  64  xx (00)
 XX  xx  XX  xx  XX  xx  XX  xx (00)
 00  xx  64  xx  64  xx  00  xx (00)
 XX  xx  XX  xx  XX  xx  XX  xx (00)
(00)(00)(00)(00)(00)(00)(00)(00)(00)
</pre><br>The parentheses are around array values that you didn't think you were accessing (they are also the pixels that are beyond the edge of the image).<br><br>I have modified your code 2 ways; the first is scaling the same amount you were scaling it but centered, the second scales it so that the corners are still in the corners.<br><br>Version 1 gives:<br>-0.25 _ 0.25 _ 0.75 _ 1.25 _ 1.75 _ 2.25 _ 2.75 _ 3.25<br>Version 2 gives:<br>0.0 _ 0.4 _ 0.9 _ 1.3 _ 1.7 _ 2.1 _ 2.6 _ 3.0<br><br>Version 1:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 400,300,32,2

Global width=4				;&lt;----- Made this global
Global Height=4				;&lt;----- Made this global
s#=2.0

Restore Image
Dim Heightmap(width,Height)

For Y=0 To width-1
	For X=0 To Height-1
		Read Heightmap(x,y)
	Next
Next

Scale(s,width,Height)
Color 0,128,0 : Rect 16,16,64*s,64*s,0

While Not KeyHit(1)
	
	rgb=(ReadPixel(MouseX(),MouseY(),FrontBuffer()) And $ff0000) / $10000
	
	Color   0,  0,  0 : Rect 0,0,24,16,1
	Color 255,255,255 : Text 0,0,rgb
	
	Flip
	
Wend

End

Function Bilinear#(x#,y#)
	
	Local x0#=Floor(x)
	Local y0#=Floor(y)
	Local x1#=Floor(x+1)					;&lt;----- Added this line (Looks redundant, but isn't - these variables
	Local y1#=Floor(y+1)					;&lt;----- Added this line    prevent reading from the wrong pixel.
	Local dx#=x-x0						;                          For example: x=-.25, y=-.25
	Local dy#=y-y0						;                              x0=-1, y0=-1, x1=0, y1=0
								;                          The problem comes when &lt;x0&gt; and &lt;y0&gt; get changed to 0
	x02x=1							;&lt;----- Added this line    because negative numbers are not allowed to access
	x03x=1							;&lt;----- Added this line    an array. If I didn't use &lt;x1&gt; and &lt;y1&gt;, I would read from
	y02x=1							;&lt;----- Added this line    &lt;Heightmap(x0+1,x0+1)&gt; i.e. &lt;Heightmap(1,1)&gt;, not from
	y03x=1							;&lt;----- Added this line    &lt;Heightmap(0,0)&gt; like we should
								;&lt;----- Added this line
	If x0&lt;0 Then x02x=0					;&lt;----- Added this line
	If x1&gt;width-1 Then x03x=0				;&lt;----- Added this line
	If y0&lt;0 Then y02x=0					;&lt;----- Added this line
	If y1&gt;height-1 Then y03x=0				;&lt;----- Added this line
								;&lt;----- Added this line
	If x02x=0 Then x0=0					;&lt;----- Added this line ("r-whatever" is going to be zero, so just make sure we
	If y02x=0 Then y0=0					;&lt;----- Added this line    don't get a "array out of bounds" error)
	If x03x=0 Then x1=0					;&lt;----- Added this line ("r-whatever" is going to be zero, so just make sure we
	If y03x=0 Then y1=0					;&lt;----- Added this line    don't get a "array out of bounds" error)
								;&lt;----- Added this line
	Local r0=Heightmap(x0,y0)*x02x*y02x			;&lt;----- Added "*x02x*y02x" to this line (makes it 0 if either value is out of bounds)
	Local r1=Heightmap(x1,y0)*x03x*y02x			;&lt;----- Added "*x03x*y02x" to this line (makes it 0 if either value is out of bounds)
	Local r2=Heightmap(x1,y1)*x03x*y03x			;&lt;----- Added "*x03x*y03x" to this line (makes it 0 if either value is out of bounds)
	Local r3=Heightmap(x0,y1)*x02x*y03x			;&lt;----- Added "*x02x*y03x" to this line (makes it 0 if either value is out of bounds)

	Return LinearInterpolate(LinearInterpolate(r0,r1,dx),LinearInterpolate(r3,r2,dx),dy)
	
End Function

Function Scale(ratio#,width%,height%)
	
	Local x%,y%,c%
	
	For y=0 To (height*ratio)-1
		
		out$=""
		
		For x=0 To (width*ratio)-1
			
			Local u# = ((x+.5)*1.0/ratio)-.5	;&lt;----- Changed the formula to be centered on the pixel
			Local v# = ((y+.5)*1.0/ratio)-.5	;&lt;-----  rather than the upper-left corner of the pixel
						
			c=Bilinear(u,v)
			
			Color c,c,c
			Rect (x+1)*16,(y+1)*16,16,16,1
			
			out$=out$+Fill(c,3)
			If x&lt;(width*ratio)-1 Then out$=out$+","
			
		Next
		
		DebugLog out
		
	Next
	
End Function

Function LinearInterpolate#(x1#,x2#,mu#=0.5)
	
	Return (x1*(1.0-mu)+x2*mu)
	
End Function

Function Fill$(number%,lenght%=2)
	
	Local r$=""
	
	For i=1 To lenght-Len(Str(number))
		
		r$=r$+" "
		
	Next
	
	Return r$+Str(number)
	
End Function

.Image
Data   0, 64, 64,  0
Data  64,255,255, 64
Data  64,255,255, 64
Data   0, 64, 64,  0
</textarea><br><br>Version 2:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 400,300,32,2

Global width=4				;&lt;----- Made this global
Global Height=4				;&lt;----- Made this global
s#=2.0

Restore Image
Dim Heightmap(width,Height)

For Y=0 To width-1
	For X=0 To Height-1
		Read Heightmap(x,y)
	Next
Next

Scale(s,width,Height)
Color 0,128,0 : Rect 16,16,64*s,64*s,0

While Not KeyHit(1)
	
	rgb=(ReadPixel(MouseX(),MouseY(),FrontBuffer()) And $ff0000) / $10000
	
	Color   0,  0,  0 : Rect 0,0,24,16,1
	Color 255,255,255 : Text 0,0,rgb
	
	Flip
	
Wend

End

Function Bilinear#(x#,y#)
	
	Local x0#=Floor(x)
	Local y0#=Floor(y)
	Local x1#=Floor(x+1)					;&lt;----- Added this line (Looks redundant, but isn't - these variables
	Local y1#=Floor(y+1)					;&lt;----- Added this line    prevent reading from the wrong pixel.
	Local dx#=x-x0						;                          For example: x=-.25, y=-.25
	Local dy#=y-y0						;                              x0=-1, y0=-1, x1=0, y1=0
								;                          The problem comes when &lt;x0&gt; and &lt;y0&gt; get changed to 0
	x02x=1							;&lt;----- Added this line    because negative numbers are not allowed to access
	x03x=1							;&lt;----- Added this line    an array. If I didn't use &lt;x1&gt; and &lt;y1&gt;, I would read from
	y02x=1							;&lt;----- Added this line    &lt;Heightmap(x0+1,x0+1)&gt; i.e. &lt;Heightmap(1,1)&gt;, not from
	y03x=1							;&lt;----- Added this line    &lt;Heightmap(0,0)&gt; like we should
								;&lt;----- Added this line
	If x0&lt;0 Then x02x=0					;&lt;----- Added this line
	If x1&gt;width-1 Then x03x=0				;&lt;----- Added this line
	If y0&lt;0 Then y02x=0					;&lt;----- Added this line
	If y1&gt;height-1 Then y03x=0				;&lt;----- Added this line
								;&lt;----- Added this line
	If x02x=0 Then x0=0					;&lt;----- Added this line ("r-whatever" is going to be zero, so just make sure we
	If y02x=0 Then y0=0					;&lt;----- Added this line    don't get a "array out of bounds" error)
	If x03x=0 Then x1=0					;&lt;----- Added this line ("r-whatever" is going to be zero, so just make sure we
	If y03x=0 Then y1=0					;&lt;----- Added this line    don't get a "array out of bounds" error)
								;&lt;----- Added this line
	Local r0=Heightmap(x0,y0)*x02x*y02x			;&lt;----- Added "*x02x*y02x" to this line (makes it 0 if either value is out of bounds)
	Local r1=Heightmap(x1,y0)*x03x*y02x			;&lt;----- Added "*x03x*y02x" to this line (makes it 0 if either value is out of bounds)
	Local r2=Heightmap(x1,y1)*x03x*y03x			;&lt;----- Added "*x03x*y03x" to this line (makes it 0 if either value is out of bounds)
	Local r3=Heightmap(x0,y1)*x02x*y03x			;&lt;----- Added "*x02x*y03x" to this line (makes it 0 if either value is out of bounds)

	Return LinearInterpolate(LinearInterpolate(r0,r1,dx),LinearInterpolate(r3,r2,dx),dy)
	
End Function

Function Scale(ratio#,width%,height%)
	
	Local x%,y%,c%
	Local ratio2w#=(ratio#*width-1)/(width-1.0)		;&lt;----- Added this line
	Local ratio2h#=(ratio#*height-1)/(height-1.0)		;&lt;----- Added this line
	
	For y=0 To (height*ratio)-1
		
		out$=""
		
		For x=0 To (width*ratio)-1
			
			Local u# = x*1.0/ratio2w#	;&lt;----- Changed the formula to be corner to corner
			Local v# = y*1.0/ratio2h#	;&lt;-----  rather pull a bit of black from beyond the edges
						
			c=Bilinear(u,v)
			
			Color c,c,c
			Rect (x+1)*16,(y+1)*16,16,16,1
			
			out$=out$+Fill(c,3)
			If x&lt;(width*ratio)-1 Then out$=out$+","
			
		Next
		
		DebugLog out
		
	Next
	
End Function

Function LinearInterpolate#(x1#,x2#,mu#=0.5)
	
	Return (x1*(1.0-mu)+x2*mu)
	
End Function

Function Fill$(number%,lenght%=2)
	
	Local r$=""
	
	For i=1 To lenght-Len(Str(number))
		
		r$=r$+" "
		
	Next
	
	Return r$+Str(number)
	
End Function

.Image
Data   0, 64, 64,  0
Data  64,255,255, 64
Data  64,255,255, 64
Data   0, 64, 64,  0
</textarea><br><br>I'm not going to try to make a spline interpolation function right now, its a bit more complex and it's getting late for me.  Hope one of these works for ya (I personally prefer version 1, but that's just me) <br><br></td></tr></table><br>
<a name="940613"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you! For my test data I compared the debug entries with the results I got in Photoshop, dunno which bilinear filter they use. But after hours of testing I found that your second solution is brilliant for my needs. I am now able to "resize" a 64x64 heightmap part to 512x512, blend it with a fractal image and get 8 times higher resolution with it and maintaining the basic heightmap structure (in just 200ms). And it is still tileable after that! <br><br></td></tr></table><br>
<a name="940717"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Subirenihil</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah, Photoshop likely (don't know if you changed any setting when you resized your test image) used a fancier function to enlarge it.<br><br>My code could probably be optimized to be more efficient, but glad to have helped.<br><br>Now...which version goes in the code archives....? <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
