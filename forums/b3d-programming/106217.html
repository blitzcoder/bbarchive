<!DOCTYPE html><html lang="en" ><head ><title >Start Screen: how to select with keys or mouse</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Start Screen: how to select with keys or mouse</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Start Screen: how to select with keys or mouse</a><br><br>
<a name="1305280"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RubyEnemy</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> how do you have use the arrow keys or the mouse to select the start, controls, and scores options when starting a game? <br><br></td></tr></table><br>
<a name="1305298"></a>

<a name="1305300"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> One of the most annoying and boring parts of the game development process in my opinion is coding the front-end and user interface - unfortunately, there's no simple native Blitz3D solution.<br><br>Essentially, you need to read and resolve all of the user input and its on-screen appearance.<br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Graphics3D 800,600,32


Const STATE_MENU_MAIN=0
Const STATE_MENU_OPTIONS=1
Const STATE_GAME_INITIALISE=2
Const STATE_GAME_RUNNING=3
Const STATE_GAME_OVER=4

Global MACHINE_STATE

Global MENU_HIGHLIGHTED=0

Const MENU_MAIN_SELECTIONS=2
Const MENU_OPTIONS_SELECTIONS=3

Const BUTTON_WIDTH=128
Const BUTTON_HEIGHT=48

Global MENU_BUTTONS[MENU_MAIN_SELECTIONS+MENU_OPTIONS_SELECTIONS]

Const BUTTON_MAIN_START=0
Const BUTTON_MAIN_OPTIONS=1

Const BUTTON_OPTIONS_BACK=2
Const BUTTON_OPTIONS_GRAPHICS=3
Const BUTTON_OPTIONS_SOUND=4

Const KEY_SCANCODE_UP=200
Const KEY_SCANCODE_DOWN=208
Const KEY_SCANCODE_SPACE=57

Global KEY_SELECTION_UP=KEY_SCANCODE_UP
Global KEY_SELECTION_DOWN=KEY_SCANCODE_DOWN
Global KEY_SELECTION_GO=KEY_SCANCODE_SPACE


Runtime


Function Initialise()
	;Load Assets here etc.
	
	MENU_BUTTONS[BUTTON_MAIN_START]=CreateImage(BUTTON_WIDTH,BUTTON_HEIGHT)
	SetBuffer ImageBuffer(MENU_BUTTONS[BUTTON_MAIN_START])
	Text BUTTON_WIDTH Shr True ,BUTTON_HEIGHT Shr True,"START GAME",True,True
	
	MENU_BUTTONS[BUTTON_MAIN_OPTIONS]=CreateImage(BUTTON_WIDTH,BUTTON_HEIGHT)
	SetBuffer ImageBuffer(MENU_BUTTONS[BUTTON_MAIN_OPTIONS])
	Text BUTTON_WIDTH Shr True,BUTTON_HEIGHT Shr True,"OPTIONS",True,True
	
	
	
	
	
	MENU_BUTTONS[BUTTON_OPTIONS_BACK]=CreateImage(BUTTON_WIDTH,BUTTON_HEIGHT)
	SetBuffer ImageBuffer(MENU_BUTTONS[BUTTON_OPTIONS_BACK])
	Text BUTTON_WIDTH Shr True ,BUTTON_HEIGHT Shr True,"BACK",True,True
	
	MENU_BUTTONS[BUTTON_OPTIONS_GRAPHICS]=CreateImage(BUTTON_WIDTH,BUTTON_HEIGHT)
	SetBuffer ImageBuffer(MENU_BUTTONS[BUTTON_OPTIONS_GRAPHICS])
	Text BUTTON_WIDTH Shr True ,BUTTON_HEIGHT Shr True,"GRAPHICS",True,True
	
	MENU_BUTTONS[BUTTON_OPTIONS_SOUND]=CreateImage(BUTTON_WIDTH,BUTTON_HEIGHT)
	SetBuffer ImageBuffer(MENU_BUTTONS[BUTTON_OPTIONS_SOUND])
	Text BUTTON_WIDTH Shr True ,BUTTON_HEIGHT Shr True,"SOUND",True,True
	
	
End Function

Function Runtime()
	Initialise
	SetBuffer BackBuffer()
	Main_Loop
	EndGraphics
	End 
End Function

Function Main_Loop()
	
	While (Not(KeyDown(1)))
		StartLoop 
		UpdateLoop
		EndLoop
	Wend
	
End Function

Function StartLoop()
	Cls
End Function

Function UpdateLoop()
	
		Select (MACHINE_STATE)
			Case STATE_MENU_MAIN,STATE_MENU_OPTIONS:
				Menu
			Case STATE_GAME_INITIALISE:
				RuntimeError("Game Has Begun...")
			Case  STATE_GAME_RUNNING:
			Case STATE_GAME_OVER:
		End Select
		
End Function

Function EndLoop()
	Render
End Function

Function Render()
	Flip
End Function

Function Menu()
	
	Local SelectIonStart
	Local SelectionEnd
	
	Select (MACHINE_STATE)
		Case STATE_MENU_MAIN:
			SelectIonStart=BUTTON_MAIN_START
			SelectionEnd=BUTTON_MAIN_OPTIONS
			
		Case STATE_MENU_OPTIONS:
			SelectIonStart=BUTTON_OPTIONS_BACK
			SelectionEnd=BUTTON_OPTIONS_SOUND
			
	End Select
	
	DrawMenuButtons(SelectIonStart,SelectionEnd)
	UpdateMouseSelection(SelectIonStart,SelectionEnd)
	UpdateKeyboardSelection(SelectIonStart,SelectionEnd)
	ShowSelectionRectangle(SelectIonStart,SelectionEnd)
End Function

Function MenuSelectEvent()
	;Responds to Menu Selection and changes Machine State
	Select (MENU_HIGHLIGHTED)
		Case BUTTON_MAIN_START:
			MACHINE_STATE=STATE_GAME_INITIALISE
			
		Case BUTTON_MAIN_OPTIONS:
			MACHINE_STATE=STATE_MENU_OPTIONS
			MENU_HIGHLIGHTED=BUTTON_OPTIONS_BACK
			
		Case BUTTON_OPTIONS_BACK:
			MACHINE_STATE=STATE_MENU_MAIN
			MENU_HIGHLIGHTED=BUTTON_MAIN_START
			
		Case BUTTON_OPTIONS_GRAPHICS:
			;... etc. Not completed here
			
		Case BUTTON_OPTIONS_SOUND:
			;... etc. Not completed here
	End Select
End Function

Function UpdateMouseSelection(SelectionStart,SelectionEnd)
	;Updates Selection based on MouseOver - Can be replaced with ImagesCollide() if using an Image for the pointer
	Local Iter
	
	Local X
	Local Y
	
	Local Count=SelectionEnd-SelectionStart
	
	For Iter=0 To Count
		X=GetXPosition(Iter,Count)		
		Y=GetYPosition(Iter,Count)
		
		If (GetMouseOver(X,Y,BUTTON_WIDTH,BUTTON_HEIGHT))
			DebugLog(Iter)
			MENU_HIGHLIGHTED=Iter+SelectionStart
			
			If (MouseHit(1))
				;Keep this here, so Mouseclick only calls event whilst mouseover
				MenuSelectEvent
			End If	
				
		End If
		
	Next
End Function	

Function UpdateKeyboardSelection(SelectIonStart,SelectionEnd)
	;Updates Selection based on Key Input
	
	Local SelectionChange=KeyHit(KEY_SELECTION_DOWN)-KeyHit(KEY_SELECTION_UP)
	
	If (SelectionChange)
		MENU_HIGHLIGHTED=MENU_HIGHLIGHTED+SelectionChange
		If (MENU_HIGHLIGHTED&gt;SelectionEnd)
			MENU_HIGHLIGHTED=SelectIonStart
		End If
		If (MENU_HIGHLIGHTED&lt;SelectIonStart)
			MENU_HIGHLIGHTED=SelectionEnd
		End If
	End If
	
	If (KeyHit(KEY_SELECTION_GO))
		MenuSelectEvent
	End If
	
End Function	

Function GetMouseOver(x,y,w,h)
	Local mx=MouseX()
	Local my=MouseY()
	Return  (  ((mx&gt;x) And (mx&lt;(x+w)))    And    ((my&gt;y) And (my&lt;(y+h)))  )
End Function
	
Function ShowSelectionRectangle(SelectionStart,SelectionEnd)
	Rect GetXPosition(SelectionStart,SelectionEnd),GetYPosition(MENU_HIGHLIGHTED-SelectionStart,SelectionEnd),BUTTON_WIDTH,BUTTON_HEIGHT,False
End Function

Function DrawMenuButtons(ButtonStart,ButtonEnd)
		;Draws the buttons from the library
	Local Count=ButtonEnd-ButtonStart
	
	Local Iter
	Local X
	Local Y
	For Iter= 0 To Count
		X=GetXPosition(Iter,Count)
		Y=GetYPosition(Iter,Count)
		DrawImage MENU_BUTTONS[Iter+ButtonStart],X,Y
	Next
End Function		

Function GetXPosition(Item,Count)
	;So the same function can be used for selection rectangle, mouse events and the actual button drawing
	Return (GraphicsWidth() Shr True) - (BUTTON_WIDTH Shr True)
End Function

Function GetYPosition(Item,Count)		
	;So the same function can be used for selection rectangle, mouse events and the actual button drawing
	Return  (BUTTON_HEIGHT*1.5)*(Item+1)
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1305359"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> You have to code all that functionality yourself.<br><br>That means using commands like keydown, keyhit, mousedown, mousehit along with either images or sprites for button images, and the mousex, mousey commands to check if the cursor is within the boundaries of the button images.<br><br>There's a fair bit to do to get that to do it - but if you design it well you can reuse it elsewhere easily throughout the rest of your program. <br><br></td></tr></table><br>
<a name="1305387"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RubyEnemy</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks _PJ_  you're a big help <br><br></td></tr></table><br>
<a name="1305628"></a>

<a name="1305629"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RubyEnemy</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> how would i apply my custom welcome screen to the the code you gave me?<br><br>Const ESC_KEY=1<br>Const Q_KEY=16<br><br>;launches the welcome functions<br>welcome()<br><br>;creating welcome function<br>Function welcome()<br>Graphics 640,480<br>screen=LoadImage("textures/truewelcome.png")<br>DrawImage screen,0,-75<br>While Not KeyDown(1)<br>If KeyDown(16) Then Return<br>Wend<br>End Function <br><br></td></tr></table><br>
<a name="1305640"></a>

<a name="1305641"></a>

<a name="1305642"></a>

<a name="1305643"></a>

<a name="1305644"></a>

<a name="1305645"></a>

<a name="1305646"></a>

<a name="1305647"></a>

<a name="1305648"></a>

<a name="1305650"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dan</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, first, you will need to decide if you want to use only the mouse, or arrow keys or maybe both on your start screen.<br><br>If you want to use keys only, then imagine having a variable called menue<br><br>This menue variable is starting with the default number 1. This would represent the start button.<br>number 2 would be controls, and 3 would be score.<br><br>now you will need to increase and decrease this menue variable with the arrow keys.<br><br>The limits would be minimum 1, maximum 3.<br><br>Pressing enter/return would take you to the variable's destination.<br><br>here is an example following the above's description:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
menue=1
menuedelay=MilliSecs()
a$=""

SetBuffer BackBuffer()

;Main menue loop starts here
Repeat
	
	Gosub menukeys
	Gosub drawscreen
	
	If KeyDown(28) Or KeyDown(156) ;Enter or return
		Select menue
			Case 1
			   Gosub start
			Case 2
			   Gosub controls
			Case 3
			   Gosub score
		End Select 
	EndIf
	Flip 
Until KeyDown(1)
;Escape key exits the main menue loop

End

.start
;game start
a$="Starting"
Return

.controls
;Setting up controls
a$="Controls"
Return

.score
;Displaying score
a$="Score"
Return

.drawscreen
Cls

Color $ff,$ff,$ff
Text 0,0,"Last item choosen was: "+a$

;if variable number then color red, else color white

If menue=1 Then Color $ff,0,0 Else Color $ff,$ff,$ff
Text 10,100,"Start"

If menue=2 Then Color $ff,0,0 Else Color $ff,$ff,$ff
Text 10,120,"Controls"

If menue=3 Then Color $ff,0,0 Else Color $ff,$ff,$ff
Text 10,140,"Score"

Return

.menukeys
If MilliSecs()-menuedelay &gt; 150             ;Delaying the keypress or else it would be too fast, switching between 1 and 3
	menuedelay=MilliSecs()
	
	If KeyDown(200) Then menue=menue-1		;KeyUp
	If KeyDown(208) Then menue=menue+1		;KeyDown
	If menue&lt;=1 Then menue=1
	If menue=&gt;3 Then menue=3
EndIf

Return
</textarea><br><br>in this example i have used simple text functions to display the menue. <br>you can easily replace it to display boxes and even images. <br><br></td></tr></table><br>
<a name="1305908"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RubyEnemy</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> how would you make the text appear bigger? <br><br></td></tr></table><br>
<a name="1305970"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> You would use the font commands, loadfont and setfont.<br><br>Or alternatively you would use bitmap fonts, put them on a texture and use 2d-in-3d and scale them on the fly. <br><br></td></tr></table><br>
<a name="1305985"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry for the late reply. Hadn't noticed there was more to this.<br><br>Here's an updated version of the same code I posted above, but now including the Welcome Screen and also a custom loaded font so you can alter the size.<br><br><pre class=code>

Graphics3D 800,600,32

Const WELCOME_SCREEN_PATH$="textures/truewelcome.png"

Const FONT_NAME$="Arial"
Const FONT_SIZE=18


Const STATE_WELCOME=0
Const STATE_MENU_MAIN=1
Const STATE_MENU_OPTIONS=2
Const STATE_GAME_INITIALISE=3
Const STATE_GAME_RUNNING=4
Const STATE_GAME_OVER=5

Global MACHINE_STATE
Global MENU_HIGHLIGHTED=0

Const MENU_MAIN_SELECTIONS=2
Const MENU_OPTIONS_SELECTIONS=3

Const BUTTON_WIDTH=128
Const BUTTON_HEIGHT=48

Global MENU_BUTTONS[MENU_MAIN_SELECTIONS+MENU_OPTIONS_SELECTIONS]

Const BUTTON_MAIN_START=0
Const BUTTON_MAIN_OPTIONS=1

Const BUTTON_OPTIONS_BACK=2
Const BUTTON_OPTIONS_GRAPHICS=3
Const BUTTON_OPTIONS_SOUND=4

Const KEY_SCANCODE_UP=200
Const KEY_SCANCODE_DOWN=208
Const KEY_SCANCODE_SPACE=57
Const KEY_SCANCODE_Q=16

Global KEY_SELECTION_UP=KEY_SCANCODE_UP
Global KEY_SELECTION_DOWN=KEY_SCANCODE_DOWN
Global KEY_SELECTION_GO=KEY_SCANCODE_SPACE

Global IMG_WELCOME_SPLASH

Runtime


Function Initialise()
	;Load Assets here etc.
	
	IMG_WELCOME_SPLASH=LoadImage(WELCOME_SCREEN_PATH$)
	
	Local Font=LoadFont(FONT_NAME,FONT_SIZE)
	SetFont Font
	
	MENU_BUTTONS[BUTTON_MAIN_START]=CreateImage(BUTTON_WIDTH,BUTTON_HEIGHT)
	SetBuffer ImageBuffer(MENU_BUTTONS[BUTTON_MAIN_START])
	Text BUTTON_WIDTH Shr True ,BUTTON_HEIGHT Shr True,"START GAME",True,True
	
	MENU_BUTTONS[BUTTON_MAIN_OPTIONS]=CreateImage(BUTTON_WIDTH,BUTTON_HEIGHT)
	SetBuffer ImageBuffer(MENU_BUTTONS[BUTTON_MAIN_OPTIONS])
	Text BUTTON_WIDTH Shr True,BUTTON_HEIGHT Shr True,"OPTIONS",True,True
	
	
	
	
	
	MENU_BUTTONS[BUTTON_OPTIONS_BACK]=CreateImage(BUTTON_WIDTH,BUTTON_HEIGHT)
	SetBuffer ImageBuffer(MENU_BUTTONS[BUTTON_OPTIONS_BACK])
	Text BUTTON_WIDTH Shr True ,BUTTON_HEIGHT Shr True,"BACK",True,True
	
	MENU_BUTTONS[BUTTON_OPTIONS_GRAPHICS]=CreateImage(BUTTON_WIDTH,BUTTON_HEIGHT)
	SetBuffer ImageBuffer(MENU_BUTTONS[BUTTON_OPTIONS_GRAPHICS])
	Text BUTTON_WIDTH Shr True ,BUTTON_HEIGHT Shr True,"GRAPHICS",True,True
	
	MENU_BUTTONS[BUTTON_OPTIONS_SOUND]=CreateImage(BUTTON_WIDTH,BUTTON_HEIGHT)
	SetBuffer ImageBuffer(MENU_BUTTONS[BUTTON_OPTIONS_SOUND])
	Text BUTTON_WIDTH Shr True ,BUTTON_HEIGHT Shr True,"SOUND",True,True
	
	FreeFont Font
End Function

Function Runtime()
	Initialise
	SetBuffer BackBuffer()
	Main_Loop
	EndGraphics
	End 
End Function

Function Main_Loop()
	
	While (Not(KeyDown(1)))
		StartLoop 
		UpdateLoop
		EndLoop
	Wend
	
End Function

Function StartLoop()
	Cls
End Function

Function UpdateLoop()
	
	Select (MACHINE_STATE)
		Case STATE_WELCOME:
			WelcomeScreen
		Case STATE_MENU_MAIN,STATE_MENU_OPTIONS:
			Menu
		Case STATE_GAME_INITIALISE:
			RuntimeError("Game Has Begun...")
		Case  STATE_GAME_RUNNING:
		Case STATE_GAME_OVER:
	End Select
	
End Function

Function EndLoop()
	Render
End Function

Function Render()
	Flip
End Function

Function Menu()
	
	Local SelectIonStart
	Local SelectionEnd
	
	Select (MACHINE_STATE)
		Case STATE_MENU_MAIN:
			SelectIonStart=BUTTON_MAIN_START
			SelectionEnd=BUTTON_MAIN_OPTIONS
			
		Case STATE_MENU_OPTIONS:
			SelectIonStart=BUTTON_OPTIONS_BACK
			SelectionEnd=BUTTON_OPTIONS_SOUND
			
	End Select
	
	DrawMenuButtons(SelectIonStart,SelectionEnd)
	UpdateMouseSelection(SelectIonStart,SelectionEnd)
	UpdateKeyboardSelection(SelectIonStart,SelectionEnd)
	ShowSelectionRectangle(SelectIonStart,SelectionEnd)
End Function

Function MenuSelectEvent()
	;Responds to Menu Selection and changes Machine State
	Select (MENU_HIGHLIGHTED)
		Case BUTTON_MAIN_START:
			MACHINE_STATE=STATE_GAME_INITIALISE
			
		Case BUTTON_MAIN_OPTIONS:
			MACHINE_STATE=STATE_MENU_OPTIONS
			MENU_HIGHLIGHTED=BUTTON_OPTIONS_BACK
			
		Case BUTTON_OPTIONS_BACK:
			MACHINE_STATE=STATE_MENU_MAIN
			MENU_HIGHLIGHTED=BUTTON_MAIN_START
			
		Case BUTTON_OPTIONS_GRAPHICS:
			;... etc. Not completed here
			
		Case BUTTON_OPTIONS_SOUND:
			;... etc. Not completed here
	End Select
End Function

Function UpdateMouseSelection(SelectionStart,SelectionEnd)
	;Updates Selection based on MouseOver - Can be replaced with ImagesCollide() if using an Image for the pointer
	Local Iter
	
	Local X
	Local Y
	
	Local Count=SelectionEnd-SelectionStart
	
	For Iter=0 To Count
		X=GetXPosition(Iter,Count)		
		Y=GetYPosition(Iter,Count)
		
		If (GetMouseOver(X,Y,BUTTON_WIDTH,BUTTON_HEIGHT))
			DebugLog(Iter)
			MENU_HIGHLIGHTED=Iter+SelectionStart
			
			If (MouseHit(1))
				;Keep this here, so Mouseclick only calls event whilst mouseover
				MenuSelectEvent
			End If	
			
		End If
		
	Next
End Function	

Function UpdateKeyboardSelection(SelectIonStart,SelectionEnd)
	;Updates Selection based on Key Input
	
	Local SelectionChange=KeyHit(KEY_SELECTION_DOWN)-KeyHit(KEY_SELECTION_UP)
	
	If (SelectionChange)
		MENU_HIGHLIGHTED=MENU_HIGHLIGHTED+SelectionChange
		If (MENU_HIGHLIGHTED&gt;SelectionEnd)
			MENU_HIGHLIGHTED=SelectIonStart
		End If
		If (MENU_HIGHLIGHTED&lt;SelectIonStart)
			MENU_HIGHLIGHTED=SelectionEnd
		End If
	End If
	
	If (KeyHit(KEY_SELECTION_GO))
		MenuSelectEvent
	End If
	
End Function	

Function GetMouseOver(x,y,w,h)
	Local mx=MouseX()
	Local my=MouseY()
	Return  (  ((mx&gt;x) And (mx&lt;(x+w)))    And    ((my&gt;y) And (my&lt;(y+h)))  )
End Function

Function ShowSelectionRectangle(SelectionStart,SelectionEnd)
	Rect GetXPosition(SelectionStart,SelectionEnd),GetYPosition(MENU_HIGHLIGHTED-SelectionStart,SelectionEnd),BUTTON_WIDTH,BUTTON_HEIGHT,False
End Function

Function DrawMenuButtons(ButtonStart,ButtonEnd)
		;Draws the buttons from the library
	Local Count=ButtonEnd-ButtonStart
	
	Local Iter
	Local X
	Local Y
	For Iter= 0 To Count
		X=GetXPosition(Iter,Count)
		Y=GetYPosition(Iter,Count)
		DrawImage MENU_BUTTONS[Iter+ButtonStart],X,Y
	Next
End Function		

Function GetXPosition(Item,Count)
	;So the same function can be used for selection rectangle, mouse events and the actual button drawing
	Return (GraphicsWidth() Shr True) - (BUTTON_WIDTH Shr True)
End Function

Function GetYPosition(Item,Count)		
	;So the same function can be used for selection rectangle, mouse events and the actual button drawing
	Return  (BUTTON_HEIGHT*1.5)*(Item+1)
End Function

Function WelcomeScreen()
	DrawImage IMG_WELCOME_SPLASH,(GraphicsWidth() Shr True)-ImageWidth(IMG_WELCOME_SPLASH),(GraphicsHeight() Shr True)-ImageHeight(IMG_WELCOME_SPLASH)
	If ((KeyHit(KEY_SELECTION_GO)) Or MouseHit(1))
		MACHINE_STATE=STATE_MENU_MAIN
	End If
End Function
</pre> <br><br></td></tr></table><br>
<a name="1307636"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RubyEnemy</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've decided to use this as my menu screen because it was simpler and now in just need to figure out ;how or where i would create a code to when i press start it would start the game instead of saying that i hit the start button?<br><br>IF you want the code for our game then ask? <br><br><br><br><br><br>menue=1<br>menuedelay=MilliSecs()<br>a$=""<br><br>SetBuffer BackBuffer()<br>Graphics3D 640,480,16,2<br><br>;Main menue loop starts here<br>Repeat<br>	<br>	Gosub menukeys<br>	Gosub drawscreen<br>	<br>	If KeyDown(28) Or KeyDown(57) ;Enter or Space<br>		Select menue<br>			Case 1<br>			   Gosub START<br>			Case 2<br>			   Gosub CONTROLS<br>			Case 3<br>			   Gosub SCORE<br>			Case 4<br>				Gosub OPTIONS<br>		End Select <br>	EndIf<br>	Flip <br>Until KeyDown(1)<br>;Escape key exits the main menue loop<br><br>End<br><br>.start<br>;game start<br>A$="Starting"<br>Return<br><br>.controls<br>;Setting up controls<br>A$="Controls"<br>Return<br><br>.options<br>;geyying options<br>A$="Options"<br>Return <br><br><br>.score<br>;Displaying score<br>A$="Score"<br>Return<br><br>.drawscreen<br>Cls<br>Graphics3D 640,480 <br>screen=LoadImage("textures/truewelcome.png")<br>DrawImage screen,0,0<br>Const font_nameA$="ariel"<br>Const font_size=20<br><br><br>Color $ff,$ff,$ff<br>Text 0,0,"Last item choosen was: "+A$<br><br>;If variable number Then Color red, Else Color white<br><br>If menue=1 Then Color $ff,0,0 Else Color $ff,$ff,$ff<br>Text 10,100,"Start"<br><br><br>If menue=2 Then Color $ff,0,0 Else Color $ff,$ff,$ff<br>Text 10,140,"Controls"<br><br><br>If menue=3 Then Color $ff,0,0 Else Color $ff,$ff,$ff<br>Text 10,180,"Score"<br><br><br>If menue=4 Then Color $ff,0,0 Else Color $ff,$ff,$ff<br>Text 10,220,"Options"<br><br><br>Return<br><br><br>.menukeys<br>If MilliSecs()-menuedelay &gt; 75             ;Delaying the keypress or else it would be too fast, switching between 1 and 3<br>	menuedelay=MilliSecs()<br>	<br>	If KeyDown(200) Then menue=menue-1		;KeyUp<br>	If KeyDown(208) Then menue=menue+1		;KeyDown<br>	If menue&lt;=1 Then menue=1<br>	If menue=&gt;4 Then menue=4<br>EndIf<br><br>Return <br><br></td></tr></table><br>
<a name="1307640"></a>

<a name="1307641"></a>

<a name="1307642"></a>

<a name="1307643"></a>

<a name="1307644"></a>

<a name="1307645"></a>

<a name="1307646"></a>

<a name="1307647"></a>

<a name="1307648"></a>

<a name="1307651"></a>

<a name="1307652"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dan</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, please put the code which you want to write here on forums, in code boxes.<br><br>Before pasting the text, use <br><br><div class="quote"> <br>Codebox shoud be written in lower case letters:<br><br>[Codebox]<br>   paste your text here<br>[/Codebox]<br> <br></div><br><br><br>Here is the above Code with some Improvements. <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
menue=1
menuedelay=MilliSecs()
a$=""


Graphics3D 640,480,16,2

SetBuffer BackBuffer()

screen=LoadImage("textures/truewelcome.png")

Font=LoadFont("Arial",30,False,False,False)    ;This will load the desired Font
SetFont font                                   ;This sets the Font for use

;Main menue loop starts here
Repeat
	
	Gosub menukeys
	Gosub drawscreen
	
	If KeyDown(28) Or KeyDown(57) ;Enter or Space
		Select menue
			Case 1
				Gosub start
			Case 2
				Gosub controls
			Case 3
				Gosub score
			Case 4
				Gosub options
		End Select 
	EndIf
	Flip 
Until KeyDown(1)
;Escape key exits the main menue loop

End

.start
;game start
A$="Starting"
Return

.controls
;Setting up controls
A$="Controls"
Return

.options
;geyying options
A$="Options"
Return 


.score
;Displaying score
A$="Score"
Return

.drawscreen
Cls

DrawImage screen,0,0

Color $ff,$ff,$ff
Text 0,0,"Last item choosen was: "+A$                 ;This displays which menu you have used. you can remove this line and replace it with your own text

;If variable number Then Color red, Else Color white

If menue=1 Then Color $ff,0,0 Else Color $ff,$ff,$ff
Text 10,100,"Start"


If menue=2 Then Color $ff,0,0 Else Color $ff,$ff,$ff
Text 10,140,"Controls"


If menue=3 Then Color $ff,0,0 Else Color $ff,$ff,$ff
Text 10,180,"Score"


If menue=4 Then Color $ff,0,0 Else Color $ff,$ff,$ff
Text 10,220,"Options"


Return


.menukeys
If MilliSecs()-menuedelay &gt; 75 ;Delaying the keypress or else it would be too fast, switching between 1 and 3
	menuedelay=MilliSecs()
	
	If KeyDown(200) Then menue=menue-1	;KeyUp
	If KeyDown(208) Then menue=menue+1	;KeyDown
	If menue&lt;=1 Then menue=1
	If menue=&gt;4 Then menue=4
EndIf

Return 
</textarea><br><br>You want to make sure, that Graphic command is Called only 1 time in your program.<br><br>Setbuffer command is used, normaly, after Graphic.<br><br>Loading Images shall be done after Graphic command is set. They will Remain in the memory, so you dont need to load them in a loop.<br><br>Loading images and then executing the Graphic command will clear every loaded images. This will Crash your program, as soon as you try to draw the image.<br><br><br><div class="quote"> <br>how or where i would create a code to when i press start it would start the game<br> <br></div><br><br>This is the code which do something after you hit enter:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
	If KeyDown(28) Or KeyDown(57) ;Enter or Space
		Select menue
			Case 1
				Gosub start
			Case 2
				Gosub controls
			Case 3
				Gosub score
			Case 4
				Gosub options
		End Select 
	EndIf
</textarea><br><br>After hitting the Return button,the Gosub statement leads you to the labels. To start the game, you would then make a game loop inbetween .start and the Return :<br><pre class=code>
.start         ;.start is an label, here does the "Gosub start" jumps into.
;game start

A$="Starting"  ;this is the text, which is displayed in my example.It is here,where your game starts.

Return         ;After the Game ends, Return jumps back to the line where Gosub start was called
</pre> <br><br></td></tr></table><br>
<a name="1308087"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RubyEnemy</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> how would i have an image showing for the controls of my game when  i hit enter on the controls option and i can go back to the menu screen again? <br><br></td></tr></table><br>
<a name="1308717"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RubyEnemy</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> so any helpful tips? <br><br></td></tr></table><br>
<a name="1308759"></a>

<a name="1308762"></a>

<a name="1308763"></a>

<a name="1308765"></a>

<a name="1308766"></a>

<a name="1308767"></a>

<a name="1308768"></a>

<a name="1308813"></a>

<a name="1308814"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dan</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> you want to display a picture, which is explaining which keys are used ?<br><br>then youll have to load a picture into a variable, right after screen=LoadImage("textures/truewelcome.png")<br><br><pre class=code>
img_controls=LoadImage("images/controls.png")
</pre><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
.controls
 ;Displaying control screen

 Flushkeys()    ;reset the previous pressed keys
 Repeat
	Cls
	DrawImage img_controls,0,0
	Delay 1
	Flip
 Until CheckReturn()  ;Enter or Return keys

Return
</textarea><br><br>at the end of the listing use (copy&amp;paste) following function:<br><br><pre class=code>
Function CheckReturn()
 if keydown(28) or Keydown(57)      ;check if user has pressed the enter or return keys
   while keydown(28) or Keydown(57) ;Wait until user has released the keys
      delay 1
   Wend
   Return 1
 EndIf
 Return 0 
End Function
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
