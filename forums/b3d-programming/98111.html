<!DOCTYPE html><html lang="en" ><head ><title >UDP Stream does not exist?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >UDP Stream does not exist?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >UDP Stream does not exist?</a><br><br>
<a name="1143901"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, all. As you may or may not already know, I've been working on a server and client example. So I found this GODSEND of a UDP Library, called "NetBlitz3D". Can someone PLEASE tell me how I can make it so when I join the game, NOT as a host, it doesnt say "UDP Stream does not exist" ?<br><br>Here's the includes file: <br><br>nb.bb: <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;NetBlitz, The Ultimate Best System Of Easy UDP, using DirectPlay like commands
;Created By David Dawkins, credit would be nice if you use these functions.
;Current Version: 1.3
;Special Thanks goes to Surreal for helping me figure out that 'localhost' is diff than ''

;Needed for directplay type commands
Type nbplayer
Field name$,id$
Field host,ip,port
End Type

Global serverip,nbmessage$,nbname$,serverport,nbuserid$

Function StartNBGame(port=80)
c = CreateUDPStream(port)
dport = UDPStreamPort(c)
If dport &lt;&gt; port Then CloseUDPStream(c):c = 0
DebugLog "Created New NetBlitz Game"
Return c
End Function

Function CreateNBPlayer$(name$,sip$,udpstream,hosting=0)
DebugLog "Created NetBlitz Player: "+name
go = CountHostIPs(sip) 
ip = HostIP(1)
nbname = name
n.nbplayer = New nbplayer
n\name = name:n\port=nbgameport(udpstream)
If hosting = 1
serverip = ip:n\host= 1
serverport = nbgameport(udpstream)
EndIf
If hosting = 0
serverip = ip
WriteString(udpstream,"100:"+name)
SendUDPMsg(udpstream,ip,80)
;Delay 1000
CheckNBDataBase(udpstream,0)
EndIf
CountHostIPs("")
n\id= converttoid(HostIP(1),nbgameport(udpstream))
n\ip=HostIP(1)
nbuserid = converttoid(HostIP(1),nbgameport(udpstream))
Return nbuserid
End Function

Function DeleteNBPlayer(udpstream)
DebugLog "Deleted NetBlitz Player"
For i.nbplayer = Each nbplayer
If i\id &lt;&gt; nbuserid
WriteString(udpstream,"101:")
SendUDPMsg(udpstream,i\ip,i\port)
EndIf
Next
End Function

Function StopNBGame(udpstream)
DebugLog "NetBlitz Game Stopped"
CloseUDPStream(udpstream)
End Function

Function NBPlayerName$(id$)
For n.nbplayer= Each nbplayer
If n\id = id Then Return n\name
Next
End Function

Function NBPlayerLocal(ip) 
CountHostIPs("")
If HostIP(1) = ip
Return 1
Else
Return HostIP(1)
EndIf
End Function

Function RecvNBMsg(udpstream,readm=1)
g = RecvUDPMsg(udpstream)
If g &lt;&gt; 0
DebugLog "Recieved NetBlitz Message"
If readm = 1
nbmessage = ReadString(udpstream)
EndIf
Return 1
EndIf
Return 0
End Function

Function NBMsgType(udpstream)
typ = Left(nbmessage,3)
Return typ
End Function

Function NBMsgFrom$(udpstream)
ip = UDPMsgIP(udpstream)
port = UDPMsgPort(udpstream)
tempid$=converttoid(ip,port)
For n.nbplayer = Each nbplayer
If n\id = tempid Then Return n\id
Next
End Function

Function NBMsgData$(udpstream)
msg$ = nbmessage
index=Instr( msg,":" )
msg = Mid(msg$,index+1)
Return msg
End Function

Function SendNBMsg(typ,msg$,idto$,udpstream)
If idto = 0
For i.nbplayer = Each nbplayer
If i\id &lt;&gt; nbuserid
WriteString(udpstream,typ+":"+msg)
SendUDPMsg(udpstream,i\ip,i\port)
EndIf
Next
Else
For i.nbplayer = Each nbplayer
If i\id = idto
WriteString(udpstream,typ+":"+msg)
SendUDPMsg(udpstream,i\ip,i\port)
EndIf
Next
EndIf
DebugLog "Sent Message: "+msg
End Function

;Extra NetBlitz Commands To Make The Internet Easier
Function NBMsgTimeOut(milliseconds)
UDPTimeouts(milliseconds)
End Function

Function WriteNBString(typ,texts$,udpstream)
WriteString(udpstream,typ+":"+texts)
End Function

Function SendNBPacket(idto$,udpstream)
If idto = 0
For i.nbplayer = Each nbplayer
If i\id &lt;&gt; nbuserid
SendUDPMsg(udpstream,i\ip,i\port)
EndIf
Next
Else
For i.nbplayer = Each nbplayer
If i\id = idto
SendUDPMsg(udpstream,i\ip,i\port)
EndIf
Next
EndIf
DebugLog "Sent Message Packet"
End Function

Function NBCopyPacket(udpstream,deststream,buffer)
CopyStream udpstream,deststream,buffer
End Function

Function CheckNBDatabase(udpstream,msgtype=1)
If msgtype = 0
time = MilliSecs()
Repeat
x = recvnbmsg(udpstream,1)
If x &gt; 0
If nbmsgtype(udpstream) &lt;&gt; 102
x = 0
EndIf
EndIf
If MilliSecs() - time &gt; 10000
RuntimeError "Timeout Error"+Chr(10)+"Could Not Connect To Server"
EndIf
Until x &gt; 0
If nbmsgtype(udpstream) = 102
serverport = nbmsgport(udpstream)
serverip = nbmsgip(udpstream)
;i.nbplayer = New nbplayer
;i\ip = nbmsgfrom(udpstream)
;i\port = nbmsgport(udpstream)
;readnbpacket(udpstream)
;i\name = nbmsgdata(udpstream)
;i\id = Int(Str$(serverip)+Str$(serverport))
serverid$=converttoid(serverip,serverport)

n.nbplayer = New nbplayer
n\name = nbmsgdata(udpstream)
n\ip = ReadInt(udpstream)
n\port = ReadInt(udpstream)
n\id = converttoid(n\ip,n\port)
While Not Eof(udpstream)
n.nbplayer = New nbplayer
msg$ = ReadString(udpstream)
index=Instr( msg,":" )
msg = Mid(msg$,index+1)
n\name = msg
n\ip = ReadInt(udpstream)
n\port = ReadInt(udpstream)
n\id = converttoid(n\ip,n\port)
Wend

For i.nbplayer = Each nbplayer
If i\id = serverid Then i\host=1 : Exit
Next

For n.nbplayer = Each nbplayer
If n\host = 0 And n\id &lt;&gt; nbuserid
WriteString(udpstream,"100:"+nbname)
SendUDPMsg(udpstream,n\ip,n\port)
EndIf
Next

DebugLog countnbplayers()+" Playing"
Return 1
EndIf
ElseIf msgtype = 1
;WriteString(udpstream,"102:TempMessage")
;WriteString(udpstream,"102:"+nbplayerlocal()) Experimenting
;WriteString(udpstream,"102:"+nbname)
;WriteString(udpstream,"102:"+nbgameport(udpstream))
For i.nbplayer = Each nbplayer
WriteString(udpstream,"102:"+i\name)
WriteInt(udpstream,i\ip)
WriteInt(udpstream,i\port)
Next
i.nbplayer = New nbplayer
i\name = nbmsgdata(udpstream)
i\ip = nbmsgip(udpstream)
i\port = nbmsgport(udpstream)
i\id = converttoid(i\ip,i\port)
sendnbpacket(i\id,udpstream)
Return 1
EndIf
End Function

Function CountNBPlayers()
For i.nbplayer = Each nbplayer
bcount = bcount + 1
Next
Return bcount
End Function

Function ReadNBPacket(udpstream)
nbmessage = ReadString(udpstream)
Return nbmessage
End Function

Function NBGamePort(udpstream)
port = UDPStreamPort(udpstream)
Return port
End Function

Function NBMsgPort(udpstream)
port = UDPMsgPort(udpstream)
Return port
End Function

Function NBMsgIP(udpstream)
ip = UDPMsgIP(udpstream)
Return ip
End Function

Function CheckAvailPorts()
For br = 1 To 10000
c = CreateUDPStream(br)
If c &lt;&gt; 0
Exit
EndIf
Next
CloseUDPStream(c)
Return br
End Function

Function NBPlayerPort(id$)
For i.nbplayer = Each nbplayer
If i\id = id
Return i\port
EndIf
Next
End Function

Function NBPlayerIP(id$)
For i.nbplayer = Each nbplayer
If i\id = id
Return i\ip
EndIf
Next
End Function

Function ConvertToID$(ip,port)
s$ = Str$(ip)+"-"+Str$(port)
Return s
End Function</textarea><br><br>Example.bb: <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;Here is a more realistic demo of NetBlitz, this is 3d and created by David Dawkins
;The level was built in Maplet

AppTitle "NetBlitz 3d!"
SeedRnd MilliSecs()

Type player

	Field x#,y#,z#

	Field name$,net_id$

	Field ent,cam,piv

End Type

;Spawn Points
;1,-3,0
;63,5,21
;-61,3,-19
;-31,-11,2

loca%=1
host%=1
ip$ = "127.0.0.1"
port% = 5000

Include "nb.bb"

Global mainstream = startnbgame(port%)

If mainstream = 0 Then RuntimeError("Port is already being used!") : End

;name$ = Input("Enter Name: ")

name$ = "Thundros"

Graphics3D 640,480,0,2

Global level = CreatePlane() ;LoadMesh("test3d.b3d")
EntityColor level, 0, 64, 0

EntityFX level,1

MoveEntity level,0,-5,0

CreateLight()

EntityType level,2

Global play.player

Global mastertime ;This timer is so that all the positions are right

id$ = CreatenbPlayer( name$,ip,mainstream,host )

play = createplayer(name,id)

If host = 0

	For n.nbplayer = Each nbplayer
	
		If n\id &lt;&gt; nbuserid
		
			pack$ = play\x+" "+play\y+" "+play\z

			SendNBMsg(1,pack,n\id,mainstream)

			p.player = createplayer(n\name,n\id)

			FreeEntity p\cam
			
		EndIf
		
	Next
	
EndIf

Collisions 1,2,2,1

Main()

Function Main()

mastertime = MilliSecs()

While Not KeyHit(1)

	If KeyDown(200) Or KeyDown(17) ;Forward
	
		MoveEntity play\ent,0,0,.5

		SendNBMsg(2,"1",0,mainstream)

	ElseIf KeyDown(208) Or KeyDown(31) ;Back

		MoveEntity play\ent,0,0,-.5

		SendNBMsg(2,"2",0,mainstream)
		
	EndIf

	If KeyDown(203) Or KeyDown(30) ;Left
	
		TurnEntity play\ent,0,4,0
		
		SendNBMsg(2,"3",0,mainstream)
		
	ElseIf KeyDown(205) Or KeyDown(32) ;Right
	
		TurnEntity play\ent,0,-4,0

		SendNBMsg(2,"4",0,mainstream)
		
	EndIf
	
	TranslateEntity play\ent,0,-.2,0

	dx#=EntityX( play\piv,True )-EntityX( play\cam,1 )
	dy#=EntityY( play\piv,True )-EntityY( play\cam,1 )
	dz#=EntityZ( play\piv,True )-EntityZ( play\cam,1 )

	TranslateEntity play\cam,dx*.1,dy*.1,dz*.1

	PointEntity play\cam,play\ent

	If KeyHit(2)
	
		DebugLog(EntityX(play\ent))
		DebugLog(EntityY(play\ent))
		DebugLog(EntityZ(play\ent))
		
	EndIf

	UpdateNetwork()

	UpdateWorld()
	RenderWorld()

	play\x = EntityX(play\ent)
	play\y = EntityY(play\ent)
	play\z = EntityZ(play\ent)

	If MilliSecs() - mastertime &gt; 15000
	
		pack$ = play\x+" "+play\y+" "+play\z
		SendNBMsg(1,pack,0,mainstream)
		mastertime = MilliSecs()
		
	EndIf

	For p.player = Each player
	
		CameraProject(play\cam,EntityX(p\ent),EntityY(p\ent),EntityZ(p\ent))
		If ProjectedZ() &gt; 0 Then Text(ProjectedX(),ProjectedY(),"&lt;"+p\name+"&gt;",1,1)
		
	Next

	Flip

Wend

		deletenbplayer(mainstream)
		
		Stopnbgame(mainstream)

		End

End Function

Function CreatePlayer.player(name$,id$)

spawn = Rand(1,4)

	p.player = New player
	
	p\x = 0
	p\y = 0
	p\z = 0
	
	p\name = name$

	p\ent = CreateSphere()
	UpdateNormals p\ent

	cone = CreateCone()

	RotateMesh(cone,90,0,0)
	PositionMesh cone,0,0,2
	ScaleMesh cone,.5,.5,.5
	AddMesh cone,p\ent
	FreeEntity cone

p\cam = CreateCamera()
CameraRange p\cam,.1,50000

AmbientLight 255, 255, 255

p\piv = CreatePivot(p\ent)

MoveEntity p\piv,0,5,-10

EntityColor p\ent,Rnd(255),Rnd(255),Rnd(255)

EntityType p\ent,1:EntityType p\cam,1

p\net_id = id

	If spawn = 1
	
		p\x     =   1
		p\y     =   3
		p\z     =   0
	
	ElseIf spawn = 2
	
		p\x 	=  63
		p\y 	=   5
		p\z 	=  21
		
	ElseIf spawn = 3
	
		p\x 	= -61
		p\y 	=   5
		p\z 	= -19
	
	ElseIf spawn = 4
	
		p\x 	=  -3
		p\y 	=  11
		p\z 	=   2
	
	EndIf

	PositionEntity p\ent,p\x,p\y,p\z
	PositionEntity p\cam,p\x,p\y,p\z
	
	ResetEntity p\ent
	ResetEntity p\cam

	Return p

End Function

Function UpdateNetwork()

While RecvNBMsg(mainstream)

	Select NBMsgType(mainstream)

		Case 100 ;New Player

			serverid$ = converttoid(serverip,serverport)

			If nbuserid = serverid

				checknbdatabase(mainstream) ;Make sure the host only does this

			ElseIf nbuserid &lt;&gt; serverid ;If you are not the server

			i.nbplayer = New nbplayer

				i\name = nbmsgdata(mainstream)
	
				i\ip = nbmsgip(mainstream)
	
				i\port = nbmsgport(mainstream)
	
				i\id = converttoid(i\ip,i\port)

			EndIf
			
				p.player = CreatePlayer(nbmsgdata(mainstream),nbmsgfrom(mainstream))
	
				FreeEntity p\cam
	
				pack$ = play\x+" "+play\y+" "+play\z
				
				SendNBMsg(1,pack,nbmsgfrom(mainstream),mainstream)
				
				pitch# = EntityPitch(play\ent)
				roll# = EntityRoll(play\ent)
	
				pack$=LSet$( pitch,7 )+LSet$( EntityYaw(play\ent),7 )+LSet$( roll,7 )
	
				SendNBMsg(3,pack,nbmsgfrom(mainstream),mainstream)

		Case 101 ;Player Left

			For p.player = Each player

				If p\net_id = nbmsgfrom(mainstream)
	
					FreeEntity p\ent
	
						For n.nbplayer = Each nbplayer
	
							If n\id = p\net_id Then Delete n
	
						Next
	
					Delete p : Exit
	
				EndIf

			Next
			
		Case 1 ;Position File

			For p.player = Each player

				 If p\net_id = nbmsgfrom(mainstream)

					 dat$ = nbmsgdata$(mainstream)
					 Local num1$,num2$,num3$,count
			
					 	For wc = 1 To Len(dat)
			
							 let$ = Mid(dat,wc,1)
			
							 If let = " " Then count = count + 1
			
								 If let &lt;&gt; " "
								
									 If count = 0
									
										 num1 = num1 + let
										
									 ElseIf count = 1
									
										 num2 = num2 + let
										
									 ElseIf count = 2
									
										 num3 = num3 + let
										
									 EndIf
			
								 EndIf
			
							 Next
			
								 p\x = Float(num1)
								 p\y = Float(num2)
								 p\z = Float(num3)
						
								 HideEntity p\ent
					
								 PositionEntity p\ent,p\x,p\y,p\z : ShowEntity p\ent
		
						 Exit
	
				  EndIf
				
			 Next
			
		Case 2 ;Movement File
		
			For p.player = Each player

				If p\net_id = nbmsgfrom(mainstream)

					move = nbmsgdata$(mainstream)

						If move = 1 ;Forward
	
							MoveEntity p\ent,0,0,.5
		
						ElseIf move = 2;Back
		
							MoveEntity p\ent,0,0,-.5
		
						ElseIf move = 3;Left
		
							TurnEntity p\ent,0,4,0
		
						ElseIf move = 4;Right
		
							TurnEntity p\ent,0,-4,0
	
						EndIf

					Exit

				EndIf

			Next

		Case 3 ;Rotation File

			For p.player = Each player
			
				If p\net_id = nbmsgfrom(mainstream)
				
						dat$ = nbmsgdata$(mainstream)
	
						rotx# = Float(Mid(dat,1,7))
						roty# = Float(Mid(dat,8,7))
						rotz# = Float(Mid(dat,15,7))
	
						RotateEntity p\ent,rotx,roty,rotz
	
					Exit
					
				EndIf
				
			Next
			
	End Select

Wend

	For p.player = Each player
	
		If p\net_id &lt;&gt; nbuserid

			TranslateEntity p\ent,0,-.2,0

		EndIf
		
	Next

End Function

Function select_host()

	loca = Input("Local Game?(1 - Yes,0 - No) ")
		
	host = Input("Hosting Or Joining:(0 = Join,1 = Host) ")

	If host = 0 And loca  = 0
	
		ip$ = Input("Select IP: ")
	
		port = Input("Select Port: ")
	
	ElseIf host = 0 And loca = 1
		
		ip = ""
	
		port = Input("Select Port: ")
		
	ElseIf host = 1 And loca = 0
		
		ip = ""
	
		port = 5000
		
	ElseIf host = 1 And loca = 1
		
		ip = ""
	
		port = 5000
	
	EndIf

End Function</textarea><br><br>Any help is greatly appreciated!<br><br>Thank You! :) <br><br></td></tr></table><br>
<a name="1145504"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> CAn't say for sure, but I imagine it's either<br><br><br> a: There's no instance created broadcasting/listening as a server<br><br>or <br><br> b: Connecting to a local (129.168...) or non-local IP or localhost(127.0.0.1) when other is preferred. I.e. The server is running on either on the same machine or within a local network (LAN),  though the client is attempting to (or instead, should rather be) connect to an external IP or vice-versa. <br><br> c: Ports are not confiugured for listening etc.<br><br>It would seem that the error message is telling you that there is no UDP stream, or the expected stream handle is returning null, meaning the stream hasn't been opened or has not been received correctly. This should be your first point of call in where to look. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
