<!DOCTYPE html><html lang="en" ><head ><title >Jagged Edges</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Jagged Edges</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Jagged Edges</a><br><br>
<a name="985437"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >John Blackledge</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> So now I've got my planets and atmospheres sorted I still have the problem of the planet curved edges looking very jagged against the black space background.<br><br>Can I ask for everyone's ideas on the best method to get rid of the jaggies?<br><br>If only anti-aliasing worked.<br><br>I've tried the fast libraries - they seem to have concentrated on filters of various types - but even their own demos still have spheres/balls with jagged edges.<br><br>I'm out for the day, but I'd still appreciate comments/observations from anyone, and I'll pick them up later. <br><br></td></tr></table><br>
<a name="985439"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> One way that works is to take advantage of render-to-texture, to render a viewport twice as large as your desired result, paste it on a fullscreen quad, and then render <i>that</i> to the backbuffer at the desired output resolution. I believe they call this supersampling.<br><br>There are two big disadvantages here: 1) rendering scenes at ridiculously high resolutions may damage performance significantly on some setups, and 2) DirectX7 has an upper resolution limit, which I believe is 2048x1536, so if your end resolution is greater than 1024x768 then you'll either need to put up with imperfect supersampling or come up with some kind of multiple-render arrangement for different quadrants of the screen. There are other slightly more complex methods of supersampling that don't suffer from these problems but they'd be harder to implement without shaders.<br><br>Will post an example if I can find it. <br><br></td></tr></table><br>
<a name="985446"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't like jagged edges either (specially when color contrast heightens it up), but if you think about it, the majority of professional games don't really do anything about it.<br><br>Try playing games like Microsoft's Freelancer to see how you don't really care about the edges when the atmosphere in the game makes it look like the phenomenon is "part of that world":<br><a href="http://img43.imageshack.us/img43/1182/905d1208160023freelance.jpg" target="_blank"><br><img src="http://img12.imageshack.us/img12/1182/905d1208160023freelance.jpg"></a><br><br>PS: Anti-Alias does work in Blitz3D, just not directly <b>from</b> it. I went to nVidia Control panel and set the anti-alias to "mandatory" or something like that (meaning, doesn't matter if the application has the anti-alias setting off, the card will always use it), and voilа, the B3D window was being rendered with anti-alias.<br><br>It's up to someone like MikhailV (or others who understand Blitz3D's mysterious structure) to figure out what flag inside Blitz3D turns off anti-alias for good, and overwrite that. <br><br></td></tr></table><br>
<a name="985449"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Found it.<br><br>Dump this in your FastExtend folder with the normal examples, for the media's sake.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; Example of use FastExt library
; (c) 2006-2009 created by MixailV aka Monster^Sage [monster-sage@...]  <a href="http://www.fastlibs.com" target="_blank">http://www.fastlibs.com</a>



Include "include\FastExt.bb"	; &lt;&lt;&lt;&lt;    Include FastExt.bb file

Graphics3D 800,600,32,6

InitExt					; &lt;&lt;&lt;&lt; 	Обязательно инициализуем после Graphics3D
						;               Initialize library after Graphics3D function


; Создадим 3Д мир для примера
; Create world objects
Global Camera, Light, Sphere, Cylinder, Help : CreateScene


Local FSquad=CreateQuad(GraphicsWidth()/2,GraphicsHeight()/2,Camera):EntityFX FSquad,1
SetQuadUVs(FSquad,0,0,GraphicsWidth()/2^Ceil(Log(GraphicsWidth())/Log(2)),GraphicsHeight()/2^Ceil(Log(GraphicsHeight())/Log(2)))
Local FStex=CreateTexture(GraphicsWidth()*2,GraphicsHeight()*2,1+2+48+256+FE_RENDER+FE_ZRENDER)
EntityTexture FSquad,FStex
PositionEntity FSquad,0,0,GraphicsWidth()/2

Local FSAAon=1
Local FPScount,FPStime,FPSframes

; Главный цикл
; Main loop
While Not KeyDown(1)
	MouseLook Camera
	AnimateScene
	
	If KeyHit(57) Then FSAAon=Not FSAAon
	
	If FSAAon
		HideEntity FSquad
		SetBuffer TextureBuffer(FStex)
		CameraViewport Camera,0,0,GraphicsWidth()*2,GraphicsHeight()*2
		RenderWorld
		
		ShowEntity FSquad
		SetBuffer BackBuffer()
		CameraViewport Camera,0,0,GraphicsWidth(),GraphicsHeight()
		RenderEntity(FSquad,Camera,True)
	Else
		HideEntity FSquad
		RenderWorld
	EndIf
	
	If MilliSecs()-FPStime=&gt;1000 Then FPScount=FPSframes:FPSframes=0:FPStime=MilliSecs():Else FPSframes=FPSframes+1
	Text 10,20,"FPS: "+FPScount
	Text 10,30,"Press SPACE to toggle mode"
	
	Flip 0
Wend

FreeTexture FStex
DeInitExt
End





Function CreateQuad(xsize#=1,ysize#=1,parent=0)
	Local mesh,surf,v0,v1,v2,v3,maxu#,maxv#
	
	If xsize&gt;ysize
		maxu=1
		maxv=ysize/Float(xsize)
	Else
		maxv=1
		maxu=xsize/Float(ysize)
	EndIf
	
	mesh=CreateMesh(parent)
	surf=CreateSurface(mesh)
	v0=AddVertex(surf,-xsize, ysize,0,   0,   0)
	v1=AddVertex(surf, xsize, ysize,0,maxu,   0)
	v2=AddVertex(surf, xsize,-ysize,0,maxu,maxv)
	v3=AddVertex(surf,-xsize,-ysize,0,   0,maxv)
	AddTriangle(surf,v0,v1,v2)
	AddTriangle(surf,v2,v3,v0)
	Return mesh
End Function

Function SetQuadUVs(quad,minx#,miny#,maxx#,maxy#)	;Mainly for the FSQuad
	Local s=GetSurface(quad,1)
	
	VertexTexCoords s,0,minx,miny
	VertexTexCoords s,1,maxx,miny
	VertexTexCoords s,2,maxx,maxy
	VertexTexCoords s,3,minx,maxy
End Function



; разные вспомогательные функции
; auxiliary functions

Function MouseLook ( ent, mov# = 0.3 )
	mxspd#=MouseXSpeed()*0.25
	myspd#=MouseYSpeed()*0.25	
	campitch#=EntityPitch(ent)+myspd#
	If campitch#&lt;-65 Then campitch#=-65
	If campitch#&gt;65 Then campitch#=65
	RotateEntity ent,campitch#,EntityYaw(ent)-mxspd#,EntityRoll(ent)
	If KeyDown(17) MoveEntity ent,0,0,mov
	If KeyDown(31) MoveEntity ent,0,0,-mov
	If KeyDown(32) MoveEntity ent,mov,0,0
	If KeyDown(30) MoveEntity ent,-mov,0,0
	MoveMouse GraphicsWidth()/2,GraphicsHeight()/2
End Function

Function Musor()
	For i=1 To 50
		cub=CreateCube()
		EntityColor cub,Rand(128,255),Rand(128,255),Rand(128,255)
		;EntityAlpha cub,Rnd(0.3,1.0)
		PositionEntity cub,Rnd(-10,10),Rnd(-10,10),Rnd(5,15)
		ScaleEntity cub,Rnd(0.3,0.5),Rnd(0.3,0.5),Rnd(0.3,0.5)
		;TurnEntity cub,Rnd(0,90),Rnd(0,90),Rnd(0,90)
		;EntityFX cub,32
	Next
End Function

Function LoadSkyBox( file$ )
	m=CreateMesh()
	;front face
	b=LoadBrush( file$+"_FR.jpg",49 )
	s=CreateSurface( m,b )
	AddVertex s,-1,+1,-1,0,0:AddVertex s,+1,+1,-1,1,0
	AddVertex s,+1,-1,-1,1,1:AddVertex s,-1,-1,-1,0,1
	AddTriangle s,0,1,2:AddTriangle s,0,2,3:
	FreeBrush b
	;right face
	b=LoadBrush( file$+"_LF.jpg",49 )
	s=CreateSurface( m,b )
	AddVertex s,+1,+1,-1,0,0:AddVertex s,+1,+1,+1,1,0
	AddVertex s,+1,-1,+1,1,1:AddVertex s,+1,-1,-1,0,1
	AddTriangle s,0,1,2:AddTriangle s,0,2,3
	FreeBrush b
	;back face
	b=LoadBrush( file$+"_BK.jpg",49 )
	s=CreateSurface( m,b )
	AddVertex s,+1,+1,+1,0,0:AddVertex s,-1,+1,+1,1,0
	AddVertex s,-1,-1,+1,1,1:AddVertex s,+1,-1,+1,0,1
	AddTriangle s,0,1,2:AddTriangle s,0,2,3
	FreeBrush b
	;left face
	b=LoadBrush( file$+"_RT.jpg",49 )
	s=CreateSurface( m,b )
	AddVertex s,-1,+1,+1,0,0:AddVertex s,-1,+1,-1,1,0
	AddVertex s,-1,-1,-1,1,1:AddVertex s,-1,-1,+1,0,1
	AddTriangle s,0,1,2:AddTriangle s,0,2,3
	FreeBrush b
	;top face
	b=LoadBrush( file$+"_UP.jpg",49 )
	s=CreateSurface( m,b )
	AddVertex s,-1,+1,+1,0,1:AddVertex s,+1,+1,+1,0,0
	AddVertex s,+1,+1,-1,1,0:AddVertex s,-1,+1,-1,1,1
	AddTriangle s,0,1,2:AddTriangle s,0,2,3
	FreeBrush b
	;bottom face	
	b=LoadBrush( file$+"_DN.jpg",49 )
	s=CreateSurface( m,b )
	AddVertex s,-1,-1,-1,1,0:AddVertex s,+1,-1,-1,1,1
	AddVertex s,+1,-1,+1,0,1:AddVertex s,-1,-1,+1,0,0
	AddTriangle s,0,1,2:AddTriangle s,0,2,3
	FreeBrush b
	ScaleMesh m,100,100,100
	FlipMesh m
	EntityFX m,1
	Return m
End Function

Function Reflect(Entity,Camera,RTexture)
	Local fxCamera = CreateCamera()
	HideEntity Entity
	WaterMapSize=TextureWidth(RTexture)
	CameraProjMode fxCamera,1
	CameraProjMode Camera,0
	CameraViewport fxCamera,0,0,WaterMapSize,WaterMapSize
	PositionEntity fxCamera,EntityX(Entity,1), EntityY(Entity,1) ,EntityZ(Entity,1),1; EntityY(Entity)
	; do left view
	SetCubeFace RTexture,0
	RotateEntity fxCamera,0,90,0
	RenderWorld
	CopyRect 0,0,WaterMapSize,WaterMapSize,0,0,BackBuffer(),TextureBuffer(RTexture)
	; do forward view
	SetCubeFace RTexture,1
	RotateEntity fxCamera,0,0,0
	RenderWorld
	CopyRect 0,0,WaterMapSize,WaterMapSize,0,0,BackBuffer(),TextureBuffer(RTexture)
	; do right view	
	SetCubeFace RTexture,2
	RotateEntity fxCamera,0,-90,0
	RenderWorld
	CopyRect 0,0,WaterMapSize,WaterMapSize,0,0,BackBuffer(),TextureBuffer(RTexture)
	; do backward view
	SetCubeFace RTexture,3
	RotateEntity fxCamera,0,180,0
	RenderWorld
	CopyRect 0,0,WaterMapSize,WaterMapSize,0,0,BackBuffer(),TextureBuffer(RTexture)
	; do up view
	SetCubeFace RTexture,4
	RotateEntity fxCamera,-90,0,0
	RenderWorld
	CopyRect 0,0,WaterMapSize,WaterMapSize,0,0,BackBuffer(),TextureBuffer(RTexture)		
	SetCubeFace RTexture,5
	RotateEntity fxCamera,90,0,0
	RenderWorld
	CopyRect 0,0,WaterMapSize,WaterMapSize,0,0,BackBuffer(),TextureBuffer(RTexture)	
	db=db+1
	CameraProjMode fxCamera,0
	CameraProjMode Camera,1
	ShowEntity entity
End Function

Function CreateScene()
	SetFont LoadFont("Tahoma",13)
	AmbientLight 50,50,70
	
	Camera=CreateCamera()
	ScaleEntity Camera,1,1,1
	CameraRange Camera,1,10000
	
	
	PositionEntity Camera,0,40,95
	RotateEntity Camera,1,180,0
	
	Sky = LoadSkyBox ( "..\media\sky" )
	ScaleEntity Sky, 10, 10, 10
	EntityOrder sky,1
	
	Light=CreateLight(2)
	
	Center=CreatePivot()
	
	PositionEntity Light,50,80,0
	
	ReflectTex = CreateTexture(128,128,  128+256)
	
	tex1=LoadTexture("..\media\wall.jpg")
	ScaleTexture tex1,.3,.3
	tex2=LoadTexture("..\media\wood.jpg")
	
	modelR=LoadMesh( "..\media\teapot.x" )
	PositionEntity modelR,0,35,0
	ScaleEntity modelR, 15, 15, 15
	EntityTexture modelR, ReflectTex
	EntityShininess modelR,1
	
	Sphere=CreateSphere(16)
	PositionEntity Sphere,40,40, 0
	ScaleEntity Sphere,10,10,10
	EntityTexture Sphere, ReflectTex
	EntityShininess Sphere,1
	
	Cylinder=CreateCylinder(12,1)
	PositionEntity Cylinder,30,10,20
	ScaleEntity Cylinder,5,15,5
	RotateEntity Cylinder,91,0,0
	EntityTexture Cylinder,tex2
	
	
	
	cube2=LoadMesh("..\media\cube.3DS"); create receiver
	ScaleEntity cube2,180,2,180
	EntityTexture cube2,tex1
	
	cube3=CreateCylinder(16)
	ScaleMesh cube3,3,15,3
	sp = CreateSphere(12)
	ScaleMesh sp ,5,5,5
	PositionMesh sp,0,20,0
	AddMesh sp, cube3
	FreeEntity sp
	
	
	PositionEntity cube3,60,10,60
	EntityTexture cube3,tex2
		For i=1 To 3
			cuben = CopyEntity( cube3 )
			PositionEntity cuben,60,10,60 - (30*i)
		Next
		For i=1 To 3
			cuben = CopyEntity( cube3 )
			PositionEntity cuben,-60,10,60 - (30*i)
		Next
		For i=1 To 3
			cuben = CopyEntity( cube3 )
			PositionEntity cuben,60 - (30*i),10,60 
		Next
		For i=1 To 3
			cuben = CopyEntity( cube3 )
			PositionEntity cuben,60 - (30*i),10,-60
		Next
	
	cube4=CopyEntity( cube3 )
	PositionEntity cube4,-60,10,60
	
	cube5=CopyEntity( cube3 )
	PositionEntity cube5,60,10,-60
	
	cube6=CopyEntity( cube3 )
	PositionEntity cube6,-60,10,-60
	
	Reflect( ModelR,Camera,ReflectTex)
	MoveMouse GraphicsWidth()/2,GraphicsHeight()/2
End Function

Function AnimateScene()
	TurnEntity Light,0,.5,0
 	MoveEntity Light,.0,.0,.5
 	TurnEntity Sphere,0,.3,0
 	MoveEntity Sphere,.0,.0,.2
 	TurnEntity Cylinder,0,0,1
End Function
</textarea><br><br>With a little imagination, you could set a variable to control how much bigger the original render is, to possibly allow some varying tradeoff between performance and quality. If the ScaleTexture command still works with renderable textures, this would be very easy. (EDIT2: It does, but I for one can't work out the scaling algorithm, and you don't get much speed gain anyway)<br><br>EDIT: I should add, before anyone accuses me of misrepresenting Mikhail's work, that this is not one of his examples, I just stole the "framework" from one of the postprocess examples, which is why it has his name on it and Russian text all over the place. Sorry to confuse anyone. <br><br></td></tr></table><br>
<a name="985489"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gabriel</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> PS: Anti-Alias does work in Blitz3D, just not directly from it. I went to nVidia Control panel and set the anti-alias to "mandatory" or something like that (meaning, doesn't matter if the application has the anti-alias setting off, the card will always use it), and voilа, the B3D window was being rendered with anti-alias. <br></div><br>That doesn't always work either though. Some drivers/cards it does, and some it doesn't. Antialiasing in plain vanilla Blitz3D is a lost cause. I assume it's reliable with the addon libraries but I never used any of them. <br><br></td></tr></table><br>
<a name="985498"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >John Blackledge</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yasha, a beautiful effect thanks, but plummets from 260 FPS to 39 FPS when used - drat.<br>Also you're right about the oversampling limit: anything bigger than 1024x786 caused a black screen. <br><br></td></tr></table><br>
<a name="985533"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> For me it went from 300 to 100. <br><br></td></tr></table><br>
<a name="985589"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well Gab, seeing what the FastLibs can do, I don't doubt someone will find out how to do that one day.<br><br><br><div class="quote"> For me it went from 300 to 100. <br></div><br>But it's still a considerable FPS reduction, even if it's at playable levels. When your game is starting to bulk up a lot of effects and stuff, you'll just need to start worrying if the resultant FPS is equal to or greater than 60, IMO. <br><br></td></tr></table><br>
<a name="985615"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> why not try and increase your screen resolution? That will reduce the appearance of the jagged edges, and may be faster than the anti-alias solution. <br><br></td></tr></table><br>
<a name="1060195"></a>

<a name="1060196"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ferret</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> This works fine for me, except on a widescreen resolution.<br><br>How do i do this on a resolution of 1360x768 and how do i increase the supersampling?<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1060220"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> In a word, you don't. DirectX7 has an absolute upper limit on the resolution of 2048x1536, so the extremely simplistic example above, which simply doubles the graphics size, can't be done on a display of 1360x768.<br><br>You have two options:<br><br>1) Cap the supersample at the maximum resolution your choice of aspect will allow, and use some clever scaling tricks to fit to the screen in some variable ratio. Just have to live with the fact that as the resolution increases, the antialiasing will have less and less effect (eventually zero) while still crippling performance.<br><br>2) render different rects of the scene in different passes, e.g. each quarter of the screen in a separate render then paste them together. This removes the restriction on the upper limit (not for the final display, obviously) but would require some clever camera jiggerypokery and damage performance four times as badly as thee original. I'll say right here that I have no idea if this is even possible with Blitz3D's cameras (it's probably possible in pure DirectX though).<br><br>In short... the above example is a very pretty demonstration of FSAA not actually being feasible, sorry.<br><br>Huh... looking back, that's pretty much what I posted a year ago at the top of the thread... <br><br></td></tr></table><br>
<a name="1060439"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >SoggyP</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello.<br><br>Why not just paste a blurred ring around the planet?  Not ideal but would reduce woes.<br><br>Goodbye. <br><br></td></tr></table><br>
<a name="1061413"></a>

<a name="1061414"></a>

<a name="1061415"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ferret</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> I knew this was to easy :(<br><br><font class="tiny">Last edited 2010</font> <br><br></td></tr></table><br>
<a name="1061487"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Who was John Galt?</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Use a texture with an alpha channel. Blend the alpha so the pixels become more transparent towards the direction you want a soft edge. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
