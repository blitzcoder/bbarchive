<!DOCTYPE html><html lang="en" ><head ><title >2D - Circle to Line collision issue</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >2D - Circle to Line collision issue</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >2D - Circle to Line collision issue</a><br><br>
<a name="1197268"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >necky</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi everyone!<br> <br>              I've been working on some 2D line-to-circle collision system and it's been coming on quite well. BUT, I have hit a bit of an issue with it. If you run this code below in Blitz3D and move the collision circle to the top left it starts to really shake around. (use a joypad/controller's left analogue stick to move it around). <br><br>Now the issue seems to arise when the ball goes from one line to another, especially if the two lines are dramatically different in angles.<br><br>The big question is, does anyone know, looking at my code below, if there is a solution to this issue? And if so, how would you tackle it?<br><br>Maybe a question you could possibly answer Mr Sibly, as know you have a solution to this in 3D for your sphere to poly collision system? :)<br><br>Anyway, any help would be gratefully recieved :)<br><br>all the best!<br><br>                 Mike :)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

;***********************************
;* Circle to Line collision system *
;***********************************

AppTitle "Circle to Line Collision system"

Graphics 800,600,32,2
SetBuffer BackBuffer()

Type COLLISION_LINE

	Field Collision_Line_vertA_PosX#
	Field Collision_Line_vertA_PosY#

	Field Collision_Line_vertB_PosX#
	Field Collision_Line_vertB_PosY#

End Type

Global playerX 			= 200			; 'player' screen position
Global playerY 			= 200			; 
Global Player_Radius#   = 32			; 'player' collision radius

Global Collision_New_Position_X#
Global Collision_New_Position_Y#

;-- Create some collision lines -------------

Create_Collision_Line(796, 347, 607, 315)
Create_Collision_Line(607, 315, 470, 287)
Create_Collision_Line(470, 287, 433, 234)
Create_Collision_Line(433, 234, 369, 215)
Create_Collision_Line(369, 215, 353, 228)
Create_Collision_Line(353, 228, 332, 259)
Create_Collision_Line(332, 259, 282, 270)
Create_Collision_Line(282, 270, 265, 296)
Create_Collision_Line(265, 296, 196, 309)
Create_Collision_Line(196, 309, 092, 305)
Create_Collision_Line(092, 305, 032, 292)
Create_Collision_Line(032, 292, 009, 229)
Create_Collision_Line(009, 229, 016, 174)
Create_Collision_Line(016, 174, 080, 194)
Create_Collision_Line(080, 194, 169, 133)
Create_Collision_Line(169, 133, 236, 103)
Create_Collision_Line(236, 103, 373, 083)
Create_Collision_Line(373, 083, 467, 084)
Create_Collision_Line(467, 084, 576, 075)
Create_Collision_Line(576, 075, 717, 053)
Create_Collision_Line(717, 053, 788, 034)
Create_Collision_Line(788, 034, 797, 034)
Create_Collision_Line(797, 034, 795, 352)

;-------------------------------------------

;********
;* MAIN *
;********

While KeyDown(1) = 0

	Cls

	playerX = playerX+(JoyX()*4)											; moving the 'player' around with the joypad (left analogue stick)
	playerY = PlayerY+(JoyY()*4)											;

	Color 255,255,0

	Check_Circle_To_Line_Collisions(playerX,playerY,Player_Radius#)			; check the 'player's position for line collision
	
	playerX = Collision_New_Position_X#										; apply updated player position (is different if it collides, and the same if it doesn't)
	playerY = Collision_New_Position_Y#
	
	Draw_Collision_Lines()													; visualise the collision lines

;-- draw 'player' collision circle --------------------------------------------------------

	ang = 0
	While ang&lt;360
		Rect PlayerX+(Sin(ang)*Player_Radius#),PlayerY+(Cos(ang)*Player_Radius#),1,1 
		ang = ang + 1
	Wend

;------------------------------------------------------------------------------------------


	Flip

Wend

;*************************
;* Create Collision Line *
;*************************

Function Create_Collision_Line(xa#,ya#,xb#,yb#)

level.COLLISION_LINE = New COLLISION_LINE

	level\Collision_Line_vertA_PosX#	= xa#
	level\Collision_Line_vertA_PosY#	= ya#

	level\Collision_Line_vertB_PosX#	= xb#
	level\Collision_Line_vertB_PosY#	= yb#

End Function

;************************
;* Draw Collision Lines *
;************************

Function Draw_Collision_Lines()

For level.COLLISION_LINE = Each COLLISION_LINE

	Color 255,255,0
	Line level\Collision_Line_vertA_PosX#,level\Collision_Line_vertA_PosY#, level\Collision_Line_vertB_PosX#,level\Collision_Line_vertB_PosY#
	
	Color 255,0,0
	Rect level\Collision_Line_vertA_PosX#,level\Collision_Line_vertA_PosY#,3,3
	Rect level\Collision_Line_vertB_PosX#,level\Collision_Line_vertB_PosY#,3,3
	
Next

End Function

;**********************************
;* Check Circle to Line Collision *
;**********************************

Function Check_Circle_To_Line_Collisions(circleX#,circleY#,Radius#)

Collision_New_Position_X# = circleX#
Collision_New_Position_Y# = circleY#

For level.COLLISION_LINE = Each COLLISION_LINE

	xa# 				= level\Collision_Line_vertA_PosX#
	ya# 				= level\Collision_Line_vertA_PosY#

	xb# 				= level\Collision_Line_vertB_PosX#
	yb# 				= level\Collision_Line_vertB_PosY#
	
	lineA#				= (xa#-xb#)
	lineB# 				= (ya#-yb#)

	circle_LineA# 		= (xa#-circleX#)
	circle_LineB#		= (ya#-circleY#)

	circle_LineAb# 		= (xb#-circleX#)
	circle_LineBb#		= (yb#-circleY#)

	Angle# 				= (ATan2 (lineA#,lineB#))+180
	Circle_Angle# 		= ((ATan2 (Circle_lineA#,Circle_lineB#))Mod 360) +180
	
	lineLength# 		= Sqr ( (lineA#*lineA#) + (lineB#*lineB#) )
	Circle_lineLength#  = Sqr ( (circle_lineA#*circle_lineA#) + (circle_lineB#*circle_lineB#) )
	
	PointALength#		= Sqr ( (Abs(circle_LineA#) * Abs(circle_LineA#)) + (Abs(circle_LineB#) * Abs(circle_LineB#)) )
	PointBLength#		= Sqr ( (Abs(circle_LineAb#) * Abs(circle_LineAb#)) + (Abs(circle_LineBb#) * Abs(circle_LineBb#)) )
	
	Color 255,0,0
	Line xa#,ya#,xb#,yb#

;-----Collision check -----------------------------------------------------------------------------------------

	If Abs ((xa# + (Sin(circle_Angle#-Angle#)*Circle_lineLength)) -xa#) &lt; Radius# And (ya# + (Cos(circle_Angle#-Angle#)*Circle_lineLength)) &gt; ya# And (ya# + (Cos(circle_Angle#-Angle#)*Circle_lineLength)) &lt; (ya#+lineLength#) 
	
		rotatedX# = xa#+radius#
		rotatedY# = (ya# + (Cos(circle_Angle#-Angle#)*Circle_lineLength))
		
		templineA#		= (xa#-rotatedX#)
		templineB#  	= (ya#-rotatedY#)
		
		tempangle#		= (ATan2 (templineA#,templineB#)+180) 
		tempLineLength	= Sqr ( ( Abs(templineA) * Abs(templineA)) + ( Abs(templineB) * Abs(templineB))  )
						
		tempCircleX#	= xa# - (Sin(tempangle#-angle#)*templineLength)
		tempCircleY#	= ya# + (Cos(tempangle#-angle#)*templineLength)
		
		Collision_New_Position_X# = tempCircleX#
		Collision_New_Position_Y# = tempCircleY#
		
		
		Return True
		
	EndIf
	
;---------------------------------------------------------------------------------------------------------------

Next

End Function

</textarea> <br><br></td></tr></table><br>
<a name="1197407"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi. A couple of comments.<br><br>1) Most people don't have a Joypad handy to test that code, so you should've changed it to keyboard control. That's what I did to test it here.<br><pre class=code>	playerX = playerX + ( KeyDown( 205 ) - KeyDown( 203 ) ) * 4
	playerY = PlayerY + ( KeyDown( 208 ) - Keydown( 200 ) ) * 4</pre><br>2) The problem is happening because you're using a 'deterministic' approach, in that given a collision with a line, you are directly relocating the circle itself (by using that abstract position Collision_New_Position_n). <br>When the circle collides with a line, it is being relocated to a different position, and then it happens to collide with another line <i>because</i> of that. So this becomes a cycle which gives the flickering.<br><br>You should instead be handling only its motion vector, which indirectly relocates the circle. This is a more physics oriented approach.<br><br>The desired behavior is that at the nodes common to two line segments the circle stops. You achieve this by having each collision with a line 'contribute' towards stopping the circle - so you begin with a full length vector from the player controls moving the circle, then take that vector and at each collision with a line you reduce it given the orientation of the player in relation to the line, and so the vector accumulates resistances from collisions until it results in zero and the circle stops.<br><br>If the vector isn't zero (regardless if the circle collided or not, because it can slide along a line even when colliding), the circle should keep moving for that frame. <br><br></td></tr></table><br>
<a name="1197409"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >necky</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Kryzon!<br><br>               This is terrific advice! :D Thank you so much! :D I'll take a look at this tomorrow night (I'm just off to bed).<br><br>thanks again!<br><br>                Mike <br><br></td></tr></table><br>
<a name="1197488"></a>

<a name="1197489"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >necky</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Righteo! I've rewritten my code and the results are WAY better than before. It still shudders a bit in the corners, but I think I know what this is :) The point though is Kryzon was spot on with his help, so thanks again!<br><br>Here's my code if anyone else is interested in doing this and a starting point :)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

;****************
;* 2d Collision *
;****************

Graphics 800,600,32,2

SetBuffer BackBuffer()

Global lineAx#	= 300
Global lineAy#	= 300

Global lineBx#	= 400
Global lineBy#	= 100

;-----------------------------------

Type COLLISION_LINE

	Field Collision_Line_vertA_PosX#
	Field Collision_Line_vertA_PosY#

	Field Collision_Line_vertB_PosX#
	Field Collision_Line_vertB_PosY#

End Type

;-----------------------------------

Global circleX# = 200
Global circleY# = 200

Global radius#	= 32

Global PlayerVelocityX# = 0 
Global PlayerVelocityY# = 0 

Global PlayerVelocityDamping# = 0.95

Global velocityX# 		; these are related exclusively to the circle_to_line_collision() function 
Global velocityY#		;

;-----------------------------------

Create_Collision_Line(796.0, 347.0, 607, 315)
Create_Collision_Line(607.0, 315.0, 470, 287)
Create_Collision_Line(470.0, 287.0, 433, 234)
Create_Collision_Line(433.0, 234.0, 369, 215)
Create_Collision_Line(369.0, 215.0, 353, 228)
Create_Collision_Line(353.0, 228.0, 332, 259)
Create_Collision_Line(332.0, 259.0, 282, 270)
Create_Collision_Line(282.0, 270.0, 265, 296)
Create_Collision_Line(265.0, 296.0, 196, 309)
Create_Collision_Line(196.0, 309.0, 92, 305)
Create_Collision_Line(92.0, 305.0, 32, 292)
Create_Collision_Line(32.0, 292.0, 9, 229)
Create_Collision_Line(9.0, 229.0, 16, 174)
Create_Collision_Line(16.0, 174.0, 80, 194)
Create_Collision_Line(80.0, 194.0, 169, 133)
Create_Collision_Line(169.0, 133.0, 236, 103)
Create_Collision_Line(236.0, 103.0, 373, 83)
Create_Collision_Line(373.0, 83.0, 467, 84)
Create_Collision_Line(467.0, 84.0, 576, 75)
Create_Collision_Line(576.0, 75.0, 717, 53)
Create_Collision_Line(717.0, 53.0, 788, 34)
Create_Collision_Line(788.0, 34.0, 797, 34)
Create_Collision_Line(797.0, 34.0, 795, 352)

;********
;* MAIN *
;********

While KeyDown(1)=0

Cls

Draw_Collision_Lines()
updatePlayerVelocity()

t = 0
While t&lt;360
	Plot circleX#+Sin(t)*radius#, circleY#+Cos(t)*radius#
	t=t+1
Wend

Flip

Wend

;*************************
;* Create Collision Line *
;*************************

Function Create_Collision_Line(xa#,ya#,xb#,yb#)

level.COLLISION_LINE = New COLLISION_LINE

	level\Collision_Line_vertA_PosX#	= xb#
	level\Collision_Line_vertA_PosY#	= yb#

	level\Collision_Line_vertB_PosX#	= xa#
	level\Collision_Line_vertB_PosY#	= ya#

End Function

;************************
;* Draw Collision Lines *
;************************

Function Draw_Collision_Lines()

For level.COLLISION_LINE = Each COLLISION_LINE

	Line level\Collision_Line_vertA_PosX#,level\Collision_Line_vertA_PosY#, level\Collision_Line_vertB_PosX#,level\Collision_Line_vertB_PosY#
	
Next

End Function

;**************************
;* Update Player Velocity *
;**************************

Function updatePlayerVelocity()

If KeyDown(203)=1 Then PlayerVelocityX# = -3
If KeyDown(205)=1 Then PlayerVelocityX# = 3
If KeyDown(200)=1 Then PlayerVelocityY# = -3
If KeyDown(208)=1 Then PlayerVelocityY# = 3

;---Check collision ----

velocityX# = PlayerVelocityX#
velocityY# = PlayerVelocityY#

g=0	

For level.COLLISION_LINE = Each COLLISION_LINE

		circle_To_Line_Collision(level\Collision_Line_vertA_PosX#,level\Collision_Line_vertA_PosY#, level\Collision_Line_vertB_PosX#,level\Collision_Line_vertB_PosY#, circleX#, circleY#, radius#, PlayerVelocityX#, PlayerVelocityY# )
		circle_To_Line_Collision(level\Collision_Line_vertA_PosX#,level\Collision_Line_vertA_PosY#, level\Collision_Line_vertB_PosX#,level\Collision_Line_vertB_PosY#, circleX#, circleY#, radius#, PlayerVelocityX#, PlayerVelocityY# )
	
		PlayerVelocityX# = velocityX#
		PlayerVelocityY# = velocityY#		
Next

PlayerVelocityX# = velocityX#
PlayerVelocityY# = velocityY#

circleX# = circleX# + PlayerVelocityX#
circleY# = circleY# + PlayerVelocityY#

;-- Apply Damping ------

PlayerVelocityX# = PlayerVelocityX# * PlayerVelocityDamping# 
PlayerVelocityY# = PlayerVelocityY# * PlayerVelocityDamping# 

End Function

;****************************
;* Circle to line collision *
;****************************

Function circle_To_Line_Collision(ax#, ay#, bx#, by# ,cx#, cy# ,cr#, velox#, veloy#)

; pvx# &amp; pvy# = player velocity

velocityX# = velox#
velocityY# = veloy#

vx# = bx# - ax#
vy# = by# - ay#

xdiff# = ax# - cx#
ydiff# = ay# - cy#

a# = (vx#*vx#) + (vy# * vy#)
b# = 2*( (vx#*xdiff#) + (vy# * ydiff#) )  
c# = (xdiff#*xdiff#) + (ydiff# * ydiff#) - (cr#*cr#)

quad# = (b#*b#) - (4*a#*c#)

If (quad# &gt;0 Or quad# = 0)

	quadsqrt# = Sqr(quad#) 

	For i = -1 To 1
	
		t# = (i*-b# + quadsqrt#) / (2*a#)

		x# = ax# + (i * vx# * t#)			; x# &amp; y# are the collision coordinates
		y# = ay# + (i * vy# * t#)

		lineAngle# = ATan2 (vx#,vy#) + 90
				
		If x# &gt; min#(ax#,bx#) Or x# = min#(ax#,bx#) And x# &lt; max#(ax#, bx#) Or x# = max#(ax#, bx#) And y# &gt; min#(ay#,by#) Or y# = min#(ay#,by#) And y# &lt; max#(ay#,by#) Or y# = max#(ay#,by#) 

			velocityX# = Sin(lineAngle#)*2
			velocityY# = Cos(lineAngle#)*2
	
		;	Text 200,200,"Colliding!",0,0
									
			Return True	
			
		End If

		i=i+1

	Next
	
	Return False

EndIf

End Function 

;**********************
;* Min value function *
;**********************

Function min#(a#,b#)

	If a#&lt;b#
	
		Return a#
	Else
	
		Return b#
	
	EndIf

End Function


;**********************
;* Max value function *
;**********************

Function max#(a#,b#)

	If a#&gt;b#
	
		Return a#
	Else
	
		Return b#
	
	EndIf

End Function


</textarea><br><br>Thanks<br>       Mike <br><br></td></tr></table><br>
<a name="1197545"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >necky</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey!<br><br>Well, I've been plowing on with this and have made some good progress.<br><br> I've got the circle bouncing off the lines at the correct angle now, which is good. :)<br><br>But there's an issue (like always!) where the circle sometimes gets stuck and freaks out. If you run this code below you can see what I mean. The first time it hits the wall it sticks to it before wiggling itself free. When it does it then continues to behave correctly, but can still become stuck again under the correct conditions.<br><br>From what I can tell it doesn't look happy when it collides with two lines at once.<br><br>Can anyone help me out here as I really can't figure out how to fix this. :/<br><br>Thanks in advance :)<br><br>         Mike<br><br><pre class=code>

;****************
;* 2d Collision *
;****************

Graphics 800,600,32,2

SetBuffer BackBuffer()

Global lineAx#	= 300
Global lineAy#	= 300

Global lineBx#	= 400
Global lineBy#	= 100

;-----------------------------------

Type COLLISION_LINE

	Field Collision_Line_vertA_PosX#
	Field Collision_Line_vertA_PosY#

	Field Collision_Line_vertB_PosX#
	Field Collision_Line_vertB_PosY#

End Type

;-----------------------------------

Global circleX# = 450
Global circleY# = 200

Global radius#	= 32

Global PlayerVelocityX# = 1 ;.5
Global PlayerVelocityY# = 1 ;.5

Global PlayerSpeed#		= 1

Global PlayerVelocityDamping# = 0.75

Global velocityX# 			; these are related exclusively to the circle_to_line_collision() function 
Global velocityY#			;
Global collisionFinished    ;

;-----------------------------------

Create_Collision_Line(174.0, 83.0, 88, 192)
Create_Collision_Line(88.0, 192.0, 69, 303)
Create_Collision_Line(69.0, 303.0, 121, 399)
Create_Collision_Line(121.0, 399.0, 207, 394)
Create_Collision_Line(207.0, 394.0, 265, 341)
Create_Collision_Line(265.0, 341.0, 323, 304)
Create_Collision_Line(323.0, 304.0, 383, 308)
Create_Collision_Line(383.0, 308.0, 456, 357)
Create_Collision_Line(456.0, 357.0, 490, 427)
Create_Collision_Line(490.0, 427.0, 541, 482)
Create_Collision_Line(541.0, 482.0, 645, 489)
Create_Collision_Line(645.0, 489.0, 711, 423)
Create_Collision_Line(711.0, 423.0, 735, 344)
Create_Collision_Line(735.0, 344.0, 748, 236)
Create_Collision_Line(748.0, 236.0, 701, 185)
Create_Collision_Line(701.0, 185.0, 670, 134)
Create_Collision_Line(670.0, 134.0, 685, 69)
Create_Collision_Line(685.0, 69.0, 640, 29)
Create_Collision_Line(640.0, 29.0, 517, 32)
Create_Collision_Line(517.0, 32.0, 430, 76)
Create_Collision_Line(430.0, 76.0, 378, 109)
Create_Collision_Line(378.0, 109.0, 293, 101)
Create_Collision_Line(293.0, 101.0, 247, 57)
Create_Collision_Line(247.0, 57.0, 165, 87)

;********
;* MAIN *
;********

While KeyDown(1)=0

Cls

Draw_Collision_Lines()
updatePlayerVelocity()

t = 0
While t&lt;360
	Plot circleX#+Sin(t)*radius#, circleY#+Cos(t)*radius#
	t=t+1
Wend

Flip

Wend

;*************************
;* Create Collision Line *
;*************************

Function Create_Collision_Line(xa#,ya#,xb#,yb#)

level.COLLISION_LINE = New COLLISION_LINE

	level\Collision_Line_vertA_PosX#	= xb#
	level\Collision_Line_vertA_PosY#	= yb#

	level\Collision_Line_vertB_PosX#	= xa#
	level\Collision_Line_vertB_PosY#	= ya#

End Function

;************************
;* Draw Collision Lines *
;************************

Function Draw_Collision_Lines()

For level.COLLISION_LINE = Each COLLISION_LINE

	Line level\Collision_Line_vertA_PosX#,level\Collision_Line_vertA_PosY#, level\Collision_Line_vertB_PosX#,level\Collision_Line_vertB_PosY#
	
Next

End Function

;**************************
;* Update Player Velocity *
;**************************

Function updatePlayerVelocity()

;If KeyDown(203)=1 Then PlayerVelocityX# = -1
;If KeyDown(205)=1 Then PlayerVelocityX# = 1
;If KeyDown(200)=1 Then PlayerVelocityY# = -1
;If KeyDown(208)=1 Then PlayerVelocityY# = 1

If Not JoyX()&gt;-0.1 And JoyX()&lt;0.1

	If collisionFinished =0
		PlayerVelocityX# = (JoyX()*PlayerSpeed#)											; moving the 'player' around with the joypad (left analogue stick)
	EndIf
	
EndIf


If Not JoyY()&gt;-0.1 And JoyY()&lt;0.1

	If collisionFinished =0
		PlayerVelocityY# = (JoyY()*PlayerSpeed#)											; moving the 'player' around with the joypad (left analogue stick)
	EndIf
	
EndIf

;---Check collision ----

velocityX# = PlayerVelocityX#
velocityY# = PlayerVelocityY#

g=0	

collisionFinished = 0 

For level.COLLISION_LINE = Each COLLISION_LINE

	If collisionFinished = 0

		circle_To_Line_Collision(level\Collision_Line_vertA_PosX#,level\Collision_Line_vertA_PosY#, level\Collision_Line_vertB_PosX#,level\Collision_Line_vertB_PosY#, circleX#, circleY#, radius#, PlayerVelocityX#, PlayerVelocityY# )
		PlayerVelocityX# = velocityX#
		PlayerVelocityY# = velocityY#
		
	EndIf
		

Next

PlayerVelocityX# = velocityX#
PlayerVelocityY# = velocityY#

circleX# = circleX# + PlayerVelocityX#
circleY# = circleY# + PlayerVelocityY#

;-- Apply Damping ------

;PlayerVelocityX# = PlayerVelocityX# * PlayerVelocityDamping# 
;PlayerVelocityY# = PlayerVelocityY# * PlayerVelocityDamping# 

End Function

;****************************
;* Circle to line collision *
;****************************

Function circle_To_Line_Collision(ax#, ay#, bx#, by# ,cx#, cy# ,cr#, velox#, veloy#)

; pvx# &amp; pvy# = player velocity

velocityX# = velox#
velocityY# = veloy#

vx# = bx# - ax#
vy# = by# - ay#

xdiff# = ax# - cx#
ydiff# = ay# - cy#

a# = (vx#*vx#) + (vy# * vy#)
b# = 2*( (vx#*xdiff#) + (vy# * ydiff#) )  
c# = (xdiff#*xdiff#) + (ydiff# * ydiff#) - (cr#*cr#)

quad# = (b#*b#) - (4*a#*c#)

lineAngle# = ATan2 (vx#,vy#); + 90

Color 255,255,255
;Line ax#,ay#,ax#+(Sin(lineAngle#)*40), ay#+(Cos(lineAngle#)*40)		

Color 0,255,0
Rect ax#,ay#,4,4
Color 255,255,255

If (quad# &gt;0 Or quad# = 0)

	quadsqrt# = Sqr(quad#) 
i= -1

	For i = -1 To 1
	
		t# = (i*-b# + quadsqrt#) / (2*a#)
		
		x# = ax# + (i * vx# * t#)			; x# &amp; y# are the collision coordinates
		y# = ay# + (i * vy# * t#)
	
		If x# &gt; min#(ax#,bx#) Or x# = min#(ax#,bx#) And x# &lt; max#(ax#, bx#) Or x# = max#(ax#, bx#) And y# &gt; min#(ay#,by#) Or y# = min#(ay#,by#) And y# &lt; max#(ay#,by#) Or y# = max#(ay#,by#) 

			Rect x#,y#,6,6

			impactLineX#= (x#-cx#)
			impactLineY#= (y#-cy#)
		
			impactAngle# = ATan2(velox#,veloy#)
			impactAngle# = -impactAngle#+(lineAngle# *2 )
		
;			Color 255,0,255
;			Line ax#,ay#,ax#+(Sin(impactAngle#)*40), ay#+(Cos(impactAngle#)*40)		

			velocityX# = Sin(impactAngle#)*3
			velocityY# = Cos(impactAngle#)*3
			
			collisionFinished = collisionFinished + 1
									
			Return True	
			
		End If

		i=i+2

	Next
	
	Return False

EndIf

End Function 

;**********************
;* Min value function *
;**********************

Function min#(a#,b#)

	If a#&lt;b#
	
		Return a#
	Else
	
		Return b#
	
	EndIf

End Function


;**********************
;* Max value function *
;**********************

Function max#(a#,b#)

	If a#&gt;b#
	
		Return a#
	Else
	
		Return b#
	
	EndIf

End Function

</pre> <br><br></td></tr></table><br>
<a name="1197598"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think your collision routine is seriously flawed.  You are not resolving penetration and you are handling velocity incorrectly.  <br><br>For penetration you should be moving the ball away from the collision by it's penetration depth + .0001 (or some small factor to ensure it's no longer colliding with the line) in the direction of the line normal.  The velocity should be correctly reflected based on the collision normals.  This should be done for every circle line collision, not just with the last collision.  <br><br>By not resolving the penetration and correctly handling the velocity, any collisions with multiple lines will keep changing the velocity causing this back and forward jittering. <br><br></td></tr></table><br>
<a name="1197606"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's essentially what I was going to  say. Sometimes the circle passes through a line segment, which must never happen. You must move the circle back so it is no longer colliding. Figure out the appropriate movement to put it back so it is exactly colliding, then multiply that amount by a small "fudge factor" such as 1.0001 so it is no longer colliding.<br><br>Determining the response based on line angles is doomed to fail at the endpoints. When the circle hits something the response is entirely determined by the point of impact. You can do this based on the "collision normal". That's usually a vector perpendicular to the thing the circle collided with. But for an endpoint this is not well defined. <br><br>However, since the moving object is a circle we know the normal must be directed from the impact point toward the center of the circle. <br><br></td></tr></table><br>
<a name="1197615"></a>

<a name="1197681"></a>

<a name="1197682"></a>

<a name="1197743"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I suggest you change your paradigm to vectors entirely.<br>It's much easier than using trigonometry.<br><br>I used this article as reference:<br><a href="http://doswa.com/2009/07/13/circle-segment-intersectioncollision.html" target="_blank">http://doswa.com/2009/07/13/circle-segment-intersectioncollision.html</a><br><br>Then I coded something. <br>It's not perfect. At all node junctions the circle jitters around. <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 800,600,0,2
SetBuffer BackBuffer()

Const PLAYER_RADIUS# = 32.0
Const PLAYER_SPEED# = 7

If PLAYER_SPEED &gt;= PLAYER_RADIUS Then RuntimeError "This collision test does not work with speeds bigger than the circle's radius."

Type CollisionLine
	Field x1#, y1# ;Point A.
	Field x2#, y2# ;Point B.
	
	Field vecLength#
	Field unitVecX#, unitVecY#
End Type

Type Actor
	Field radius#
	Field numCollisions%
	
	Field x#, y#
	Field motionVecX#, motionVecY#
End Type 


Global Player.Actor = CreatePlayer()

;CreateCollisionLine( 796, 347, 607, 315 )
;CreateCollisionLine( 607, 315, 470, 287 )
;CreateCollisionLine( 470, 287, 433, 234 )
;CreateCollisionLine( 433, 234, 369, 215 )
;CreateCollisionLine( 369, 215, 353, 228 )
;CreateCollisionLine( 353, 228, 332, 259 )
;CreateCollisionLine( 332, 259, 282, 270 )
;CreateCollisionLine( 282, 270, 265, 296 )
;CreateCollisionLine( 265, 296, 196, 309 )
;CreateCollisionLine( 196, 309, 092, 305 )
;CreateCollisionLine( 092, 305, 032, 292 )
;CreateCollisionLine( 032, 292, 009, 229 )
;CreateCollisionLine( 009, 229, 016, 174 )
;CreateCollisionLine( 016, 174, 080, 194 )
;CreateCollisionLine( 080, 194, 169, 133 )
;CreateCollisionLine( 169, 133, 236, 103 )
;CreateCollisionLine( 236, 103, 373, 083 )
;CreateCollisionLine( 373, 083, 467, 084 )
;CreateCollisionLine( 467, 084, 576, 075 )
;CreateCollisionLine( 576, 075, 717, 053 )
;CreateCollisionLine( 717, 053, 788, 034 )
;CreateCollisionLine( 788, 034, 797, 034 )
;CreateCollisionLine( 797, 034, 795, 352 )

CreateCollisionLine( 300, 300, 500, 300 )
CreateCollisionLine( 300, 300, 300, 550 )
CreateCollisionLine( 500, 300, 750, 200 )

Local fpsTimer = CreateTimer( 30 )


While Not KeyHit( 1 )
	WaitTimer( fpsTimer )
	Cls
	
	PreparePlayer()

	CheckCollisions()

	UpdatePlayer()
	
	DrawGame()	
	
	Text 10,10,Player\numCollisions
	If (Player\motionVecX &lt;&gt; 0) Or (Player\motionVecY &lt;&gt; 0) Then Text 10,30,"MOVING" 
	
	Flip
Wend 

End 


Function CreateCollisionLine.CollisionLine( x1#, y1#, x2#, y2# )
	
	l.CollisionLine = New CollisionLine
	l\x1 = x1
	l\y1 = y1
	l\x2 = x2
	l\y2 = y2
	l\vecLength = Distance2D( x1, y1, x2, y2 )
	l\unitVecX = ( x2 - x1 ) / l\vecLength
	l\unitVecY = ( y2 - y1 ) / l\vecLength
	
	Return l
	
End Function


Function CreatePlayer.Actor()

	p.Actor = New Actor
	p\radius = PLAYER_RADIUS
	p\x = 200
	p\y = 200
		
	Return p

End Function


;Gathers the player's INTENTIONS for the game frame: stand still or move in a certain direction.
Function PreparePlayer()

	Local length# = 0
	Local motionUnitX#, motionUnitY#
	
	;Keyboard:
	Player\motionVecX = ( KeyDown( 205 ) - KeyDown( 203 ) ) 	
	Player\motionVecY = ( KeyDown( 208 ) - KeyDown( 200 ) )
	
	;Joypad:
	;Player\motionVecX = JoyX()
	;Player\motionVecY = JoyY()
	
	;Calculate a unit vector (0.0 ~ 1.0) to avoid diagonal speeds be faster than horizontal or vertical.
	If ( Player\motionVecX &lt;&gt; 0 ) Or ( Player\motionVecY &lt;&gt; 0 ) Then 
		length = Distance2D( 0.0, 0.0, Player\motionVecX, Player\motionVecY )
		motionUnitX = Player\motionVecX / length
		motionUnitY = Player\motionVecY / length
	EndIf 
	
	;Crude movement, does not into account soft movements from a joypad's analogue stick.
	Player\motionVecX = motionUnitX * PLAYER_SPEED
	Player\motionVecY = motionUnitY * PLAYER_SPEED 
	
End Function


;Updates the position of the player with whatever direction and speed that was solved.
Function UpdatePlayer()
	
	Player\x = Player\x + Player\motionVecX
	Player\y = Player\y + Player\motionVecY

End Function


Function DrawActor( x#, y#, radius# )
	
	Local tempX# = x - radius
	Local tempY# = y - radius
	Color( 210, 0, 0 )
	Oval( tempX, tempY, 2 * radius, 2 * radius, False )
	Color( 200, 200, 0 )
	Line( x - 4, y, x + 4, y )
	Line( x, y - 4, x, y + 4 )	

End Function


Function DrawGame( )
	
	For l.CollisionLine = Each CollisionLine
		Color( 150, 150, 0 )
		Line( l\x1, l\y1, l\x2, l\y2 )
		Color( 160, 0, 0 )
		Rect( l\x1-2, l\y1-2, 4, 4, True )
	Next
	
	DrawActor( Player\x, Player\y, Player\radius )

End Function


Function CheckCollisions()
	
	Player\numCollisions = 0
	If ( Player\motionVecX = 0 ) And ( Player\motionVecY = 0 ) Then Return 
	
	Local newPlayerX = Player\x + Player\motionVecX
	Local newPlayerY = Player\y + Player\motionVecY
	
	Local aToPlayer_X#
	Local aToPlayer_Y#
	
	Local projLength#
	Local projX#, projY#
	
	Local closestPointX#
	Local closestPointY#
	Local closestLength#
	Local closestDirX#
	Local closestDirY#
	
	Local penetrationX#
	Local penetrationY#
	
	Local weirdOffset# = 6
	
	For l.CollisionLine = Each CollisionLine
		
		;Calculate vector from point 'A' to the 'player'.
		aToPlayerX = newPlayerX - l\x1
		aToPlayerY = newPlayerY - l\y1
		
		;Retrieve the length of the projection of vector 'aToPlayer' onto 'collision line'.
		projLength = DotProduct( aToPlayerX, aToPlayerY, l\unitVecX, l\unitVecY )
		
		;Find the closest point to the 'player' on the 'collision line' (akin to finding the projection vector in point A's space).
		If projLength &lt; 0.0 Then 
		
			closestPointX = l\x1
			closestPointY = l\x1
		
		ElseIf projLength &gt; l\vecLength Then
		
			closestPointX = l\x2
			closestPointY = l\y2
		
		Else
		
			closestPointX = l\x1 + ( projLength * l\unitVecX ) 
			closestPointY = l\y1 + ( projLength * l\unitVecY )
		
		EndIf 
		
		;A square-root call per line. Not ideal, but works.
		closestLength# = Distance2D( newPlayerX, newPlayerY, closestPointX, closestPointY )
		
		If closestLength &lt; ( Player\radius ) Then
			
			Player\numCollisions = Player\numCollisions + 1
						
			closestDirX = (newPlayerX - closestPointX) / closestLength
			closestDirY = (newPlayerY - closestPointY) / closestLength
				
			penetrationX = ( Player\radius - closestLength ) * closestDirX
			penetrationY = ( Player\radius - closestLength ) * closestDirY 
				
			Player\motionVecX = Player\motionVecX + Int( penetrationX )
			Player\motionVecY = Player\motionVecY + Int( penetrationY )
			
			newPlayerX = Player\x + Player\motionVecX
			newPlayerY = Player\y + Player\motionVecY

		EndIf
			
	Next
	
End Function


Function DotProduct#(  aX#, aY#, bX#, bY# )
	
	Return aX * bX + aY * bY
	
End Function


Function Distance2D#( aX#, aY#, bX#, bY# )
	
	Return Sqr( ( bX - aX ) * ( bX - aX ) + ( bY - aY ) * ( bY - aY ) )
	
End Function


Function Min#( a#, b# )

	If a &lt; b Return a
	Return b
	
End Function</textarea> <br><br></td></tr></table><br>
<a name="1197676"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >necky</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Kryzon!<br>                This is terrific! Thank you! :D Thanks again for all your help, you've been brilliant!<br><br>all the best!<br>                 Mike <br><br></td></tr></table><br>
<a name="1197742"></a>

<a name="1197755"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi. As it was, the collision function would check for collisions based on the same "future position" from the player.<br>But when colliding against several lines at once, this has them all correcting against the exact same old position.<br><br>So every time a collision is detected you need to update the "future position" with these corrections - this means each line will give corrections consecutively, based on each other's corrections. It further improves against the jittering; It's still not perfect but it's much better.<br><br>In the CheckCollisions function, add two lines in this block:<br><pre class=code>		If closestLength &lt; ( Player\radius ) Then
			
			Player\numCollisions = Player\numCollisions + 1
						
			closestDirX = (newPlayerX - closestPointX) / closestLength
			closestDirY = (newPlayerY - closestPointY) / closestLength
				
			penetrationX = ( Player\radius - closestLength ) * closestDirX
			penetrationY = ( Player\radius - closestLength ) * closestDirY 
				
			Player\motionVecX = Player\motionVecX + Int( penetrationX )
			Player\motionVecY = Player\motionVecY + Int( penetrationY )
			
			;Update the future position being tested with the corrections from each collision.
			newPlayerX = Player\x + Player\motionVecX ;*** Added.
			newPlayerY = Player\y + Player\motionVecY ;*** Added.			

		EndIf</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
