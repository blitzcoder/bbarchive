<!DOCTYPE html><html lang="en" ><head ><title >Help with Gravity/Bounce Code...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Help with Gravity/Bounce Code...</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Help with Gravity/Bounce Code...</a><br><br>
<a name="509232"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I whipped up this little demo to show basic gravity and a ball bouncing.  The problem is that it's buggy as heck.  Sometimes it bounces super high.  Other times it bounces twice and stops.  See anything wrong?<br><br>btw: if you want to run it you need the vector.bb lib from the archives<br><br><pre class=code>
;// Gravity and Bounce Example
Graphics3D 800,600,16
SetBuffer BackBuffer()

Include "vector.bb"

;// Set Gravity meters per sec
g# = -9.8

;// Ball Parameters
ball = CreateSphere()
ScaleEntity ball,0.5,0.5,0.5
mass = 10

;// Set Initial Forces and Vectors
vFor.vector = vector()				;force vector
vAcc.vector = vector()				;acceleration vector
vVel.vector = vector()				;velocity vector
vPos.vector = vector(0,20,0)		        ;position vector

;// Timing 
NewTime% = MilliSecs()
OldTime% = NewTime

;// Camera and Lights
camera = CreateCamera()
PositionEntity camera,0,0,-20
light  = CreateLight()

;// Main Loop
While Not KeyHit(1)
Cls

;// Find Delta Time
NewTime = MilliSecs()
delta# = Float (NewTime - OldTime) / 1000
OldTime = NewTime
FPS = 1 / delta

;// Reset Forces to Zero Each Cycle
Vector_Reset vFor

;// Calculate Gravity
vFor\y = g * mass

;// Collision
If vPos\y &lt; 0 Then Collided = True
If Collided = True
	speed# = Vector_Magnitude(vVel)
	collided = False
	vFor\y = -vFor\y * (Float (speed / 2)) * mass
	vVel\y = 0
EndIf

;// Up Arrow Key applies 100 force upwards
If KeyDown(200) Then vFor\y = 100

;// Calc New Acc, Vel, and Pos
Vector_DivideScalar vAcc,vFor,mass
Vector_AddTimeStep vVel,vAcc,delta
Vector_AddTimeStep vPos,vVel,delta

;// Position the Ball
Vector_PositionEntity ball,vPos

UpdateWorld
RenderWorld

;// Show What's Happening
Text 5,5,"FPS=" + FPS
Text 5,50,"Speed=" + Int(Vector_Magnitude(vVel))
Vector_Show(vFor,5,100,"Force")
Vector_Show(vAcc,5,175,"Acc")
Vector_Show(vVel,5,250,"Vel")
Vector_Show(vPos,5,325,"Pos")

Flip
Wend
End
</pre> <br><br></td></tr></table><br>
<a name="509241"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> No problems yet and I ran it about 12 times... same results every time...<br>{{EDIT}}OK after I post that I see what you mean... it starts going wonky...<br><br>It looks like the velocity is being ADDED to rather than being subtracted in a constant ratio... I changed the drop height to 15 (added a mirror plane as well for fun) and it sometimes adds and adds... it is probably something in the vector.bb... let me look...<br><br>Oh my code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;// Gravity and Bounce Example
Graphics3D 800,600
SetBuffer BackBuffer()

Include "vector.bb"

;// Camera and Lights
camera = CreateCamera()
PositionEntity camera,0,1,-20
light=CreateLight()
RotateEntity light,90,0,0

;// Set Gravity meters per sec
g# = -9.8

;// Ball Parameters
ball = CreateSphere()
ScaleEntity ball,0.5,0.5,0.5
mass = 10

; for a bit of fun
plane=CreatePlane()
grass_tex=LoadTexture( "tmp11.jpg" )
EntityTexture plane,grass_tex
EntityAlpha plane,0.5
PositionEntity plane,0,0,-.5
; make plane a mirror
mirror=CreateMirror()

;// Set Initial Forces and Vectors
vFor.vector = vector()				;force vector
vAcc.vector = vector()				;acceleration vector
vVel.vector = vector()				;velocity vector
vPos.vector = vector(0,15,0)		        ;position vector

;// Timing 
NewTime% = MilliSecs()
OldTime% = NewTime

;// Main Loop
While Not KeyHit(1)
Cls

;// Find Delta Time
NewTime = MilliSecs()
delta# = Float (NewTime - OldTime) / 1000
OldTime = NewTime
FPS = 1 / delta

;// Reset Forces to Zero Each Cycle
Vector_Reset vFor

;// Calculate Gravity
vFor\y = g * mass

;// Collision
If vPos\y &lt; 0 Then Collided = True
If Collided = True
	speed# = Vector_Magnitude(vVel)
	collided = False
	vFor\y = -vFor\y * (Float (speed / 2)) * mass / 2
	vVel\y = 0
EndIf

;// Up Arrow Key applies 100 force upwards
If KeyDown(200) Then vFor\y = 100

If KeyHit(57) = True Then 
	vPos.vector = vector(0,15,0)
	;Vector_PositionEntity ball,vPos
EndIf

;// Calc New Acc, Vel, and Pos
Vector_DivideScalar vAcc,vFor,mass
Vector_AddTimeStep vVel,vAcc,delta
Vector_AddTimeStep vPos,vVel,delta

;// Position the Ball
Vector_PositionEntity ball,vPos

UpdateWorld
RenderWorld

;// Show What's Happening
Text 5,5,"FPS=" + FPS
Text 5,50,"Speed=" + Int(Vector_Magnitude(vVel))
Vector_Show(vFor,5,100,"Force")
Vector_Show(vAcc,5,175,"Acc")
Vector_Show(vVel,5,250,"Vel")
Vector_Show(vPos,5,325,"Pos")

Flip
Wend
End
</textarea> <br><br></td></tr></table><br>
<a name="509253"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I made a small change to the vector.bb it is slightly more accurate and realistic change the funtion thus:<br><pre class=code>
Function Vector_Magnitude#(v.Vector)
	Return Sqr((v\x * v\x) + (v\y * v\y) + (v\z * v\z)) / 2
End Function
</pre>and run it now!!!<br>Then I did this to see:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;// Gravity and Bounce Example
Graphics3D 800,600
SetBuffer BackBuffer()

Include "vector.bb"

;// Camera and Lights
camera = CreateCamera()
PositionEntity camera,0,1,-20
light=CreateLight()
RotateEntity light,90,0,0

;// Set Gravity meters per sec
g% = -9 ; changed it to flat number

;// Ball Parameters
ball = CreateSphere()
ScaleEntity ball,0.5,0.5,0.5
mass% = 10

; for a bit of fun
plane=CreatePlane()
grass_tex=LoadTexture( "tmp11.jpg" )
EntityTexture plane,grass_tex
EntityAlpha plane,0.5
PositionEntity plane,0,0,-.5
; make plane a mirror
mirror=CreateMirror()

;// Set Initial Forces and Vectors
vFor.vector = vector()				;force vector
vAcc.vector = vector()				;acceleration vector
vVel.vector = vector()				;velocity vector
vPos.vector = vector(0,15,0)		        ;position vector

NewTime = MilliSecs()
OldTime = NewTime



;// Main Loop
While Not KeyHit(1)
Cls

NewTime = MilliSecs()
delta# = Float (NewTime - OldTime) / 1000
OldTime = NewTime
FPS = 1 / delta

;// Reset Forces to Zero Each Cycle
Vector_Reset vFor

;// Calculate Gravity
grav% = g * mass ; so you can show it
vFor\y = g * mass 

;// Collision
If vPos\y &lt; 0 Then Collided = True
If Collided = True
	speed% = Vector_Magnitude(vVel)
	collided = False
	vFor\y = -vFor\y * speed% / 2  * mass% 
	vVel\y = 0
EndIf

;// Up Arrow Key applies 100 force upwards
If KeyDown(200) Then vFor\y = 100

If KeyHit(57) = True Then 
	vPos.vector = vector(0,15,0)
	;Vector_PositionEntity ball,vPos
EndIf

;// Calc New Acc, Vel, and Pos
Vector_DivideScalar vAcc,vFor,mass
Vector_AddTimeStep vVel,vAcc,delta
Vector_AddTimeStep vPos,vVel,delta

;// Position the Ball
Vector_PositionEntity ball,vPos

UpdateWorld
RenderWorld

;// Show What's Happening
Text 5,5,"FPS=" + FPS
Text 5,30,"Speed=" + Int(Vector_Magnitude(vVel))
Text 5,60,"Grav: "+grav

Vector_Show(vFor,5,100,"Force: ")
Vector_Show(vAcc,5,175,"Acc: ")
Vector_Show(vVel,5,250,"Vel: ")
Vector_Show(vPos,5,325,"Pos: ")

Flip
Wend
End
</textarea><br>MASS seems to be a part of the problem... monkey with that and see...<br><br>For some reason velocity adds at some point(possibly because of mass be applied wrongly I am uncertain!)... which is why I squelched magnitude a bit...<br><br>RZ <br><br></td></tr></table><br>
<a name="509267"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RGR</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Change Flip to Flip False<br>The ball drops like a stone.<br>This may give you the idea what's wrong ... delta calculation could be wrong was my first thought when I looked at the code <br><br></td></tr></table><br>
<a name="509344"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> but it is weird that the bouncing would reduce normally and then spike to a positive 40 to 60 + up a bounce... <br><br>BUT since MASS and GRAVITY effect DeltaV in his program it could just be something so simple... <br><br>Hmmm<br><br>RZ <br><br></td></tr></table><br>
<a name="509407"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sweenie</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> You consider the ball has collided if the y-position is below 0 right?<br>Now, what will happen if the ball is still below 0 in the next frame?<br>When you detect the collision, try positioning the ball at 0 or a little bit above, like 0.001 or something, and see if that helps.<br><br>[Edit] To "visualize" the problem, add a CollisionCount variable that you increase by one at every collision and print it on screen. <br><br></td></tr></table><br>
<a name="509531"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yep it was registering multiple collisions on a single bounce.  Setting the vPos\y to 0 after a successful collision fixed it. <br><br></td></tr></table><br>
<a name="509680"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> See... it was something very simple... Good catch Sweenie! <br><br></td></tr></table><br>
<a name="510020"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well it works for a simple bouncing off a floor (actualy a limiting value).  Now make it so you can have 2 balls bounce off each other with the right trajectory.  Not simple. <br><br></td></tr></table><br>
<a name="510077"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Doing a pool type game? There MIGHT be some sample code...<br><br>RZ <br><br></td></tr></table><br>
<a name="510140"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> It is pretty simple really ... I'm sure darkeagle / swift and others have examples in the archives or over at bcoder.<br><br>In fact here's one ..<br><a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=670" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=670</a> <br><br></td></tr></table><br>
<a name="510187"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sweenie</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> You might have read this already but I post it anyway.<br>It's a great tutorial on the topic.<br><a href="http://www.gamasutra.com/features/20020118/vandenhuevel_01.htm" target="_blank">http://www.gamasutra.com/features/20020118/vandenhuevel_01.htm</a> <br><br></td></tr></table><br>
<a name="510292"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> This demo - not by me, BTW - may be of some use?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;
; Bouncing ball physics - by Vincent DeCampo 2/1/04
;

Const GRAVITY#=-.05
Const BALLS=1

Graphics3D 640,480

;*** Type for Ball ***
Type Ball_Object
	Field Entity
	Field Pivot
	Field x#
	Field y#
	Field z#
	Field dx#
	Field dy#
	Field dz#
	Field xMin#
	Field xMax#
	Field yMin#
	Field yMax#
	Field zMin#
	Field zMax#
	Field elasticity#
	Field friction#
End Type

;*** Lets create the floor ***
Room=CreateTerrain(64)
PositionEntity room,-32,0,-32
EntityColor Room, 0, 150, 0

;******* Create Initial Ball ******
Blitz.Ball_Object = New Ball_Object
InitBallVars(Blitz)
Collisions BALLS, BALLS, 1, 2

;***** Create Camera *****
camera=CreateCamera()
PositionEntity camera,0,5,-40
PointEntity camera, Blitz\Entity

;**********************************
;			 Main Loop
;**********************************
;
While Not KeyHit(1)

	If KeyHit(57) Then

		;***** Seed RND Generator *****
		SeedRnd (MilliSecs())
		
		;*** Create New Ball ***
		Blitz.Ball_Object = New Ball_Object	
		InitBallVars Blitz

	End If

	;**** Perform Room Physics ****
	For Ball.Ball_Object = Each Ball_Object
		BallPhysics (Ball)		
	Next

	UpdateWorld	

	;**** Perform Ball-on-Ball Physics ****
	CheckCollisions (Blitz)
		
	RenderWorld 
RenderWorld
	Text 0,5,"Press SPACE to throw another ball"
	Text 0,15,"Press ESC to Exit" + TrisRendered()

	Flip 1
Wend

End
Function Flipper() 
While ScanLine()&lt;&gt;0: Wend 
Flip False 
End Function 
;**********************************

Function CheckCollisions (Ball.Ball_Object)

	For C.Ball_Object = Each Ball_Object
				
		If EntityCollided(c\Entity, BALLS)
					
			For i = 1 To CountCollisions(c\Entity)

				Ent=CollisionEntity (c\Entity, i)

				;Get the normal of the surface which the entity collided with. 
				Nx# = CollisionNX(c\Entity, i) 
				Ny# = CollisionNY(c\Entity, i) 
				Nz# = CollisionNZ(c\Entity, i) 
			
				;Compute the dot product of the entity's motion vector And the normal of the surface collided with. 				
				VdotN# = (c\dx# * Nx# + c\dy# * Ny# + c\dz# * Nz#)
									
				;Calculate the normal force. 
				NFx# = -2.0 * Nx# * VdotN# 
				NFy# = -2.0 * Ny# * VdotN# 
				NFz# = -2.0 * Nz# * VdotN# 
										
				;Add the normal force to the direction vector. 
				c\dx# = c\dx# + NFx#/2
				c\dy# = c\dy# + NFy#/2	;Force /2 assumes balls equal in mass
				c\dz# = c\dz# + NFz#/2
								
				For Hit.Ball_Object = Each Ball_Object

					If Hit\Entity = Ent Then
						
						;Add negative force to collision entity vector		
						Hit\dx# = Hit\dx# - NFx#/2
						Hit\dy# = Hit\dy# - NFy#/2	;Force /2 assumes balls equal in mass
						Hit\dz# = Hit\dz# - NFz#/2		
											
					End If
																									
				Next

			Next
												
		EndIf
		
	Next
							
End Function

;**********************************

Function InitBallVars(Ball.Ball_Object)

	;Create Ball
	Ball\Entity=CreateSphere()
	ScaleEntity Ball\Entity, 1,1,1
	EntityType Ball\Entity, BALLS

	;txt=LoadTexture("blitzlogo.bmp")
	;EntityTexture Ball\Entity, txt
	
	;Create Pivot
	Ball\Pivot=CreatePivot()
	
	;*** Variables for Ball ***	
	Ball\x#=0
	Ball\y#=20	;set ball 20 units above floor
	Ball\z#=0
	Ball\dx#=Rand(5)
	Ball\dy#=0	;at rest
	Ball\dz#=Rand(5)
	Ball\yMin#=1
	Ball\yMax#=9999
	Ball\xMin#=-32
	Ball\xMax#=32
	Ball\zMin#=-32
	Ball\zMax#=32
	Ball\elasticity#=.75	;value from &gt;0 to &lt;1 (1=pure elastic collision)
	Ball\friction#=.01
	;**************************

End Function

;**********************************

Function BallPhysics (Ball.Ball_Object)

	If ball\y   =&lt; Ball\yMin# Then
		Ball\y  =  Ball\yMin#
		Ball\dy = -Ball\dy
		Ball\dy =  Ball\dy * Ball\elasticity
	End If

	If Ball\x   =&lt; Ball\xMin# Then
		Ball\x  =  Ball\xMin#
		Ball\dx = -Ball\dx
		Ball\dx =  Ball\dx * Ball\elasticity
		UpdatePivot (Ball)
	End If

	If  Ball\x &gt;=  Ball\xMax# Then
		Ball\x  =  Ball\xMax#
		Ball\dx = -Ball\dx
		Ball\dx =  Ball\dx * Ball\elasticity
		UpdatePivot (Ball)
	End If
	
	If Ball\z   =&lt; Ball\zMin# Then
		Ball\z  =  Ball\zMin#
		Ball\dz = -Ball\dz
		Ball\dz =  Ball\dz * Ball\elasticity
		UpdatePivot (Ball)
	End If

	If Ball\z  &gt;=  Ball\zMax# Then
		Ball\z  =  Ball\zMax#
		Ball\dz = -Ball\dz
		Ball\dz =  Ball\dz * Ball\elasticity
		UpdatePivot (Ball)
	End If
	
	If Ball\y  &gt;= Ball\yMin# Then
		Ball\dy = Ball\dy + GRAVITY
	End If	

	Ball\x = Ball\x + Ball\dx
	Ball\z = Ball\z + Ball\dz
	Ball\y  = Ball\y + Ball\dy

	Ball\dx = Ball\dx - (Ball\dx*Ball\friction)
	Ball\dz = Ball\dz - (Ball\dz*Ball\friction)

	;Move ball to new coords
	PositionEntity  Ball\Entity, Ball\x, Ball\y, Ball\z, 0

	;Rotate mesh to give appearance of rolling	
	TurnEntity Ball\Entity, Ball\dz*50,0,-Ball\Dx*50, 1
			
End Function

Function UpdatePivot (Ball.Ball_Object)

	;Move pivot to coords + deltas
	PositionEntity Ball\Pivot, Ball\x+Ball\dx, Ball\y+Ball\dy, Ball\z+Ball\dz
	
	PointEntity Ball\Entity, Ball\Pivot, 0		

End Function
</textarea><br><br>[edit] Actually, I've just seen some 'sticky' collisions happening when 2 balls hit, so there must be a bugette somewhere. Like I said, not my code so I ain't fixing it. :) <br><br></td></tr></table><br>
<a name="510465"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think if you translate the 2 balls involved in the collisions by a small fraction of the collision normals then the stickyness will be eliminated. <br><br></td></tr></table><br>
<a name="510474"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sweenie</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> a) Move the balls apart along the collisionnormal until they are separated(as Stevie says) and apply the collisionresponse<br><br>or<br><br>b) Only allow collisionsresponse to happen when the balls are intersecting and moving towards each other(non separating) and ignore the collisionresponse if they are moving away from each other(separating) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
