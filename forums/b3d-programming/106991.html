<!DOCTYPE html><html lang="en" ><head ><title >Code structure</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Code structure</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Code structure</a><br><br>
<a name="1321675"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Wolfsong73</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello<br><br>So, was wondering about this today at work, and thought I'd ask.<br><br>I know in programming, people tend to develop their own styles, and there's no one way to do anything.<br><br>I'm curious about how people structure their code? <br><br>For example, do you define all your images, functions and such at the beginning of the code? Do you put certain things at the end? Perhaps some things are kept in separate files which are then included, etc. etc?<br><br>It seems like, though I'm still very new to all this, that learning some good "housekeeping" habits would be a good idea, esp. as I start to create more complex programs. So, I'm looking for some ideas/advice/examples of how people structure/organize their code.<br><br>Thanks! <br><br></td></tr></table><br>
<a name="1321678"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> personally the structure of my code (using Blitz3d) currently looks like this (the structure of my old codes may be slightly different, but most of them work well) :<br><br><pre class=code>
;initialize constants, globals, lists (arrays and customtypes)
;functions/procedures, systems
;load premade components (meshes, skeletons (joints/bones), animations, textures, images, fonts, sounds)
;initialize graphics window
;initialize camera
;initialize listener
;build world
;mainloop
 ;getinput (mouse, keyboard)
 ;mode interaction 2D (update menu)
 ;mode interaction 3D (update world, entities)
 ;go to backbuffer
 ;render3d
 ;draw2d
 ;flip backbuffer to frontbuffer
 ;play/stop sounds
;exit
</pre><br><br>but this is just my structure, you can organize your code as you prefer (if it works well) <br><br></td></tr></table><br>
<a name="1321681"></a>

<a name="1321682"></a>

<a name="1321683"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> here is a basic scene with a grid and first person view controls <br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D(1000,625,32,2)

SeedRnd(MilliSecs())

;Input
Global MX%
Global MY%
Global MXDiff%
Global MYDiff%

;Camera
Global Camera = CreateCamera()
CameraViewport(Camera,0,0,GraphicsWidth(),GraphicsHeight())
CameraRange(Camera,0.2,200)
CameraClsColor(Camera,000,000,000)

;Origine
Origine = CreateCube()
ScaleMesh(Origine,0.01,0.01,0.01)
EntityColor(Origine,255,000,255)
EntityFX(Origine,1)

;InteractionMode
Global InteractionMode%
Const C2D% = 1
Const C3D% = 2
InitializeInteractionMode()

;Ghost
Global GhostRoot
Global GhostRootYaw#
Global GhostEyes
Global GhostEyesPitch#
BuildGhost()
InitializeGhost()

;Zone
Global GridMesh = CreateGrid(100,100)
EntityFX(GridMesh,1)

Global XCube = CreateCube()
ScaleMesh(XCube,1.0/2,1.0/2,1.0/2)
PositionMesh(XCube,0,1.0/2,0)
HideEntity(XCube)

Global ThingsCount%
Dim Thing_Mesh(100)

For n% = 1 To 100 Step 1
 ThingsCount = ThingsCount + 1
 TI% = ThingsCount
 Thing_Mesh(TI) = CopyEntity(XCube)
 EntityColor(Thing_Mesh(TI),Rand(025,255),Rand(025,255),Rand(025,255))
 PositionEntity(Thing_Mesh(TI),Rand(0,99)+0.5,0,Rand(0,99)+0.5,True)
Next

;DLight
Global DLight = CreateLight(1)
LightColor(DLight,192,192,192)
PositionEntity(DLight,-1000,1000,-1000,True)
RotateEntity(DLight,45,-45,0,True)

AmbientLight(032,032,032)

PositionEntity(GhostRoot,0,1.65,-3,True)

Global MainLoopTimer = CreateTimer(30)

Main()

End()

Function Main()

 Repeat

  GetInput()

  UpdateInteractionMode()
  If( InteractionMode = C2D )
   ;
  Else If( InteractionMode = C3D )
   UpdateGhost() 	   
  EndIf

  PositionRotateEntityLikeOtherEntity(Camera,GhostEyes)

  WireFrame(False)
  If( KeyDown(2)=1 )
   WireFrame(True)
  EndIf

  SetBuffer(BackBuffer())
  RenderWorld()

  ;Flip(1)
  WaitTimer(MainLoopTimer)
  VWait():Flip(False)

 Until( KeyDown(1)=1 )

End Function

Function CText(TextStr$,PX%,PY%)
 Text(PX,PY,TextStr,False,False)
End Function

Function PositionEntityLikeOtherEntity(Entity,OEntity)
 PositionEntity(Entity,EntityX(OEntity,True),EntityY(OEntity,True),EntityZ(OEntity,True),True)
End Function

Function RotateEntityLikeOtherEntity(Entity,OEntity)
 RotateEntity(Entity,EntityPitch(OEntity,True),EntityYaw(OEntity,True),EntityRoll(OEntity,True),True)
End Function

Function PositionRotateEntityLikeOtherEntity(Entity,OEntity)
 PositionEntity(Entity,EntityX(OEntity,True),EntityY(OEntity,True),EntityZ(OEntity,True),True)
 RotateEntity(Entity,EntityPitch(OEntity,True),EntityYaw(OEntity,True),EntityRoll(OEntity,True),True)
End Function

Function CreateGrid(GWidth%,GDepth%)
 Mesh = CreateMesh()
 Surface = CreateSurface(Mesh)
 AddVertex(Surface, 0.0, 0.0, GDepth, Float(0)/16, Float(0)/16)
 AddVertex(Surface, GWidth, 0.0, GDepth, Float(16)/16, Float(0)/16)
 AddVertex(Surface, 0.0, 0.0, 0.0, Float(0)/16, Float(16)/16)
 AddVertex(Surface, GWidth, 0.0, 0.0, Float(16)/16, Float(16)/16)
 AddTriangle(Surface,0,1,2)
 AddTriangle(Surface,2,1,3)
 UpdateNormals(Mesh)

 Texture = CreateTexture(16,16,1)
 SetBuffer(TextureBuffer(Texture))
 ClsColor(001,001,001)
 Cls()
 Color(255,255,255)
 Line(0,0,16-1,0)
 Line(0,16-1,16-1,16-1)
 Line(0,0,0,16-1)
 Line(16-1,0,16-1,16-1)
 ;TextureBlend(Texture,1)
 ScaleTexture(Texture,1.0/GWidth,1.0/GDepth)
 EntityTexture(Mesh,Texture)

 Return Mesh

End Function

Function InitializeInteractionMode()
 InteractionMode = C3D
 HidePointer()
End Function

Function UpdateInteractionMode() 

 If( KeyHit(15)=1 )
  If( InteractionMode = C2D )
   InteractionMode = C3D
   HidePointer()
  Else If( InteractionMode = C3D )
   InteractionMode = C2D
   ShowPointer()
  EndIf
 EndIf

End Function

Function BuildGhost()
 
 GhostRoot = CreatePivot()

 GhostEyes = CreatePivot()
 PositionRotateEntityLikeOtherEntity(GhostEyes,GhostRoot)
 EntityParent(GhostEyes,GhostRoot,True)

End Function

Function InitializeGhost()
 PositionEntity(GhostRoot,0,0,-3,True)
End Function

Function UpdateGhost()

 MoveMouse(GraphicsWidth()/2,GraphicsHeight()/2)
 GhostRootYaw = GhostRootYaw - Float(MXDiff)/10
 RotateEntity(GhostRoot,0,GhostRootYaw,0,False)
 GhostEyesPitch = GhostEyesPitch + Float(MYDiff)/10
 If( GhostEyesPitch &lt; -89 )
  GhostEyesPitch = -89
 Else If( GhostEyesPitch &gt; 89 )
  GhostEyesPitch = 89
 EndIf
 RotateEntity(GhostEyes,GhostEyesPitch,0,0,False)

 If( KeyDown(42) = 0 And KeyDown(29) = 0 )
  Speed# = 0.1
 Else If( KeyDown(42) = 1 And KeyDown(29) = 0 )
  Speed# = 1
 Else If( KeyDown(42) = 0 And KeyDown(29) = 1 )
  Speed# = 0.01
 EndIf

 If( KeyDown(17)=1 Or MouseDown(1)=1 )
  MoveEntity(GhostRoot,0,0,Speed)
 Else If( KeyDown(31)=1 Or MouseDown(2)=1 )
  MoveEntity(GhostRoot,0,0,-Speed)
 EndIf
 If( KeyDown(30)=1 )
  MoveEntity(GhostRoot,-Speed,0,0)
 Else If( KeyDown(32)=1 )
  MoveEntity(GhostRoot,Speed,0,0)
 EndIf
 If( KeyDown(16)=1 )
  MoveEntity(GhostRoot,0,-Speed,0)
 Else If( KeyDown(18)=1 )
  MoveEntity(GhostRoot,0,Speed,0)
 EndIf

End Function

Function GetInput()

 MX = MouseX()
 MY = MouseY()

 MXDiff = MouseXSpeed()
 MYDiff = MouseYSpeed()

End Function
</textarea><br><br>(as you can see the structure of this code is different than what i have explained in my previous post, but it works well...) <br><br></td></tr></table><br>
<a name="1321686"></a>

<a name="1321687"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> one tip I always do :<br>All functions and extra stuff should always be defined before anything else<br>(In the example of RemiD, the Graphics3D and all the stuff until the first function should be after the last function)<br><br>It asserts everything is initialized before any call.<br>(you can have some initialization to do, like, defining globals, or some function calls to initialize some libraries)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; ********************************************
; Example of a standalone library
Type StuffManager
	Field State%
End Type
Global AMandatoryGlobal.StuffManager = Null

Function InitStuff()
	AMandatoryGlobal = New StuffManager
	AMandatoryGlobal\State = 0
	OnInitStuff()
End Function

Function OnInitStuff()
	; set the state of the library
	AMandatoryGlobal\State = 1
End Function

Function UpdateStuff()
	ClsColor 50,100,128
	Cls
	Color 100,200,255
	Text 10,10, "This value '"+AMandatoryGlobal\State+"' can be drawn"
	Text 10,25, "only if you put this code before the main stuff."
	Text 10,40, "Otherwise it will try to access a Null variable"
	Text 10,55, "because the variable is not defined yet."
	Text 10,70, ""
	Text 10,100, "- Hit any key to continue -"
	Flip True
	
	WaitKey()
	
	; process some update
	; this function is called from the main loop
	; if the variable is not initialized, it will crash
	Return AMandatoryGlobal\State
End Function

; init this library on the fly.
InitStuff()
</textarea><br><br>This is just a small library that doesn't really do anything, but it's just for the purpose<br>Depending on how you include it in your code, it can generate an error<br><br>(the following will fail)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,0,2

SetBuffer BackBuffer()
Local State = UpdateStuff()

ClsColor 150,100,50
Cls
Color 255,128,000
Text 10,10, "Library State : "+State
Text 10,25, "- Hit any key to end -"
Flip True

WaitKey()

End


; If you put it here, it will fail !

; ********************************************
; Example of a standalone library
Type StuffManager
	Field State%
End Type
Global AMandatoryGlobal.StuffManager = Null

Function InitStuff()
	AMandatoryGlobal = New StuffManager
	AMandatoryGlobal\State = 0
	OnInitStuff()
End Function

Function OnInitStuff()
	; set the state of the library
	AMandatoryGlobal\State = 1
End Function

Function UpdateStuff()
	ClsColor 50,100,128
	Cls
	Color 100,200,255
	Text 10,10, "This value '"+AMandatoryGlobal\State+"' can be drawn"
	Text 10,25, "only if you put this code before the main stuff."
	Text 10,40, "Otherwise it will try to access a Null variable"
	Text 10,55, "because the variable is not defined yet."
	Text 10,70, ""
	Text 10,100, "- Hit any key to continue -"
	Flip True
	
	WaitKey()
	
	; process some update
	; this function is called from the main loop
	; if the variable is not initialized, it will crash
	Return AMandatoryGlobal\State
End Function

; init this library on the fly.
InitStuff()
</textarea><br><br>(while this one will work)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">; But if you put it before your main stuff, it works perfectly
Type StuffManager
	Field State%
End Type
Global AMandatoryGlobal.StuffManager = Null

Function InitStuff()
	AMandatoryGlobal = New StuffManager
	AMandatoryGlobal\State = 0
	OnInitStuff()
End Function

Function OnInitStuff()
	; set the state of the library
	AMandatoryGlobal\State = 1
End Function

Function UpdateStuff()
	ClsColor 50,100,128
	Cls
	Color 100,200,255
	Text 10,10, "This value '"+AMandatoryGlobal\State+"' can be drawn"
	Text 10,25, "only if you put this code before the main stuff."
	Text 10,40, "Otherwise it will try to access a Null variable"
	Text 10,55, "because the variable is not defined yet."
	Text 10,70, ""
	Text 10,100, "- Hit any key to continue -"
	Flip True
	
	WaitKey()
	
	; process some update
	; this function is called from the main loop
	; if the variable is not initialized, it will crash
	Return AMandatoryGlobal\State
End Function

; init this library on the fly.
InitStuff()








Graphics3D 800,600,0,2

SetBuffer BackBuffer()
Local State = UpdateStuff()

ClsColor 150,100,50
Cls
Color 255,128,000
Text 10,10, "Library State : "+State
Text 10,25, "- Hit any key to end -"
Flip True

WaitKey()

End
</textarea><br><br>Or you can also save the library in an other bb file and "Include" it before your loop<br>[Codebox]<br>Include "MyLibrary.bb"<br><br>Graphics 800,600,0,2 ... etc ...<br>[/Codebox]<br><br><br>So, preserving SDk, Libraries (etc ...) above your main code is the best way to get everything working without missing some initializations<br>BUT ! (there is a BUT) if the library contains some initialization that create 3D objects (a global pivot for example) or load DirectX Objects (texture, images ...), it will fail if you don't create a graphics context.<br>So, you can make something slightly different like this :<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,0,2
SetBuffer BackBuffer()

- Your Libraries here -

- your main code here -
</textarea><br><br><br>A another tip (that beginners seems to forget and not having learnt at all) : Indent your code !<br>use tabulation to make your code clear and easy to read. <br><br></td></tr></table><br>
<a name="1321689"></a>

<a name="1321690"></a>

<a name="1321692"></a>

<a name="1321693"></a>

<a name="1321694"></a>

<a name="1321695"></a>

<a name="1321696"></a>

<a name="1321697"></a>

<a name="1321698"></a>

<a name="1321724"></a>

<a name="1321738"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I agree that it is better to write constants/globals/lists/functions before anything else because if you try to use a variable/reference before it has been created, then you will get an error (because it is empty or because it is already defined in another part of the program)...<br><br>But this structure works well too (similar structure than my code example):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,0,2

SetBuffer BackBuffer()

Type StuffManager
	Field State%
End Type
Global AMandatoryGlobal.StuffManager = Null

InitStuff()

Local State = UpdateStuff()

ClsColor 150,100,50
Cls
Color 255,128,000
Text 10,10, "Library State : "+State
Text 10,25, "- Hit any key to end -"
Flip True

WaitKey()

End

Function InitStuff()
	AMandatoryGlobal = New StuffManager
	AMandatoryGlobal\State = 0
	OnInitStuff()
End Function

Function OnInitStuff()
	; set the state of the library
	AMandatoryGlobal\State = 1
End Function

Function UpdateStuff()
	ClsColor 50,100,128
	Cls
	Color 100,200,255
	Text 10,10, "This value '"+AMandatoryGlobal\State+"' can be drawn"
	Text 10,25, "only if you put this code before the main stuff."
	Text 10,40, "Otherwise it will try to access a Null variable"
	Text 10,55, "because the variable is not defined yet."
	Text 10,70, ""
	Text 10,100, "- Hit any key to continue -"
	Flip True
	
	WaitKey()
	
	; process some update
	; this function is called from the main loop
	; if the variable is not initialized, it will crash
	Return AMandatoryGlobal\State
End Function
</textarea><br><br>in any case the variables/references must be created before being used by procedures/functions, to prevent any error (you don't want your program to act on something which does not exist...)... <br><br></td></tr></table><br>
<a name="1321744"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> <img src="http://i66.tinypic.com/wbumfo.png"> <br><br></td></tr></table><br>
<a name="1321852"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I build a skeleton in code.<br><br>So most games follow a very similar pathway:<br><br>load()<br>..loop..<br>handleinput()<br>updateenvironment()<br>updatecreatures()<br>updatebullets()<br>cleardisplay()<br>drawstuff()<br>..endloop..<br>unload()<br><br>from there I write the headers of other functions I need and work inwards. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
