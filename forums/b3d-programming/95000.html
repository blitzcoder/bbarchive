<!DOCTYPE html><html lang="en" ><head ><title >Masked Texture</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Masked Texture</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Masked Texture</a><br><br>
<a name="1092472"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Moore</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ive tried creating a masked texture. It seems to work fine when I load a texture but when I create one on the fly it doesnt work... The black still appears. With the code below the goal is to just see a red circle with out the back around it.<br><br><pre class=code>
Graphics3D 800, 600
camera = CreateCamera()
AmbientLight 255,255,255

txt = CreateTexture(100,100,8) ; flag for color, alpha and mask
img = CreateImage(100, 100)
SetBuffer ImageBuffer(img)
Color 255,0,0
Oval 0,0,100,100

CopyRect 0,0,100,100,1,1, ImageBuffer(img), TextureBuffer(txt)

sprite = CreateSprite(camera)
EntityTexture sprite, txt
PositionEntity sprite,1,1,3

cube = CreateCube()
ScaleEntity cube, 5, 5, 5
PositionEntity cube,1,1,10

PointEntity camera, sprite

While Not KeyHit(1)
	SetBuffer BackBuffer()
	RenderWorld()
	Flip
	If KeyDown(200) Then TurnEntity camera,5,0,0
	If KeyDown(208) Then TurnEntity camera,-5,0,0
	If KeyDown(203) Then TurnEntity camera,0,5,0
	If KeyDown(205) Then TurnEntity camera,0,-5,0
Wend

End
</pre> <br><br></td></tr></table><br>
<a name="1092477"></a>

<a name="1092478"></a>

<a name="1092479"></a>

<a name="1092480"></a>

<a name="1092483"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rroff</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try with just flags 4 or flags 1+4 for createtexture (for alpha to work you'd have to write directly to the alpha channel, masking is different).<br><br>EDIT: Thats not working either<br><br><br>As an aside you really want to be using powers of 2 i.e. 64, 128, 256, etc. when working with textures for dimensions as no all videocards can handle non powers of 2 correctly.<br><br>EDIT2: Even more curiously if I set the mask flags on create texture say 1+4 and change the coordinate set around in copyrect it shows what should be drawn behind the masked bit, but offset by that amount... weird.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1092498"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> That is a known problem. There are multiple threads on this subject.<br>Flag 2 and 4 don't work with CreateTexture.<br>You could try two things:<br>1. Manually add alpha to texture's pixels<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 800, 600, 0, 2
camera = CreateCamera()
CameraClsColor camera, 0, 255,0 
AmbientLight 255,255,255

txt = CreateTexture(128,128,1+2) ; flag for color and alpha
img = CreateImage(128,128)
SetBuffer ImageBuffer(img)
Color 255,0,0
Oval 0,0,128,128

;manually copy pixels, so alpha can be filled in
For i = 0 To 127
For j = 0 To 127
	c = ReadPixel(i, j, ImageBuffer(img)) And $FFFFFF ;read pixel from texture
	If c = 0 ;if color = black
		WritePixel i, j, 0, TextureBuffer(txt) ;write transparent black pixel
	Else ;if color &lt;&gt; black
		WritePixel i, j, c Or $FF000000, TextureBuffer(txt) ;write color and 100% alpha to texture
	End If
Next
Next

sprite = CreateCube(camera) ;createsprite doesn't work on my computer, and prob. others, too
EntityTexture sprite, txt
PositionEntity sprite,0,0,15

PointEntity camera, sprite

While Not KeyHit(1)
	SetBuffer BackBuffer()
	RenderWorld()
	Flip
	If KeyDown(200) Then TurnEntity camera,5,0,0
	If KeyDown(208) Then TurnEntity camera,-5,0,0
	If KeyDown(203) Then TurnEntity camera,0,5,0
	If KeyDown(205) Then TurnEntity camera,0,-5,0
Wend

End</textarea><br>or<br>2. Use LoadTexture, since LoadTexture's flag 2 and 4 do work<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 800, 600, 0, 2
camera = CreateCamera()
CameraClsColor camera, 0, 255,0 
AmbientLight 255,255,255

img = CreateImage(128,128)
SetBuffer ImageBuffer(img)
Color 255,0,0
Oval 0,0,128,128
SaveImage img, "temp_texture.bmp"
txt = LoadTexture("temp_texture.bmp", 1+4)
DeleteFile "temp_texture.bmp"

sprite = CreateCube(camera) ;createsprite doesn't work on my computer, and prob. others, too
EntityTexture sprite, txt
PositionEntity sprite,0,0,15

PointEntity camera, sprite

While Not KeyHit(1)
	SetBuffer BackBuffer()
	RenderWorld()
	Flip
	If KeyDown(200) Then TurnEntity camera,5,0,0
	If KeyDown(208) Then TurnEntity camera,-5,0,0
	If KeyDown(203) Then TurnEntity camera,0,5,0
	If KeyDown(205) Then TurnEntity camera,0,-5,0
Wend

End</textarea> <br><br></td></tr></table><br>
<a name="1092538"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm thinking it's something the Blitz3D texture loader does (judging from the way Max2D handles masked images). When the masked texture is loaded, all black pixels are repainted with alpha &lt; 0.5 or any other value that will make them fail an Alpha Test (which is what Max2D uses for masked images).<br><br>Creating on-the-fly is most likely not running through this masking algorithm. <br><br></td></tr></table><br>
<a name="1092575"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Moore</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> MARK please include this bug in the next fix.<br><br>For now it seems LoadSprite("temp_texture.bmp", 1+4, camera) works fine too.<br><br>Thanks guys! <br><br></td></tr></table><br>
<a name="1092593"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BIG BUG</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is no bug, its just how it works... <br><br></td></tr></table><br>
<a name="1092602"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually it does work. you have to set the alpha bits in the texture manually though.<br><br>Ie createtexture(width,height,1+2) ;alpha or createtexture(width,height,1+4) ;masked<br><br>then go through the texture pixel by pixel and change the Alpha value of each ARGB element to 0, or the alpha level you want.<br><br>I do this all the time so I know it works.<br><br>Also - there is a way of doing it much faster which is to create a template texture..basically an alpha texture or masked texture with the entire texture's alpha value in the RGB values being zero, then use copyrect with this texture and you can then create textures which are fully transparent correctly much faster. <br><br></td></tr></table><br>
<a name="1092606"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Moore</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> "This is no bug, "<br><br>When the only work around is not documented, nor apparent and it slows things down I would say thats a bug.<br><br>When you create a texture that is masked it should automatically be completely masked and not require more lines of code to get it there. That would be the intuitive thing.<br><br>Moreover there should be an integrated means to manipulate alpha just as you control color... hmmm perhaps thats a fix... rewrite Color to be<br><br>Color(r, g, b, a# = 1.0) insted of Color(r,g,b)<br><br>Besides all that thank you everyone, its working now and better yet I know why it wasnt. <br><br></td></tr></table><br>
<a name="1092648"></a>

<a name="1092649"></a>

<a name="1092650"></a>

<a name="1092736"></a>

<a name="1092827"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Moore</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here you go. Pixel perfect sprites with masking on load or create.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; Pixies 2 Created by NRMStudios
; NRMStudios@...

; Based on "pixies.bb"
; by skidracer

; Improvements
; Animation Support
; Masking Support

; Graphics3D 800,600 ; for demo

Global pixies_mask_texture = CreateTexture(1024,1024,4)
Global pixies_mask_size = 1024
mask_texture(pixies_mask_texture)

;DEMO =========================================================================================================

;	camera = CreateCamera()
;	CameraClsColor camera, 0,100,0 ; makes background green instead of black
;	
;	
;	; Create sprite
;	sprite = CreatePixie(camera, 128, 128, 5, 2)
;	brush = GetEntityBrush(sprite)
;	t = GetBrushTexture(brush) ; get the texture
;	image = CreateImage(128,128,2)
;	
;	; add a red circle to the first frame
;	SetBuffer ImageBuffer(image, 0)
;	;SetBuffer TextureBuffer(t, 0)
;	Color 255,0,0
;	Oval 10, 10, 100, 100, True
;	Color 1,0,0
;	Oval 10, 10, 100, 100, False
;	Color 0,255,0
;	Text 50,50, "HELLO!", True, True
;	; add a blue circle to the second frame
;	SetBuffer ImageBuffer(image, 1)
;	;SetBuffer TextureBuffer(t, 1)
;	Color 0,0,255
;	Oval 10, 10, 100, 100, True
;	Color 1,0,0
;	Oval 10, 10, 100, 100, False
;	Color 255,255,255
;	Text 50,50, "HELLO!", True, True
;	
;	CopyRect 0,0,128,128,0,0,ImageBuffer(image,0),TextureBuffer(t,0) 
;	CopyRect 0,0,128,128,0,0,ImageBuffer(image,1),TextureBuffer(t,1)
;	mask_texture(t,128,128,2) 
;	FreeImage image
;	
;	; position entity on the screen
;	PositionEntity sprite, 400,200, 0
;
;	; load an animated sprite, image is 100x50 of two triangles each being 50x50 with a black masking
;	sprite2 = LoadAnimPixie(camera,"triangle.png", 50, 50, 5)
;	PositionEntity sprite2, 50, 50, 0
;	
;	sprite3 = LoadPixie(camera,"triangle.png", 5)
;	PositionEntity sprite3, 50, 150, 0
;	
;	
;	time = MilliSecs()
;	While Not KeyHit(1)
;		; Timer to animate the image
;		If MilliSecs() - time &gt; 1000 Then
;			time = MilliSecs()
;			If frame = 1 Then frame = 0 Else frame = 1 ; advance the frame
;			EntityTexture sprite, t, frame ; update the entity texture
;		EndIf
;		
;		SetBuffer BackBuffer()
;		RenderWorld()
;		Flip
;	Wend
;	
;	End
;	
; End of demo =================================================================================================================================

Function mask_texture(t, frames = 1, mask_color = 0); ...................................................................... mask_texture
; Drops the alpha of all pixels that match the masking color to 0
	w = TextureWidth(t)
	h = TextureHeight(t)
	For fi = 0 To frames - 1
		buffer = TextureBuffer(t, fi)
		LockBuffer buffer
		For iw = 0 To w - 1
			For ih = 0 To h - 1
				c = ReadPixelFast(iw, ih, buffer) And $FFFFFF 		; Read pixel from texture
				If c = mask_color 									; If Color = mask_color
					WritePixelFast iw, ih, 0, buffer 				; Write transparent black pixel
				Else 												; If color != mask_color
					WritePixelFast iw, ih, c Or $FF000000, buffer 	; Write color and 100% alpha to texture
				End If
			Next
		Next
		UnlockBuffer buffer
	Next 
End Function

Function SaveAnimImage(file$, img, frames = 1, states = 1) ; ..................................................................... SaveAnimImage
; This function is NOT needed for pixies to work.
	w = ImageWidth(img)
	h = ImageHeight(img)
	image = CreateImage(w*frames,h*states)
	For ii = 0 To states - 1
		For i = 0 To frames - 1
			CopyRect 0, 0, w, h, i * w, ii * h, ImageBuffer(img,i), ImageBuffer(image)
		Next
	Next
	chk = SaveImage(image, file)
	FreeImage image
	Return chk
End Function

Function flush_texture(t, frames = 1)
; Fast way to set the alpha of all pixels in a texture to 0 using copyrect

	w = TextureWidth(t)
	h = TextureHeight(t)
	size = w
	If h &gt; size Then size = h
	
	; adjust size of blank mask as needed
	If pixies_mask_size &lt; size Then
		While pixies_mask_size &lt; size
			pixies_mask_size = pixies_mask_size * 2
		Wend
		FreeTexture pixies_mask_texture
		pixies_mask_texture = CreateTexture(pixies_mask_size, pixies_mask_size, 5)
		mask_texture(pixies_mask_texture, pixies_mask_size, pixies_mask_size)
	EndIf
	
	For i = 0 To frames - 1
		CopyRect 0,0,w,h,0,0,TextureBuffer(pixies_mask_texture), TextureBuffer(t,i)
	Next
End Function

Function CreatePixie(camera, w#, h#, flags=1, frames=1, viewwidth# = 0, viewheight# = 0) ; CreatePixie
	; load squared texture
	
	texture = CreateTexture(w,h,flags,frames)
	width = TextureWidth(texture)
	height = TextureHeight(texture)
	
	If w&lt;&gt;width Or h&lt;&gt;height
		For i = 0 To frames - 1
			If (flags And (1 Shl 2)) &lt;&gt; 0 Then ; if image is masked
				flush_texture(texture, frames) ; flush the alpha value from the texture 
			Else ; image is not masked
				SetBuffer TextureBuffer(texture, i) ; provide a plain black background with full alpha
				Color 255,255,255
				Rect 0,0,iwidth,iheight,True
			EndIf
		Next
	
		ScaleTexture texture,Float(width)/w,Float(height)/h ; will blitzmax need float()?
		width=w
		height=h
	ElseIf (flags And (1 Shl 2)) &lt;&gt; 0 Then
		flush_texture(texture, frames)
	EndIf
	
	; change these for viewports
	If viewwidth = 0 Then viewwidth = GraphicsWidth()
	If viewheight = 0 Then viewheight = GraphicsHeight()
	
	; find existing pixiespace parented to camera
	magic=0
	n=CountChildren(camera)
	For i=1 To n
		If EntityName(GetChild(camera,i))="pixiespace" 
			magic=GetChild(GetChild(camera,i),1)
		EndIf
	Next
	If magic=0
		magic=CreatePivot(camera) 
		NameEntity(magic,"pixiespace")
		aspect# = (viewheight/viewwidth) 			;aspect# = viewheight/viewwidth
		PositionEntity magic,-1,aspect,1			;PositionEntity magic,-1,aspect,1
		scale#=(2.0/viewwidth) 						;scale# = 2.0/viewwidth
		ScaleEntity magic,scale,-scale,-scale 
		magic=CreatePivot(magic)
		PositionEntity magic,-.5,-.5,0 				;PositionEntity magic,-.5,-.5,0
	EndIf
	
	; create sprite from texture as child of magic overlay	
	sprite=CreateSprite()
	EntityParent sprite,magic		;cludge for blitz bug in createsprite(parent)
	brush=CreateBrush()
	BrushFX brush,1
	BrushTexture brush,texture
	PaintEntity sprite,brush
	FreeBrush brush
	SpriteViewMode sprite,2 
	scale#=1.0/viewwidth 
	ScaleSprite sprite,width*scale,height*scale
	Return sprite
End Function

Function LoadAnimPixie(camera,file$, w#, h#, flgs = 1, count = 0, frst=0, viewwidth# = 0, viewheight# = 0)
; load squared texture
	image=LoadImage(file)
	iwidth#=ImageWidth(image)
	iheight#=ImageHeight(image)
	If count = 0 Then 
		count = (iwidth/w)*(iheight/h)
	EndIf
	FreeImage image
	image = LoadAnimImage(file, w, h, frst, count)
	iwidth = ImageWidth(image)
	iheight = ImageHeight(image)
	texture=LoadAnimTexture(file, flgs, w, h, frst, count)
	width#=TextureWidth(texture)
	height#=TextureHeight(texture)
	
	For i = 0 To count - 1
		If iwidth&lt;&gt;width Or iheight&lt;&gt;height
			CopyRect 0,0,iwidth,iheight,0,0,ImageBuffer(image,i), TextureBuffer(texture,i) 
			
			ScaleTexture texture, width / (w), height / (h)
			width=iwidth
			height=iheight
		EndIf
	Next

	If (flgs And (1 Shl 2)) &lt;&gt; 0 Then mask_texture(texture, count)
	
	FreeImage image

	; change these for viewports
	If viewwidth = 0 Then viewwidth = GraphicsWidth()
	If viewheight = 0 Then viewheight = GraphicsHeight()
	
; find existing pixiespace parented to camera
	magic=0
	n=CountChildren(camera)
	For i=1 To n
		If EntityName(GetChild(camera,i))="pixiespace" 
			magic=GetChild(GetChild(camera,i),1)
		EndIf
	Next
	If magic=0
		magic=CreatePivot(camera) 
		NameEntity(magic,"pixiespace")
		aspect# = (viewheight/viewwidth) 			;aspect# = viewheight/viewwidth
		PositionEntity magic,-1,aspect,1			;PositionEntity magic,-1,aspect,1
		scale#=(2.0/viewwidth) 						;scale# = 2.0/viewwidth
		ScaleEntity magic,scale,-scale,-scale 
		magic=CreatePivot(magic)
		PositionEntity magic,-.5,-.5,0 				;PositionEntity magic,-.5,-.5,0
	EndIf
	
; create sprite from texture as child of magic overlay	
	sprite=CreateSprite()
	EntityParent sprite,magic		;cludge for blitz bug in createsprite(parent)
	brush=CreateBrush()
	BrushFX brush,1
	BrushTexture brush,texture
	PaintEntity sprite,brush
	FreeBrush brush
	SpriteViewMode sprite,2 
	scale#=1.0/viewwidth 
	ScaleSprite sprite,width*scale,height*scale
	Return sprite
End Function

Function LoadPixie(camera,file$, flags)
; load squared texture
	texture=LoadTexture(file, flags)
	width#=TextureWidth(texture)
	height#=TextureHeight(texture)
	image=LoadImage(file)
	iwidth#=ImageWidth(image)
	iheight#=ImageHeight(image)

	If iwidth&lt;&gt;width Or iheight&lt;&gt;height
		SetBuffer TextureBuffer(texture)
		
		CopyRect 0,0,iwidth,iheight,0,0,ImageBuffer(image), TextureBuffer(texture)
	
		ScaleTexture texture,Float(width)/iwidth,Float(height)/iheight ; will blitzmax need float()?
		width=iwidth
		height=iheight
	EndIf

	FreeImage image
	
	If (flags And (1 Shl 2)) &lt;&gt; 0 Then mask_texture(Texture, width, height)
	
; change these for viewports
	viewwidth#=GraphicsWidth()
	viewheight#=GraphicsHeight()
	
; find existing pixiespace parented to camera
	magic=0
	n=CountChildren(camera)
	For i=1 To n
		If EntityName(GetChild(camera,i))="pixiespace" 
			magic=GetChild(GetChild(camera,i),1)
		EndIf
	Next
	If magic=0
		magic=CreatePivot(camera) 
		NameEntity(magic,"pixiespace")
		aspect# = (viewheight/viewwidth) 			;aspect# = viewheight/viewwidth
		PositionEntity magic,-1,aspect,1			;PositionEntity magic,-1,aspect,1
		scale#=(2.0/viewwidth) 						;scale# = 2.0/viewwidth
		ScaleEntity magic,scale,-scale,-scale 
		magic=CreatePivot(magic)
		PositionEntity magic,-.5,-.5,0 				;PositionEntity magic,-.5,-.5,0
	EndIf
	
; create sprite from texture as child of magic overlay	
	sprite=CreateSprite()
	EntityParent sprite,magic		;cludge for blitz bug in createsprite(parent)
	brush=CreateBrush()
	BrushFX brush,1
	BrushTexture brush,texture
	PaintEntity sprite,brush
	FreeBrush brush
	SpriteViewMode sprite,2 
	scale#=1.0/viewwidth 
	ScaleSprite sprite,width*scale,height*scale
	Return sprite
End Function
</textarea><br><br><font class="tiny">Last edited 2011 Forgot to set frame in mask texture</font><br><br><font class="tiny">Last edited 2011 Fixed bug that would not allow for uneven texture (miss named variable)</font><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1092652"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice! May be an idea to post this in the code archives. Makes it easier to find. <br><br></td></tr></table><br>
<a name="1092654"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I would disagree it's a bug. When you load a texture, it's not exactly quick. The time taken to load the texture, probably includes the time to set the alpha information as well. It would be nice to have this though. Especially on copyrect, I would love it to copy alpha information :) <br><br></td></tr></table><br>
<a name="1092792"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Moore</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow... so the text function does not work on textures. Read a few posts on it.<br><br>SO... if you want to use text then... create a image, draw and write to your hearts content on it, use copyrect to transfer it to the texture and mask_texture to fix the alpha. Uhg! <br><br></td></tr></table><br>
<a name="1092793"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Copyrect *does* copy alpha information - I use it all the time - I create a base texture with all argb values as 0 then copyrect this to any new alphaed/masked textures I create and it does indeed copy the alpha values. Note this is from a texture to another texture, not from or to the backbuffer.. <br><br></td></tr></table><br>
<a name="1092794"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Example:<br><br><pre class=code>
Graphics3D 512,512,0,2
camera=CreateCamera()
CameraClsColor camera,32,32,32
alphatexture=CreateTexture(128,128,1+2)
SetBuffer TextureBuffer(alphatexture)
LockBuffer
For y=0 To TextureHeight(alphatexture)-1
	For x=0 To TextureWidth(alphatexture)-1
		WritePixelFast x,y,0
	Next
Next
UnlockBuffer
SetBuffer BackBuffer()

cube=CreateCube()
EntityFX cube,1+16
cubetex=CreateTexture(128,128,1+2)
EntityTexture cube,cubetex
MoveEntity camera,0,0,-4


Repeat

	TurnEntity cube,1,2,3
	RenderWorld
	;press space to copyrect the alphatexture to the cube texture..and we'll draw somethings to it too!
	If KeyHit(57) Then 
		CopyRect 0,0,128,128,0,0,TextureBuffer(alphatexture),TextureBuffer(cubetex)
		SetBuffer TextureBuffer(cubetex)
		Color 127+Rand(1,4)*32,127+Rand(1,4)*32,127+Rand(1,4)*32
		Text 64,64,"Hello",1,1
		SetBuffer BackBuffer()
	EndIf 
	Flip

Until KeyDown(1)

</pre> <br><br></td></tr></table><br>
<a name="1092954"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, sorry I should have been more specific. It doesn't copy alpha information from the backbuffer. Say you draw a cube on the cameraclscolor of white, it will copy the white. Just would be handy to not do that :) Apologies for the confusion :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
