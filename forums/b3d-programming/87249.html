<!DOCTYPE html><html lang="en" ><head ><title >Higher-order functions</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Higher-order functions</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Higher-order functions</a><br><br>
<a name="988709"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just fiddling around a bit... I make no guarantee that any of this is sensible or even accurate.<br><br>For those who aren't aware, a higher-order function is one which can take functions as parameters, or return a function as a result. While this sort of thing is possible in BlitzMax, Blitz3D doesn't offer any such functionality, largely due to not supporting function pointers (until recently, with the introduction of a couple of rather nice DLLs, but let's ignore this).<br><br>Now there's a long-established way around the lack of function pointers which will be familiar to anyone who's attempted to build a scripting language, which is to set constants to represent your functions and execute them via an Eval() or Apply() (or whatever) function, using a Select structure to choose the right function to call (I suggest you stop reading now if you don't like this method). The automatic type conversion in Blitz3D makes strings the logical (if rather slow) choice for passing parameters through Eval().<br><pre class=code>

;Function pointer list - making these Globals might look tidier?
Const PTR_NullFunction=0
Const PTR_Add=1:NewFunction("Add",PTR_Add,2)
Const PTR_Mul=2:NewFunction("Mul",PTR_Mul,2)
Const PTR_Pow=3:NewFunction("Pow",PTR_Pow,2)
Const PTR_Max=4:NewFunction("Max",PTR_Max,2)
Const PTR_Square=5:NewFunction("Square",PTR_Square,1)



;Simple example functions - all very trivial
Function Add(x,y)
	Return x+y
End Function

Function Mul(x,y)
	Return x*y
End Function

Function Pow(x,y)
	Return x^y
End Function

Function Max(x,y)
	If x&gt;y Then Return x:Else Return y
End Function

Function Square(x)
	Return x*x
End Function
</pre><br>Then we could do something like this, to use the constants as function pointers (not that this is very pointful in itself):<br><pre class=code>
Print Add(5,8)
Print Eval(PTR_Add,5,8)
Print Max(5,8)
Print Eval(PTR_Max,5,8)
Print Pow(5,8)
Print Eval(PTR_Pow,5,8)

WaitKey
End
</pre><br>Calling the functions directly, as above, is not especially useful. However, with a means to call a function from an integer variable, it becomes much easier to write a map or fold function - a higher-order function taking a function as one of its parameters. The map function takes a list and applies the specified function to each element of the list:<br><pre class=code>
;Examples of higher-order functions
Function Map(func,list[5],arg$=NULLSTR)		;Given the highly limited nature of Blitz arrays, use a bank for this in a real program
	Local i
	For i=0 To 5
		list[i]=Eval(func,list[i],arg)
	Next
End Function

Function LeftFold$(func,list[5],rval$)
	Local i
	For i=0 To 5
		rval=Eval(func,rval,list[i])
	Next
	Return rval
End Function
</pre><br>The other advantage of using Eval() to call functions rather than doing it directly, is that they don't necessarily need to be fully applied. Higher-order functions can also return functions as their results - partial application means that a function that isn't given enough arguments to return a value will instead return a function that can be given more arguments:<br><pre class=code>
Print Add(5,3)
Print Eval(PTR_Add,5,3)
Local add5=Eval(PTR_Add,5)
Print Eval(add5,3)
Print Eval(add5,5)

WaitKey
End
</pre><br>This is closely connected to "currying". Since obviously the add5() function is not a real Blitz3D function, it needs to be created as a function type-object. While it can't be called directly in code, it can be called through Eval() exactly the same way as any other function with an integer pointer. However, if we can create function objects, there's also no reason to be limited to just partial application of existing functions; we can also compose them together using a technique known as lambda abstraction:<br><pre class=code>
add5=Lambda("x","add(x,5)")
Print Eval(add5,3)

Print Eval(Lambda("x,y,f","f(ARG_0,square(f(x,y)))",1),5,2,PTR_Add)

WaitKey
End
</pre><br>The string on the left is a list of parameters in the order they are passed when the function is called. The function returns the ultimate value of the expression in the string on the right. For the sake of elegance, this implementation accepts more arguments which it will swap out for specific names ("arg_0", "arg_1" etc), but since the two main fields are strings, it would be perfectly possible to simply concatenate any values in directly if desired. Do notice that in the second example, the outermost function f() is also a variable, called by integer handle as in the map() example above - functions are first-class (but typed) objects in this system and can be passed anywhere any other free variable can.<br><br>Aside from providing a slightly more interesting way to declare functions, function literals (anonymous functions) have other uses. They can be passed directly into calls to other higher-order functions such as map(), when the function required doesn't really need its own declaration. Or they can be returned from other "factory" functions, having had some of the free variables bound, to create what is known as a closure:<br><pre class=code>
Function AddN(n)
	Return Lambda("x","add(x,arg_0)",n)
End Function

Local add2=AddN(2)
Print Eval(add2,5)
Local add3=AddN(3)
Print Eval(add3,5)
Local add4=AddN(4)
Print Eval(add4,5)

WaitKey
End
</pre><br>This is again connected to currying (although they are distinct concepts, this system doesn't really demonstrate the difference). The idea is to create new functions with fewer arguments, having "bound over" the ones that aren't required to change for the time being, thus having a function that more closely reflects what it's being used for. The ability to close over variables and quickly compose functions together into temporary objects has endless applications, anyway, and is a large part of the basis of functional programming.<br><br>OK, enough teasing, here is the (horrible, slow and inefficient) code that powers this abomination:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

;====================================================================
;Higher-order function system
;====================================================================


;====================================================================
;Constants
;====================================================================
Const MAX_ARGS=7		;This can be increased, but consider smaller functions! Counting from 0 this gives 8 arguments
Const NULLSTR$="£$%^"	;Any highly unlikely string here. Feel free to change it - this is to allow passing "" as a valid parameter

Const A_TYPE_FREE=0		;Free variable type - this needs to be 0
Const A_TYPE_BVAL=%01	;Bound value type
Const A_TYPE_FUNC=%10	;Function call type

Const STD_NAME_0$="arg_0"	;standard variable names allow slightly more elegant lambda abstractions. replace these with any name not in use elsewhere
Const STD_NAME_1$="arg_1"
Const STD_NAME_2$="arg_2"
Const STD_NAME_3$="arg_3"	;The lambda expressions themselves are not case sensitive, but these names
Const STD_NAME_4$="arg_4"	; need to be in whatever case the expressions are converted to
Const STD_NAME_5$="arg_5"
Const STD_NAME_6$="arg_6"
Const STD_NAME_7$="arg_7"
;====================================================================


;====================================================================
;Types
;====================================================================
;Function type
Type FunctionObject		;One of these for every function involved
	Field ID,ev_ID			;Unique identifier, identifier of base function (for partially applied functions)
	Field name$				;Necessary if it's going to be referenced in an anonymous function
	Field helper,hname$		;True if a helper function, helper identifier
	Field no_args_p			;Number of args to pass
	Field no_args_t			;Number of args including bound upvalues
	Field lambdaL$
	Field lambdaR$
	Field uniqueEnv				;True to create a new lexical environment when evaluating
	Field arg_name$[MAX_ARGS]	;Parameter names, where relevant
	Field arg_type%[MAX_ARGS]
	Field arg_value$[MAX_ARGS]	;Values for partial application/binding. Final one contains a function ID for anonymous functions - do not use it.
End Type

;Lexical environment
Type LexicalEnv		;Bind upvalues to this
	Field aCount
	Field arg_name$[MAX_ARGS]
	Field arg_type%[MAX_ARGS]
	Field arg_value$[MAX_ARGS]
End Type
;====================================================================


;====================================================================
;Globals
;====================================================================
Global FunctionCount		;Not actually an accurate count, but incremented when a function object is created
Global LambdaName$,LambdaType,LambdaValue$		;Multiple return values from ParseLamda
;====================================================================


;====================================================================
;Functions
;====================================================================

;The eval function (technically, the "apply" function - Lambda() is technically an "eval"). Add as many arguments as required
Function Eval$(func,arg0$=NULLSTR,arg1$=NULLSTR,arg2$=NULLSTR,arg3$=NULLSTR,arg4$=NULLSTR,arg5$=NULLSTR,arg6$=NULLSTR,arg7$=NULLSTR)	;Unfortunately can't have unlimited parameters in B3D
	Local aCount,cArg,i,j,ev_ID,rFunc$,arg$[MAX_ARGS],c_arg$[MAX_ARGS]
	Local f.FunctionObject,newf.FunctionObject,env.LexicalEnv
	
	rFunc=func
	For f=Each FunctionObject
		If f\ID=func Then Exit
	Next
	
	arg[0]=arg0		;This is horrible and slow, but it allows a nicer function call (ie. with commas)
	arg[1]=arg1		;Might get a speedup by using nested If structure (see Lambda()), but this is clearer
	arg[2]=arg2
	arg[3]=arg3
	arg[4]=arg4
	arg[5]=arg5
	arg[6]=arg6
	arg[7]=arg7
	
	env=Last LexicalEnv
	If env=Null
		For i=0 To f\no_args_p-1		;Count passed arguments
			If arg[i]=NULLSTR Then Exit
		Next
		aCount=i
	Else	;If a lexical environment object exists there cannot have been any arguments passed directly
		aCount=f\no_args_p		;Known in advance
	End If
	
	For i=0 To MAX_ARGS
		If i&lt;f\no_args_t
			If (f\arg_type[i] And A_TYPE_BVAL)
				c_arg[i]=f\arg_value[i]
			Else	;A_TYPE_FREE
				If f\helper
					For j=0 To MAX_ARGS
						If env\arg_name[j]=f\arg_name[i] Then c_arg[i]=env\arg_value[j]
					Next
				Else
					c_arg[i]=arg[cArg]
					cArg=cArg+1
				EndIf
			EndIf
			If (f\arg_type[i] And A_TYPE_FUNC) Then c_arg[i]=Eval(f\arg_value[i],c_arg[i])
		Else
			c_arg[i]=arg[i]
		EndIf
	Next
	
	If f\uniqueEnv
		env=New LexicalEnv
		env\aCount=aCount
		For i=0 To MAX_ARGS		;Copy upvalues
			If i&lt;aCount
				env\arg_name[i]=f\arg_name[i]
		;	env\arg_type[i]=f\arg_type[i]
				env\arg_value[i]=arg[i]
			Else
				env\arg_name[i]=NULLSTR
			EndIf
		Next
	EndIf
	
	If aCount=f\no_args_p
		
		Select func		;In a long list, consider a binary tree instead of Select
			Case PTR_NullFunction:rFunc=c_arg[0]		;Lambda without a function?
			Case PTR_Add:rFunc=Add(c_arg[0],c_arg[1])
			Case PTR_Mul:rFunc=Mul(c_arg[0],c_arg[1])
			Case PTR_Pow:rFunc=Pow(c_arg[0],c_arg[1])
			Case PTR_Max:rFunc=Max(c_arg[0],c_arg[1])
			Case PTR_Square:rFunc=Square(c_arg[0])
			Default
				If f\ev_ID=0		;If the function call ID is variable
					If f\helper&gt;1
						ev_ID=Int(arg0)
					Else
						For i=0 To MAX_ARGS
							If env\arg_name[i]=f\hname Then ev_ID=env\arg_value[i]:Exit
						Next
					EndIf
				Else
					ev_ID=f\ev_ID
				EndIf
				rFunc=Eval(ev_ID,c_arg[0],c_arg[1],c_arg[2],c_arg[3],c_arg[4],c_arg[5],c_arg[6],c_arg[7])
		End Select
		
	Else		;Partial evaluation - should ONLY occur when env=Null
		
		newf=NewFunction("",func,f\no_args_p,aCount)		;Still create a new function if identical (ie. no args passed) ? Sure, why not
		
		For i=0 To aCount		;Copy the closed values
			If arg[i]&lt;&gt;NULLSTR
				newf\arg_type[i]=A_TYPE_BVAL
				newf\arg_value[i]=arg[i]
			EndIf
		Next
		rFunc=FunctionCount
		
	EndIf
	
	If f\uniqueEnv Then Delete Last LexicalEnv
	
	Return rFunc
End Function

;This creates a function object from a string (lambda abstraction). Extra arguments are completely optional
Function Lambda(lamL$,lamR$,arg0$="",arg1$="",arg2$="",arg3$="",arg4$="",arg5$="",arg6$="",arg7$="")	;Intentionally not NULLSTR
	Local i,lam.FunctionObject
	
	lamL=Lower(lamL)		;Force case-insensitivity
	lamR=Lower(lamR)
	
	If arg0&lt;&gt;""		;Replace standard names with optional parameters
		lamR=Replace(lamR,STD_NAME_0,arg0)
		If arg1&lt;&gt;""
			lamR=Replace(lamR,STD_NAME_1,arg1)		;This totally bizarre nested structure should offer a slight speedup!
			If arg2&lt;&gt;""
				lamR=Replace(lamR,STD_NAME_2,arg2)
				If arg3&lt;&gt;""
					lamR=Replace(lamR,STD_NAME_3,arg3)
					If arg4&lt;&gt;""
						lamR=Replace(lamR,STD_NAME_4,arg4)
						If arg5&lt;&gt;""
							lamR=Replace(lamR,STD_NAME_5,arg5)
							If arg6&lt;&gt;""
								lamR=Replace(lamR,STD_NAME_6,arg6)
								If arg7&lt;&gt;""
									lamR=Replace(lamR,STD_NAME_7,arg7)
								EndIf
							EndIf
						EndIf
					EndIf
				EndIf
			EndIf
		EndIf
	EndIf
	
	For lam=Each FunctionObject
		If lam\uniqueEnv
			If lam\lambdaL=lamL		;If this function already exists, use it again
				If lam\lambdaR=lamR Then Return lam\ID
			EndIf
		EndIf
	Next
	
	lam=NewFunction("",0,0)
	lam\lambdaL=lamL
	lam\lambdaR=lamR
	lam\uniqueEnv=True
	
	For i=0 To MAX_ARGS
		If lamL&lt;&gt;""
			If Instr(lamL,",")
				lam\arg_name[i]=Trim(Left(lamL,Instr(lamL,",")-1))		;Will be used to create a lexical environment
				lamL=Trim(Right(lamL,Len(lamL)-Instr(lamL,",")))
			Else
				lam\arg_name[i]=Trim(lamL):lamL=""
			EndIf
		Else
			Exit
		EndIf
	Next
	lam\no_args_p=i
	lam\no_args_t=i
	
	LambdaValue=0
	ParseLambda(lamR,1,1,lam)		;Parse the lambda
	lam\ev_ID=LambdaValue
	
	Return lam\ID
End Function

;Helper function for Lambda()
Function ParseLambda(expression$,level,ptr,lam.FunctionObject)
	Local i,a,c$,token$,ttype,tvalue,f.FunctionObject
	
	If level&gt;lam\uniqueEnv Then lam\uniqueEnv=level
	
	c=Mid(expression,ptr,1)
	While c=" "
		ptr=ptr+1:c=Mid(expression,ptr,1)		;Skip whitespace
	Wend
	a=Asc(c)
	While a&lt;&gt;40 And a&lt;&gt;41 And a&lt;&gt;44		;Just permit everything except the actual separators ("(",")",",") - faster
		token=token+c
		ptr=ptr+1:c=Mid(expression,ptr,1)
		While c=" "
			ptr=ptr+1:c=Mid(expression,ptr,1)		;Skip whitespace
		Wend
		a=Asc(c)
	Wend
	
	For i=0 To lam\no_args_t-1		;Get bound/free state
		If token=lam\arg_name[i] Then Exit	;It's a free variable
	Next
	If i=lam\no_args_t Then ttype=ttype Or A_TYPE_BVAL	;It's a bound variable
	
	If c="("	;It's a function
		ttype=ttype Or A_TYPE_FUNC
		If (ttype And A_TYPE_BVAL)
			If Int(token)
				tvalue=Int(token)	;Allow the use of function handle rather than function name, if desired (more elegant?)
			Else
				For f=Each FunctionObject
					If Lower(f\name)=token Then tvalue=f\ID:Exit		;Token should already be Lower()-ed
				Next
			EndIf
		EndIf
		f=NewFunction("",tvalue,0)	;Set no. args later, once we have them
		f\helper=level
		f\hname=token
	Else
		LambdaName=token		;Set return values
		LambdaType=ttype
		If (ttype And A_TYPE_BVAL) Then LambdaValue=token	;All bound variables are represented literally (this is ignored for free variables)
		Return ptr
	EndIf
	
	For i=0 To MAX_ARGS
		If Mid(expression,ptr,1)=")" Then ptr=ptr+1:Exit
		ptr=ParseLambda(expression,level+1,ptr+1,lam)
		f\arg_name[i]=LambdaName		;Set arg information
		f\arg_type[i]=LambdaType
		f\arg_value[i]=LambdaValue
	Next
	
	f\no_args_p=i		;Set the argument number
	f\no_args_t=i
	
	LambdaName=token		;Set return values
	LambdaType=ttype
	LambdaValue=f\ID
	Return ptr
End Function

;Create a new function object with the specified properties
Function NewFunction.FunctionObject(name$,ev_ID,no_args_t,no_args_p=-1)
	Local f.FunctionObject=New FunctionObject
	FunctionCount=FunctionCount+1
	f\ID=FunctionCount
	f\ev_ID=ev_ID
	f\name=name		;Force case-insensitivity?
	f\no_args_t=no_args_t
	If no_args_p=-1 Then f\no_args_p=no_args_t:Else f\no_args_p=no_args_p
	Return f
End Function

;Delete a function object and any helper functions
Function FreeFunction(ID)
	Local i,f.FunctionObject
	For f=Each FunctionObject
		If f\ID=ID Then Exit
	Next
	If f&lt;&gt;Null
		If f\lambdaL&lt;&gt;"" Or f\lambdaR&lt;&gt;"" Then FreeFunction(f\ev_ID)
		For i=0 To MAX_ARGS		;Recursively free helper functions
			If f\arg_type[i]=A_TYPE_FUNC Then FreeFunction(f\arg_value[i])
		Next
		Delete f
	End If
End Function

;Set a function's name. Set it back to "" for it to be deleted by ClearFunctions
Function NameFunction(ID,name$)
	Local i,f.FunctionObject;,ev.FunctionObject
	
	name=name		;Not case-sensitive - could remove this if speed is a problem
	
	For f=Each FunctionObject
		If f\ID=ID Then Exit
	Next
	;Check if it's a lambda
	If f&lt;&gt;Null		;Do something if it's null?
		f\name=name
		If f\lambdaL&lt;&gt;"" Or f\lambdaR&lt;&gt;"" Then NameFunction(f\ev_ID,name+"'")
		For i=0 To MAX_ARGS		;Recursively name helper functions to save them from ClearFunctions - or expose them to it
			If f\arg_type[i]=A_TYPE_FUNC
				If name="" Then NameFunction(f\arg_value[i],""):Else NameFunction(f\arg_value[i],name+"'")
			EndIf
		Next
	EndIf
End Function

;Delete any unneeded functions (since they're created in bucketloads). Only functions named "" are deleted
Function ClearFunctions()
	Local f.FunctionObject,SurvivorCount
	
	For f=Each FunctionObject
		If f\name=""
			Delete f		;If a function has a set name it is spared; set the name to "" again to have it deleted
		Else
			If f\ID&gt;SurvivorCount Then SurvivorCount=f\ID
		EndIf
	Next
	
	FunctionCount=SurvivorCount		;Reduce the number of wasted IDs
End Function
;====================================================================
</textarea><br><br>You should define a constant for all your native Blitz3D functions that you want to make accessible to this system, and call NewFunction() in the same order (NewFunction() increments a global variable so doing this out of order will break things). You'll also have to add these functions to the main Select/Case list inside Eval(), which ought to be fairly straightforward.<br><br>The Lambda() function has two syntactic quirks (by necessity - as far as possible the goal of this system is to make the syntax look natural, to actually speed up programming). Firstly, since the arguments are string literals, there is no access to any Blitz3D variables or globals. This is not a bad thing, as it helps maintain functional purity. Also because they are literals, to get values into the function definition, you'll need to either use string concatenation ( Lambda ("x", "add ( x , "+N+")") ) or the known constant names, passing the values as extra arguments ( Lambda ( "x", "add ( x , arg_0 )" , N ) ). I think the second looks much nicer, but they are effectively identical (actually the first should be slightly faster). The second quirk is that there are no infix operators - if you want to write a complex parser go ahead, but currently it only supports function calls, so any simple operations will still need to have a function defined (eg. add(x,y) - with a little imagination you could also devise functions for If/Then/Else, sequential execution of commands, etc.). Anonymous functions should be case-insensitive, achieved by using the Lower() command - if you want to change the known constant names, make sure their definitions are lower-case.<br><br>Because functions are created by the bucketload (Lambda() creates loads of helper-functions) and there is no garbage collection in Blitz3D (not for this, anyway), you'll need to get rid of function objects manually. You can use FreeFunction(ID), or ClearFunctions, which deletes all functions that haven't been explicitly named (so if you want to save a function, name it with NameFunction(ID,name) - to expose it for deletion, rename it as ""). NameFunction and FreeFunction are recursive and should cover helper functions too. You don't need to call ClearFunctions constantly though, as Lambda() will not create a new anonymous function object if an identical one already exists.<br><br>I make no guarantee that I have understood the concepts involved correctly, so take anything I say above with a pinch of salt (except for the fact that functional programming rules, because it simply does). For further reading on the subject, see:<br><br><a href="http://haskell.org/haskellwiki/Functional_programming" target="_blank">http://haskell.org/haskellwiki/Functional_programming</a><br><a href="http://en.wikipedia.org/wiki/Functional_programming" target="_blank">http://en.wikipedia.org/wiki/Functional_programming</a><br><a href="http://haskell.org/haskellwiki/Lambda_abstraction" target="_blank">http://haskell.org/haskellwiki/Lambda_abstraction</a><br><a href="http://en.wikipedia.org/wiki/Anonymous_function" target="_blank">http://en.wikipedia.org/wiki/Anonymous_function</a><br><br>...which were my main points of reference while doing this.<br><br>Have fun...<br><br><br>EDIT: All of this is supposed to work out-of-the-box as include files, hence the horrible syntax. I'm now working on a preprocessor (almost a source-to-source translator, really) that will hopefully make things a little prettier, and a lot faster, but obviously that's a very different undertaking (will include OOP).<br><br>EDIT2: OK so the preprocessor turned into something else entirely. Still trying to think of ways to make this practical though. Can't think of any good ways to imitate garbage collection... <br><br></td></tr></table><br>
<a name="988720"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Niiiiice.<br><br>Disappointed you didn't find a way to work with real blitz functions, but very clever anyway. <br><br></td></tr></table><br>
<a name="988757"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WERDNA</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote">   partial application means that a function that isn't given enough arguments to return a value will instead return a function that can be given more arguments:  <br></div><br><br>I was just about to ask if there was a way to do this, lol.<br><br>You just saved me the trouble :)<br><br>Awesome post Yasha!<br><br>W E R D N A <br><br></td></tr></table><br>
<a name="989156"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Glad you guys liked it! A little more testing revealed some bugs, which I found and fixed; if you're playing with this (or, God forbid, found a practical use for it), please copy the code again. The main problem was in ParseLambda(), not handling function composition properly.<br><br><br><br>And now for something completely stupid...<br><br>The idea of using FP style in Blitz3D got me thinking along a very, very odd path. Best illustrated by (a very messy and largely uncommented) example. First, you'll need to update the list of functions available to Eval() (this is its Select/Case structure, inside the function):<br><br><pre class=code>
Case PTR_Add:rFunc=Add(c_arg[0],c_arg[1])
Case PTR_Mul:rFunc=Mul(c_arg[0],c_arg[1])
Case PTR_Pow:rFunc=Pow(c_arg[0],c_arg[1])
Case PTR_Max:rFunc=Max(c_arg[0],c_arg[1])
Case PTR_Identity:rFunc=Identity(c_arg[0])
Case PTR_ConcStr:rFunc=ConcStr(c_arg[0],c_arg[1])
Case PTR_IIf:rFunc=IIf(c_arg[0],c_arg[1],c_arg[2])
Case PTR_Eq:rFunc=Eq(c_arg[0],c_arg[1])
Case PTR_Right:rFunc=Right(c_arg[0],c_arg[1])
Case PTR_maybeUnit:rFunc=maybeUnit(c_arg[0])
Case PTR_maybeBind:rFunc=maybeBind(c_arg[0],c_arg[1])
Case PTR_maybeDiv:rFunc=maybeDiv(c_arg[0],c_arg[1])
Case PTR_stateUnit:rFunc=stateUnit(c_arg[0])
Case PTR_stateBind:rFunc=stateBind(c_arg[0],c_arg[1])
Case PTR_Random:rFunc=Random(c_arg[0])
</pre><br>That goes in the library in the post above, which is the file referred to below as "HOF.bb". Next, an include file (no real reason other than to keep it organised) - "Monad.bb":<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Type Monad
	Field name$
	Field constructor		;Define the monadic type?
	Field mUnit		;Return: function to map a value to a value of corresponding monadic type
;					; ie. Takes an unamplified value and amplifies it
	Field mBind		;Accept a monadic type (computation), take its underlying value (result), apply f to this, output a unified result of monadic type (run result)
;					; ie. Access underlying value and feed to the next function. Implementation depends on nature of amplified type
End Type


Function DefineMonad(name$,mUnit,mBind)
	Local m.Monad,f.FunctionObject
	
	;Test monad laws?
	
	m=New Monad		;If valid, create &amp; return
	m\name=name
	m\mUnit=mUnit
	m\mBind=mBind
	
	For f=Each FunctionObject	;If defining via Lambda(), make sure the functions aren't cleared
		If f\ID=mUnit
			If f\name="" Then NameFunction(f\ID,name+"_Unit")
		ElseIf f\ID=mBind
			If f\name="" Then NameFunction(f\ID,name+"_Bind")
		EndIf
	Next
	
	Return Handle(m)
End Function

Function FreeMonad(mon)
	Local m.Monad,f.FunctionObject
	
	m=Object.Monad(mon)
	For f=Each FunctionObject	;Un-name function objects if they were only being kept for the monad
		If f\ID=m\mUnit
			If f\name=m\name+"_Unit" Then NameFunction(f\ID,"")
		ElseIf f\ID=m\mBind
			If f\name=m\name+"_Bind" Then NameFunction(f\ID,"")
		EndIf
	Next
	
	Delete m
End Function

Function Unit$(mon,value$)		;This is not unit, this merely calls the relevant monad's unit function
	Local m.Monad=Object.Monad(mon)
	Return Eval(m\mUnit,value)
End Function

Function Bind$(mon,id$,k)		;This is not bind, this merely calls the relevant monad's bind function
	Local m.Monad=Object.Monad(mon)
	Return Eval(m\mBind,id,k)
End Function


;"Maybe" - just because we have lambdas doesn't mean we *have* to use them, although it would be cleaner (but a LOT more work)

Function maybeUnit$(value$)
	Return MaybeTrue+value
End Function

Function maybeBind$(x$,k)
	If Left(x,5)=MaybeTrue
		Return Eval(k,Right(x,Len(x)-5))		;Function is applied to the underlying value
	Else
		Return MaybeFalse
	EndIf
End Function

Function maybeDiv$(a#,b#)		;Outputs a monadic type - really this should be a lift of regular Div()
	If b&lt;&gt;0 Then Return MaybeTrue+Str(a/b)
	Return MaybeFalse
End Function


;"State" - similarly

Function stateUnit(a$)
	Return Lambda("s","concStr(arg_0,s)",a+StateDivider)
End Function

Function stateBind$(x$,k)
	Return Eval(k,Right(x,Len(x)-Instr(x,StateDivider)))
End Function
</textarea><br><br>And finally, the example:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

;Higher-order function system
Include "HOF.bb"
Include "Monad.bb"

;Function pointer list - making these Globals might look tidier?
Const PTR_NullFunction=0
Const PTR_Add=1:NewFunction("Add",PTR_Add,2)
Const PTR_Mul=2:NewFunction("Mul",PTR_Mul,2)
Const PTR_Pow=3:NewFunction("Pow",PTR_Pow,2)
Const PTR_Max=4:NewFunction("Max",PTR_Max,2)
Const PTR_Identity=5:NewFunction("Identity",PTR_Identity,1)
Const PTR_Unit=6:NewFunction("Unit",PTR_Unit,2)
Const PTR_ConcStr=7:NewFunction("ConcStr",PTR_ConcStr,2)
Const PTR_IIf=8:NewFunction("IIf",PTR_IIf,3)
Const PTR_Eq=9:NewFunction("Eq",PTR_Eq,2)
Const PTR_Right=10:NewFunction("Right",PTR_Right,2)

Const PTR_maybeUnit=11:NewFunction("maybeUnit",PTR_maybeUnit,1)		;Easier to do than Lambda()! Also much faster
Const PTR_maybeBind=12:NewFunction("maybeBind",PTR_maybeBind,2)
Const PTR_maybeDiv=13:NewFunction("maybeDiv",PTR_maybeDiv,2)

Const MaybeTrue$="Just "
Const MaybeFalse$="Nothing"

Const PTR_stateUnit=14:NewFunction("stateUnit",PTR_stateUnit,1)
Const PTR_stateBind=15:NewFunction("stateBind",PTR_stateBind,2)
Const PTR_Random=16:NewFunction("Random",PTR_Random,1)

Const StateDivider$=";"		;This can be any character apart from parentheses or comma, as Lambda() uses those


;Main
Local ident=DefineMonad("Identity",PTR_Identity,Lambda("x,k","k(identity(x))"))
Local maybe=DefineMonad("Maybe",PTR_maybeUnit,PTR_maybeBind)
Local state=DefineMonad("State",PTR_stateUnit,PTR_stateBind)

Print Bind(ident,Unit(ident,60),Lambda("x","add(x,5)"))		;Identity - pointless example
Print Bind(maybe,Bind(maybe,maybeDiv(116,2),Lambda("x","maybeDiv(x,42)")),Lambda("x","maybeDiv(x,2)"))		;Maybe - division
Print Bind(maybe,Bind(maybe,maybeDiv(116,0),Lambda("x","maybeDiv(x,42)")),Lambda("x","maybeDiv(x,2)"))		;Maybe - division by zero
Print Bind(state,Bind(state,Bind(state,Eval(Unit(state,0),1),Lambda("x","random(x)")),Lambda("x","Random(x)")),Lambda("x","Random(x)"))		;State threading

FreeMonad(ident)
FreeMonad(maybe)
FreeMonad(state)
ClearFunctions


WaitKey
End



;Base functions
Function Add(x,y)
	Return x+y
End Function

Function Mul(x,y)
	Return x*y
End Function

Function Pow(x,y)
	Return x^y
End Function

Function Max(x,y)
	If x&gt;y Then Return x:Else Return y
End Function

Function Identity$(x$)
	Return x
End Function

Function ConcStr$(x$,y$)
	Return x+y
End Function

Function IIf$(a,b$,c$)
	If a Then Return b:Else Return c
End Function

Function Eq(a$,b$)
	Return (a=b)
End Function

Function Random$(seed)
	SeedRnd seed
	Return Str(Rand(1,1000))+StateDivider+Str(seed+1)
End Function
</textarea><br><br>That increasingly-unwieldy list of function pointers is making me think a program to read source code and build the list automatically would be a nice idea...<br><br>Anyway, I cannot think of a single practical use for this, but it was an amusing thought-experiment, and now you can all tell me how I've completely misunderstood my own idea. I'm now going to stop messing about before my brain melts, because I'm really quite new to FP, as is painfully obvious.<br><br>Further reading:<br><a href="http://haskell.org/haskellwiki/Monad" target="_blank">http://haskell.org/haskellwiki/Monad</a><br><a href="http://en.wikipedia.org/wiki/Monad_(functional_programming)" target="_blank">http://en.wikipedia.org/wiki/Monad_(functional_programming)</a> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
