<!DOCTYPE html><html lang="en" ><head ><title >bullets in a gun</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >bullets in a gun</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >bullets in a gun</a><br><br>
<a name="1191843"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Captain Wicker (crazy hillbilly)</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">	Graphics3D 800,600,32,2

	SetBuffer(BackBuffer())


	Global gun, cubes, plane, bullets

	HidePointer()


	Global cam=CreateCamera()



	Global light=CreateLight()

	CameraFogRange cam,10,100

	CameraFogMode cam,1

	EntityParent light,cam

	CameraFogColor cam,0,0,0

	plane=CreatePlane(4)
	ScaleEntity plane,200,200,200
	MoveEntity plane,0,-5,0
	EntityParent plane,cam



	Type TBULLETS

		Field X
		Field Y
		Field Z
	
	End Type



	MakeGunner()


	For c=1 To 100
		cubes=CreateCube()
		ScaleEntity cubes,Rnd(15),Rnd(15),Rnd(15)
		EntityColor cubes,Rnd(255),Rnd(255),Rnd(255)
		PositionEntity cubes,Rand(-255,255),Rnd(0),Rand(-255,255)
	Next 

	For b=1 To 100
		bullets=CreateSphere(24)
		ScaleEntity bullets,.2,.2,.2
		EntityColor bullets,255,255,0
	Next
	
	EntityShininess cubes,90
	EntityShininess bullets,90
	EntityShininess gun,90
	

	


	While Not KeyHit(1)



	MoveCammy()


	If KeyDown(57)
	
		For X=1 To 10
			For Y=1 To 10
				For Z=1 To 10
					bull.TBULLETS=New TBULLETS
					bull\x=EntityX(bullets)
					bull\y=EntityY(bullets)
					bull\z=EntityZ(bullets)
				Next
			Next
		Next
	
		For bull.TBULLETS = Each TBULLETS
			bull\x = bull\x + 1
		Next

		
	EndIf
	
		

	

	UpdateWorld()
	RenderWorld() 
	Flip(-1)


	Wend
	End 




	Function MakeGunner()
	
		gun=CreateCylinder(24)
	
		PositionEntity gun,EntityX(cam),-1,EntityZ(cam)+2
	
		ScaleEntity gun,.25,.5,.8
	
		RotateEntity gun,25,0,0
		
		EntityParent gun,cam


	End Function 




	Function MoveCammy()

		If KeyDown(203) Then TurnEntity cam,0,1,0
	
		If KeyDown(205) Then TurnEntity cam,0,-1,0
	
		If KeyDown(200) Then MoveEntity cam,0,0,1
	
		If KeyDown(208) Then MoveEntity cam,0,0,-1
	
	
	End Function </textarea><br>it's been a while since I fiddled with b3d. I am trying to make bullets shoot out of the cylinder when I press the space bar key. I successfully did this once in an fps I was doing, but forgot and lost the source. Lol. Anybody? :D <br><br></td></tr></table><br>
<a name="1191850"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob the Great</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I feel like programming today, so don't be surprised if you see an abnormal amount of posts from me.<br><br>I took what you had posted and modified it to do what I think you were getting at. Most of it is unchanged, but I commented everything that I removed or added to make it work.<br><br>Good luck.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
	Graphics3D 800,600,32,2

	SetBuffer(BackBuffer())


	Global gun, cubes, plane, bullets

	HidePointer()


	Global cam=CreateCamera()



	Global light=CreateLight()

	CameraFogRange cam,10,100

	CameraFogMode cam,1

	EntityParent light,cam

	CameraFogColor cam,0,0,0

	plane=CreatePlane(4)
	ScaleEntity plane,200,200,200
	MoveEntity plane,0,-5,0
	EntityParent plane,cam



	Type TBULLETS

		Field entity
		;Field X ;x,y,z can always be retreived with EntityX, EntityY, EntityZ
		;Field Y
		;Field Z
	
	End Type



	MakeGunner()


	For c=1 To 100
		cubes=CreateCube()
		ScaleEntity cubes,Rnd(15),Rnd(15),Rnd(15)
		EntityColor cubes,Rnd(255),Rnd(255),Rnd(255)
		PositionEntity cubes,Rand(-255,255),Rnd(0),Rand(-255,255)
	Next 

;	Don't make all of your bullets at once. Make one every time you hit the space bar.
;	For b=1 To 100
;		bullets=CreateSphere(24)
;		ScaleEntity bullets,.2,.2,.2
;		EntityColor bullets,255,255,0
;	Next

;	Make a master bullet to copy later
	bullets=CreateSphere(24)
	ScaleEntity bullets,.2,.2,.2
	EntityColor bullets,255,255,0
	HideEntity bullets ;Hide it so that you don't have a random bullet floating around.
	
	EntityShininess cubes,90
	EntityShininess bullets,90
	EntityShininess gun,90
	

	


	While Not KeyHit(1)



	MoveCammy()


	If KeyDown(57)
	
;		Unless you want the extreme shotgun effect, this isn't the right approach.
;		For X=1 To 10
;			For Y=1 To 10
;				For Z=1 To 10
;					bull.TBULLETS=New TBULLETS
;					bull\x=EntityX(bullets)
;					bull\y=EntityY(bullets)
;					bull\z=EntityZ(bullets)
;				Next
;			Next
;		Next

		bull.TBULLETS=New TBULLETS
		bull\entity = CopyEntity(bullets) ;copy the master bullet
		PositionEntity bull\entity,EntityX(cam),EntityY(cam),EntityZ(cam) ;position at the camera (or the gun)
		RotateEntity bull\entity,EntityPitch(cam),EntityYaw(cam),EntityRoll(cam) ;rotate the same direction
		
	
;		This is the right idea, but you want to update the bullets even if you don't hold down the space bar.
;		For bull.TBULLETS = Each TBULLETS
;			bull\x = bull\x + 1
;		Next

		
	EndIf
	
	;This will update each bullet every loop (note that it's not inside the If KeyDown(57) scope.
	;You should probably put this in its own function eventually
	For bull.TBULLETS = Each TBULLETS ;for every bullet made
		MoveEntity bull\entity,0,0,1 ;move it forward locally
		If EntityDistance(bull\entity,cam) &gt; 100 ;if it's too far away
			FreeEntity bull\entity ;get rid of the bullet
			Delete bull ;get rid of the corresponding type
		EndIf
	Next
	
		

	

	UpdateWorld()
	RenderWorld() 
	Flip(-1)


	Wend
	End 




	Function MakeGunner()
	
		gun=CreateCylinder(24)
	
		PositionEntity gun,EntityX(cam),-1,EntityZ(cam)+2
	
		ScaleEntity gun,.25,.5,.8
	
		RotateEntity gun,25,0,0
		
		EntityParent gun,cam


	End Function 




	Function MoveCammy()

		If KeyDown(203) Then TurnEntity cam,0,1,0
	
		If KeyDown(205) Then TurnEntity cam,0,-1,0
	
		If KeyDown(200) Then MoveEntity cam,0,0,1
	
		If KeyDown(208) Then MoveEntity cam,0,0,-1
	
	
	End Function 
</textarea> <br><br></td></tr></table><br>
<a name="1191969"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Captain Wicker (crazy hillbilly)</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks Rob. much appreciated. :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
