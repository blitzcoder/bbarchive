<!DOCTYPE html><html lang="en" ><head ><title >SaveBuffer as PNG or JPEG?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >SaveBuffer as PNG or JPEG?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >SaveBuffer as PNG or JPEG?</a><br><br>
<a name="961470"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DheDarkhCustard</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Does anyone know of any libs or code that lets me export buffers as anything other than BMP?<br><br>Thanks. <br><br></td></tr></table><br>
<a name="961475"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> FreeImage?<br><a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=1732" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=1732</a> <br><br></td></tr></table><br>
<a name="965307"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Virtech</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Is it possible using Freeimage to specify the compression ratio of saved .jpg?<br>The default quality seems very low. <br><br></td></tr></table><br>
<a name="965590"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can specify the compression ratio in freeimage. I believe it's an optional parameter in the function. <br><br></td></tr></table><br>
<a name="965716"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Virtech</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>From the freeimage decls there is no extra parameter for specifying the compression ratio of jpg's.<br> <br>Im using the command - FiSaveImage(img, filename$) <br><br></td></tr></table><br>
<a name="965737"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is the FreeImage lib I use (FI_B3D_min.bb):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; FreeImage Open-source Userlib Include
; Writen By Paul Snart
; Copyright Pacific Software
;
; Cut-down version with bug fixes by Chris Chadwick 13/7/2005.
;
; This Userlib is free to use in commercial and non-commercial programs
; Please also follow the guidlines set by the FreeImage Development team
; with regards to a GPL License (Included)
;
; #### Main Include Library ####
;
; Version: 1.3
;


	Global FIImage.ImageList
	Global BiHeader = CreateBank(18)
	Global FIB_RedMask[4],FIB_GreenMask[4],FIB_BlueMask[4],FIB_BitDepth[4]
	Global Int32 = CreateBank(4)
	Global Word = CreateBank(2)
	
	FIB_BitDepth[1]=16
	FIB_RedMask[1]=$1F
	FIB_GreenMask[1]=$7E0
	FIB_BlueMask[1]=$F800
	
	FIB_BitDepth[2]=16
	FIB_RedMask[2]=$1F
	FIB_GreenMask[2]=$3E0
	FIB_BlueMask[2]=$7C00
	
	FIB_BitDepth[3]=24
	FIB_RedMask[3]=$FF0000
	FIB_GreenMask[3]=$FF00
	FIB_BlueMask[3]=$FF
	
	FIB_BitDepth[4]=32
	FIB_RedMask[4]=$FF0000
	FIB_GreenMask[4]=$FF00
	FIB_BlueMask[4]=$FF
	
	Dim CRC_Table(256)
	Dim FI_FileEXT$(24,1)
	Dim FI_SaveList(24)

	ComputeCRC()

	Restore LoadExtentions
	Read FI_FIFType
	Repeat
	
		Read FI_FileEXT(FI_FIFType,0)
		FI_FileEXT(FI_FIFType,1) = FI_GetFIFDescription(FI_FIFType) 
		If FI_FIFSupportsWriting(FI_FIFType) = 1 Then FI_SaveList(FI_FIFType) = True
		Read FI_FIFType
	
	Until FI_FIFType = -1

	Type ImageList
	
		; Original/New File Info
		Field FileName$
		Field FileDirectory$
		Field ImageFormat%
		Field ImageFormatDescript$
		Field File_Size%
		Field Format%
		Field LoadFlags%
		Field SaveFlags%
		Field FIBITMAP ; If Used
		
		; Blitz Image Variables
		Field Image%
		Field Width%
		Field Height%
		Field DisplayPitch%
		Field ImageSize%
		Field Depth%
		Field RedMask%
		Field GreenMask%
		Field BlueMask%
		
		; Alpha &amp; Palette Storage
		Field Palette%
		Field PixMap%
		Field AlphaMap%
		Field AlphaPitch%
		
		; FIBITMAP Variables
		Field DIBSize%
		Field Pitch%
		Field Planes%
		Field BitCount%
		Field Flags% 
		Field SizeImage%
		Field XPelsPerMeter% 
		Field YPelsPerMeter% 
		Field ClrUsed%
		Field ClrImportant%
		Field FIRedMask%
		Field FIGreenMask%
		Field FIBlueMask%
	
	End Type

	;
	; #### Constant Declarations ####
	;
	
	; **** Image Format Constants
	Const FIF_UNKNOWN=-1
	Const FIF_BMP=0
	Const FIF_ICO=1
	Const FIF_JPEG=2
	Const FIF_JNG=3
	Const FIF_KOALA=4
	Const FIF_LBM=5
	Const FIF_MNG=6
	Const FIF_PBM=7
	Const FIF_PBMRAW=8
	Const FIF_PCD=9
	Const FIF_PCX=10
	Const FIF_PGM=11
	Const FIF_PGMRAW=12
	Const FIF_PNG=13
	Const FIF_PPM=14
	Const FIF_PPMRAW=15
	Const FIF_RAS=16
	Const FIF_TARGA=17
	Const FIF_TIFF=18
	Const FIF_WBMP=19
	Const FIF_PSD=20
	Const FIF_CUT=21
	Const FIF_IFF=FIF_LBM
	Const FIF_XBM=22
	Const FIF_XPM=23
	Const FIF_DDS=24

	.LoadExtentions
	Data FIF_BMP,"BMP"
	Data FIF_ICO,"ICO"
	Data FIF_JPEG,"JPG"
	Data FIF_JNG,"JNG"
	Data FIF_KOALA,"KOA"
	Data FIF_LBM,"IFF"
	Data FIF_MNG,"MNG"
	Data FIF_PBM,"PBM"
	Data FIF_PBMRAW,"PBX"
	Data FIF_PCD,"PCD"
	Data FIF_PCX,"PCX"
	Data FIF_PGM,"PGM"
	Data FIF_PGMRAW,"PGX"
	Data FIF_PNG,"PNG"
	Data FIF_PPM,"PPM"
	Data FIF_PPMRAW,"PPX"
	Data FIF_RAS,"RAS"
	Data FIF_TARGA,"TGA"
	Data FIF_TIFF,"TIFF"
	Data FIF_WBMP,"WBMP"
	Data FIF_PSD,"PSD"
	Data FIF_CUT,"CUT"
	Data FIF_XBM,"XBM"
	Data FIF_XPM,"XPM"
	Data FIF_DDS,"DDS"
	Data -1

	; **** Image Types
	Const FIT_UNKNOWN=0			; unknown Type
	Const FIT_BITMAP=1			; standard image			: 1-, 4-, 8-, 16-, 24-, 32-bit
	Const FIT_UINT16=2			; array of unsigned short	: unsigned 16-bit
	Const FIT_INT16=3			; array of short			: signed 16-bit
	Const FIT_UINT32=4			; array of unsigned long	: unsigned 32-bit
	Const FIT_INT32=5			; array of long				: signed 32-bit
	Const FIT_FLOAT=6			; array of Float			: 32-bit IEEE floating point
	Const FIT_DOUBLE=7			; array of double			: 64-bit IEEE floating point
	Const FIT_COMPLEX=8			; array of FICOMPLEX		: 2 x 64-bit IEEE floating point
	
	; **** Image Colour Type Constants
	Const FIC_MINISWHITE=0		; min value is white
	Const FIC_MINISBLACK=1		; min value is black
	Const FIC_RGB=2				; RGB Color model
	Const FIC_PALETTE=3			; Color map indexed
	Const FIC_RGBALPHA=4		; RGB Color model with alpha channel
	Const FIC_CMYK=5			; CMYK Color model
	
	; **** Colour Channel Constants
	Const FICC_RGB=0			; Use red, green And blue channels
	Const FICC_RED=1			; Use red channel
	Const FICC_GREEN=2			; Use green channel
	Const FICC_BLUE=3			; Use blue channel
	Const FICC_ALPHA=4			; Use alpha channel
	Const FICC_BLACK=5			; Use black channel
	Const FICC_REAL=6			; Complex images: use real part
	Const FICC_IMAG=7			; Complex images: use imaginary part
	Const FICC_MAG=8			; Complex images: use magnitude
	Const FICC_PHASE=9		
	
	; **** Colour Quantize Constants
	Const FIQ_WUQUANT=0			; Xiaolin Wu Color quantization algorithm
	Const FIQ_NNQUANT=1			; NeuQuant neural-net quantization algorithm by Anthony Dekker
	
	; **** Dither Constants
	Const FID_FS=0				; Floyd &amp; Steinberg error diffusion
	Const FID_BAYER4x4=1		; Bayer ordered dispersed dot dithering (order 2 dithering matrix)
	Const FID_BAYER8x8=2		; Bayer ordered dispersed dot dithering (order 3 dithering matrix)
	Const FID_CLUSTER6x6=3		; Ordered clustered dot dithering (order 3 - 6x6 matrix)
	Const FID_CLUSTER8x8=4		; Ordered clustered dot dithering (order 4 - 8x8 matrix)
	Const FID_CLUSTER16x16=5	; Ordered clustered dot dithering (order 8 - 16x16 matrix)
	
	; **** Resample Consrants
	Const FILTER_BOX=0			; Box, pulse, Fourier window, 1st order (constant) b-spline
	Const FILTER_BICUBIC=1		; Mitchell &amp; Netravali's two-param cubic filter
	Const FILTER_BILINEAR=2		; Bilinear filter
	Const FILTER_BSPLINE=3		; 4th order (cubic) b-spline
	Const FILTER_CATMULLROM=4	; Catmull-Rom spline, Overhauser spline
	Const FILTER_LANCZOS3=5		; Lanczos3 filter
	
	; **** Loading/Saving Flag Constants 
	Const BMP_DEFAULT=0
	Const BMP_SAVE_RLE=1
	Const CUT_DEFAULT=0
	Const DDS_DEFAULT=0
	Const ICO_DEFAULT=0
	Const IFF_DEFAULT=0
	Const JPEG_DEFAULT=0
	Const JPEG_FAST=1
	Const JPEG_ACCURATE=2
	Const JPEG_QUALITYSUPERB=$80
	Const JPEG_QUALITYGOOD=$100
	Const JPEG_QUALITYNORMAL=$200
	Const JPEG_QUALITYAVERAGE=$400
	Const JPEG_QUALITYBAD=$800
	Const KOALA_DEFAULT=0
	Const LBM_DEFAULT=0
	Const MNG_DEFAULT=0
	Const PCD_DEFAULT=0
	Const PCD_BASE=1				; load the bitmap sized 768 x 512
	Const PCD_BASEDIV4=2			; load the bitmap sized 384 x 256
	Const PCD_BASEDIV16=3			; load the bitmap sized 192 x 128
	Const PCX_DEFAULT=0
	Const PNG_DEFAULT=0
	Const PNG_IGNOREGAMMA=1			; avoid gamma correction
	Const PNM_DEFAULT=0
	Const PNM_SAVE_RAW=0			; If set the writer saves in RAW format (i.e. P4, P5 Or P6)
	Const PNM_SAVE_ASCII=1			; If set the writer saves in ASCII format (i.e. P1, P2 Or P3)
	Const PSD_DEFAULT=0
	Const RAS_DEFAULT=0
	Const TARGA_DEFAULT=0
	Const TARGA_LOAD_RGB888=1       ; If set the loader converts RGB555 And ARGB8888 -&gt; RGB888.
	Const TIFF_DEFAULT=0
	Const TIFF_CMYK=$1				; reads/stores tags For separated CMYK (use | To combine with compression flags)
	Const TIFF_PACKBITS=$100  		; save using PACKBITS compression
	Const TIFF_DEFLATE=$200 	 	; save using DEFLATE compression
	Const TIFF_ADOBE_DEFLATE=$400	; save using ADOBE DEFLATE compression
	Const TIFF_NONE=$800  			; save without any compression
	Const WBMP_DEFAULT=0
	Const XBM_DEFAULT=0
	Const XPM_DEFAULT=0


;
; Fast Save JPG Images (24Bit Only).
;
Function SaveJPG(Image, File$, Compression%=JPEG_QUALITYSUPERB)

	; Compression:
	; JPEG_QUALITYSUPERB 	= Superb Quality, Low Compression (100:1)
	; JPEG_QUALITYGOOD 		= Good Quality, Low-Medium Compression (75:1) (DEFAULT)
	; JPEG_QUALITYNORMAL 	= Normal Quality, Normal Compression (50:1)
	; JPEG_QUALITYAVERAGE	= Average Quality, Normal-High Compression (25:1)
	; JPEG_QUALITYPOOR		= Poor Quality, High Compression (10:1)

	If Image
		FIBITMAP = FIQSend(Image)
		Suc = FI_Save(FIF_JPEG,FIBITMAP,File$,Compression)
		FI_Unload FIBITMAP
		If Not Suc Then RuntimeError "JPG Save Failed" + Chr(10) + File$
	Else
		RuntimeError "Image Instance Not Found (JPG Save)"
	EndIf

End Function


;
; Fast Save TGA Images (24Bit Only).
;
Function SaveTGA(Image, File$)

	If Image
		FIBITMAP = FIQSend(Image)
		Suc = FI_Save(FIF_TARGA,FIBITMAP,File$,0)
		FI_Unload FIBITMAP
		If Not Suc Then RuntimeError "TGA Save Failed" + Chr(10) + File$
	Else
		RuntimeError "Image Instance Not Found (TGA Save)" 
	EndIf

End Function

;
; Fast Save TIFF Images (24Bit Only).
;
Function SaveTIFF(Image, File$, Compression%=TIFF_NONE)

	; Compression
	; TIFF_PACKBITS 			= save using PACKBITS compression
	; TIFF_DEFLATE		 		= save using DEFLATE compression
	; TIFF_ADOBE_DEFLATE	= save using ADOBE DEFLATE compression
	; TIFF_NONE		  			= save without any compression (DEFAULT)

	If Image
		FIBITMAP = FIQSend(Image)
		Suc = FI_Save(FIF_TIFF,FIBITMAP,File$,Compression)
		FI_Unload FIBITMAP
		If Not Suc Then RuntimeError "TIFF Save Failed" + Chr(10) + File$
	Else
		RuntimeError "Image Instance Not Found (TIFF Save)" 
	EndIf	

End Function


;
; Fast Save BMP Images (24Bit Only).
;
Function SaveBMP(Image, File$)
		
	If Image
		FIBITMAP = FIQSend(Image)
		Suc = FI_Save(FIF_BMP,FIBITMAP,File$,0)
		FI_Unload FIBITMAP
		If Not Suc Then RuntimeError "BMP Save Failed" + Chr(10) + File$
	Else
		RuntimeError "Image Instance Not Found (BMP Save)" 
	EndIf

End Function


;
; Fast Save PNG Images (24Bit Only).
;
Function SavePNG(Image, File$)
	
	If Image
		FIBITMAP = FIQSend(Image)		
		Suc = FI_Save(FIF_PNG,FIBITMAP,File,0)
		FI_Unload FIBITMAP
		If Not Suc Then RuntimeError "PNG Save Failed" + Chr(10) + File$
	Else
		RuntimeError "Image Instance Not Found (PNG Save)" 
	EndIf

End Function


;
; Load image file.
;
Function FILoadImage(File$)

	Local FI_FIFType%, FIBITMAP%
	Local FI_n%, ImType$, ImageBank
	Local FIF_FLAG%
	
	FI_FIFType = FI_GetFileType(File$,0)
	If FI_FIFType = -1
		FI_FIFType = FI_GetFIFFromFilename(File$)
		If FI_FIFType = -1 Then Return 0 ; ** FAIL
	EndIf
	
	If FI_FIFType = 0 Then FIF_FLAG = PNG_AVOIDGAMMA
	
	FIBITMAP = FI_Load(FI_FIFType,File$,FIF_FLAG)
	If FIBITMAP = 0 Then Return 0 ; ** FAIL

	FIImage = New ImageList
	
	; Get file type (extension), name and directory.
	For FI_n = Len(File$) To 1 Step -1
		If Mid(File$,FI_n,1) = "."
			ImType$ = Upper(Right(File$,Len(File$)-FI_n))
		EndIf
		
		If Mid(File$,FI_n,1) = "\"
			FIImage\FileName = Right(File$,Len(File$)-FI_n)
			FIImage\FileDirectory = Left(File$,FI_n)
			Exit
		EndIf
	Next
	
	If FIImage\FileName = ""
		FIImage\FileName = File$
		FIImage\FileDirectory = CurrentDir()
	EndIf

	PopulateFIType FIImage,FIBITMAP
	FIImage\File_Size = FileSize(File$)
	FIImage\ImageFormatDescript = FI_GetFIFDescription(FI_FIFType)
	FIImage\ImageFormat = FI_FIFType
	FIImage\Image = CreateImage(FIImage\Width,FIImage\Height)
	
	iBuffer = ImageBuffer(FIImage\Image)
	LockBuffer iBuffer
	ImageBank = FI_PeekIntEx(iBuffer,72)
	FIImage\DisplayPitch = FI_PeekIntEx(iBuffer,56)
	FIImage\ImageSize = FIImage\Height*FIImage\Pitch
	FIImage\Depth = FI_PeekIntEx(iBuffer,100)
	FIImage\RedMask = FI_PeekIntEx(iBuffer,112)
	FIImage\GreenMask = FI_PeekIntEx(iBuffer,116)
	FIImage\BlueMask = FI_PeekIntEx(iBuffer,120)
	FI_ConvertToRawBits3D ImageBank,FIBITMAP,FIImage\DisplayPitch,FIImage\Depth,FIImage\RedMask,FIImage\GreenMask,FIImage\BlueMask,0
	UnlockBuffer iBuffer

	Image = FIImage\Image
	FI_Unload FIBITMAP
	Delete FIImage
	Return Image

End Function


;
;
;
Function FI_PeekIntEx(iBuffer, Offset)

	RtlMoveMemory_From Int32,iBuffer+Offset,4
	Return PeekInt(Int32,0)

End Function

;
;
;
Function FI_PeekWordEx(iBuffer, Offset)

	RtlMoveMemory_From Word,iBuffer+Offset,2
	Return PeekShort(Word,0)

End Function


;
; Quickly Sends a Blitz Image to an FIBITMAP Instance.
;
Function FIQSend(Image)

	Local iBuffer%, iPitch%, iFormat%
	Local FIBITMAP%, nFIBITMAP%, iBank%

	iBuffer = ImageBuffer(Image)
	LockBuffer iBuffer
	iBank = FI_PeekIntEx(iBuffer,72)
	iPitch = FI_PeekIntEx(iBuffer,56)
	FIBITMAP = FI_ConvertFromRawBits3D(iBank,ImageWidth(Image),ImageHeight(Image),iPitch,FI_PeekIntEx(iBuffer,100),FI_PeekIntEx(iBuffer,112),FI_PeekIntEx(iBuffer,116),FI_PeekIntEx(iBuffer,120),0)
	UnlockBuffer iBuffer
		
	If FI_GetBPP(FIBITMAP) &lt;&gt; 24
		nFIBITMAP = FI_ConvertTo24Bits(FIBITMAP)
		If nFIBITMAP
			FI_Unload FIBITMAP
			FIBITMAP = nFIBITMAP
		EndIf
	EndIf
	
	Return FIBITMAP

End Function

;
; Quickly Returns an FIBITMAP Instance to a BlitzImage.
;
Function FIQGrab(Image, FIBITMAP)

	Local iBuffer%, iPitch%, iFormat%
	Local iBank%

	iBuffer = ImageBuffer(Image)
	LockBuffer iBuffer
	iBank = FI_PeekIntEx(iBuffer,72)
	iPitch = FI_PeekIntEx(iBuffer,56)
	iFormat = FI_PeekIntEx(iBuffer,104)
	FI_ConvertToRawBits3D iBank,FIBITMAP,iPitch,FIB_BitDepth[iFormat],FIB_RedMask[iFormat],FIB_GreenMask[iFormat],FIB_BlueMask[iFormat],0
	UnlockBuffer iBuffer

End Function


;
; Populate the FreeImage Type Instance (.ImageList).
;
Function PopulateFIType(FI.ImageList, FIBITMAP)

	FI\Width				= FI_GetWidth(FIBITMAP)	
	FI\Height				= FI_GetHeight(FIBITMAP)
	FI\BitCount			= FI_GetBPP(FIBITMAP)
	FI\Pitch				= FI_GetPitch(FIBITMAP)
	FI\ClrUsed			= FI_GetColorsUsed(FIBITMAP)
	FI\FIRedMask		= FI_GetRedMask(FIBITMAP)
	FI\FIGreenMask	= FI_GetGreenMask(FIBITMAP)
	FI\FIBlueMask		= FI_GetBlueMask(FIBITMAP)
	FI\DIBSize			= FI_GetDIBSize(FIBITMAP)

End Function


;
;
;
Function ComputeCRC()

	c = CreateBank(4)
	
	For n=0 To 255
		PokeInt c,0,n
		
		For k = 0 To 7
			If (PeekInt(c,0) And 1)
				PokeInt c,0,($EDB88320 Xor (PeekInt(c,0) Shr 1))
			Else
				PokeInt c,0,(PeekInt(c,0) Shr 1)
			EndIf
		Next
		
		CRC_Table(n) = CreateBank(4)
		PokeInt CRC_Table(n),0,PeekInt(c,0)
	Next
	
	FreeBank c

End Function
</textarea><br><br>And here's the decls (FreeImage.decls):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
.lib "data\FreeImage.dll"

FI_Allocate%( Width%,Height%,BPP%,RedMask%,GreenMask%,BlueMask% ) : "_FreeImage_Allocate@24"
FI_Load%( ImageType%,File$,Mode% ) : "_FreeImage_Load@12"
FI_Save%( ImapeType%,FIBITMAP%,File$,Flags% ) : "_FreeImage_Save@16"
FI_Unload%( FIBITMAP% ) : "_FreeImage_Unload@4"
FI_GetFIFDescription$( FREE_IMAGE_FORMAT% ) : "_FreeImage_GetFIFDescription@4"
FI_GetFileType%( File$,Default% ) : "_FreeImage_GetFileType@8"
FI_ConvertToRawBits( Bank*,FIBITMAP%,Pitch%,Depth%,Red_Mask%,Green_Mask%,Blue_Mask%,TopDown% ) : "_FreeImage_ConvertToRawBits@32"
FI_ConvertToRawBits3D( Pointer,FIBITMAP%,Pitch%,Depth%,Red_Mask%,Green_Mask%,Blue_Mask%,TopDown% ) : "_FreeImage_ConvertToRawBits@32"
FI_GetFIFFromFilename%( File$ ) : "_FreeImage_GetFIFFromFilename@4"
FI_GetColorsUsed%( FIBITMAP% ) : "_FreeImage_GetColorsUsed@4"
FI_GetBits%( FIBITMAP% ) : "_FreeImage_GetBits@4"
FI_GetLine%( FIBITMAP%,ScanLine% ) : "_FreeImage_GetLine@8"
FI_GetBPP%( FIBITMAP% ) : "_FreeImage_GetBPP@4"
FI_GetWidth%( FIBITMAP% ) : "_FreeImage_GetWidth@4"
FI_GetHeight%( FIBITMAP% ) : "_FreeImage_GetHeight@4"
FI_GetPitch%( FIBITMAP% ) : "_FreeImage_GetPitch@4"
FI_GetDIBSize%( FIBITMAP% ) : "_FreeImage_GetDIBSize@4"
FI_GetPalette%( FIBITMAP% ) : "_FreeImage_GetPalette@4"
FI_GetDotsPerMeterX%( FIBITMAP% ) : "_FreeImage_GetDotsPerMeterX@4"
FI_GetDotsPerMeterY%( FIBITMAP% ) : "_FreeImage_GetDotsPerMeterY@4"
FI_GetInfoHeader%( FIBITMAP% ) : "_FreeImage_GetInfoHeader@4"
FI_GetInfo%( FIBITMAP% ) : "_FreeImage_GetInfo@4"
FI_GetColorType%( FIBITMAP% ) : "_FreeImage_GetColorType@4"
FI_GetRedMask%( FIBITMAP% ) : "_FreeImage_GetRedMask@4"
FI_GetGreenMask%( FIBITMAP% ) : "_FreeImage_GetGreenMask@4"
FI_GetBlueMask%( FIBITMAP% ) : "_FreeImage_GetBlueMask@4"
FI_GetTransparencyCount%( FIBITMAP% ) : "_FreeImage_GetTransparencyCount@4"
FI_GetTransparencyTable%( FIBITMAP% ) : "_FreeImage_GetTransparencyTable@4"
FI_SetTransparencyTable%( FIBITMAP%,Table*,Size ) : "_FreeImage_SetTransparencyTable@12"
FI_SetTransparent( FIBITMAP%,Flag% ) : "_FreeImage_SetTransparent@8"
FI_IsTransparent%( FIBITMAP% ) : "_FreeImage_IsTransparent@4"
FI_ConvertTo8Bits%( FIBITMAP% ) : "_FreeImage_ConvertTo8Bits@4"
FI_ConvertTo16Bits555%( FIBITMAP% ) : "_FreeImage_ConvertTo16Bits555@4"
FI_ConvertTo16Bits565%( FIBITMAP% ) : "_FreeImage_ConvertTo16Bits565@4"
FI_ConvertTo24Bits%( FIBITMAP% ) : "_FreeImage_ConvertTo24Bits@4"
FI_ConvertTo32Bits%( FIBITMAP% ) : "_FreeImage_ConvertTo32Bits@4"
FI_ColorQuantize%( FIBITMAP%,Flag% ) : "_FreeImage_ColorQuantize@8"
FI_Threshold%( FIBITMAP%,Flag% ) : "_FreeImage_Threshold@8"
FI_Dither%( FIBITMAP%,Algo% ) : "_FreeImage_Dither@8"
FI_ConvertFromRawBits%( Bank*,Width%,Height%,Pitch%,Depth%,RedMask%,GreenMask%,BlueMask%,TopDown% ) : "_FreeImage_ConvertFromRawBits@36"
FI_ConvertFromRawBits3D%( Pointer,Width%,Height%,Pitch%,Depth%,RedMask%,GreenMask%,BlueMask%,TopDown% ) : "_FreeImage_ConvertFromRawBits@36"
FI_Clone%(DIB%) : "_FreeImage_Clone@4"
FI_FIFSupportsReading%( FREE_IMAGE_FORMAT% ) : "_FreeImage_FIFSupportsReading@4"
FI_FIFSupportsWriting%( FREE_IMAGE_FORMAT% ) : "_FreeImage_FIFSupportsWriting@4"
FI_FIFSupportsExportBPP%( FREE_IMAGE_FORMAT%, BPP% ) : "_FreeImage_FIFSupportsExportBPP@8"
FI_ZLibCompress%( BankOut*,BSizeOut%,BankIn*,BSizeIn% ) : "_FreeImage_ZLibCompress@16"
FI_ZLibUncompress%( BankOut*,BSizeOut%,BankIn*,BSizeIn% ) : "_FreeImage_ZLibUncompress@16"
FI_RotateClassic%( FIBITMAP%,Angle% ) : "_FreeImage_RotateClassic@8"
FI_RotateEx%( FIBITMAP%,Angle%,Shiftx%,Shifty%,offx%,offy%,Masked% ) : "_FreeImage_RotateEx@28"
FI_FlipHorizontal%( FIBITMAP% ) : "_FreeImage_FlipHorizontal@4"
FI_FlipVertical%( FIBITMAP% ) : "_FreeImage_FlipVertical@4"
FI_Rescale%( FIBITMAP%,Width%,Height%,Filter% ) : "_FreeImage_Rescale@16"
FI_AdjustCurve%( FIBITMAP%,Bank*,Channel% ) : "_FreeImage_AdjustCurve@12"
FI_AdjustGamma%( FIBITMAP%,Gamma% ) : "_FreeImage_AdjustGamma@8"
FI_AdjustBrightness%( FIBITMAP%,Percent% ) : "_FreeImage_AdjustBrightness@8"
FI_AdjustContrast%( FIBITMAP%,Percent% ) : "_FreeImage_AdjustContrast@8"
FI_Invert%( FIBITMAP% ) : "_FreeImage_Invert@4"
FI_GetHistogram%( FIBITMAP%,Bank*,Channel% ) : "_FreeImage_GetHistogram@12"
FI_GetChannel%( FIBITMAP%,Channel% ) : "_FreeImage_GetChannel@8"
FI_SetChannel%( FIBITMAP%, FIBITMAP8%, Channel% ) : "_FreeImage_SetChannel@12"
FI_Copy%( FIBITMAP%,XA%,XB%,YA%,YB% ) : "_FreeImage_Copy@20"
FI_Paste%( FIBITMAP%,X%,Y%,Alpha% ) : "_FreeImage_Paste@16"
FI_GetVersion$() : "_FreeImage_GetVersion@0"
</textarea><br><br>As you can see, the SaveJPG function allows you to speicify the compression in the 3rd parameter. <br><br></td></tr></table><br>
<a name="966053"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Virtech</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes i can see your version have the extra parameter, but mine doesn't :/<br>I couldnt run your files because of missing function "RtlMoveMemory_From" <br><br></td></tr></table><br>
<a name="966755"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh, sorry. You'll also need this decls, too:<br><br>kernel32.decls<br><pre class=code>
.lib "kernel32.dll"
RtlMoveMemory_To%(Destination%,Source*,Length%) : "RtlMoveMemory"
RtlMoveMemory_From%(Destination*,Source%,Length%) : "RtlMoveMemory" 
RtlMoveMemory_Ex%(Destination%,Source%,Length%) : "RtlMoveMemory"
</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
