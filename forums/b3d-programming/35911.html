<!DOCTYPE html><html lang="en" ><head ><title >putting tiles on board</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >putting tiles on board</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >putting tiles on board</a><br><br>
<a name="392131"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> O so I am alternating between an Othello game and a chess game and I have run in to a bit of a tedious problem.<br><br>Putting tiles.<br><br>This is a sample of what I had done:<br><pre class=code>
empty=LoadMesh("square.b3d")
PositionEntity empty,-45,.15,-45
EntityPickMode empty,2

emp2=CopyEntity (empty)
PositionEntity emp2,-45,.15,-35
EntityPickMode emp2,2

emp3=CopyEntity (empty)
PositionEntity emp3,-45,.15,-25
EntityPickMode emp3,2

emp4=CopyEntity (empty)
PositionEntity emp4,-45,.15,-15
EntityPickMode emp4,2

emp5=CopyEntity (empty)
PositionEntity emp5,-45,.15,-5
EntityPickMode emp5,2

emp6=CopyEntity (empty)
PositionEntity emp6,-45,.15,5
EntityPickMode emp6,2

emp7=CopyEntity (empty)
PositionEntity emp7,-45,.15,15
EntityPickMode emp7,2
</pre> yadda yadda... I have 100 of these to do the hard way and then I thought about it... for next... DUH!!!<br><br>So I did this:<br><pre class=code>For empnumb = 2 To 100
	emp+empnumb = CopyEntity (empty)
Next
</pre> but it tells me emp is not a function. I tried it with emp(empnumb) and it told me the same thing... D'oh!!! <br><br>How can I do this without typing my wrists off???<br><br>-Rook <br><br></td></tr></table><br>
<a name="392132"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> do this:<br>dim emp(100) - outside of your main loop (where you declare all your other global variables)<br><br>for empnumb=2 to 100<br>emp(empnumb)=copyentity(empty)<br>next<br><br>However, if you are doing chess then it may be better to simply have a single flat quad and texture it with a chess board pattern.  <br><br>Is there a reason why you want to have an entity for each individual square on the board? <br><br></td></tr></table><br>
<a name="392151"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WolRon</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Rook, unless you specifically NEED each square to be made of a different entity, then just do as Matty said and use a single quad. <br><br></td></tr></table><br>
<a name="392228"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually I tried to do it with types. Didn't work right.<br>The locations on the board as well as the screen are -45 to 45 X and -45 to 45 Z. Stepping by 10.<br><br>SO I did this:<br><pre class=code>
empty=LoadMesh("square.b3d")
PositionEntity empty,-45,.15,-45
EntityPickMode empty,2
HideEntity empty

Type empty
	Field entity
	Field ex
	Field ey
	Field ez
	Field Color
End Type

For space = 1 To 100
	For locx = -45 To 45 Step 10
		For locz = -45 To 45 Step 10
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = locx
			n\ey = .15
			n\ez = locz
		Next
	Next
Next
	
</pre>But for some reason that won't display all the squares... I think they overlap. Very slow.<br><br>I put the empty squares with a function I call outside of the main program loop like this:<br><pre class=code>
Function put_empty()

For n.empty = Each empty
		EntityType n\entity,2
		EntityPickMode n\entity,2
		PositionEntity n\entity,n\ex,n\ey,n\ez
Next

End Function
</pre>So what am I doing wrong???<br>-RZ <br><br></td></tr></table><br>
<a name="392241"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am not being clever enough... always my problem! But I have a new one... look:<br><pre class=code>

empty=LoadMesh("square.b3d")
PositionEntity empty,45,.15,-45
RotateEntity empty,0,0,0
EntityPickMode empty,2
HideEntity empty

Type empty
	Field entity
	Field ex
	Field ey
	Field ez
	Field kolor
End Type

For space = 1 To 25
	For locx = 5 To 45 Step 10
		For locz = 5 To 45 Step 10
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = locx
			n\ey = 1
			n\ez = locz
			n\kolor = 0
		Next
	Next
Next

For space = 1 To 25
	For locx = -5 To -45 Step -10
		For locz = 5 To 45 Step 10
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = locx
			n\ey = 1
			n\ez = locz
			n\kolor = 0
		Next
	Next
Next

For space = 1 To 25
	For locx = 5 To 45 Step 10
		For locz = -5 To -45 Step -10
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = locx
			n\ey = 1
			n\ez = locz
			n\kolor = 0
		Next
	Next
Next

For space = 1 To 25
	For locx = -5 To -45 Step -10
		For locz = -5 To -45 Step -10
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = locx
			n\ey = 1
			n\ez = locz
			n\kolor = 0
		Next
	Next
Next

</pre>This covers all quads... positive and negative numbers are plunked correctly. BUT IT IS SO SLOW!!! I run this and it takes about 10 seconds before the square is highlighted by my mousepick routine. Is there no easier way??? <br><br></td></tr></table><br>
<a name="392440"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mighty MAC</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Rook,<br><br>What does the 'For space=1 to 25' loop mean ?<br><br>Looks like you are creating 25 meshes for each field of your board. <br><br>Try deleting the 'Space=1 to 25' loop.<br><br>Marcus <br><br></td></tr></table><br>
<a name="392442"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mighty MAC</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try this:<br><br><pre class=code>
Graphics3D 800,600,16
SetBuffer BackBuffer()

cam=CreateCamera()
PositionEntity cam,0,10,-80
lig=CreateLight()

empty=CreateCube()
PositionEntity empty,45,.15,-45
RotateEntity empty,0,0,0
EntityPickMode empty,2
HideEntity empty

Type empty
	Field entity
	Field ex
	Field ey
	Field ez
	Field kolor
End Type

	For locx = 5 To 45 Step 10
		For locz = 5 To 45 Step 10
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = locx
			n\ey = 1
			n\ez = locz
			n\kolor = 0
		Next
	Next

	For locx = -5 To -45 Step -10
		For locz = 5 To 45 Step 10
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = locx
			n\ey = 1
			n\ez = locz
			n\kolor = 0
		Next
	Next

	For locx = 5 To 45 Step 10
		For locz = -5 To -45 Step -10
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = locx
			n\ey = 1
			n\ez = locz
			n\kolor = 0
		Next
	Next

	For locx = -5 To -45 Step -10
		For locz = -5 To -45 Step -10
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = locx
			n\ey = 1
			n\ez = locz
			n\kolor = 0
		Next
	Next

For n=Each empty
 PositionEntity n\entity,n\ex,n\ey,n\ez
Next

While Not KeyDown(1)
 UpdateWorld
 RenderWorld
 Flip
Wend
</pre><br><br>Marcus <br><br></td></tr></table><br>
<a name="392486"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WolRon</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Dude! just do this:<pre class=code>Graphics3D 800, 600, 16, 0
SetBuffer BackBuffer()

cam = CreateCamera()
PositionEntity cam, 0, 10, -80
light = CreateLight()

;create a square
board = CreateMesh()
boardsurf = CreateSurface(board)
v1 = AddVertex boardsurf, -1, 0, -1
v2 = AddVertex boardsurf, -1, 0, 1
v3 = AddVertex boardsurf, 1, 0, -1
v4 = AddVertex boardsurf, 1, 0, 1
AddTriangle, boardsurf, v1, v2, v3
AddTriangle, boardsurf, v3, v2, v4

ScaleEntity board, 45, 0, 45

boardtex = LoadTexture("checkered_board.png")
TextureEntity board, boardtex
</pre>I typed this into the reply box so I don't know if it contains errors...<br><br>Like I said before, unless you specifically NEED each square to be made of a different entity, then just create one entity textured with the checker pattern as in the code above. <br><br></td></tr></table><br>
<a name="392766"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Mighty Mac and Wolron... Thanks.... Both solutions work. I want the squares to be mouse pickable so you can plunk game pieces down on the board in 3d. I have to work out a way to pass that location to the game engine simply and cleanly.<br><br>I thought of doing the Othello type (well several people suggested it before I thought of it truly) because the game idea I have would use pieces. They would not flip or anything but similar checking would occour in the game AI.<br><br>Playing with both ideas more... Sure to learn much!<br><br>MightyMac... Your idea works but I cannot see the mouse... Hmmm... I can see the entitypicked when I run the mouse over what I think is the board.<br><br>Look:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 800,600,16
SetBuffer BackBuffer()

player=CreatePivot()
camera=CreateCamera(player)
light=CreateLight()
RotateEntity light,45,45,0
AmbientLight 48,48,48    ; was 32,32,32

EntityType camera,1
PositionEntity camera,0,53,-69
RotateEntity camera,35,0,0

board=LoadMesh("squareboard.b3d")
UpdateNormals board
EntityFX board,0
ScaleEntity board,5.5,5.5,5.5


empty=LoadMesh("square.b3d")
EntityPickMode empty,2
HideEntity empty

Type empty
	Field entity
	Field ex
	Field ey
	Field ez
	Field kolor
End Type

	For locx = 5 To 45 Step 10
		For locz = 5 To 45 Step 10
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = locx
			n\ey = 1
			n\ez = locz
			n\kolor = 0
		Next
	Next

	For locx = -5 To -45 Step -10
		For locz = 5 To 45 Step 10
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = locx
			n\ey = 1
			n\ez = locz
			n\kolor = 0
		Next
	Next

	For locx = 5 To 45 Step 10
		For locz = -5 To -45 Step -10
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = locx
			n\ey = 1
			n\ez = locz
			n\kolor = 0
		Next
	Next

	For locx = -5 To -45 Step -10
		For locz = -5 To -45 Step -10
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = locx
			n\ey = 1
			n\ez = locz
			n\kolor = 0
		Next
	Next

For n=Each empty
 PositionEntity n\entity,n\ex,n\ey,n\ez
Next

ShowPointer 

While Not KeyDown(1)
x=MouseX()
y=MouseY()

e=CameraPick(camera,x,y)
	If e&lt;&gt;entity#
		If entity Then EntityColor entity,255,255,255 ; was 255,255,255
		entity#=e
	EndIf

If MouseHit(1)                ; push L/mousebutton and get the piece or location
		;Select n\entity
		;Case n.empty
		;thing$=" "+n\ex
		;thing2$=" "+n\ez
		;Default 
		;thing$=NADA
		;End Select
		
	End If
		
	If entity#
		EntityColor entity,0,120,255 ; 73,253,146 lime 254,216,38 gold 254,80,232 magenta
	EndIf

 UpdateWorld
 RenderWorld
Text 0,10,"X: "+x
Text 0,20,"Y: "+y
Text 0,30,"Z: "+z
Text 0,50,"THING: "+thing$
 Flip
Wend
</textarea> Oy! :]<br><br>But great help... I have to shut down the CPU now... horrible rains in Houston.... We are 22+ inches for the year!!!<br>-Rook <br><br></td></tr></table><br>
<a name="392897"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well I buffed and modded this... I tried to create three arrays to hold info.<br><br>First array called gameboard to hold state of gameboard and hopefully be reconfigurable...<br><br>Seconf array called boardx to hold X addresses for each piece so I could pass that info back and forth both to update the gameboard and put new pieces in proper location...<br><br>Third array is much like seconf only holds Z addresses...<br><br>But I only see 2 pieces.<br><br><pre class=code>
; Hexathello (OK Othello but I hope to get the square thing figured out
; then nail a hex thing!
; Ralph Dunn
Graphics3D 800,600,16,2
SetBuffer BackBuffer()

player=CreatePivot()
camera=CreateCamera(player)
light=CreateLight()
RotateEntity light,45,45,0
AmbientLight 48,48,48    ; was 32,32,32

EntityType camera,1
PositionEntity camera,0,53,-69
RotateEntity camera,35,0,0

Global square
Dim boardarea(10,10)
Dim boardx(10,10)
Dim boardz(10,10)

For fun=0 To 9
	boardx(fun,0)=-45
	boardx(fun,1)=-35
	boardx(fun,2)=-25
	boardx(fun,3)=-15
	boardx(fun,4)=-5
	boardx(fun,5)=5
	boardx(fun,6)=15
	boardx(fun,7)=25
	boardx(fun,8)=35
	boardx(fun,9)=45
Next

For fu=0 To 9
	boardz(fu,0)=-45
	boardz(fu,1)=-35
	boardz(fu,2)=-25
	boardz(fu,3)=-15
	boardz(fu,4)=-5
	boardz(fu,5)=5
	boardz(fu,6)=15
	boardz(fu,7)=25
	boardz(fu,8)=35
	boardz(fu,9)=45
Next


; #######################################  Load Mesh Objects
gameboard=LoadMesh("squareboard.b3d")
UpdateNormals gameboard
EntityFX gameboard,0
ScaleEntity gameboard,5.5,5.5,5.5

empty=LoadMesh("square.b3d")
EntityPickMode empty,2
HideEntity empty

wpiece=LoadMesh("piece.b3d")
UpdateNormals wpiece
EntityShininess wpiece,1
PositionEntity wpiece,5,3,-5

bpiece=CopyMesh(wpiece)
RotateEntity bpiece,180,0,0
UpdateNormals bpiece
EntityShininess bpiece,1
PositionEntity bpiece,-5,3,5
; ######################################  Plunk Tiles
; -----------------&gt; Thanks Mighty Mac and Wolron
Type empty
	Field entity
	Field kolor
End Type

	For tiles = 1 To 100
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\kolor = 0
	Next



; now create an array called boardarea of 10 by 10 and
; fill in the entire matrix with 0
For fillx = 0 To 9
	For fillz=0 To 9
		boardarea(fillx,fillz)=0
	Next
Next

; Now read it and put an new entity in that location...
; Somehow I get ONLY 2 entitys on the gameboard!!!

For row = 0 To 9
	For col=0 To 9
		If boardarea(row,col) = 0
		PositionEntity n\entity,boardx(row,col),1,boardz(row,col)
		EndIf
	Next
Next


; ##############################################
ShowPointer 

While Not KeyDown(1)

x=MouseX()
y=MouseY()

e=CameraPick(camera,x,y)
	If e&lt;&gt;entity#
		If entity Then EntityColor entity,255,255,255 ; was 255,255,255
		entity#=e
	EndIf

If MouseHit(1)                ; push L/mousebutton and get the piece or location
		Select empty			; I can select a type according to docs
		Case empty			; I can't use n\entity... OBJECT DOES NOT EXIST
		thing$=" "+boardx		; but NOTHING seems to be returned but 0,0
		thing2$=" "+boardz		; I tried n\ex, ex, and locx... (also n\ez...)
		Default 			; with x and y I get the mouse values... ALL OVER THE MAP
		thing$="NADA"
		thing2$="ZIP"
		End Select
		
	End If
		
	If entity#
		EntityColor entity,0,120,255 ; 73,253,146 lime 254,216,38 gold 254,80,232 magenta
	EndIf

UpdateWorld
RenderWorld

Text 0,10,"X: "+x
Text 0,20,"Y: "+y
Text 0,30,"Z: "+z
Text 0,50,"THING: "+thing$
Text 0,60,"THING2: "+thing2$
 
Flip

Wend

End
</pre><br><br>I can't even get it to communicate anymore!!! Oy!!!<br><br>I know I am close... how close??? It may be a polar thing. Hmmm if I bumped everything over so it is all positive numbers would that help???<br><br>Terminally confused...<br><br>-ROOK <br><br></td></tr></table><br>
<a name="392900"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Couple of things I noticed - there may be others :)<br><br><pre class=code>
; now create an array called boardarea of 10 by 10 and
; fill in the entire matrix with 0
For fillx = 0 To 9
	For fillz=0 To 9
		boardarea(fillx,fillz)=0
	Next
Next
</pre><br>This code isn't needed since integer arrays are automatically 'zeroed'.<br><br><br><br><pre class=code>
; Now read it and put an new entity in that location...
; Somehow I get ONLY 2 entitys on the gameboard!!!

For row = 0 To 9
	For col=0 To 9
		If boardarea(row,col) = 0
		PositionEntity n\entity,boardx(row,col),1,boardz(row,col)
		EndIf
	Next
Next
</pre><br>The problem is you're continually (re)positioning the same entity (n\entity) every time through the loop. <br><br></td></tr></table><br>
<a name="392901"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes Big10p!!! I just solved it too... it was my boardx(10,10) and my boardx(10,10)... too much data and too much the same data... <br>GIGO<br>Now if I could just solve the mousepick issue... Would y'all mind trying this?<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; Hexathello (OK Othello but I hope to get the square thing figured out
; then nail a hex thing!
; Ralph Dunn
Graphics3D 800,600,16,2
SetBuffer BackBuffer()

player=CreatePivot()
camera=CreateCamera(player)
light=CreateLight()
RotateEntity light,45,45,0
AmbientLight 48,48,48    ; was 32,32,32

EntityType camera,1
PositionEntity camera,0,53,-69
RotateEntity camera,35,0,0

Global square
Dim boardarea(10,10)
Dim boardx(10)
Dim boardz(10)

; now create an array called boardarea of 10 by 10 and
; fill in the entire matrix with 0
For fillx = 0 To 9
	For fillz=0 To 9
		boardarea(fillx,fillz)=0
	Next
Next

boardx(0)=-45
boardx(1)=-35
boardx(2)=-25
boardx(3)=-15
boardx(4)=-5
boardx(5)=5
boardx(6)=15
boardx(7)=25
boardx(8)=35
boardx(9)=45

boardz(0)=-45
boardz(1)=-35
boardz(2)=-25
boardz(3)=-15
boardz(4)=-5
boardz(5)=5
boardz(6)=15
boardz(7)=25
boardz(8)=35
boardz(9)=45



; #######################################  Load Mesh Objects
gameboard=LoadMesh("squareboard.b3d")
UpdateNormals gameboard
EntityFX gameboard,0
ScaleEntity gameboard,5.5,5.5,5.5

empty=LoadMesh("square.b3d")
EntityPickMode empty,2
HideEntity empty

wpiece=LoadMesh("piece.b3d")
UpdateNormals wpiece
EntityShininess wpiece,1
PositionEntity wpiece,5,3,-5

bpiece=CopyMesh(wpiece)
RotateEntity bpiece,180,0,0
UpdateNormals bpiece
EntityShininess bpiece,1
PositionEntity bpiece,-5,3,5

; ######################################  Plunk Tiles
; -----------------&gt; Thanks Mighty Mac and Wolron
Type empty
	Field entity
	Field ex
	Field ez
	Field kolor
End Type
	
For row = 0 To 9
	For col=0 To 9
			n.empty = New empty
			n\entity = CopyEntity(empty)
			n\ex = boardx(row)
			n\ez = boardz(col)
			n\kolor = 0
	Next
Next

; Now read it and put an new entity in that location...
; Somehow I get 
; a diagonal line of entitys on the gameboard

For row = 0 To 9
	For col=0 To 9
	If boardarea(row,col) = 0
			For n.empty = Each empty
				EntityPickMode n\entity,2
				PositionEntity n\entity,n\ex,1,n\ez
			Next
		EndIf
	Next
Next


; ##############################################
ShowPointer 

While Not KeyDown(1)

x=MouseX()
y=MouseY()

e=CameraPick(camera,x,y)
	If e&lt;&gt;entity#
		If entity Then EntityColor entity,255,255,255 ; was 255,255,255
		entity#=e
	EndIf

If MouseHit(1)=True Then CameraPick(camera,MouseX(),MouseY())
		
	If entity#
		EntityColor entity,0,120,255 ; 73,253,146 lime 254,216,38 gold 254,80,232 magenta
	EndIf

UpdateWorld
RenderWorld

Text 0,10,"X: "+x
Text 0,20,"Y: "+y
Text 0,30,"Z: "+z
Text 0,50,"THING: "+pickedentity()

 
Flip

Wend

End
</textarea>I may be on the right track!!! If I can get the mousepick to access the coordinates AND update gameboard then all is right with the universe. :]<br><br>How can I return information instead of a series of numbers with this???<br>-RZ <br><br></td></tr></table><br>
<a name="392914"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Rook, TBH, I think things are going to get messy doing it this way. Why not just keep everything nice and compact in an array of types? Something like:<br><br><pre class=code>
Type squareT
	Field entity
	Field x#, z#
	Field kolor
End Type

Dim board.squareT(9,9)	; A 10x10 board

row_pos# = -45
For row = 0 To 9
	col_pos# = -45
	For col = 0 To 9
			n.squareT = New squareT
			n\entity = CopyEntity(empty)
			n\x = col
			n\z = row
			n\kolor = 0
			board(row,col) = n
			EntityPickMode n\entity,2
			PositionEntity n\entity,n\x,1,n\z
			col_pos = col_pos + 10
	Next
	row_pos = row_pos + 10
Next
</pre> <br><br></td></tr></table><br>
<a name="392957"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah I see what you did... but squares go from -45,-45 to 45,45... wait wait wait... I see how you did that. How would this work with my pickentity command... only one way to find out! Let me run it :]<br>-RZ <br><br></td></tr></table><br>
<a name="392959"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Had to fix:<br>n\x = row<br>n\z = col<br><br>to <br><br>n\x = row_pos<br>n\z = col_pos<br><br>EDIT::::<br>It worketh!!!! Its Alive... ALIVE!!!<br><br>I fixed the code thussly:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; Hexathello (OK Othello but I hope to get the square thing figured out
; then nail a hex thing!
; Ralph Dunn
Graphics3D 800,600,16,2
SetBuffer BackBuffer()

player=CreatePivot()
camera=CreateCamera(player)
light=CreateLight()
RotateEntity light,45,45,0
AmbientLight 48,48,48    ; was 32,32,32

EntityType camera,1
PositionEntity camera,0,53,-69
RotateEntity camera,35,0,0

Global square
Dim boardarea(10,10)
Dim boardx(10)
Dim boardz(10)
Dim board.squareT(9,9)	; A 10x10 board
Global ent
Global xx
Global zz
Global colour
Global thing

; now create an array called boardarea of 10 by 10 and
; fill in the entire matrix with 0
;For fillx = 0 To 9
;	For fillz=0 To 9
;		boardarea(fillx,fillz)=0
;	Next
;Next

boardx(0)=-45
boardx(1)=-35
boardx(2)=-25
boardx(3)=-15
boardx(4)=-5
boardx(5)=5
boardx(6)=15
boardx(7)=25
boardx(8)=35
boardx(9)=45

boardz(0)=-45
boardz(1)=-35
boardz(2)=-25
boardz(3)=-15
boardz(4)=-5
boardz(5)=5
boardz(6)=15
boardz(7)=25
boardz(8)=35
boardz(9)=45



; #######################################  Load Mesh Objects
gameboard=LoadMesh("squareboard.b3d")
UpdateNormals gameboard
EntityFX gameboard,0
ScaleEntity gameboard,5.5,5.5,5.5

empty=LoadMesh("square.b3d")
EntityPickMode empty,2
HideEntity empty

wpiece=LoadMesh("piece.b3d")
UpdateNormals wpiece
EntityShininess wpiece,1
PositionEntity wpiece,5,3,-5

bpiece=CopyMesh(wpiece)
RotateEntity bpiece,180,0,0
UpdateNormals bpiece
EntityShininess bpiece,1
PositionEntity bpiece,-5,3,5

; ######################################  Plunk Tiles
; -----------------&gt; Thanks Big10p for the type and plunk code
; -----------------&gt; :] (MightyMac And Wolron too)
Type squareT
	Field entity
	Field x#, z#
	Field kolor
End Type

row_pos# = -45
For row = 0 To 9
	col_pos# = -45
	For col = 0 To 9
			n.squareT = New squareT
			n\entity = CopyEntity (empty)
			n\x = col_pos
			n\z = row_pos
			n\kolor = 0
			board(row,col) = n
			EntityPickMode n\entity,2
			PositionEntity n\entity,n\x,1,n\z
			col_pos = col_pos + 10
	Next
	row_pos = row_pos + 10
Next


; ############################################## Start of Game Loop
ShowPointer 

While Not KeyDown(1)

x=MouseX()
y=MouseY()

e=CameraPick(camera,x,y)
	If e&lt;&gt;entity#
		If entity Then EntityColor entity,255,255,255 
		entity#=e
	EndIf

If MouseHit(1) = True Then 
;thing=CameraPick(camera,x,y)
	For n.squareT = Each squareT
		Select e 
			Case entity
			ent=n\entity 
			xx=EntityX(entity)
			zz=EntityZ(entity) 
			colour = n\kolor
			Default 
			pp=0
		End Select
	Next
EndIf

	If entity#
		EntityColor entity,0,120,255 ; 73,253,146 lime 254,216,38 gold 254,80,232 magenta
	EndIf

UpdateWorld
RenderWorld

Text 0,10,"X: "+x
Text 0,20,"Y: "+y
Text 0,30,"Z: "+z
Text 0,50,"THING: "+e
Text 0,60,"XX "+xx
Text 0,70,"ZZ "+zz

Flip

Wend

End
</textarea>It returns location so the program knows where toplace the new piece... now to make it update the array and check for stuff... <br><br>I learned a lot creating this. Maybe another tutorial on how to create a simple mouse selected boardgame should be written... To help noobs like me??? Nah... IMO most noobs don't seem to realize there is a TUTORIALS forum. :]<br><br>-RZ <br><br></td></tr></table><br>
<a name="393194"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>Had to fix: <br>n\x = row <br>n\z = col <br><br>to <br><br>n\x = row_pos <br>n\z = col_pos <br> <br></div><br><br>Yeah, sorry about that. Strangest thing is, I had just put my head down to go to sleep last night when this bug suddenly popped into my head. Bizarre. :)<br><br>Note that you now don't need the boardx and boardz arrays as they're redundant.<br><br>I see you intend to change to using a hex-tiled board at some point so I put this little demo together. Hope it's of some use.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;
; 3D hex tile grid demo by big10p (A.K.A. Chris Chadwick) 2004
;

	Graphics3D 640,480,32,2
	SetBuffer BackBuffer()

	WireFrame 0
	AntiAlias 0

	SeedRnd MilliSecs()

	Global frame_count%
	Global fps%
	Global slowest_fps%
	Global fps_timeout%
	Global frame_time%
	Global slowest_frame%
	Global frame_start%
	fps_timer = CreateTimer(60)
	slowmo% = False
	wiref% = False

	piv = CreatePivot()
	cam = CreateCamera(piv)
	PositionEntity cam,0,0,-18
	TurnEntity piv,45,0,0
	
	light = CreateLight()
	TurnEntity light,10,10,0
	
	Type cellT
		Field ent
		Field id%
		Field x#,z#
	End Type
	
	Dim board.cellT(0,0)

	Const CELL_YELLOW%	= 0
	Const CELL_BLUE%	= 1
	Const CELL_GAP#		= 0.1
	Global cell_mesh
	Global cell_size#
	Global board_rows%, board_cols%
	create_cell_mesh()
	create_board(10,10)
	
	g = 0
	
	; --- Main loop ---
	
	While Not KeyHit(1)
		frame_start = MilliSecs()

		If KeyHit(28) Then slowmo = Not slowmo
		If KeyHit(14) Then wiref = Not wiref : WireFrame wiref

		If MouseHit(1) And g=0
			e = CameraPick(cam,MouseX(),MouseY())
			If e
				cell.cellT = Object.cellT(EntityName(e))
				If cell\id = CELL_YELLOW
					EntityColor e,0,0,255
					cell\id = CELL_BLUE
				Else
					EntityColor e,255,255,0
					cell\id = CELL_YELLOW
				EndIf
				g = 5
			EndIf
		EndIf
		
		If g
			display_board(CELL_GAP + Sin(g))
			g = g + 5
			If g &gt; 180 Then g = 0
		EndIf
		
		RenderWorld
		show_info()
				
		frame_time = MilliSecs() - frame_start	
		
		WaitTimer(fps_timer)
		Flip(1)

		If slowmo Then Delay 200
	Wend

	ClearWorld	

	End


;
;
;
Function create_cell_mesh(size#=2)

	cell_size = size
	hs# = size/2

	temp = CreateCylinder(6,1)	
	FitMesh temp,-hs,-(hs/2),-hs,size,hs,size
	cell_mesh = CopyMesh(temp)		; Make single surface.
	FreeEntity temp
	HideEntity cell_mesh
	
End Function


;
;
;
Function create_board(rows, cols, gap#=CELL_GAP)

	board_rows = rows
	board_cols = cols
	Dim board(rows-1,cols-1)
	
	total_size# = cell_size + gap
	
	x_off# = total_size
	z_off# = total_size - (total_size/4)
	start_x# = -(((((cols-1) * x_off)) / 2) + total_size/4)
	start_z# = (((rows-1) * z_off) / 2)
	
	cell_t = 0
	z_pos# = start_z
	For r = 1 To rows
		x_pos# = start_x + ((Not(r And 1)) * total_size/2)
		For c = 1 To cols
			cell.cellT = New cellT
			board(r-1,c-1) = cell
			If cell_t
				cell\ent = CopyEntity(cell_mesh)
				cell\id = CELL_YELLOW
				EntityColor cell\ent,255,255,0
			Else
				cell\ent = CopyEntity(cell_mesh)
				cell\id = CELL_BLUE
				EntityColor cell\ent,0,0,255
			EndIf			
			NameEntity cell\ent,Handle(cell) 
			cell\x = x_pos
			cell\z = z_pos
			PositionEntity cell\ent,x_pos,0,z_pos
			EntityPickMode cell\ent,2
			x_pos = x_pos + x_off
			cell_t = Not cell_t
		Next
		z_pos = z_pos - z_off
	Next

End Function


;
;
;
Function display_board(gap#=.1)

	total_size# = cell_size + gap
	
	x_off# = total_size
	z_off# = total_size - (total_size/4)
	start_x# = -(((((board_cols-1) * x_off)) / 2) + total_size/4)
	start_z# = (((board_rows-1) * z_off) / 2)
	
	z_pos# = start_z
	For r = 1 To board_rows
		x_pos# = start_x + ((Not(r And 1)) * total_size/2)
		For c = 1 To board_cols
			cell.cellT = board(r-1,c-1)
			cell\x = x_pos
			cell\z = z_pos
			PositionEntity cell\ent,x_pos,0,z_pos
			x_pos = x_pos + x_off
		Next
		z_pos = z_pos - z_off
	Next

End Function


;
; Display debug info
;
Function show_info()
	
	If fps_timeout
		frame_count = frame_count + 1

		If MilliSecs() &gt; fps_timeout Then
			fps_timeout = MilliSecs() + 1000 
			fps = frame_count 
			frame_count = 0 
		
			If fps &lt; slowest_fps Or slowest_fps = 0 Then slowest_fps = fps
		EndIf 
		
		If frame_time &gt; slowest_frame Then slowest_frame = frame_time
		
		Color 0,255,0
		Text 10,10," Triangles: " + TrisRendered()
		Color 255,255,0
		Text 10,25," Millisecs: " + frame_time
		Text 10,40,"   Slowest: " + slowest_frame
		Color 0,255,255
		Text 10,55,"       FPS: " + fps
		Text 10,70,"     Worst: " + slowest_fps
		Color 255,255,255
		Text 10,100,"Click on a cell to toggle it's color"		
	Else
		; First call initialization.
		fps_timeout = MilliSecs() + 1000 
	EndIf
	
End Function
</textarea> <br><br></td></tr></table><br>
<a name="393201"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Big10p... Ohhh it is scary... answering questions I intend to ask BEFORE I ask them!!! ;]<br><br>And while I am at it... VERY COOL DEMO!!!! I give you 12 stars out of 5!!!<br><br>-Rook <br><br></td></tr></table><br>
<a name="393551"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Maybe I'm psychic ...or, maybe I read the comment in your code mentioning you intend to move to using a hex-tile board. :)<br><br>Actually, doing that demo has given me the beginnings of an idea for a game (not a board game). :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
