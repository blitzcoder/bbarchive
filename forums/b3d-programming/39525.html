<!DOCTYPE html><html lang="en" ><head ><title >Tokamak Rope Demo</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Tokamak Rope Demo</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Tokamak Rope Demo</a><br><br>
<a name="441046"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Picklesworth</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I was just reminded of a little rope demo that Constantine, Botbuilder, saad, and myself did a while back in the blitz-tokamak forum.<br>Here's the post<br><a href="http://playerfactory.proboards25.com/index.cgi?board=tokamak2&amp;action=display&amp;num=1090192420&amp;start=0" target="_blank">http://playerfactory.proboards25.com/index.cgi?board=tokamak2&amp;action=display&amp;num=1090192420&amp;start=0</a><br>Here's what I think is the most stable version (may have been adjusted by me a bit later and not posted on the thread). It isn't a full rope building function, but it is fun to play with to learn how to make a nice rope sim. A full function would use triginometry for the section placement and fun stuff like that. Feel free to add to this however you want - maybe we can make this perfect:<br>Also, there's an odd glitch where the rope makes the box shoot upwards often (you'll see when you run it)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Tokamak Rope demo
;V. 3
;By Constantine,Botbuilder, Mr. Picklesworth, and Saad

Const FPS=60
;=========================
Const number = 40 ;Number of rope points
Const Ydistance = 3 ;This also defines the resolution of the rope. \\ Adjust the height of the mesh and RidigBody scale manually if you change it.

Global Lastnumber = number


;=========================
Graphics3D 800,600,0,2
SetBuffer BackBuffer()

WireFrame False

campiv=CreatePivot()

camera = CreateCamera(campiv)
PositionEntity camera,0,2,-170
RotateEntity camera,30,0,0
CameraClsColor camera,82,134,172
light=CreateLight()
PositionEntity light,7,15,-5

TOKSIM_CreateSimulator(0,-20,0)
;========================

Dim mesh (number)
Dim rb (number)
Dim joint(number)

;create the hook
hookPivot = CreatePivot()
PositionEntity hookpivot,0,20,0

hook = CreateSphere()
PositionEntity hook,EntityX#(hookPivot),EntityY#(hookPivot),EntityZ#(hookPivot)
EntityAlpha hook,.5

;========================

TOKSIM_SetRigidBodiesCount number+1
TOKSIM_SetAnimatedBodiesCount 1
TOKSIM_SetGeometriesCount number+2
TOKSIM_CreateSimulator(0,-10,0)

;ground material
TOKSIM_SetMaterial 0,0.8,0

For i=1 To number - 1

    mesh(i) = CreateCylinder()
    ScaleEntity mesh(i),1,1.7,1
    EntityColor mesh(i),196,118,2
    EntityPickMode mesh(i),2
    NameEntity mesh(i),i

    rb(i) = TOKRB_Create()
    TOKRB_AddCylinder rb(i),2.14,1
    TOKRB_SetPosition rb(i),EntityX#(hookPivot),EntityY#(hookPivot)-(i*Ydistance),EntityZ#(hookPivot)
    TOKRB_SetLinearDamping rb(i),0.0001
    TOKRB_SetAngularDamping rb(i),0.0
    TOKRB_SetMass rb(i),20
    TOKRB_SetCylinderInertiaTensor rb(i),2.14,1,20

    TOKRB_CollideConnected rb(i),True

	TOKRB_SetCollisionID rb(i),0

    If i=1
        joint(i) = TOKJOINT_Create(1,rb(i),0)
        TOKJOINT_SetPositionAndRotationWorld(joint(i),0.0,EntityY#(hookPivot)-(i*(Ydistance/2)),0.0,0,0,90)
    Else
        joint(i) = TOKJOINT_Create(2,rb(i-1),rb(i))
        TOKJOINT_SetPositionAndRotationWorld(joint(i),0.0,TOKRB_GetY(rb(i-1))-1.5,0.0,0,0,90)
    EndIf

    TOKJOINT_SetType(joint(i),1)
    TOKJOINT_SetLowerLimit joint(i),3.14*0.0
    TOKJOINT_SetUpperLimit joint(i),3.14*0.8
    TOKJOINT_EnableLimit joint(i),True
    TOKJOINT_Enable(joint(i),True)
    TOKJOINT_SetDampingFactor joint(i),80.0

Next

GroundA=TOKAB_Create()
GroundGeom = TOKAB_AddBox(GroundA,200,1,200)
TOKGEOM_SetMaterialIndex GroundGeom,0
TOKAB_SetPosition GroundA,0,-200,0
gr=CreateCube()
ScaleEntity gr,100,.5,100
PositionEntity gr,0,-200,0

;the last section that it is holding (test)
i = number

mesh(i) = CreateCube()
ScaleEntity mesh(i),20,10,15,True
EntityColor mesh(i),255,0,0
EntityPickMode mesh(i),2
NameEntity mesh(i),i

rb(i) = TOKRB_Create()
TOKRB_AddBox rb(i),40,20,30
TOKRB_SetPosition rb(i),EntityX#(hookPivot),EntityY#(hookPivot)-(i*Ydistance),EntityZ#(hookPivot)
TOKRB_SetBoxInertiaTensor rb(i),40.0,20.0,30.0,200
TOKRB_SetMass rb(i),200
TOKRB_CollideConnected rb(i),True

TOKRB_SetLinearDamping rb(i),0.01
TOKRB_SetAngularDamping rb(i),0.01

TOKRB_SetCollisionID rb(i),1

joint(i) = TOKJOINT_Create(2,rb(i-1),rb(i))
TOKJOINT_SetPositionAndRotationWorld(joint(i),0.0,TOKRB_GetY(rb(i-1))-1.5,0.0,0,0,90)

TOKJOINT_SetType(joint(i),1)
TOKJOINT_SetLowerLimit joint(i),3.14*0.0
TOKJOINT_SetUpperLimit joint(i),3.14*0.8
TOKJOINT_EnableLimit joint(i),True
TOKJOINT_Enable(joint(i),True)
TOKJOINT_SetDampingFactor joint(i),80.0

;TOKSIM_SetCollisionResponse 0,1,0

period=1000/FPS
time=MilliSecs()-period

; MainLoop
While Not KeyHit(1)
    If Not KeyDown(57) Then TurnEntity campiv,0,.1,0
    Repeat
    elapsed=MilliSecs()-time
    Until elapsed
    
    ticks=elapsed/period
    tween#=Float(elapsed Mod period)/Float(period)
    
    For k=1 To ticks
        time=time+period
        If k=ticks Then CaptureWorld
        TOKSIM_Advance(1.9/FPS,2)
        UpdateWorld
    Next
        
    ; destroying sections
    If MouseDown(1) And SAllowed = True Then
        CameraPick(camera,MouseX(),MouseY())
        If Not PickedEntity() = False Then
            Sallowed = False
            Jnumber = EntityName(PickedEntity())
            If Jnumber = number
                TOKRB_ApplyImpulse2 rb(Jnumber),0,0,.1,PickedX() * 2,PickedY() * 2,PickedZ() * 2
                Lastnumber = JNumber - 1
            Else
                TOKRB_ApplyImpulse2 rb(Jnumber),0,0,.1 ,PickedX() * 2,PickedY() * 2,PickedZ() * 2
                If Not joint(JNumber) = False
                    TOKJOINT_Enable joint(Jnumber),False
                    Lastnumber = JNumber - 1
                EndIf
            EndIf
        EndIf
    Else
        If Not MouseDown(1) Then SAllowed = True
    EndIf
    
    ;bouncing sections
    If MouseDown(2)
        CameraPick(camera,MouseX(),MouseY())
        If Not PickedEntity() = False
            Jnumber = EntityName(PickedEntity())
            TOKRB_ApplyImpulse2 rb(Jnumber),0,0,10 ,PickedX() * 2,PickedY() * 2,PickedZ() * 2
        EndIf
    EndIf
        
    For i=1 To number
        PositionEntity mesh(i),TOKRB_GetX#(rb(i)),TOKRB_GetY#(rb(i)),TOKRB_GetZ#(rb(i))
        RotateEntity mesh(i),TOKRB_GetPitch#(rb(i)),TOKRB_GetYaw#(rb(i)),TOKRB_GetRoll#(rb(i))
        
        If KeyDown( 208 )=True Then TOKRB_ApplyImpulse rb(Lastnumber),0,0,-.1
        If KeyDown( 200 )=True Then TOKRB_ApplyImpulse rb(Lastnumber),0,0,.1
        If KeyDown( 203 )=True Then TOKRB_ApplyImpulse rb(Lastnumber),-.1,0,0
        If KeyDown( 205 )=True Then TOKRB_ApplyImpulse rb(Lastnumber),.1,0,0
    Next
    
    RenderWorld tween	
    
    Text 0,0,"Physics Time:"+TOKSIM_GetPhysicsTime()*1000.0+ " milliseconds"
    Text 0,10,"Render Time:"+Str(elapsed)+ " milliseconds"
    Flip False

Wend

TOKSIM_DestroySimulator()

End</textarea><br><br>Note: You need the latest tokamak wrapper and dll. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
