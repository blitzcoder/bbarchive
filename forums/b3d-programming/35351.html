<!DOCTYPE html><html lang="en" ><head ><title >Strange...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Strange...</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Strange...</a><br><br>
<a name="384537"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >REDi</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am writing a landscape app that exports to b3d, when I load the b3d model into blitz it looks perfect alpha blended land layers, sky ect all looks fine, but the loaded model says it has no children and only one surface! (the sky)<br><br>If I free the entity the sky disappears but the landscape doesn't, any ideas what I've done wrong? (I'll post b3d file if needed)<br><br>BTW I did load it with LoadAnimMesh()<br><br>Thanks in advance. <br><br></td></tr></table><br>
<a name="384555"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >REDi</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> B3D model and textures was here, but now I've removed the link ;) <br><br></td></tr></table><br>
<a name="384970"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >REDi</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Bump<br><br>Has ne1 come across this problem before? <br><br></td></tr></table><br>
<a name="384978"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Braneloc</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Fascinating !<br><br>Looks like you have exported two objects in one somehow.<br><br>Blitz only recognises the sky(in an animmesh) - hence the sky moves when you tell it to, and vanishes when you tell it to - but the landscape does not.  The landscape is not even being added to the entity system(though it is added to the rendersystem), hence it being orphaned, uncontrollable and visible.<br><br>Loading as a normal mesh(or into UU3D), it loads the other way round... and plays with the landscape nicely, but seems to be ignoring the sky ! <br><br>Your mesh also crashes blitz a little bit...<br><br>I suggest you look very carefully at your exporter with intense suspicion.  If the mesh was saved correctly it would collapse into one (on LoadMesh) with no problems.<br><br>You may get better results exporting two totally seperate objects(seperate files works), or one object with multiple surfaces than experimenting with object children. <br><br></td></tr></table><br>
<a name="384996"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >REDi</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah it's very odd!<br><br>There are actualy three meshs to the landscape, so all three are being orphaned, but I can't get why this is happening.<br><br>Here's the exporter functions... (if anyones interested)<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function WriteBB3DLandscape( f_name$ )

	file=WriteFile( f_name$ )

	b3dSetFile( file )
	
	b3dBeginChunk( "BB3D" )
		b3dWriteInt( 1 )	;version

		b3dBeginChunk( "TEXS" )
		tcount = -1
		For OptimizedMesh.OptimizedMesh = Each OptimizedMesh
			tcount = tcount + 1
			For LandscapeLayer.LandscapeLayer = Each LandscapeLayer
				If LandscapeLayer\TextureID = OptimizedMesh\TextureID
					Current.LandscapeLayer = LandscapeLayer.LandscapeLayer
					LandscapeLayer.LandscapeLayer = Last LandscapeLayer
				EndIf
			Next
			b3dWriteString("LayerTexture_"+OptimizedMesh\TexCount+".bmp")   ;texture file name
	 		b3dWriteInt(1)								;blitz3D TextureFLags default=1
			b3dWriteInt(2)          					;TextureBlend: default=2
			b3dWriteFloat(Current\TexturePositionU#)	;x position of texture: Default=0
			b3dWriteFloat(Current\TexturePositionV#)    ;y position of texture: default=0
			b3dWriteFloat(1.0/Current\TextureScaleU#)	;x scale of texture: Default=1
			b3dWriteFloat(1.0/Current\TextureScaleV#)   ;y scale of texture: default=1
			b3dWriteFloat(0)        					;rotation of texture (in radians): default=0	
		Next
		If SkyActive = 1
			tcount = tcount + 1
			skybrush = tcount
			b3dWriteString("Sky.bmp")				;texture file name
	 		b3dWriteInt(1)			 				;blitz3D TextureFLags default=1
			b3dWriteInt(2)           				;TextureBlend: default=2
			b3dWriteFloat(0)		 				;x position of texture: default=0
			b3dWriteFloat(0)         				;y position of texture: default=0
			b3dWriteFloat(1.0/SkyTextureScaleU#)	;x scale of texture: default=1
			b3dWriteFloat(1.0/SkyTextureScaleV#)    ;y scale of texture: default=1
			b3dWriteFloat(0)        				;rotation of texture (in radians): default=0	
		EndIf
		If LightMapTexture &lt;&gt; 0
			tcount = tcount + 1
			lightmapbrush = tcount
			b3dWriteString("LightMap.bmp")				;texture file name
	 		b3dWriteInt(1)			 				;blitz3D TextureFLags default=1
			b3dWriteInt(2)           				;TextureBlend: default=2
			b3dWriteFloat(0.02)		 				;x position of texture: default=0
			b3dWriteFloat(0.02)         			;y position of texture: default=0
			b3dWriteFloat(1.0/-1.065)				;x scale of texture: default=1
			b3dWriteFloat(1.0/-1.065)    			;y scale of texture: default=1
			b3dWriteFloat(0)        				;rotation of texture (in radians): default=0	
		EndIf
		b3dEndChunk()								; ends of TEXS chunk
		
		b3dBeginChunk( "BRUS" )
			If LightMapTexture &lt;&gt; 0
				b3dWriteint( 2 )					; textures per brush (with lightmap)
			Else
				b3dWriteInt( 1 )					;0 textures per brush
			EndIf
			For OptimizedMesh.OptimizedMesh = Each OptimizedMesh
				b3dWriteString( "LayerBrush_"+OptimizedMesh\TexCount )		;name
				b3dWriteFloat( 1 )					;red
				b3dWriteFloat( 1 )					;green
				b3dWriteFloat( 1 )					;blue
				b3dWriteFloat( 1 )					;alpha
				b3dWriteFloat( 0 )					;shininess
				b3dWriteInt( 1 )					;blend
				If OptimizedMesh.OptimizedMesh = First OptimizedMesh
					b3dWriteInt( 2 )
				Else
					b3dWriteInt( 32 + 2 )					;FX
				EndIf
  				b3dWriteInt( OptimizedMesh\TexCount-1 )   	;textures used in brush			
				If LightMapTexture &lt;&gt; 0
					b3dWriteInt( lightmapbrush )			; lightmap
				EndIf
			Next
			If SkyActive = 1
				b3dWriteString( "SkyBrush")			;name
				b3dWriteFloat( 1 )					;red
				b3dWriteFloat( 1 )					;green
				b3dWriteFloat( 1 )					;blue
				b3dWriteFloat( 1 )					;alpha
				b3dWriteFloat( 0 )					;shininess
				b3dWriteInt( 1 )					;blend
				b3dWriteInt( 2 )					;fx
  				b3dWriteInt( skybrush )   			;textures used in brush	
				If LightMapTexture &lt;&gt; 0
					b3dWriteInt( -1 )				; no texture?
				EndIf
			EndIf
		b3dEndChunk()	;end of BRUS chunk

		count = 0
		For OptimizedMesh.OptimizedMesh = Each OptimizedMesh
			mesh = OptimizedMesh\MeshID
			name$ = "LandscapeLayer"+count : count=count+1
			b3dBeginChunk( "NODE" )
				b3dWriteString( name$ )
				b3dWriteFloat( EntityX(mesh) )		;x_pos
				b3dWriteFloat( EntityY(mesh) )		;y_pos
				b3dWriteFloat( EntityZ(mesh) )		;z_pos
				b3dWriteFloat( EntityScaleX(mesh) )	;x_scale
				b3dWriteFloat( EntityScaleY(mesh) )	;y_scale
				b3dWriteFloat( EntityScaleZ(mesh) )	;z_scale
				b3dWriteFloat( 1 )	                ;rot_w    ?- what the #£!&amp; -?
 				b3dWriteFloat( EntityPitch(mesh) )	;rot_x
				b3dWriteFloat( EntityYaw(mesh) )	;rot_y
				b3dWriteFloat( EntityRoll(mesh) )	;rot_z
				WriteMESH( mesh,OptimizedMesh.OptimizedMesh )
			b3dEndChunk()	;end of NODE chunk
		Next
		If SkyActive = 1
			mesh = SkyID
			name$ = "Sky" : count=count+1
			b3dBeginChunk( "NODE" )
				b3dWriteString( name$ )
				b3dWriteFloat( 0 )					;x_pos
				b3dWriteFloat( 0 )					;y_pos
				b3dWriteFloat( 0 )					;z_pos
				b3dWriteFloat( EntityScaleX(mesh) )	;x_scale
				b3dWriteFloat( EntityScaleY(mesh) )	;y_scale
				b3dWriteFloat( EntityScaleZ(mesh) )	;z_scale
				b3dWriteFloat( 1 )	                ;rot_w    ?- what the #£!&amp; -?
 				b3dWriteFloat( EntityPitch(mesh) )	;rot_x
				b3dWriteFloat( EntityYaw(mesh) )	;rot_y
				b3dWriteFloat( EntityRoll(mesh) )	;rot_z
				WriteSkyMESH(mesh,skybrush)
			b3dEndChunk()	;end of NODE chunk
		EndIf
		
	b3dEndChunk()	;end of BB3D chunk
	
	CloseFile file
End Function





Function WriteMESH( mesh,OptimizedMesh.OptimizedMesh )

	n_surfs=CountSurfaces( mesh )
	
	b3dBeginChunk( "MESH" )
		b3dWriteInt( -1 )				;no 'entity' brush
		
		b3dBeginChunk( "VRTS" )
			b3dWriteInt( 3 )			;flags - 0=no normal/color
			b3dWriteInt( 1 )			;0 tex_coord sets
			b3dWriteInt( 2 )			;0 coords per set
			
			For k=1 To n_surfs
				surf=GetSurface( mesh,k )
				n_verts=CountVertices( surf )-1
				
				For j=0 To n_verts
					b3dWriteFloat( VertexX( surf,j ) )
					b3dWriteFloat( VertexY( surf,j ) )
					b3dWriteFloat( VertexZ( surf,j ) )
					b3dWriteFloat( VertexNX( surf,j ) )
					b3dWriteFloat( VertexNY( surf,j ) )
					b3dWriteFloat( VertexNZ( surf,j ) )
					b3dWriteFloat( VertexRed( surf,j ) )
					b3dWriteFloat( VertexGreen( surf,j ) )
					b3dWriteFloat( VertexBlue( surf,j ) )
					b3dWriteFloat( VertexAlpha( surf,j ) )
					b3dWriteFloat( VertexU( surf,j ) )
					b3dWriteFloat( VertexV( surf,j ) )
				Next
			Next
		b3dEndChunk()	;end of VRTS chunk
		
		first_vert=0
		For k=1 To n_surfs
			surf=GetSurface( mesh,k )
			n_tris=CountTriangles( surf )-1
			
			b3dBeginChunk( "TRIS" )
				b3dWriteInt( OptimizedMesh\TexCount-1 )		;brush for these triangles
				
				For j=0 To n_tris
					b3dWriteInt( first_vert+TriangleVertex( surf,j,0 ) )
					b3dWriteInt( first_vert+TriangleVertex( surf,j,1 ) )
					b3dWriteInt( first_vert+TriangleVertex( surf,j,2 ) )
				Next
				
			b3dEndChunk()	;end of TRIS chunk
			
			first_vert=first_vert+CountVertices( surf )
			
		Next
		
	b3dEndChunk()	;end of MESH chunk

End Function









Function WriteSkyMESH(mesh,brush )

	n_surfs=CountSurfaces( mesh )
	
	b3dBeginChunk( "MESH" )
		b3dWriteInt( -1 )				;no 'entity' brush
		
		b3dBeginChunk( "VRTS" )
			b3dWriteInt( 0 )			;flags - 0=no normal/color
			b3dWriteInt( 1 )			;0 tex_coord sets
			b3dWriteInt( 2 )			;0 coords per set
			
			For k=1 To n_surfs
				surf=GetSurface( mesh,k )
				n_verts=CountVertices( surf )-1
				
				For j=0 To n_verts
					b3dWriteFloat( VertexX( surf,j ) )
					b3dWriteFloat( VertexY( surf,j ) )
					b3dWriteFloat( VertexZ( surf,j ) )
					;b3dWriteFloat( VertexNX( surf,j ) )
					;b3dWriteFloat( VertexNY( surf,j ) )
					;b3dWriteFloat( VertexNZ( surf,j ) )
					;b3dWriteFloat( VertexRed( surf,j ) )
					;b3dWriteFloat( VertexGreen( surf,j ) )
					;b3dWriteFloat( VertexBlue( surf,j ) )
					;b3dWriteFloat( VertexAlpha( surf,j ) )
					b3dWriteFloat( VertexU( surf,j ) )
					b3dWriteFloat( VertexV( surf,j ) )
				Next
			Next
		b3dEndChunk()	;end of VRTS chunk
		
		first_vert=0
		For k=1 To n_surfs
			surf=GetSurface( mesh,k )
			n_tris=CountTriangles( surf )-1
			
			b3dBeginChunk( "TRIS" )
				b3dWriteInt( brush )		;brush for these triangles
				
				For j=0 To n_tris
					b3dWriteInt( first_vert+TriangleVertex( surf,j,0 ) )
					b3dWriteInt( first_vert+TriangleVertex( surf,j,1 ) )
					b3dWriteInt( first_vert+TriangleVertex( surf,j,2 ) )
				Next
				
			b3dEndChunk()	;end of TRIS chunk
			
			first_vert=first_vert+CountVertices( surf )
			
		Next
		
	b3dEndChunk()	;end of MESH chunk

End Function
</textarea> <br><br></td></tr></table><br>
<a name="385009"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Braneloc</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, I see what you are doing.<br><br>You are setting up the nodes as siblings, rather than children, in other words multiple root nodes, which I don't seem to remember blitz supporting.<br><br>If you write a "root" node, then have everything as children to that node, you may have better success.  You'll still need to run findchild on the sky to move it seperately. <br><br></td></tr></table><br>
<a name="385015"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >REDi</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Braneloc, now I get it...<br><pre class=code>
NODE
   MESH
   NODE
      MESH
   ENDNODE
ENDNODE
</pre><br>Instead of<br><pre class=code>
NODE
   MESH
ENDNODE
NODE
   MESH
ENDNODE
</pre><br>Works a treat! :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
