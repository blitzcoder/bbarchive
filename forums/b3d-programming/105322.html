<!DOCTYPE html><html lang="en" ><head ><title >ReadWrite Bit maps</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >ReadWrite Bit maps</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >ReadWrite Bit maps</a><br><br>
<a name="1282740"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm having a problem in trying to convert between bitmap images (where pixel status is only considered 0 or 1) and banks.<br><br>The issue seems to be specifically particular with trying to write the  Image data to a bank.<br><br>I have functions which can read/write accurately from the banks unsing ReadBytes and WriteBytes, so the problem can only be in writing to the banks byte by byte is different somehow (Or I'm missing something glaringly obvious).<br><br>The Read/WriteBytes theoretically should simply write bytes in sequence, and I believe this does so in 8 bit bytes regardless.<br>This would most likely be related as Isuspect that the Endian of long bytes may cause the sequence to be reversed...<br><br><br><br><br><br><br><br><br>Here's a sample of code that highlights the issue:<br><pre class=code>
Graphics 1024,768,32

Const SPRITE_MAX=255
Const UNIT_RESOLUTION=8
Const SPR_BIT_DEPTH=8

;1-8		Sprite Ref	:	Eight Bits	;Maximum
;9-10		Frames		:	Two Bits	;0 or 3 Units should never be used, so +1 to raw data
;11-12		Width		:	Two Bits	;in UNITs - 0 or 3 Units should never be used, so +1 to raw data
;13-14		Height		: 	Two Bits	;in UNITs - 0 or 3 Units should never be used, so +1 to raw data
;15			Reversal	:	One Bit		;if Reverse direction requires additional animation frames

Const SPR_FRAMECOUNT_BYTE_OFFSET=9
Const SPR_FRAMECOUNT_BIT_DEPTH=2
Const SPR_DIMENSIONW_BYTE_OFFSET=11
Const SPR_DIMENSIONW_BIT_DEPTH=2
Const SPR_DIMENSIONH_BYTE_OFFSET=13
Const SPR_DIMENSIONH_BIT_DEPTH=2

Const SPR_ALTREVERSE_BYTE_OFFSET=15	;Whether reverse Direction has separate animations
Const SPR_ALTREVERSE_BIT_DEPTH=1

Const MAX_FRAMES=4

Const Root$="C:\Users\A User\Documents\Some Directory\"


Type SPRITE
	Field Reference
	Field Frames
	Field FrameBank[MAX_FRAMES]
	Field Width
	Field Height
	Field Reversal
End Type


Global Count



MakeSpriteData






























Function MakeSpriteData()
	
	CountSprites
	WriteSprites
	
End Function


Function CountSprites() 
	Count=0
	Local Directory=ReadDir(Root)
	Local File$
	Local Path$
	File=NextFile(Directory)
	While (File&lt;&gt;"")
		Path$=Root+File
		If (FileType(Path)=1)
			If (Right(File,3)="bmp")
				Count=Count+1
			End If
		End If
		File=NextFile(Directory)
	Wend
	
	CloseDir Directory
	
	If (Count&gt;SPRITE_MAX)
		Count=SPRITE_MAX
	End If
	
End Function

Function WriteSprites()
	Local Directory=ReadDir(Root)
	Local File$=NextFile(Directory)
	Local Path$
	
	Local Stream=WriteFile(Root+"Default.spr")
	WriteByte(Stream,Count)
	
	Local counter=0
	While (File&lt;&gt;"") And (counter&lt;=Count)
		Path$=Root+File
		If (FileType(Path)=1)
			If (Right(File,3)="bmp")
				counter=counter+1
				ProcessSpriteFile(Path,Stream)
			End If
		End If
		File=NextFile(Directory)
	Wend
	CloseFile Stream
	CloseDir Directory
End Function

Function ProcessSpriteFile(Path$,Stream)
	Local Bank
	Local S.SPRITE
	
	S=ReadSprite(Path)
	WriteSprite(Stream,S)
	
	
	For Bank=1 To S\Frames
		FreeBank S\FrameBank[Bank-1]
	Next
	
	Delete S
	
End Function

Function ReadSprite.SPRITE(Path$)
	
	Local Name$=GetName(Path)
	Local Flags=GetFlags(Name)
	
	Local S.SPRITE=New SPRITE
	S\Reference=Int(Left(Name,Instr(Name," ")-1))
	S\Width=Left(Str(Flags),1)
	S\Height=Mid(Str(Flags),2,1)
	S\Frames=Mid(Str(Flags),3,1)
	S\Reversal=Int(Right(Str(Flags),1))
	
	Local Frame
	Local Bank
	Local Byte
	Local Buffer
	
	Local Image=LoadAnimImage(Path,S\Width*UNIT_RESOLUTION,S\Height*UNIT_RESOLUTION,0,S\Frames)
	
	For Frame=1 To S\Frames
		Bank=CreateBank(S\Width*S\Height*UNIT_RESOLUTION)
		Byte=BankSize(Bank)
		S\FrameBank[Frame-1]=Bank
		
		BufferToBank(S,ImageBuffer(Image,Frame-1),Bank,S\Width,S\Height)
		
	Next
	
	FreeImage Image
	Return S
	
End Function

Function WriteSprite(Stream,S.SPRITE)
	WriteSpriteToFile(S,Stream)
End Function

Function BufferToBank(S.SPRITE,Buffer,Bank,Width,Height)
	LockBuffer Buffer
	
	Local X
	Local Y
	Local n
	Local W=Width*UNIT_RESOLUTION
	Local H=Height*UNIT_RESOLUTION
	
	Local Byte
	Local Pixel
	
	For Y=0 To H-1
		
		
		Byte=0
		
		
		For X=0 To W-1 Step UNIT_RESOLUTION
			
			
			Pixel=( ReadPixelFast(X+n,Y,Buffer))
			
			If (Pixel=-1)
				Byte=Byte+(1 Shl n)
			End If
			
			PokeFrameLine(S,Bank,Y,Byte)
			
		Next
	Next
	UnlockBuffer Buffer
End Function	

Function GetName$(Path$)
	Return Right(Path,Len(Path)-Len(TrimPath(Path)))
End Function	

Function TrimPath$(FilePath$)
	If (FilePath&lt;&gt;"")
		If (((Right(FilePath,1)=".") Or  (Right(FilePath,1)="\") Or (Right(FilePath,1)="/")))
			Return FilePath
		End If
		
		Local Count
		Local Char$
		Local Length=Len(FilePath)
		
		For Count=Length To 1 Step -1
			Char=Mid(FilePath,Count,1)
			If (Char="\")
				Exit
			End If
		Next
		Return Left(FilePath,Count)
	End If
End Function

Function GetFlags(Name$)
	Local ReferenceEnd=Instr(Name," ")
	
	Return Int(Mid(Name,ReferenceEnd+1,4))
End Function

Function PokeFrameLine(S.SPRITE,bank,Y,Byte)
	Select S\Width
		Case 1:
			PokeByte bank,Y,Byte
		Case 2:
			PokeShort bank,Y*2,Byte		
		Case 4:
			PokeInt bank,Y*4,Byte
		Default:
			RuntimeError "Invalid runlength request: "+S\Width
	End Select
End Function

Function WriteSpriteToFile(S.SPRITE,Stream)
	
	Local UnitWidth=S\Width
	Local BitwiseWidth=(UnitWidth Shr True)
	Local ByteValueWidth=BitwiseWidth Shl (SPR_DIMENSIONW_BYTE_OFFSET-1)
	
	Local UnitHeight=S\Height
	Local BitwiseHeight=(UnitHeight Shr True)
	Local ByteValueHeight=BitwiseHeight Shl (SPR_DIMENSIONH_BYTE_OFFSET-1)
	
	Local Frames=S\Frames
	Local BitwiseFrames=(S\Frames Shr True)
	Local ByteValueFrames=BitwiseFrames Shl (SPR_FRAMECOUNT_BYTE_OFFSET-1)
	
	Local Reversal=S\Reversal Shl (SPR_ALTREVERSE_BYTE_OFFSET-1)
	
	
	WriteShort Stream,S\Reference+ByteValueWidth+ByteValueHeight+ByteValueFrames+Reversal
	
	Local Frame
	Local Bank
	Local Byte
	
	For Frame=1 To Frames
		Bank=S\FrameBank[Frame-1]
		Byte=BankSize(Bank)
		WriteBytes(Bank,Stream,0,Byte)
	Next
	
End Function</pre><br><br>Otherwise, when NOT trying to onvert between image files, the Banks can be read and written perfectly with the following:<br><br><pre class=code>
Function WriteSpritesToFile()
	ChangeDir DATA_STORE_PATH
	Local Path$=DATA_STORE_PATH+MRU_SPRITEFILE+".spr"
	Local Stream=WriteFile(Path)
	Local S.SPRITE
	Local Iter
	
	WriteByte Stream,SPRITE_COUNT
	
;	For Iter=0 To SPRITE_COUNT
;		S=Object.SPRITE(SPRITES[Iter])
	
	For S=Each SPRITE 
		If (S&lt;&gt;Null)
			WriteSpriteToFile(S,Stream)
		End If
	Next
	
;	Next
	
	CloseFile Stream
	
	Local Message$=WRITEFAILUREMESSAGE
	
	If (FileType(Path)=1)
		If (FileSize(Path))
			Message$=WRITESUCCESSMESSAGE
		End If 
	End If
	
	Notify Message
End Function

Function WriteSpriteToFile(S.SPRITE,Stream)
	
	Local UnitWidth=S\Width
	Local BitwiseWidth=(UnitWidth Shr True)
	Local ByteValueWidth=BitwiseWidth Shl (SPR_DIMENSIONW_BYTE_OFFSET-1)
	
	Local UnitHeight=S\Height
	Local BitwiseHeight=(UnitHeight Shr True)
	Local ByteValueHeight=BitwiseHeight Shl (SPR_DIMENSIONH_BYTE_OFFSET-1)
	
	Local Frames=S\Frames
	Local BitwiseFrames=(S\Frames Shr True)
	Local ByteValueFrames=BitwiseFrames Shl (SPR_FRAMECOUNT_BYTE_OFFSET-1)
	
	Local Reversal=S\Reversal Shl (SPR_ALTREVERSE_BYTE_OFFSET-1)
	
	
	WriteShort Stream,S\Reference+ByteValueWidth+ByteValueHeight+ByteValueFrames+Reversal
	
	Local Frame
	Local Bank
	Local Byte
	
	For Frame=1 To Frames
		Bank=S\FrameBank[Frame-1]
		Byte=BankSize(Bank)
		WriteBytes(Bank,Stream,0,Byte)
	Next
	
End Function

Function ImportSpriteFile(FilePath$)
	Local Stream=ReadFile(FilePath)
	If (Stream)
		Local Path$=TrimPath(FilePath)
		Local Name$=Right(FilePath,Len(FilePath)-Len(Path))
		MRU_SPRITEFILE=Left(Name,Len(Name)-4)
		GetSpritesDataFromStream(Stream)
		CloseFile Stream
	End If
End Function

Function GetSpritesDataFromStream(Stream)
	SPRITE_COUNT=ReadByte(Stream)
	GetSpritesFromStream(Stream)
End Function

Function GetSpritesFromStream(Stream)
	Local Count
	For Count=0 To SPRITE_COUNT
		GetSpriteDataFromStream(Stream)
	Next
End Function

Function GetSpriteDataFromStream(Stream)
	Local S.SPRITE=New SPRITE
	
	Local Byte=ReadShort(Stream)
	
	S\Reference=GetSpriteReferenceFromByte(Byte)
	SPRITES[S\Reference]=Handle(S)
	
	S\Width=GetFrameWidthUnitsFromByte(Byte)
	S\Height=GetFrameHeightUnitsFromByte(Byte)
	
	S\Frames=GetFramesFromByte(Byte)
	S\Reversal=GetReversalFlagFromByte(Byte)
	SetSpriteBanksFromStream(S,Stream)
	
End Function	

Function SetSpriteBanksFromStream(S.SPRITE,Stream)
	Local Frame
	Local Bank
	Local Byte
	For Frame=1 To S\Frames
		Bank=CreateBank(S\Width*S\Height*UNIT_RESOLUTION)
		Byte=BankSize(Bank)
		ReadBytes(Bank,Stream,0,Byte)
		S\FrameBank[Frame-1]=Bank
	Next
End Function</pre> <br><br></td></tr></table><br>
<a name="1282812"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> The issue seems to be specifically particular with trying to write the Image data to a bank. <br></div><br>Just reading the code, not trying to run it...<br><br>Writing image data to bank seems to be the BufferToBank function, which contains<br><pre class=code>
		For X=0 To W-1 Step UNIT_RESOLUTION
			
			
			Pixel=( ReadPixelFast(X+n,Y,Buffer))
			
			If (Pixel=-1)
				Byte=Byte+(1 Shl n)
			End If
			
			PokeFrameLine(S,Bank,Y,Byte)
			
		Next
</pre><br>using n as an offset. But n never changes, is always the inital value of zero. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
