<!DOCTYPE html><html lang="en" ><head ><title >card value</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >card value</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >card value</a><br><br>
<a name="471603"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am stuck at having a think at this and I hope someone can help me reason this out. I don't really want someone to code for me... no... I want to try to d it myself but I just can't reason it well.<br><br>Situation: I am thinking of coding a matching type game in B3D... it's what I have... and lets say the entities are cards.<br><br>I have a routine that creates a deck and shuffles it. It can even output to a file if needed.<br><br>The thing is I want the computer to be able to compare the values of a card... to know the values of a card so it can tell if the values is proper for the gemaplay.<br><br>Should I define the cards as a TYPE or???<br><br>I mean you can put a lot of dta into a TYPE... but can you compare them??? <br><br>I need help... I have been away too long.<br><br>RZ <br><br></td></tr></table><br>
<a name="471606"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Have an array, representing the board. In each array slot, is a number. That number is the value of the card. If the user picks 2,3 and 5,8, then compare the values in the array slots. :o) <br><br></td></tr></table><br>
<a name="471694"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >WolRon</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is some code snippets from a 5 card draw Jacks or Better Poker game I made once.  You can see how it basically worked and then apply whatever you need to your game.<br><br>Set up the types and arrays:<br><pre class=code>Type Card
	Field suit
	Field value
End Type

Dim NewDeck.Card(52)
Dim Deck.Card(52)
Dim Hand.Card(5)
Dim NewHand.Card(5)
Dim Hold(5)
</pre><br>NewDeck is all 52 cards in order.  Used only for shuffling.<br>Deck is the current (shuffled) deck that is in play.<br>Hand is the current 5-card hand that the player holds.<br>NewHand is the new hand being dealt to the player after he draws.<br>Hold is an array that keeps track of which cards the player is keeping or throwing.<br><br><br>Load the arrays with NEW instances of the card types (only need to do once).  Suits are 1-4.  Values are 1-13.<br><pre class=code>
For iter = 1 To 52
	Deck(iter) = New Card
	NewDeck(iter) = New Card
	NewDeck(iter)\suit = Ceil((53 - Float(iter)) / 13)
	NewDeck(iter)\value = iter Mod 13
	If NewDeck(iter)\value = 0 Then NewDeck(iter)\value = 13
Next
For iter = 1 To 5
	Hand(iter) = New card
	NewHand(iter) = New card
Next
</pre><br>Shuffling the deck is done by setting up the array "NewDeck" with all of the 52 cards (in order), and then randomly pulling them out and placing them in the shuffled deck "Deck".<br><pre class=code>
Function ShuffleDeck()
	channel = PlaySound(sndShuffle)
	For iter = 1 To 52 
		NewDeck(iter)\suit = Ceil((53 - Float(iter)) / 13)
		NewDeck(iter)\value = iter Mod 13
		If NewDeck(iter)\value = 0 Then NewDeck(iter)\value = 13
	Next
	For iter = 1 To 52
		Repeat
			Randomcard = Rnd(1, 52)
		Until NewDeck(Randomcard)\value &lt;&gt; 0
		Deck(iter)\suit = NewDeck(Randomcard)\suit
		Deck(iter)\value = NewDeck(Randomcard)\value
		NewDeck(Randomcard)\value = 0
	Next
	While ChannelPlaying(channel)
	Wend
End Function
</pre><br>Cards are dealt into the hand one at a time off of the deck.  If "entirehand" is true then all 5 cards are replaced, otherwise only the cards that are not being 'held' by the player are replaced.  If the deck is near the end (&gt;47), then it is reshuffled first.<br><pre class=code>
Function Deal(entirehand)
	nextcard = 0
	Repeat
		nextcard = nextcard + 1
		If nextcard &gt; 47
			nextcard = 1
			ShuffleDeck()
		EndIf 
	Until Deck(nextcard)\value &lt;&gt; 0
	If entirehand = True 
		For iter = 1 To 5
			NewHand(iter)\suit = Deck(nextcard)\suit
			NewHand(iter)\value = Deck(nextcard)\value
			Deck(nextcard)\value = 0
			nextcard = nextcard + 1
		Next
	Else
		For iter = 1 To 5
			If Hold(iter) = False 
				NewHand(iter)\suit = Deck(nextcard)\suit
				NewHand(iter)\value = Deck(nextcard)\value
				Deck(nextcard)\value = 0
				nextcard = nextcard + 1
			Else
				NewHand(iter)\suit = Hand(iter)\suit
				NewHand(iter)\value = Hand(iter)\value
			EndIf 
		Next
	EndIf 
End Function
</pre><br>The players hand is checked for winning combinations.  Note that they are checked in order of importance (for example, so that a full house is detected before a pair).<br><pre class=code>
Function EvaluateHand()
	CheckForRoyalFlush()
	If Rank &gt; 0 Then Return
	CheckForStraightFlush()
	If Rank &gt; 0 Then Return
	CheckForFourOfAKind()
	If Rank &gt; 0 Then Return
	CheckForFullHouse()
	If Rank &gt; 0 Then Return
	CheckForFlush()
	If Rank &gt; 0 Then Return
	CheckForStraight()
	If Rank &gt; 0 Then Return
	CheckForThreeOfAKind()
	If Rank &gt; 0 Then Return
	CheckForTwoPair()
	If Rank &gt; 0 Then Return
	CheckForJacksOrBetter()
End Function
</pre><br>And here is where the real magic happens.<br>Rank equals 0.  If a match is found then the Rank is set to equal 1 - 9.  If no match is found then it simply returns (leaving the rank of the hand equal to 0).<br><pre class=code>
Function CheckForRoyalFlush()
	;check for flush
	For iter = 2 To 5
		If Hand(1)\suit &lt;&gt; Hand(iter)\suit Then Return 
	Next
	;check for royals
	For iter = 1 To 5
		If Hand(iter)\value = 1 Then ace = ace + 1
	Next
	If ace &lt;&gt; 1 Then Return
	For royal = 13 To 10 Step -1
		For iter = 1 To 5
			If Hand(iter)\value = royal Then thisroyal = thisroyal + 1
		Next
		If thisroyal &lt;&gt; 1 Then Return
		thisroyal = 0
	Next
	Rank = 9
End Function

Function CheckForStraightFlush()
	;check for flush
	For iter = 2 To 5
		If Hand(1)\suit &lt;&gt; Hand(iter)\suit Then Return 
	Next
	;check for high card
	highcard = Hand(1)\value
	For iter = 2 To 5
		If Hand(iter)\value &gt; highcard Then highcard = Hand(iter)\value
	Next
	If highcard &lt; 5 Then Return
	;check for straight
	For thiscard = highcard To highcard - 4 Step -1
		For iter = 1 To 5
			If Hand(iter)\value = thiscard Then thisstraight = thisstraight + 1
		Next
		If thisstraight &lt;&gt; 1 Then Return
		thisstraight = 0
	Next
	Rank = 8
End Function

Function CheckForFourOfAKind()
	For compare = 1 To 5
		thiscard = Hand(compare)\value
		For iter = 1 To 4
			If thiscard = Hand((compare + iter) - 5 * ((compare + iter) &gt; 5))\value Then count = count + 1
		Next
		If count = 3
			Rank = 7
			Return 
		EndIf
		count = 0
	Next
End Function

Function CheckForFullHouse()
	Check(0) = 0
	Check(1) = 0
	For compare = 1 To 5
		thiscard = Hand(compare)\value
		For iter = 1 To 4
			If thiscard = Hand((compare + iter) - 5 * ((compare + iter) &gt; 5))\value
				count = count + 1
			Else
				Check(checkcount) = ((compare + iter) - 5 * ((compare + iter) &gt; 5))
				checkcount = checkcount + 1
			EndIf
		Next
		If count = 2
			threeOfAKind = True 
		EndIf
		count = 0
		checkcount = 0
		If threeOfAKind = True Then Exit 
	Next
	If Not threeOfAKind Then Return
	If Hand(Check(0))\value = Hand(Check(1))\value Then Rank = 6
End Function

Function CheckForFlush()
	;check for flush
	For iter = 2 To 5
		If Hand(1)\suit &lt;&gt; Hand(iter)\suit Then Return 
	Next
	Rank = 5
End Function

Function CheckForStraight()
	;check for ace in hand
	For iter = 1 To 5
		If Hand(iter)\value = 1 Then ace = ace + 1
	Next
	If ace &gt; 1 Then Return
	If ace = 1
		;check for ace through 10 straight
		For thiscard = 13 To 10 Step -1
			For iter = 1 To 5
				If Hand(iter)\value = thiscard Then thisstraight = thisstraight + 1
			Next
			If thisstraight &lt;&gt; 1 Then noHighStraight = True 
			thisstraight = 0
		Next
		If noHighStraight = False
			Rank = 4
			Return 
		EndIf 
	EndIf
	;no high straight so check for regular straight
	;check for high card
	highcard = Hand(1)\value
	For iter = 2 To 5
		If Hand(iter)\value &gt; highcard Then highcard = Hand(iter)\value
	Next
	If highcard &lt; 5 Then Return
	;check for straight
	For thiscard = highcard To highcard - 4 Step -1
		For iter = 1 To 5
			If Hand(iter)\value = thiscard Then thisstraight = thisstraight + 1
		Next
		If thisstraight &lt;&gt; 1 Then Return
		thisstraight = 0
	Next
	Rank = 4
End Function

Function CheckForThreeOfAKind()
	For compare = 1 To 5
		thiscard = Hand(compare)\value
		For iter = 1 To 4
			If thiscard = Hand((compare + iter) - 5 * ((compare + iter) &gt; 5))\value Then count = count + 1
		Next
		If count = 2
			Rank = 3
			Return 
		EndIf
		count = 0
	Next
End Function

Function CheckForTwoPair()
	Check(0) = 0
	Check(1) = 0
	Check(2) = 0
	For compare = 1 To 5
		thiscard = Hand(compare)\value
		For iter = 1 To 4
			If thiscard = Hand((compare + iter) - 5 * ((compare + iter) &gt; 5))\value
				count = count + 1
			Else
				Check(checkcount) = ((compare + iter) - 5 * ((compare + iter) &gt; 5))
				checkcount = checkcount + 1
			EndIf
		Next
		If count = 1
			pair = True 
		EndIf
		count = 0
		checkcount = 0
		If pair = True Then Exit 
	Next
	If Not pair Then Return
	If Hand(Check(0))\value = Hand(Check(1))\value Then Rank = 2
	If Hand(Check(1))\value = Hand(Check(2))\value Then Rank = 2
	If Hand(Check(2))\value = Hand(Check(0))\value Then Rank = 2
End Function

Function CheckForJacksOrBetter()
	For compare = 1 To 5
		thiscard = Hand(compare)\value
		For iter = 1 To 4
			If thiscard = Hand((compare + iter) - 5 * ((compare + iter) &gt; 5))\value
				If thiscard &gt; 10 And Hand((compare+iter)-5*((compare+iter)&gt;5))\value &gt; 10
					Rank = 1
					Return
				EndIf
				If thiscard = 1 And Hand((compare+iter)-5*((compare+iter)&gt;5))\value = 1
					Rank = 1
					Return
				EndIf
			EndIf
		Next
	Next
End Function
</pre> <br><br></td></tr></table><br>
<a name="472406"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rook Zimbabwe</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I can see how this could be modified to fit almost any puzzle instance game as well... Once match values have been set. Hmmm... lots to learn. Thanks WolRon! Thanks Ross C!<br>RZ <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
