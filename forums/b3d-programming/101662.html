<!DOCTYPE html><html lang="en" ><head ><title >How to color selected words in a sentence?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >How to color selected words in a sentence?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >How to color selected words in a sentence?</a><br><br>
<a name="1208267"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi all, I'm working on a part of my game, and was wondering how to color a single word by using some small text in a sentence like so: <br><br>"[c=$FF0000]my[/c] [c=#00FF00]colored[/c] [c=#0000FF]text[/c] here"<br><br>I've been trying for the last 10 hours to get this code working, but I can't...<br><br>If anyone could help, that would be GREAT!<br><br>Here's the code: <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Global mystring$="[c=$FF0000]This string[/c] [c=$00FF00]has five[/c] [c=$0000FF]words[/c]"

ColorWords(mystring$)

Color 255, 255, 255

Print
Print "The First word:"+Left(mystring$,Instr(mystring," ",1)-1)
Print "The Rest      :"+Mid$(mystring$,Instr(mystring," ",1)+1)

WaitKey

End

Function hex2dec%(hex_number$)
; Converts the supplied hex number into a decimal number
; If hex_number isn't a valid hex number, then returns -1
; written by Matt Burns (Blu_Matt / matt@...)
	Local the_hex$=Upper(Trim(hex_number$))		; the hex number
	Local base_power%=0							; the current base power
	Local base%=16								; the base to convert from
	Local the_dec%=0							; the decimal representation
	Local chars%=0
	Local hex_length%=Len(the_hex$)				; current length of the hex
	If Left(the_hex$,1)="$" Then 				; removes any leading "$"
		the_hex$=Right(the_hex$,hex_length%-1)
	EndIf
	hex_length%=Len(the_hex$)					; current length of the hex
	Local chars_left%=hex_length%				; current number of chars left
	Local hex_left$=the_hex$					; current hex left
	Repeat
		current_hex$=Right(hex_left$,1) 		; gets the current hex char
		If ((Asc(current_hex$)&gt;=Asc("0")) And (Asc(current_hex$)&lt;=Asc("9"))) Then
			hex_dec%=current_hex$				; digit 0-9
		ElseIf ((Asc(current_hex$)&gt;=Asc("A")) And (Asc(current_hex$)&lt;=Asc("F"))) Then
			hex_dec%=Asc(current_hex$)-55		; char A-F
		Else
			Return -1 							; found an illegal character, aborting...
		EndIf
		the_dec%=the_dec%+((base%^base_power%)*hex_dec%)	; add the local hex digit to the total
		base_power%=base_power%+1				; increase the base power
		chars_left%=chars_left%-1				; reduce the number of chars left
		hex_left$=Left(hex_left$,chars_left%)	; sets the remainder
	Until chars_left%=0
	Return the_dec%								; return the result
End Function

Function WordWrap(A$,X,Y,W,H,Leading=0)
;Display A$ starting at X,Y - no wider than W and no taller than H (all in pixels).
;Leading is optional extra vertical spacing in pixels
;To Do (if needed): force break if single word is too big to fit on line (currently function will hang if this happens)
	LinesShown=0
	Height=StringHeight(A$)+Leading
	While Len(A)&gt;0
		space=Instr(A$," ")
		If space=0 Then space=Len(A$)
		temp$=Left$(A$,space)
		trimmed$=Trim$(temp);we might ignore a final space
		extra=0;we haven't ignored it yet
		;ignore final space if doing so would make a word fit at end of line:
		If (StringWidth (b$+temp$)&gt;W) And (StringWidth (b$+trimmed$)&lt;=W) Then temp=trimmed:extra=1
		If StringWidth (b$+temp$)&gt;W Then;too big, so print what will fit
			Text X,LinesShown * Height + Y,b$
			LinesShown=LinesShown+1
			b$=""
		Else;append it to B$ (which will eventually be printed) and remove it from A$
			b$=b$+temp$
			A$=Right$(A$,Len(A$)-(Len(temp$)+extra))
		EndIf
		If ((LinesShown+1)*Height)&gt;H Then Exit;the next line would be too tall, so leave
	Wend
	If (b$&lt;&gt;"")And((LinesShown+1)&lt;=H) Then Text X,LinesShown*Height+Y,b$;print any remaining text if it'll fit vertically
End Function

Function ColorWords(txt$)
	If Instr(mystring$, "[c=")
		r$ = Mid$(mystring, 4, 7)
		Color BP_GetIntRed(Int(r$)), 0, 0
	ElseIf Right$(mystring$, 4)
		Color 255, 255, 255
	EndIf
End Function</textarea><br><br>Thank you all so kindly! <br><br></td></tr></table><br>
<a name="1208270"></a>

<a name="1208271"></a>

<a name="1208272"></a>

<a name="1208273"></a>

<a name="1208274"></a>

<a name="1208282"></a>

<a name="1208283"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> instead of drawing the complete sentence with one TEXT() command , you have to devide the sentence into parts and draw the parts with a lot of TEXT() commands. <br><br>Here is an executable sample, that demonstrates the dividing into parts:<br><br><pre class=code>Graphics 800,600
T$="Abc[c=1]def[/c]ghi"
Cls
DrawColorText T
Flip 0
WaitKey


Function DrawColorText(t$)
	Print " step:     raw string:              part text:"
	Print "-----------------------------------------------"
	Text X,380,"final text:"
	Repeat 
		i=i+1
		Print "   " + i + "       " + t

		markA=Instr(t,"[",1)

		If markA=1
			; color command is next:
			markB=Instr(t,"]",markA)
			ChangeColor Mid(t,markA+1,markB-markA-1)
			t=Mid(t,markB+1)
			
		ElseIf markA&gt;0
			; text part is next:
			part$=Left(t,markA-1)
			Text 300,i*20, Part
			Text X,400,part
			x=x+StringWidth(part)
			t=Mid(t,markA)

		Else
			; rest of the text
			Text 300,i*20, t
			Text X,400,t
			t=""
		EndIf
		
		
	Until t=""

End Function



Function ChangeColor(Col$)
	Print "            --&gt;" + Col
	If Col="c=1"
		Color 255,0,0
	ElseIf Col="/c"
		Color 255,255,255
	EndIf

End Function
</pre> <br><br></td></tr></table><br>
<a name="1208293"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, Midimaster. Thank you very much for your help! I just wanted to know one thing though? Why is "[/c]" showing up in the sentence? Shouldn't that mark the end of the colored word or letter, thereby having it disappear?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics 800,600,0,2
T$="Abc[c=1]def[/c]ghi"
Cls
DrawColorText T
Flip
WaitKey

Function DrawColorText(t$)
	Print " step:     raw string:              part text:"
	Print "-----------------------------------------------"
	Text X,380,"final text:"
	Repeat 
		i=i+1
		Print "   " + i + "       " + t
		
		markA=Instr(t,"[",1)
		
		If markA=1
			; color command is next:
			markB=Instr(t,"]",markA)
			ChangeColor Mid(t,markA+1,markB-markA-1)
			t=Mid(t,markB+1)
			
		ElseIf markA&gt;0
			; text part is next:
			part$=Left(t,markA-1)
			Text 300,i*20, Part
			Text X,400,part
			x=x+StringWidth(part)
			t=Mid(t,markA)
			
		Else
			; rest of the text
			Text 300,i*20, t
			Text X,400,t
			t=""
		EndIf
		
		
	Until t=""
	
End Function

Function ChangeColor(Col$)
	Print "            --&gt;" + Col
	If Col="c=1"
		Color 255,0,0
	ElseIf Col="/c"
		Color 255,255,255
	EndIf
	
End Function</textarea><br><br>Thanks again! <br><br></td></tr></table><br>
<a name="1208297"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> When I start the exe I get a real perfect output: The main output is the sentence at the koordinates 0,400 (left down corrner below "final text:").<br><br>I added the other printouts only for a better understanding, how the algo works....<br><br>Can you send a screenshot of what you are seeing? <br><br></td></tr></table><br>
<a name="1208299"></a>

<a name="1208303"></a>

<a name="1208305"></a>

<a name="1208306"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> EDIT: Nvm, figured it out! =D<br><br>Thanks again, Midimaster! You are amazing! :D<br><br>For those of whom wish to have the fix, here it is!<br><br>EDIT 2: This code has been fixed to include the colors of the rainbow + 3 extra :P<br><br>ColoredWords.bb: <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 800,600,0,2

	T$="[c=1]A[/c][c=2]b[/c][c=3]c[c=4]d[/c][c=5]e[/c][c=6]f[/c][c=7]g[c=8]h[/c][c=9]i[/c]"
	
	While Not KeyHit(1)
		
			UpdateWorld()
			RenderWorld()
			DrawColorText 0, 0, T
			
			Flip 0
			
	Wend
	
End

Function DrawColorText(offsetx#, offsety#, t$)
	Repeat
		i=i+1
		markA=Instr(t,"[",1)
		
		If markA=1
			; color command is next:
			markB=Instr(t,"]",markA)
			ChangeColor Mid(t,markA+1,markB-markA-1)
			t=Mid(t,markB+1)
		ElseIf markA&gt;0
			;Text part is Next:
			part$=Left(t,markA-1)
			Text offsetx#+X,offsety#,part
			x=x+StringWidth(part)
			t=Mid(t,markA)
			
		Else
			; rest of the text
			Text offsetx#+X,offsety#,t
			t=""
		EndIf
		
		
	Until t=""
	
End Function



Function ChangeColor(Col$)
	;Red
	If Col="c=1" Then Color 255,0,0
	;Green
	If Col="c=2" Then Color 0, 255, 0
	;Blue
	If Col="c=3" Then Color 0, 0, 255
	;Yellow
	If Col="c=4" Then Color 255, 255, 0
	;Purple
	If Col="c=5" Then Color 255, 0, 255
	;Orange
	If Col="c=6" Then Color 64, 128, 0
	;Indigo
	If Col="c=7" Then Color 0, 0, 128
	;Royal Light Purple
	If Col="c=8" Then Color 102, 102, 255
	;Royal Light Blue
	If Col="c=9" Then Color 68, 207, 252
	;Default - White
	If Col="/c" Then Color 255,255,255
End Function</textarea> <br><br></td></tr></table><br>
<a name="1208319"></a>

<a name="1208320"></a>

<a name="1208321"></a>

<a name="1208322"></a>

<a name="1208323"></a>

<a name="1208324"></a>

<a name="1208325"></a>

<a name="1208326"></a>

<a name="1208327"></a>

<a name="1208328"></a>

<a name="1208329"></a>

<a name="1208330"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I appreciate you're trying to incorporate some sort of html here but it would be rediculously fiddly to create the colours for each string.  What I do is have a text string and a colour string both of the same length.  Simple to write and simple to handle.<br><br>e.g.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,0,2

	Global T$ = "ABCDEFGHI THIS TEXT IS WHITE, THIS TEXT IS CYAN"
	Global C$ = "123456789ZZZZZZZZZZZZZZZZZZZZ$$$$$$$$$$$$$$$$$$"

	While Not KeyHit(1)
		
		UpdateWorld()
		RenderWorld()
		DrawColorText 0, 0, T, C
			
		Flip False
			
	Wend
	
End

Function DrawColorText(offsetx#, offsety#, t$, c$)

	Local L, CH$, CO$, Length, X
	
	Length = Len( t$ )
	For L = 1 To Length
		CH$ = Mid$(t$,L,1)
		CO$ = Mid$(c$,L,1) 
		ChangeColor( CO )
		Text offsetx + X, offsety, CH$
		X = X + StringWidth( CH$ )
	Next
	
End Function


Function ChangeColor(Col$)

	Select Col
		Case "1" Color 255,0,0 			;red
		Case "2" Color 0, 255, 0		;green
		Case "3" Color 0, 0, 255		;blue
		Case "4" Color 255, 255, 0		;yellow
		Case "5" Color 255, 0, 255		;purple
		Case "6" Color 64, 128, 0		;orange
		Case "7" Color 0, 0, 128		;indigo
		Case "8" Color 102, 102, 255	;royal light purple
		Case "9" Color 68, 207, 252		;royal light blue
		Case "Z" Color 255,255,255		;white
		Case "$" Color 0,255,255		;CYAN
	End Select

End Function
</textarea><br><br>Jesus .... took me about 10 goes to get this code in a box ... <br><br></td></tr></table><br>
<a name="1208333"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ooooooooo, I LIKE this! Thanks to both of you! :D <br><br></td></tr></table><br>
<a name="1208520"></a>

<a name="1208521"></a>

<a name="1208522"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skidracer</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> that adds another dimension to text, <br><br>that is way cool Stevie<br>.........***.............. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
