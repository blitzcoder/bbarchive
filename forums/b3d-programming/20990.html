<!DOCTYPE html><html lang="en" ><head ><title >Platform Collision Test  with Pushable Objects</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Platform Collision Test  with Pushable Objects</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Platform Collision Test  with Pushable Objects</a><br><br>
<a name="215374"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Physt</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I added pushable objects.  It's almost right however you can sometime get stuck in moveable objects or at times quantum tunnel right through them. :(  Has to be a simple fix.  Any suggestions?<br><br><br>Arrow keys = move<br>left ctrl = jump<br>+ and - change viewport size<br><br><pre class=code>
; platformer collision test
; Kenneth "Physt" Lemieux
; kenlem@...
; No Rights Reserved

; Left Ctrl Key = jump 
; Arrow Keys = move 

;Include "viewport.bb"

Global viewportSize# = 1.0

; DEFINES
Global GRAVITY# = -0.0108
Global FLOOR_FRICTION# = .7
Global camera


Type actor
	Field mesh
	Field x#
	Field y#
	Field z#
	Field xvel#
	Field yvel#
	Field zvel#
	Field speed#
	Field isOnground#
	Field isOnObject#
End Type

frameTimer=CreateTimer(30)



WaitTimer(frameTimer) 




Global jumpheight# = 0.35
Global speed# = 0.07


Const numplatforms=6

Graphics3D 640,480,16,1

Gosub InitGame
Gosub GameLoop

End


; *********
.InitGame
; *********
frametimer = CreateTimer(60)

light=CreateLight() 
RotateEntity light,45,45,0
camera=CreateCamera() 
;SetCameraFOV(camera, 90) 
PositionEntity camera, 10, 15, 10
player = CreateCylinder(32)
FitMesh player, -1, -1, -1, 2, 2, 2, False

grid1=CreateGridTexture(32,64,128)
grid2=CreateGridTexture(240,72,20)
SetBuffer BackBuffer()


level = CreateCube() : EntityTexture level, grid1
FitMesh level, -40, -40, -40, 80, 80, 80, True 
FlipMesh level

Dim platform(numplatforms)
platform(1)=CreatePlatform(-20, -25 , 0, 10,1,40 , grid2)
platform(2)=CreatePlatform(-10,-30, 10,10,1,10 , grid2)
platform(3)=CreatePlatform(-5,-40, 15,10,5,10 , grid2)
platform(4)=CreatePlatform(-16,-20,  7,10,1,10 , grid2)
platform(5)=CreatePlatform(-16,-15, 15,5,1,5 , grid2)
platform(6)=CreatePlatform(6, -40, 12,5,20,5 , grid2)

EntityType player,1
EntityType level, 2
For d=1 To numplatforms
	EntityType platform(d), 2
Next
Collisions 1, 2, 2, 2
Collisions 1, 1, 3, 2


playerActor.actor = New actor
playerActor\Mesh = player
playerActor\x# = 10
playerActor\y# = -20
playerActor\z# = 10
playerActor\speed# = 0.07 



AddCan(-13, -11, 17)
AddCan(-11, -11, 17)
AddCan(-10, -11, 17)
AddCan(-9, -11, 17)
AddCan(-8, -11, 17)
AddCan(8, 20, 14.6)

AddCan(13, -11, 17)
AddCan(11, -11, 17)
AddCan(10, -11, 17)
AddCan(9, -11, 17)
AddCan(8, -11, 17)
AddCan(8, 20, -14.6)


Return

.GameLoop

While Not KeyDown(1)
	WaitTimer(frameTimer) 
	AdjustViewport()
	ControlActor(playerActor)
	UpdateAllActorCollisions(playerActor)
	UpdateAllActors()
	UpdateWorld()
	PostUpdateAllActors()
	FollowCam(camera, player)
	
	RenderWorld
	;Text 10,10, EntityX(playerActor\mesh) + ", " + EntityY(playerActor\mesh) + ", " + EntityZ(playerActor\mesh)
	Flip
Wend
Return

Function adjustviewport()

		If KeyHit(74) Then
			ShrinkViewport(camera)
		End If 
	
		If KeyHit(78) Then
			EnlargeViewport(camera)
		End If 
End Function 

Function UpdateAllActors()

	For a.actor = Each actor
		a\yvel# = a\yvel# + GRAVITY#
		a\x# = a\x# + a\xvel#
		a\y# = a\y# + a\yvel# 
		a\z# = a\z# + a\zvel#
		a\xvel# = a\xvel# * FLOOR_FRICTION#
		a\zvel# = a\zvel# * FLOOR_FRICTION#
		PositionEntity a\mesh, a\x#, a\y#, a\z#
	Next

End Function 

; reset actor postions based on collision results. called after UpdateWorld()
Function PostUpdateAllActors()

	For a.actor = Each actor
		a\x# = EntityX(a\mesh)
		a\y# = EntityY(a\mesh)
		a\z# = EntityZ(a\mesh)
		;PositionEntity a\mesh, a\x#, a\y#, a\z#
	Next

End Function 

Function CreateGridTexture(r,g,b)
	tex=CreateTexture(64,64,9)
	SetBuffer TextureBuffer(tex)
	Color r,g,b : Rect 0, 0, 64,64, 1
	Color 255,255,255 : Rect 0, 0, 64,64, 0
	ScaleTexture tex, .1, .1
	Return tex
End Function

Function CreatePlatform(x#,y#,z#,w#,h#,d#,tex)
	plat=CreateCube()
	FitMesh plat, x, y, z, w, h, d, False
	EntityTexture plat, tex
	Return plat
End Function

Function ControlActor(a.actor)

	dx=KeyDown(203)-KeyDown(205) ; left / right cursors
	dy=KeyDown(208)-KeyDown(200) ; up / down cursors
	TurnEntity a\mesh, 0, dx*3, 0
	
	TFormVector 0, 0, dy, a\mesh, 0
	a\xvel# = a\xvel# - (TFormedX() * a\speed#)
	a\yvel# = a\yvel# - (TFormedY() * a\speed#)
	a\zvel# = a\zvel# - (TFormedZ() * a\speed#)
	
	If a\IsOnground And KeyDown(29) Then 
		a\yvel# = a\yvel# + jumpheight#	
	End If
	
End Function 


Function UpdateAllActorCollisions(playerActor.actor)

	For a.actor = Each actor
		collide = CountCollisions (a\mesh)
		a\isOnground = False
		a\isOnObject = False
		If collide &gt; 0
			For i = 1 To collide
				entity = CollisionEntity(a\mesh, i) 
				et = GetEntityType(entity)
				nx# = CollisionNX(a\mesh,  i)
				ny# = CollisionNY(a\mesh,  i)
				nz# = CollisionNZ(a\mesh,  i)
				If ny# = 1
					a\yvel# = 0
					a\yvel# = a\yvel# - GRAVITY#
					a\isOnground = True
					If et = 1 Then 	
						a\isOnObject = True
					End If 
				End If
				If ny# = -1 Then 
					a\yvel# = -a\yvel# * .5
				End If 
								
				entity = CollisionEntity(a\mesh, i) 
				If playerActor\isOnground = True And entity &lt;&gt; playerActor\mesh Then 
					If et = 1 Then 
					 	AddPlayerVelocityToActor(playerActor, entity)
					End If 
				End If 
				
			Next 
			
			
		End If 
	Next 


End Function 


Function FollowCam(camera, target)
	PositionEntity camera, EntityX(target), EntityY(target)+2.5, EntityZ(target)
	RotateEntity camera ,EntityPitch(target), EntityYaw(target)+4, EntityRoll(target)
	MoveEntity camera, 0, 0, -5.8
	PointEntity camera, target
End Function 


Function AddPlayerVelocityToActor(playerActor.actor, entity)


	; find the actor in the actor types with the entity that matches this one
	e.actor = First actor
	done = False
	While e &lt;&gt; Null And done = False
		If e\mesh = entity Then 
			If playerActor\isOnObject = False Then 
				e\xvel# = playerActor\xvel#
				e\zvel# = playerActor\zvel#
				done = True
			End If
		End If 
		e.actor = After(e)
	Wend 
	

End Function 


Function AddCan(argX#, argY#, argZ#)

	can.actor = New actor
	;can\Mesh = CreateCylinder(32)
	can\Mesh = CreateCube()
	FitMesh can\Mesh, -1, -1, -1, 2, 2, 2, False
	EntityType can\Mesh,1
	can\x# = argX#
	can\y# = argY#
	can\z# = argZ#

End Function 




Function ShrinkViewport(camera)
	If viewportSize#&gt; .1 Then
		viewportSize#= viewportSize#- .1
		SetCameraViewport(camera)
	End If 
	

End Function


Function EnlargeViewport(camera)

	If viewportSize#&lt; 1 Then
		viewportSize#= viewportSize#+ .1
		SetCameraViewport(camera)
	End If 
	
End Function 


Function SetCameraViewport(camera)

	; clear the screen
	
	width = GraphicsWidth()
	height = GraphicsHeight()
	newWidth = Int(GraphicsWidth() * viewportSize#)
	newHeight = Int(GraphicsHeight() * viewportSize#)
	xoffset = (width/2) - (newWidth/2) 
	yoffset = (height/2) - (newHeight/2)
	
	Color 32, 32, 32
	Flip
	Rect 0, 0, width, yoffset 
	Rect 0, yoffset+newHeight, width, height 
	Rect 0, yoffset, xoffset, newHeight
	Rect xoffset+newWidth, yoffset, width, newHeight
	Flip
	Rect 0, 0, width, yoffset 
	Rect 0, yoffset+newHeight, width, height 
	Rect 0, yoffset, xoffset, newHeight
	Rect xoffset+newWidth, yoffset, width, newHeight	

	CameraViewport camera, xoffset, yoffset, newWidth , newHeight	

End Function 

Function SetCameraFOV(Camera, FOV#) 
	CameraZoom Camera, 1.0 / Tan(FOV#/2.0) 
End Function 

</pre> <br><br></td></tr></table><br>
<a name="806319"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tin-cat</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> dig dig dig...     &gt;.&gt;<br><br><br>&lt;.&lt; <br><br></td></tr></table><br>
<a name="806329"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Can you stop bumping 4 year old threads? <br><br></td></tr></table><br>
<a name="806350"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tin-cat</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> *sigh*<br>forums make me sad.. So much useful information gets lost and eventually expires. <br><br></td></tr></table><br>
<a name="806677"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Vertigo</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I agree with Tin-Cat... there really should be a better way of archiving "useful" threads... Such as maybe a digg system or something on the forums where users can mark things as important? <br><br></td></tr></table><br>
<a name="953997"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Expires how? They don't eventually get deleted do they? <br><br></td></tr></table><br>
<a name="953999"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gabriel</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think this is still relevant:<br><div class="quote"> Can you stop bumping 4 year old threads?  <br></div><br>Except that this thread is now five years old. <br><br></td></tr></table><br>
<a name="954003"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> So what if it's old if it could help someone. <br><br></td></tr></table><br>
<a name="954006"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Gabriel</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> So what if it's old if it could help someone.  <br></div><br>Every thread in the entire forum MIGHT help someone, but you can't bump them all constantly just in case. If someone needs help, they'll find it with the search function, read it and learn what they need. Bumping five year-old threads for no reason is just rude to the people who currently have an issue they want help with. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
