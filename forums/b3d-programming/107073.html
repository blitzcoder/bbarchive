<!DOCTYPE html><html lang="en" ><head ><title >Mmove Entity Mouse?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Mmove Entity Mouse?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Mmove Entity Mouse?</a><br><br>
<a name="1323491"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <img src="http://i68.tinypic.com/90nltd.png"><br><br><br>Thanks to all who help I managed to put buckets and manipulate their properties position by a properties window. Now I would like to know how I can change those data, its position on the scene with a mouse drag. How can I do that?<br><br>In modeling programs is that if they go difentes vectors of colors and if I drag one entity moves in the direction of the axis both positive and negative. Any suggestions? <br><br></td></tr></table><br>
<a name="1323545"></a>

<a name="1323546"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Flanker</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Create meshes for axis that you position at your entity position and make them pickable. Just a test but there might be more mathematical solution for this :<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">SeedRnd MilliSecs()

Graphics3D 800,600,32,2
SetBuffer BackBuffer()

camera = CreateCamera()
CameraRange camera,0.1,1000
CameraClsColor camera,60,50,50

light = CreateLight()
RotateEntity light,70,30,0

For i = 1 To 10
	cube = CreateCube()
	EntityColor cube,Rand(255),Rand(255),Rand(255)
	PositionEntity cube,Rnd(20)-10,Rnd(20)-10,Rnd(20)-10
	EntityPickMode cube,2
Next

pivot = CreatePivot()

Xaxis = CreateCylinder()
ScaleEntity Xaxis,0.05,2,0.05
EntityColor Xaxis,255,0,0
RotateEntity Xaxis,0,0,-90
MoveEntity Xaxis,0,2,0
EntityParent Xaxis,pivot
EntityPickMode Xaxis,2
EntityOrder Xaxis,-1
EntityFX Xaxis,1

Yaxis = CreateCylinder()
ScaleEntity Yaxis,0.05,2,0.05
EntityColor Yaxis,0,255,0
MoveEntity Yaxis,0,2,0
EntityParent Yaxis,pivot
EntityPickMode Yaxis,2
EntityOrder Yaxis,-1
EntityFX Yaxis,1

Zaxis = CreateCylinder()
ScaleEntity Zaxis,0.05,2,0.05
EntityColor Zaxis,0,0,255
RotateEntity Zaxis,90,0,0
MoveEntity Zaxis,0,2,0
EntityParent Zaxis,pivot
EntityPickMode Zaxis,2
EntityOrder Zaxis,-1
EntityFX Zaxis,1

HideEntity pivot


While Not KeyHit(1)

	If MouseHit(2)
		temp = MouseXSpeed()
		temp = MouseYSpeed()
		temp = MouseZSpeed()
		mouse_X = MouseX()
		mouse_Y = MouseY()
	ElseIf MouseDown(2)
		msXspd# = MouseXSpeed()
		msYspd# = MouseYSpeed()
		
		camPitch# = camPitch + msYspd/10
		camYaw# = camYaw - msXspd/10
	
		If camPitch &gt; 90 Then camPitch = 90
		If camPitch &lt; -90 Then camPitch = -90
		
		RotateEntity camera,camPitch,camYaw,0
		
		MoveMouse mouse_X,mouse_Y
		
		MoveEntity camera,0,0,MouseZSpeed()
		
		FlushMouse()
		
	ElseIf MouseHit(1)
	
		CameraPick camera,MouseX(),MouseY()
		
		If PickedEntity()
			If PickedEntity() = Xaxis Or PickedEntity() = Yaxis Or PickedEntity() = Zaxis
				temp = MouseXSpeed()
				temp = MouseYSpeed()
				temp = MouseZSpeed()
			Else
				ShowEntity pivot
				PositionEntity pivot,EntityX(PickedEntity()),EntityY(PickedEntity()),EntityZ(PickedEntity())
				entity = PickedEntity()
			EndIf
		Else
			HideEntity pivot
		EndIf	
	ElseIf MouseDown(1)
		
		If PickedEntity() = Xaxis
			distance# = EntityDistance(camera,entity)/450
			If EntityZ(camera) &lt; EntityZ(entity)
				MoveEntity pivot,Float(MouseXSpeed())*distance,0,0
			Else
				MoveEntity pivot,Float(-MouseXSpeed())*distance,0,0
			EndIf
			PositionEntity entity,EntityX(pivot),EntityY(pivot),EntityZ(pivot)
		ElseIf PickedEntity() = Yaxis
			distance# = EntityDistance(camera,entity)/450
			MoveEntity pivot,0,Float(-MouseYSpeed())*distance,0
			PositionEntity entity,EntityX(pivot),EntityY(pivot),EntityZ(pivot)
		ElseIf PickedEntity() = Zaxis
			distance# = EntityDistance(camera,entity)/450
			If EntityX(camera) &lt; EntityX(entity)
				MoveEntity pivot,0,0,Float(-MouseXSpeed())*distance
			Else
				MoveEntity pivot,0,0,Float(MouseXSpeed())*distance
			EndIf
			PositionEntity entity,EntityX(pivot),EntityY(pivot),EntityZ(pivot)
		EndIf
		
	EndIf
	
	ScaleEntity pivot,EntityDistance(pivot,camera)/10,EntityDistance(pivot,camera)/10,EntityDistance(pivot,camera)/10


	RenderWorld()
	
	Text 10,10,"Right mouse for freelook, mousewheel to move camera, left mouse to pick object and drag"
	
	Flip

Wend

End</textarea> <br><br></td></tr></table><br>
<a name="1323547"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try this:<br><br><a href="/codearcs/codearcs.php?code=379" >2D-to-3D entity positioning (or something)</a> <br><br></td></tr></table><br>
<a name="1323554"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Thanks You Guys. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
