<!DOCTYPE html><html lang="en" ><head ><title >Rotating Entity Child to another Entity Child</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Rotating Entity Child to another Entity Child</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Rotating Entity Child to another Entity Child</a><br><br>
<a name="1225302"></a>

<a name="1225316"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Spike314</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi All<br><br>I'm trying to line up prefab entities with each other but it doesn't work all the time.<br><br>Each entity have children which are the points that require to be lined up by rotating the main entity only this is want I'm trying to achieve <a href="http://gamedevelopment.tutsplus.com/tutorials/bake-your-own-3d-dungeons-with-procedural-recipes--gamedev-14360" target="_blank">Bake Your Own 3D Dungeons With Procedural Recipes</a><br><br>This is the code I have to position and rotate the entity<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
	If New_Building= True
		B.Building = New Building

		B\M=CopyEntity(Junction(Rand(0,1)))	
		B\Typ = TYP_JUNCTION
		GetExits(B)

	Else
		B.Building =Last Building

		Loop_Count=Loop_Count+1
		
	EndIf

		If Loop_Count=&gt;Max_loop Then Return
		
	For E=0 To B\Exit_Count-1

		If B\Exits_Used[E]=0

			yaw# = EntityYaw(B\Exits[E],1)
			x# = EntityX(B\Exits[E],1)
			y# = EntityY(B\Exits[E],1)
			z# = EntityZ(B\Exits[E],1)
			
			Select B\Typ
				Case TYP_JUNCTION

					NB.Building = New Building					
					NB\M=CopyEntity(Corridor(Rand(0,2)))									
					NB\Typ = TYP_CORRIDOR
					
				Case TYP_ROOM
				
				Case TYP_CORRIDOR

					NB.Building = New Building					
					NB\M=CopyEntity(Junction(Rand(0,1)))									
					NB\Typ = TYP_JUNCTION
				
			End Select

			GetExits(NB)
			ME = SelectExit(NB)			
			nyaw# = EntityYaw(NB\Exits[ME],1)
			nx# = EntityX(NB\Exits[ME],1)
			ny# = EntityY(NB\Exits[ME],1)
			nz# = EntityZ(NB\Exits[ME],1)
;THIS WERE THE PROBLEM IS
			TFormVector nx,ny,nz,0,B\Exits[E]		;&lt;&lt;			
			AlignToVector NB\M,TFormedX(),0,TFormedZ(),1,1;&lt;&lt;
			TurnEntity NB\M,0,DeltaYaw(NB\Exits[ME],NB\M)-90,0					;&lt;&lt;
					
			PositionEntity NB\M,x,y,z
			MoveEntity NB\M,-nx,-ny,-nz
					
			B\Exits_Used[E] = True
			NB\Exits_Used[ME] = True
			EntityParent NB\M,B\M
		;DEBUG INFO
			R=Rand(0,255):G=Rand(0,255):BL=Rand(0,255)
			EntityColor GetChild(B\Exits[E],1),R,G,BL
			EntityColor GetChild(NB\Exits[ME],1),R,G,BL			

			GenBuilding()
		EndIf
		
	Next
</textarea><br><br>OK Ive converted some of the code on the page above but the angles still are not right.<br>Here is the code.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function MatchExits(oldExit,newExit)
;		var newModule = newExit.transform.parent;
		newModule = GetParent(newExit)
;		var forwardVectorToMatch = -oldExit.transform.forward;
		forwardVectorToMatch = -EntityZ(oldExit,1)
;		var correctiveRotation = Azimuth(forwardVectorToMatch) - Azimuth(newExit.transform.forward);
		correctiveRotation# = Azimuth(EntityX(OldExit,1),EntityY(OldExit,1),forwardVectorToMatch)-Azimuth(EntityX(newExit,1),EntityY(newExit,1),EntityZ(newExit,1))						
;		newModule.RotateAround(newExit.transform.position, Vector3.up, correctiveRotation);
		RotateEntity newModule,0,correctiveRotation,0
;		var correctiveTranslation = oldExit.transform.position - newExit.transform.position;
		correctiveTranslationX# = EntityX(OldExit,1) - EntityX(newExit,1)
		correctiveTranslationY# = EntityY(OldExit,1) - EntityY(newExit,1)
		correctiveTranslationZ# = EntityZ(OldExit,1) - EntityZ(newExit,1)
;		newModule.transform.position += correctiveTranslation;
		PositionEntity newModule,correctiveTranslationX,correctiveTranslationY,correctiveTranslationZ
End Function

Function Azimuth#(x#,y#,z#)
		;Vector3.Angle(Vector3.forward, vector) * Mathf.Sign(vector.x);
		Return DotAngleBetween(TVector(0,0,1),TVector(x,y,z))*Sgn(z)
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1225319"></a>

<a name="1225320"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's difficult to grasp without a visualisation. Please post one or more screenshots. <br><br></td></tr></table><br>
<a name="1225335"></a>

<a name="1225342"></a>

<a name="1225343"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Spike314</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is one using the top code.<br><img src="http://s29.postimg.org/9mllmb7dj/GEn_Shot.jpg"><br>The color cubes show the entities that are joined but the angle on most of them are wrong.<br><br>here is another one using the same code and most of them are fine and lined up correctly<br><br><img src="http://s14.postimg.org/g25pseqch/GEn_Shot2.jpg"> <br><br></td></tr></table><br>
<a name="1225364"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you. It's easier to understand now.<br><br>It seems the problem lies with the alignment. After some reflecting, the world-space yaw rotation of an exit of a module is the world-space yaw rotation of the module plus the <i>local</i> yaw rotation that the exit has in relation to the module.<br>Therefore, to make the exit be aligned to some specific world-space yaw rotation, first we need to calculate that relative rotation of the exit and then rotate the module so it makes that exit align to what we want. <br>Then you can move the module to the target exit and offset the module by a certain amount so one exit is placed on the other.<br><br>If I'm not mistaken, the code should be the following:<br><pre class=code>;Consider module "A" and module "B."
;We want to move and align a certain exit of module A with a certain exit of module B.

;First, rotate the module A so that the exit from this module is 
;aligned (in the opposite direction) to the target exit from module B, in the yaw axis.
Local targetExitYaw# = EntityYaw( exitB, True ) + 180 ;World space, and opposite direction.
Local newModuleYaw# = EntityYaw( moduleA, True ) + ( targetExitYaw - EntityYaw( exitA, True ) )
RotateEntity( moduleA, 0, newModuleYaw, 0, True )

;Second, move the module A and offset it so that the exit from this module is on top of the target exit of module B.
PositionEntity( moduleA, EntityX( exitB, True ), EntityY( exitB, True ), EntityZ( exitB, True ), True )
Local offsetX# = EntityX( moduleA, True ) - EntityX( exitB, True )
Local offsetY# = EntityY( moduleA, True ) - EntityY( exitB, True )
Local offsetZ# = EntityZ( moduleA, True ) - EntityZ( exitB, True )
TranslateEntity( moduleA, offsetX, offsetY, offsetZ, True )</pre> <br><br></td></tr></table><br>
<a name="1225385"></a>

<a name="1225389"></a>

<a name="1225390"></a>

<a name="1225395"></a>

<a name="1225399"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Spike314</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> After changing this line<pre class=code>PositionEntity( moduleA, EntityX( exitB, True ), EntityY( exitB, True ), EntityZ( exitB, True ), True )</pre> to this <pre class=code>PositionEntity( moduleA, EntityX( exitA, True ), EntityY( exitA, True ), EntityZ( exitA, True ), True )</pre> I still get the same result maybe my prefab meshes are are wrong as each exit has its own yaw and always points out.<br><br>Like so<br><pre class=code>
     0   
-90 -|- 90
    180
</pre><br>I will check all my prefab meshes to make sure all the angles a correct.<br><br>Ive checked all my Prefab meshes and all the angles on them are correct so its has to be code.<br><br>Ive uploaded my code and media files <a href="http://www8.zippyshare.com/v/52255527/file.html" target="_blank">HERE</a> <br><br></td></tr></table><br>
<a name="1225580"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Spike314</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ive re-write the code it works better but still get the same problem if anyone can help<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Global FPS_Oldtime, FPS_Newtime, FPS_Ticks
Global FPS_Current,FPS_Final
Global FPS_SampleRate   = 5  ;Take a sample every N ticks
Global FPS_Samples      = 10 ;Samples to average (res of the average)
Global FPS_BufferIndex  = 1
Global FPS_Font
Dim FPS_Buffer(10)


Const TYP_JUNCTION	= 1
Const TYP_ROOM 		= 2
Const TYP_CORRIDOR	= 3

Dim Junction(2)
Dim Corridor(3)
Dim Room(2)

Graphics3D 800,600,32,2

SeedRnd(999)

Junction(0) = LoadAnimMesh("Media\Junction01.b3d"):HideEntity Junction(0)
Junction(1) = LoadAnimMesh("Media\Junction02.b3d"):HideEntity Junction(1)
Corridor(0) = LoadAnimMesh("Media\Corridor01.b3d"):HideEntity Corridor(0)
Corridor(1) = LoadAnimMesh("Media\Corridor02.b3d"):HideEntity Corridor(1)
Corridor(2) = LoadAnimMesh("Media\Corridor03.b3d"):HideEntity Corridor(2)
Room(0)= LoadAnimMesh("Media\Room01.b3d"):HideEntity Room(0)
Room(1) = LoadAnimMesh("Media\Room02.b3d"):HideEntity Room(1)

Global Max_loop=10
Global Loop_Count=0


cam=CreateCamera()

l=CreateLight()

;GenBuilding(True)

NewGen()

Repeat

If KeyDown(200) MoveEntity Cam,0,0,1
If KeyDown(208) MoveEntity Cam,0,0,-1
If KeyDown(205) MoveEntity Cam,1,0,0
If KeyDown(203) MoveEntity Cam,-1,0,0

If MouseDown(2)
	TurnEntity Cam,MouseYSpeed()/4,-MouseXSpeed()/4,0
	MoveMouse GraphicsWidth()/2,GraphicsHeight()/2
EndIf



	UpdateWorld	
	RenderWorld
	Get_FPS()
	Flip

Until KeyHit(1)

Type Building
	Field M
	Field Typ
	Field Exits[10]
	Field Exits_Used[10]
	Field Exit_Count
End Type

Function NewGen()
	B.Building = First Building

	If B=Null
		B.Building = New Building

		B\M=CopyEntity(Junction(Rand(0,1)))	
		B\Typ = TYP_JUNCTION
		GetExits(B)			
	EndIf
	
	For B.Building=Each Building
		For E=0 To B\Exit_Count-1
			If B\Exits_Used[E]=0

				Select B\Typ
					Case TYP_JUNCTION
	
						NB.Building = New Building					
						NB\M=CopyEntity(Corridor(Rand(0,2)))									
						NB\Typ = TYP_CORRIDOR
						
					Case TYP_ROOM

						NB.Building = New Building
						NB\M=CopyEntity(Corridor(Rand(0,2)))									
						NB\Typ = TYP_CORRIDOR
						
					Case TYP_CORRIDOR
						NB.Building = New Building					

						Choose = Rand(1,2)
						If Choose = 1
							NB\M=CopyEntity(Room(Rand(0,1)))									
							NB\Typ = TYP_ROOM						
						Else								
							NB\M=CopyEntity(Junction(Rand(0,1)))									
							NB\Typ = TYP_JUNCTION
						EndIf
					
				End Select
			
				GetExits(NB)
				ME = SelectExit(NB)
				
				MatchExits(B\Exits[E],NB\Exits[ME])
				
				B\Exits_Used[E] = True
				NB\Exits_Used[ME] = True
				EntityParent NB\M,B\M
			;DEBUG INFO
				R=Rand(0,255):G=Rand(0,255):BL=Rand(0,255)
				EntityColor GetChild(B\Exits[E],1),R,G,BL
				EntityColor GetChild(NB\Exits[ME],1),R,G,BL
			
			EndIf
		Next
		
		Loop_Count=Loop_Count+1
		If Loop_Count=&gt;Max_loop Then Return
	Next
End Function

Function GenBuilding(New_Building=False)

	If New_Building= True
		B.Building = New Building

		B\M=CopyEntity(Junction(Rand(0,1)))	
		B\Typ = TYP_JUNCTION
		GetExits(B)

	Else
		B.Building =Last Building

		Loop_Count=Loop_Count+1
		
	EndIf

		If Loop_Count=&gt;Max_loop Then Return
		
	For E=0 To B\Exit_Count-1

		If B\Exits_Used[E]=0

			yaw# = EntityYaw(B\Exits[E],1)
			x# = EntityX(B\Exits[E],1)
			y# = EntityY(B\Exits[E],1)
			z# = EntityZ(B\Exits[E],1)
			
			Select B\Typ
				Case TYP_JUNCTION

					NB.Building = New Building					
					NB\M=CopyEntity(Corridor(Rand(0,2)))									
					NB\Typ = TYP_CORRIDOR
					
				Case TYP_ROOM
				
				Case TYP_CORRIDOR

					NB.Building = New Building					
					NB\M=CopyEntity(Junction(Rand(0,1)))									
					NB\Typ = TYP_JUNCTION
				
			End Select

			GetExits(NB)
			ME = SelectExit(NB)			
			nyaw# = EntityYaw(NB\Exits[ME],1)
			nx# = EntityX(NB\Exits[ME],1)
			ny# = EntityY(NB\Exits[ME],1)
			nz# = EntityZ(NB\Exits[ME],1)
	
			MatchExits(B\Exits[E],NB\Exits[ME])
			
			B\Exits_Used[E] = True
			NB\Exits_Used[ME] = True
			EntityParent NB\M,B\M
		;DEBUG INFO
			R=Rand(0,255):G=Rand(0,255):BL=Rand(0,255)
			EntityColor GetChild(B\Exits[E],1),R,G,BL
			EntityColor GetChild(NB\Exits[ME],1),R,G,BL						
			GenBuilding()
		EndIf
		
	Next

End Function

Function GetExits(B.Building)
	Main=GetChild(B\M,1)
	For A=1 To CountChildren(Main)
		child = GetChild(Main,A)
		If Left(EntityName(Child),4)="Exit"
			B\Exits[B\Exit_Count]=Child
			B\Exit_Count=B\Exit_Count+1		
;DEBUG INFO			
			C=CreateCube()
			PositionEntity C,EntityX(Child,1),EntityY(Child,1)+1,EntityZ(Child,1)
			RotateEntity C,0,EntityYaw(Child,1),0
			EntityParent C,Child
			EntityFX C,1
		;	EntityAlpha C,0.5
			ScaleEntity C,.2,.2,.2
		
		EndIf
	Next
End Function

Function SelectExit(B.Building)
	Repeat
		E=Rand(0,B\Exit_Count)
		If E&lt;B\Exit_Count
			If B\Exits_Used[E]=0 Then Return E
		EndIf
	Forever
End Function

Function MatchExits(ExitA,ExitB)

moduleA=GetParent (ExitB)
Local targetExitYaw# = EntityYaw( exitB, True ) + 180 ;World space, and opposite direction.
Local newModuleYaw# = EntityYaw( moduleA, True ) + ( targetExitYaw - EntityYaw( exitA, True ) )
RotateEntity( moduleA, 0, newModuleYaw, 0, True )

;Second, move the module A and offset it so that the exit from this module is on top of the target exit of module B.
PositionEntity( moduleA, EntityX( ExitA, True ), EntityY( ExitA, True ), EntityZ( ExitA, True ), True )
Local offsetX# = EntityX( moduleA, True ) - EntityX( exitB, True )
Local offsetY# = EntityY( moduleA, True ) - EntityY( exitB, True )
Local offsetZ# = EntityZ( moduleA, True ) - EntityZ( exitB, True )
TranslateEntity( moduleA, offsetX, offsetY, offsetZ, True )

End Function

Function Get_FPS(PosX#=10,PosY#=2)
         If FPS_Font = 0 Then
              FPS_Font=LoadFont("New Times Roman",14)
         EndIf
         FPS_Newtime = MilliSecs()
         FPS_Ticks = FPS_Ticks + 1
         If FPS_Ticks &gt; FPS_SampleRate Then
            FPS_Current = FPS_Newtime - FPS_Oldtime
            If FPS_Current = 0 Then FPS_Current = 1000 Else FPS_Current = 1000/FPS_Current
            FPS_Buffer(FPS_BufferIndex) = FPS_Current
            FPS_BufferIndex = FPS_BufferIndex + 1
            If FPS_BufferIndex &gt; FPS_Samples Then
                 For FPS_Count = 1 To FPS_Samples
                     FPS_Master = FPS_Master + FPS_Buffer(FPS_Count)                      
                 Next
                 FPS_Final = FPS_Master / FPS_Samples
                 FPS_BufferIndex = 1
            EndIf
            FPS_Ticks = 0
         EndIf
         FPS_Oldtime = MilliSecs()
         SetFont(FPS_Font)
         Text(PosX#,PosY#,"FPS:"+FPS_Final) 
         ;Return FPS_Final
End Function

</textarea> <br><br></td></tr></table><br>
<a name="1232090"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Admittedly, I only skimmed through the post here, but this may be of use I hope:<br><br>Assuming there's no transformations to the actual entity(ies)in question with regards to say, TFormPoint etc.<br><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Moves Entity by amount Distance# towards a point specified by XYZ coordinates.
Function MoveToPoint(hen_Entity,f_X#,f_Y#,f_Z#,f_Distance#=0.0,b_Global=True)
	Local f_EX#=f_X#-EntityX#(hen_Entity,b_Global);
	Local f_EY#=f_Y#-EntityY#(hen_Entity,b_Global);
	Local f_EZ#=f_Z#-EntityZ#(hen_Entity,b_Global);
	
	Local f_ActualDistance#=Sqr((f_EX*f_EX)+(f_EY*f_EY)+(f_EZ*f_EZ));
	
	If ((f_ActualDistance&lt;(abs(f_Distance)))
		f_Distance=f_ActualDistance;
	End If;
	
	TranslateEntity hen_Entity,Sgn(f_EX#)*Distance,Sgn(f_EY#)*Distance,Sgn(f_EZ#)*f_Distance,b_Global;
End Function;</textarea><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Rotates an Entity to point directly at given global coordinates, or origin if parameter arguments omitted
Function PointAt(hen_Entity, f_X#=0,f_Y#=0,f_Z#=0)
	Local f_Xdiff# = EntityX(hen_Entity,True)-f_x#;
	Local f_Ydiff# = EntityY(hen_Entity,True)-f_y#;
	Local f_Zdiff# = EntityZ(hen_Entity,True)-f_z#;
	
	Local f_Dist#=Sqr#((f_Xdiff#*f_Xdiff#)+(f_Zdiff#*f_Zdiff#));
	Local f_Pitch# = ATan2(f_Ydiff#,f_Dist#);
	Local f_Yaw# = ATan2(f_Xdiff,0-f_Zdiff#);
	RotateEntity hen_Entity,f_Pitch#,f_Yaw#,0,True;
End Function;</textarea> <br><br></td></tr></table><br>
<a name="1232213"></a>

<a name="1232215"></a>

<a name="1232216"></a>

<a name="1232217"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Once you have the delta vector you can use AlignToVector and spare the Sqr and ATan calls:<br><br><pre class=code>Function PointAt( entity, x# = 0.0, y# = 0.0, z# = 0.0 )
	Local deltaX# = x - EntityX( entity, True )
	Local deltaY# = y - EntityY( entity, True )
	Local deltaZ# = z - EntityZ( entity, True )

	AlignToVector( entity, deltaX, deltaY, deltaZ, 3 )
End Function</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
