<!DOCTYPE html><html lang="en" ><head ><title >TooD - It's just Too Dimensional!</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >TooD - It's just Too Dimensional!</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >TooD - It's just Too Dimensional!</a><br><br>
<a name="997505"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Difference</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Look what I found on my harddrive. It still compiles and runs, and it is quite fun!<br><br>tood.bb<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">;________________________________________________________________________________
;
; tooD. rev. 0.4 2003.03.28
; It's just Too Dimensional!
;
; A 2D primitives drawing library. Stolen, begged and borrowed.
;
; For BlitzBasic - www.blitzbasic.com
;
; Project by Peter Scheutz
;
; Licence: <a href="http://www.opensource.org/licenses/bsd-license.php" target="_blank">http://www.opensource.org/licenses/bsd-license.php</a>
;
; Code by: Peter Scheutz, Robin S,Stephen Greener, Shawn Swift,
; Andreas Schiffler, Simon Armstrong, Turgut Temucin
;
; Your name missing? Comments?
; please post in: <a href="http://www.blitzbasic.com/bbs/posts.php?topic=19109" target="_blank">http://www.blitzbasic.com/bbs/posts.php?topic=19109</a>
;
; Parts taken from:
; <a href="http://www.blitzbasic.com/codearcs/codearcs.php" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php</a>
; <a href="http://www.ferzkopp.net/Software/SDL_gfx-2.0/" target="_blank">http://www.ferzkopp.net/Software/SDL_gfx-2.0/</a>
;
;________________________________________________________________________________


; a set of polygons
Type tood_Polys
	Field count
	Field firstPoly.tood_Poly
End Type

; a set of points that form a polygon
Type tood_Poly
	Field count
	Field x#,y#,scale#,angle#
	Field firstPoint.tood_Point2D
	Field nextpoly.tood_Poly
End Type

; a 2d point
Type tood_Point2D
	Field x#,y#
	Field nextpoint.tood_Point2D
End Type


Const TOOD_PolyPointsMax=1000


Const TOOD_Screen=0
Const TOOD_Img=1
Const TOOD_Tex=2
Const TOOD_Canvas=3

; draw modes
Const TOOD_DmFilled = 0
Const TOOD_DmSimpleHatch=1
Const TOOD_DmAlpha=2
Const TOOD_DmCacheBank=3
Const TOOD_DmCacheImg=4
Const TOOD_DmCacheAlpha=5

; rastermodes
Const TOOD_Copy = 7
Const TOOD_Xor = 8
Const TOOD_Or=9
Const TOOD_And=10


; end point Styles
Const TOOD_ES_None = 0
Const TOOD_ES_Round=1
Const TOOD_ES_Arrow=2


Global tood_EsStyle

; cache	
Const TOOD_CmNone=0
Const TOOD_CmChacheDestBank=1
Const TOOD_CmChacheResultBank=2
Const TOOD_CmChacheResultArray=3
Const TOOD_CmChacheResultImg=4



Global tood_CmMode
Global tood_mainCacheFormat=0

Global tood_mainCacheW
Global tood_mainCacheH
Global tood_mainCacheBank
Global tood_mainCacheSize




Global tood_CacheImg
Global tood_CacheImgBuf

; states, buffers and clip
Global tood_islocked
Global tood_target
Global tood_targettype
Global tood_targetbuf
Global tood_targetw
Global tood_targeth
Global tood_dmode

; color
Global tood_argb=$FFFFFFFF
Global tood_red=$FF
Global tood_green=$FF
Global tood_blue=$FF

Global tood_alpha#=1
Global tood_invalpha#=0


 ; pre calced alphaed values
Global tood_ared=$FF
Global tood_agreen=$FF
Global tood_ablue=$FF


Global tood_offx#
Global tood_offy#


; global arrays for polygons
Dim tood_PolyInts(TOOD_PolyPointsMax)
Global tood_PolyIntsAllocated=TOOD_PolyPointsMax

Dim tood_vx(TOOD_PolyPointsMax)
Dim tood_vy(TOOD_PolyPointsMax)

Dim tood_vx2(TOOD_PolyPointsMax)
Dim tood_vy2(TOOD_PolyPointsMax)



; lock buffer if it's not already locked
Function tood_Lock()
	If tood_islocked=True Then Return
	LockBuffer tood_targetbuf
	tood_islocked=True
End Function

; unlock buffer if it's not already unlocked
Function tood_UnLock()
	If tood_islocked=False Then Return
	UnlockBuffer tood_targetbuf
	tood_islocked=False
End Function

Function tood_FreeCache()
	If tood_mainCacheBank&lt;&gt;0 Then FreeBank tood_mainCacheBank
	tood_mainCacheBank=0
	tood_mainCacheSize=0
	
	If tood_CacheImg&lt;&gt;0 Then
		FreeImage tood_CacheImg
		tood_CacheImg=0
		tood_CacheImgBuf=0
	EndIf
	
End Function


Function tood_DrawCache()
	Local x, y,off

	If tood_mainCacheSize = 0 Then Return

	tood_Lock			
	For y=0 To tood_targeth-1
	For x=0 To tood_targetw-1
		WritePixelFast x,y,PeekInt(tood_mainCacheBank,off)
		off=off+4
	Next
	Next
	

End Function


Function tood_Cache(CacheMode)
	Local x, y ,off,destpix
	Local cachelen, buf
	
	cachelen=tood_targetw*tood_targeth*4
	

	Select CacheMode
		Case TOOD_CmNone
		
		Case TOOD_CmChacheDestBank
			; empty main bank cache
			If tood_MainCache&lt;&gt;0
				If tood_mainCacheSize&lt;&gt;cachelen
					ResizeBank tood_mainCacheBank ,cachelen
				EndIf
			Else
				tood_mainCacheBank=CreateBank(cachelen)
			EndIf

			tood_mainCacheSize=cachelen
			
			tood_Lock			
			For y=0 To tood_targeth-1
			For x=0 To tood_targetw-1
				PokeInt tood_mainCacheBank,off,ReadPixelFast(x,y,tood_targetbuf)
				off=off+4
			Next
			Next
			
			tood_UnLock
		Case TOOD_CmChacheResultBank
			If tood_MainCache&lt;&gt;0
				If tood_mainCacheSize&lt;&gt;cachelen
					ResizeBank tood_mainCacheBank ,cachelen
				EndIf
			Else
				tood_mainCacheBank=CreateBank(cachelen)
			EndIf

			tood_mainCacheSize=cachelen



			tood_Lock			
			For y=0 To tood_targeth-1
			For x=0 To tood_targetw-1
			
				destpix=ReadPixelFast(x,y,tood_buf)
				
				r=(destpix Shr 16) And $FF
				g=(destpix Shr 8 ) And $FF
				b=destpix And $FF
				
				r=r*tood_invAlpha + tood_ared
				g=g*tood_invAlpha + tood_agreen
				b=b*tood_invAlpha + tood_ablue
					
				destpix=$FF000000 Or b Or g Shl 8 Or r Shl 16			
			
				PokeInt tood_mainCacheBank,off,destpix
				off=off+4
			Next
			Next
			
			tood_UnLock


		Case TOOD_CmChacheResultImg
						
			If tood_CacheImg=0 Then
				tood_FreeCache
				tood_CacheImg=CreateImage(tood_targetw,tood_targeth)
				tood_CacheImgBuf=ImageBuffer(tood_CacheImg)
			ElseIf ImageWidth(tood_CacheImg)&lt;&gt;tood_targetw Or ImageHeight(tood_CacheImg)&lt;&gt;tood_targeth
				tood_FreeCache
				tood_CacheImg=CreateImage(tood_targetw,tood_targeth)
				tood_CacheImgBuf=ImageBuffer(tood_CacheImg)
				
			Else
			
			EndIf

;			GrabImage tood_CacheImg,0,0
			
			
			buf=tood_CacheImgBuf
			LockBuffer buf
			tood_Lock
			For y=0 To tood_targeth-1
			For x=0 To tood_targetw-1
			
				destpix=ReadPixelFast(x,y,tood_buf)
				
				r=(destpix Shr 16) And $FF
				g=(destpix Shr 8 ) And $FF
				b=destpix And $FF
				
				r=r*tood_invAlpha + tood_ared
				g=g*tood_invAlpha + tood_agreen
				b=b*tood_invAlpha + tood_ablue
					
				destpix=$FF000000 Or b Or g Shl 8 Or r Shl 16			
			
				WritePixelFast x,y,destpix,buf

			Next
			Next
			UnlockBuffer buf
			tood_UnLock

	End Select

	tood_CmMode=CacheMode


End Function

Function tood_SetTarget(targettype,hand,CacheMode=TOOD_CmNone)


	tood_UnLock 

	tood_target=hand
	
	Select targettype
		Case TOOD_SCREEN
			tood_targettype=TOOD_SCREEN
			tood_targetw=GraphicsWidth()
			tood_targeth=GraphicsHeight()
			tood_targetbuf=BackBuffer()
			
		Case TOOD_IMG
			tood_targettype=TOOD_IMG
			tood_targetw=ImageWidth(tood_target)
			tood_targeth=ImageHeight(tood_target)
			tood_targetbuf=ImageBuffer(tood_target)

		Case TOOD_TEX
			;tood_targettype=TOOD_TEX		
			;tood_targetw=TextureWidth(tood_target)
			;tood_targeth=TextureHeight(tood_target)
			;tood_targetbuf=TextureBuffer(tood_target)

		Case TOOD_CANVAS		
			tood_targettype=TOOD_CANVAS
			;tood_targetw=GadgetWidth(tood_target)
			;tood_targeth=GadgetHeight(tood_target)
			;tood_targetbuf=CanvasBuffer(tood_target)

	End Select
	
	SetBuffer tood_targetbuf
	
	
	If CacheMode&lt;&gt;TOOD_CmNone Then
		tood_Cache(CacheMode)
	EndIf
	
	
End Function


Function tood_color(r,g,b,a#=1)
	tood_argb=a Shl 24 Or b Or g Shl 8 Or r Shl 16
	tood_alpha=a
	tood_invalpha=1.0-a	
	
	
	tood_red=r
	tood_green=g
	tood_blue=b
	
	tood_ared=r*a
	tood_agreen=g*a
	tood_ablue=b*a

	
		
	Color r,g,b
End Function


Function tood_DrawMode(mode)
	tood_dmode=mode
End Function


; draws a horizontal line without clipping
Function tood_Hline(x,y,w)
	Local dx
	
	For dx=x To x+w
		WritePixelFast dx,y,tood_argb
	Next

End Function

Function tood_HlineAlpha(x,y,w)
	Local dx,destpix
	Local r,g,b
	For dx=x To x+w
		destpix=ReadPixelFast(dx,y,tood_buf)
		
		r=(destpix Shr 16) And $FF
		g=(destpix Shr 8 ) And $FF
		b=destpix And $FF
		
		r=r*tood_invAlpha + tood_ared
		g=g*tood_invAlpha + tood_agreen
		b=b*tood_invAlpha + tood_ablue


			
		destpix=$FF000000 Or b Or g Shl 8 Or r Shl 16			
			
		WritePixelFast dx,y,destpix
	Next

End Function




; draws a horizontal line with clipping
Function tood_HlineClipped(x,y,w)
	Local dx
	
	Local temp
	

	
	If y&lt;0 Then Return
	If y&gt;=tood_targeth Then Return
	If x&gt;=tood_targetw Then Return
	If x&lt;0 Then
		w=w+x
		x=0
	EndIf
	
	If x+w&gt;=tood_targetw Then w=tood_targetw-x-1
	If w&lt;=0 Then Return

	
	For dx=x To x+w
		WritePixelFast dx,y,tood_argb
	Next

End Function


Function tood_HlineClippedCacheBank(x,y,w)
	Local dx
	
	Local temp
	

	
	If y&lt;0 Then Return
	If y&gt;=tood_targeth Then Return
	If x&gt;=tood_targetw Then Return
	If x&lt;0 Then
		w=w+x
		x=0
	EndIf
	
	If x+w&gt;=tood_targetw Then w=tood_targetw-x-1
	If w&lt;=0 Then Return

	
	For dx=x To x+w
		WritePixelFast dx,y,PeekInt(tood_MainCacheBank,dx*4+y*tood_targetw*4)
	Next

End Function


Function tood_HlineCacheBank(x,y,w)
	Local dx
	
	For dx=x To x+w
		WritePixelFast dx,y,PeekInt(tood_MainCacheBank,dx*4+y*tood_targetw*4)
	Next

End Function



Function tood_HlineCacheAlphaBank(x,y,w)
	Local dx,destpix
	Local r,g,b
	For dx=x To x+w-1
	
		r=PeekByte(tood_MainCacheBank,dx*4+y*tood_targetw*4+2)
		g=PeekByte(tood_MainCacheBank,dx*4+y*tood_targetw*4+1)
		b=PeekByte(tood_MainCacheBank,dx*4+y*tood_targetw*4+0)


		
		r=r*tood_invAlpha + tood_ared
		g=g*tood_invAlpha + tood_agreen
		b=b*tood_invAlpha + tood_ablue

	
		destpix=$FF000000 Or b Or g Shl 8 Or r Shl 16			
			
		WritePixelFast dx,y,destpix
		
		
	Next

End Function








Function tood_HlineCacheImg(x,y,w)
	CopyRect x,y,w,1,x,y,tood_CacheImgBuf,tood_Targetbuf
End Function


Function tood_HlineClippedAlpha(x,y,w)
	Local dx,destpix
	Local r#,g#,b#

	
	If y&lt;0 Then Return
	If y&gt;=tood_targeth Then Return
	If x&gt;=tood_targetw Then Return
	If x&lt;0 Then
		w=w+x
		x=0
	EndIf
	
	If x+w&gt;=tood_targetw Then w=tood_targetw-x-1
	If w&lt;=0 Then Return

	
	For dx=x To x+w
	
		destpix=ReadPixelFast(dx,y,tood_buf)
		r=(destpix Shr 16) And $FF
		g=(destpix Shr 8 ) And $FF
		b=destpix And $FF
		
		r=r*tood_invAlpha + tood_ared
		g=g*tood_invAlpha + tood_agreen
		b=b*tood_invAlpha + tood_ablue

			
		destpix=$FF000000 Or b Or g Shl 8 Or r Shl 16	
	
	
		WritePixelFast dx,y,destpix
	Next

End Function



; draws a horizontal hatched line without clipping
Function tood_HlineHatched(x,y,w)
	Local dx
	
	Local off
	
	
	off= (y Mod 2 + x Mod 2) And 1

	For dx=x+off To x+w Step 2
		WritePixelFast dx,y,tood_argb
	Next

End Function

; draws a horizontal hatched line with clipping
Function tood_HlineClippedHatched(x,y,w)
	Local dx
	
	Local off
	
	If y&lt;0 Then Return
	If y&gt;=tood_targeth Then Return
	If x&gt;=tood_targetw Then Return
	If x&lt;0 Then
		w=w+x
		x=0
	EndIf
	
	If x+w&gt;=tood_targetw Then w=tood_targetw-x-1
	If w&lt;=0 Then Return
	
	
	off= (y Mod 2 + x Mod 2) And 1
	
	
	For dx=x+off To x+w Step 2
		WritePixelFast dx,y,tood_argb
	Next

End Function

; Returns a tood_Poly with default scale
Function tood_CreatePoly.tood_Poly()

	p.tood_Poly=New tood_Poly
	p\scale=1

	Return p
End Function

; Add a point to a tood_poly
Function tood_AddPolyPoint(poly.tood_poly,x#,y#)

	If poly\firstPoint=Null Then
		poly\firstpoint=New tood_Point2D
		point.tood_Point2D=poly\firstpoint
	Else
		point.tood_Point2D=poly\firstPoint
		
		While point\nextpoint&lt;&gt;Null
			point=point\nextpoint
		Wend
		
		point\nextpoint=New tood_Point2D
		
		point=point\nextpoint
	EndIf

	point\x=x
	point\y=y
	
	poly\count=poly\count+1


End Function


; Draw a filled polygon.
; If poly&lt;&gt;Null, read the type,
; otherwise draw from global array tood_vx(), tood_vy()
;
Function tood_Polygon(poly.tood_poly,thickness=1)

	Local p



	If poly&lt;&gt;Null n=toodprv_ReadPoly(poly,1)


	For p= 0 To n-2
	
		tood_line tood_vx2(p),tood_vy2(p),tood_vx2(p+1),tood_vy2(p+1),thickness
	
	Next

	tood_line tood_vx2(p),tood_vy2(p),tood_vx2(0),tood_vy2(0),thickness



End Function


; Draw a filled polygon.
; If poly&lt;&gt;Null, read the type,
; otherwise draw from global array tood_vx(), tood_vy()
;
Function tood_FilledPolygon(poly.tood_poly,n=0)

    Local result
    Local i,ints
	Local y,x1,x2,y1,y2,w
    Local miny, maxy
    Local ind1, ind2


	If poly&lt;&gt;Null n=toodprv_ReadPoly(poly)
	
    
    ;Sanity check 
    If n &lt; 3 	Return -1

	; Allocate temp array, only grow array 
	If tood_PolyIntsAllocated &lt; n
		Dim tood_PolyInts(n)
		tood_PolyIntsAllocated = n		
    EndIf


	;Determine Y maxima 
    
    miny = tood_vy(0)
    maxy = tood_vy(0)
    For i = 1 To n -1
		If tood_vy(i) &lt; miny
		    miny = tood_vy(i)
		ElseIf tood_vy(i) &gt; maxy
		    maxy = tood_vy(i)
		EndIf
    Next


	If tood_dmode=TOOD_DmFilled Then tood_unLock
	If tood_dmode=TOOD_DmSimpleHatch Then tood_Lock
	If tood_dmode=TOOD_DmCacheBank Then tood_Lock
	If tood_dmode=TOOD_DmCacheImg Then tood_UnLock

	;Draw, scanning y 
     
    result = 0
    For y = miny To maxy
		ints = 0;
		For i = 0 To n-1
		    If i=0
				ind1 = n - 1
				ind2 = 0
		    Else
				ind1 = i - 1
				ind2 = i
		    EndIf
		
		    y1 = tood_vy(ind1)
		    y2 = tood_vy(ind2)
		
		    If y1 &lt; y2
				x1 = tood_vx(ind1)
				x2 = tood_vx(ind2)
		    ElseIf y1 &gt; y2
				y2 = tood_vy(ind1)
				y1 = tood_vy(ind2)
				x2 = tood_vx(ind1)
				x1 = tood_vx(ind2)
		    Else
				Goto continue
		    EndIf
		
		    If (y &gt;= y1) And (y &lt; y2)
				tood_PolyInts(ints) = (y - y1) * (x2 - x1) / (y2 - y1) + x1
				ints=ints+1
		    ElseIf (y = maxy) And (y &gt; y1) And (y &lt;= y2)
				tood_PolyInts(ints) = (y - y1) * (x2 - x1) / (y2 - y1) + x1
				ints=ints+1
		    EndIf
.continue	
		Next
		
		If ints&gt;0 Then
			toodprv_sortpolyints 0,ints -1
			For i = 0 To ints -1 Step 2
				w=tood_PolyInts(i+1)-tood_PolyInts(i)
				
				If tood_dmode = TOOD_DmFilled Then
					Rect tood_PolyInts(i),y,w,1
				ElseIf tood_dmode = TOOD_DmSimpleHatch
					tood_HlineClippedHatched tood_PolyInts(i),y,w
				ElseIf tood_dmode = TOOD_DmAlpha
					tood_HlineClippedAlpha tood_PolyInts(i),y,w
				ElseIf tood_dmode=TOOD_DmCacheBank
					tood_HlineClippedCacheBank tood_PolyInts(i),y,w
				ElseIf tood_dmode=TOOD_DmCacheImg
					tood_HlineCacheImg tood_PolyInts(i),y,w
				EndIf
	
			Next
		EndIf
    Next

End Function





Function toodprv_ReadPoly(poly.tood_poly,arr=0)

	Local n,x#,y#,sina#,cosa#,theta#
	
	point.tood_point2d=poly\firstpoint

	If arr=0 Then	
		If poly\angle=0
			While point&lt;&gt;Null
				tood_vx(n)=point\x*poly\scale+poly\x + tood_offx
				tood_vy(n)=point\y*poly\scale+poly\y + tood_offy
				point=point\nextpoint
				n=n+1
			Wend	
		Else
			theta=poly\angle
			cosa#=Cos(theta)
			sina#=Sin(theta)
				
			While point&lt;&gt;Null		
				x = cosa*point\x - sina*point\y 
				y = sina*point\x + cosa*point\y
				
				tood_vx(n)=x*poly\scale+poly\x + tood_offx
				tood_vy(n)=y*poly\scale+poly\y + tood_offy
				point=point\nextpoint
				n=n+1
			Wend		
		EndIf
	ElseIf arr=1
		If poly\angle=0
			While point&lt;&gt;Null
				tood_vx2(n)=point\x*poly\scale+poly\x + tood_offx
				tood_vy2(n)=point\y*poly\scale+poly\y + tood_offy
				point=point\nextpoint
				n=n+1
			Wend	
		Else
			theta=poly\angle
			cosa#=Cos(theta)
			sina#=Sin(theta)
				
			While point&lt;&gt;Null		
				x = cosa*point\x - sina*point\y 
				y = sina*point\x + cosa*point\y
				
				tood_vx2(n)=x*poly\scale+poly\x + tood_offx
				tood_vy2(n)=y*poly\scale+poly\y + tood_offy
				point=point\nextpoint
				n=n+1
			Wend		
		EndIf	
	EndIf
	Return n

End Function


Function toodprv_sortpolyints(l,r)
  Local p,q,h
  p=l
  q=r
  x=tood_PolyInts((l+r)/2)
  Repeat
    While tood_PolyInts(p)&lt;x
      p=p+1
    Wend
    While x&lt;tood_PolyInts(q)
      q=q-1
    Wend
    If p&gt;q Then Exit
	;SWAP------------------
	h=tood_PolyInts(q)
	tood_PolyInts(q)=tood_PolyInts(p)
	tood_PolyInts(p)=h
	;----------------------
    p=p+1
    q=q-1
    If q&lt;0 Then Exit
  Forever 
  If l&lt;q Then a=toodprv_sortpolyints(l,q)
  If p&lt;r Then a=toodprv_sortpolyints(p,r)

  Return True
End Function


Function tood_Triangle(x0,y0,x1,y1,x2,y2)
	tood_vx(0)=x0
	tood_vy(0)=y0
	tood_vx(1)=x1
	tood_vy(1)=y1
	tood_vx(2)=x2
	tood_vy(2)=y2
	
	tood_simplePoly 3
End Function

Function tood_Quad(x0,y0,x1,y1,x2,y2,x3,y3)
	tood_vx(0)=x0
	tood_vy(0)=y0
	tood_vx(1)=x1
	tood_vy(1)=y1
	tood_vx(2)=x2
	tood_vy(2)=y2
	tood_vx(3)=x3
	tood_vy(3)=y3

	tood_simplePoly 4
End Function




Function tood_SimplePoly(vcount)

	Local a,b,c,d,h,y,x,yy,xx,t,x0,x1,g0,g1,w
	
	; find top verticy

	b=vcount-1
	y=tood_vy(0)
	While c&lt;&gt;b
		c=c+1
		yy=tood_vy(c)
		If yy&lt;y y=yy d=c
	Wend
	c=d 
	t=c
	
	If tood_dmode=TOOD_DmFilled  Or tood_dmode=TOOD_DmCacheImg Then
		tood_unLock
	Else
		tood_Lock
	EndIf

	
	; draw top to bottom
	While y&lt;tood_targeth
		; get left gradient
		If y=tood_vy(c)
			While y=tood_vy(c)
				x0=tood_vx(c) Shl 16
				c=c+1
				If c&gt;b c=a
				If c=t Return
				If y&gt;tood_vy(c) Return
			Wend
			h=tood_vy(c)-y
			g0=((tood_vx(c) Shl 16)-x0)/h
		EndIf
		; get right gradient
		If y=tood_vy(d)
			While y=tood_vy(d)
				x1=tood_vx(d) Shl 16
				d=d-1
				If d&lt;a d=b
				If y&gt;tood_vy(d) Return
			Wend
			h=tood_vy(d)-y
			g1=((tood_vx(d) Shl 16)-x1)/h
		EndIf
		; calc horizontal span
		x=x1 Sar 16
		w=((x0 Sar 16)-x)  +1 ; PS corrected

		; draw down to next vert
		If (w&gt;0 And y&gt;-1 And x&lt;tood_targetw And x+w&gt;0)
		;	w=w-1	
			If x&lt;0 w=w+x x=0	;crop left
			If x+w&gt;tood_targetw w=tood_targetw-x	;crop right
			
			If tood_dmode = TOOD_DmFilled Then
				Rect x,y,w,1
			ElseIf tood_dmode = TOOD_DmSimpleHatch
				tood_HlineHatched x,y,w
			ElseIf tood_dmode = TOOD_DmAlpha
				tood_HlineAlpha x,y,w
			ElseIf tood_dmode=TOOD_DmCacheBank
				tood_HlineCacheBank x,y,w
			ElseIf tood_dmode=TOOD_DmCacheAlpha
				tood_HlineCacheAlphaBank x,y,w
			ElseIf tood_dmode=TOOD_DmCacheImg
				tood_HlineCacheImg x,y,w
			EndIf
			
		EndIf
		; next	
		x0=x0+g0
		x1=x1+g1
		y=y+1
	Wend
End Function



Function tood_Line(x0,y0,x1,y1,thickness#=1)

	Local nx#,ny#,nd#,mag#
	Local nx1#,ny1#,nx2#,ny2#

	; TODO: handle alpha and hatched
	If thickness=1 And 2=2
		tood_UnLock
		Line x0,y0,x1,y1
		Return
	EndIf


	nx = y1 - y0
	ny = x0 - x1
	nd# = x1*y0 - x0*y1

	mag = Sqr(nx*nx + ny*ny)
	nx# = Abs((nx# / mag)*thickness)
	ny# = Abs((ny# / mag)*thickness)

	

	nx1=Floor(nx*0.5)
	ny1=Floor(ny*0.5)		
	nx2=Ceil(nx*0.5)	
	ny2=Ceil(ny*0.5)		



	nx1=(nx*0.5)
	ny1=(ny*0.5)		
	nx2=(nx*0.5)	
	ny2=(ny*0.5)
	
	
	dist#= Sqr(nx*nx + ny*ny)

	If Abs(x1-x0)&lt;Abs(y1-y0) Then
	;	If Abs(nx2-nx1)&lt;thickness-0.001 Then nx2=nx2+1
	;	If Abs(nx2-nx1)&gt;thickness+0.001 Then nx2=nx2-1
	
		
		
	Else
	;	If Abs(ny2-ny1)&lt;thickness-0.001 Then ny2=ny2+1	
	;	If Abs(ny2-ny1)&gt;thickness+0.001 Then ny2=ny2-1	
	EndIf
	
	;nx2=nx2-1
	;ny2=ny2-1



;	nx=(nx*0.34)
;	ny=(ny*0.34)
;	nd# = (nd# / mag)*thickness


	tood_vx(0)=x0+nx2
	tood_vy(0)=y0+ny2
	tood_vx(1)=x0-nx1
	tood_vy(1)=y0-ny1
	tood_vx(2)=x1-nx1
	tood_vy(2)=y1-ny1
	tood_vx(3)=x1+nx2
	tood_vy(3)=y1+ny2
	
	For n=0 To 3
	;	DebugLog tood_vx(n) + " , " + tood_vy(n) 

	Next
	

;	tood_simplePoly 4	

	tood_FilledPolygon Null,4	


	; TODO: handle alpha + round
	If tood_EsStyle= TOOD_ES_Round
		tood_FilledCircle x0,y0 ,thickness
		tood_FilledCircle x1,y1 ,thickness
	EndIf

End Function



Function tood_LineOLD(x0,y0,x1,y1,thickness#=1)

	Local nx#,ny#,nd#,mag#
	Local nx1#,ny1#,nx2#,ny2#



	; TODO: handle alpha and hatched
	If thickness=1 And 2=3
		tood_UnLock
		Line x0,y0,x1,y1
		Return
	EndIf

;thickness=thickness-1

	nx = y1 - y0
	ny = x0 - x1
	nd# = x1*y0 - x0*y1

	mag = Sqr(nx*nx + ny*ny)
	nx# = ((nx# / mag)*thickness)
	ny# = ((ny# / mag)*thickness)

	DebugLog ny


	nx1=(nx*0.5)
	ny1=(ny*0.5)		
	nx2=(nx*0.5)	
	ny2=(ny*0.5)		
	
	
	dist#= Sqr(nx*nx + ny*ny)

	If Abs(x1-x0)&lt;Abs(y1-y0) Then
	;	If Abs(nx2-nx1)&lt;thickness-0.001 Then nx2=nx2+1
	;	If Abs(nx2-nx1)&gt;thickness+0.001 Then nx2=nx2-1
	
		
		
	Else
	;	If Abs(ny2-ny1)&lt;thickness-0.001 Then ny2=ny2+1	
	;	If Abs(ny2-ny1)&gt;thickness+0.001 Then ny2=ny2-1	
	EndIf
	
	nx2=nx2-1
	ny2=ny2-1



;	nx=(nx*0.34)
;	ny=(ny*0.34)
;	nd# = (nd# / mag)*thickness


	tood_vx(0)=x0-nx1
	tood_vy(0)=y0-ny1
	tood_vx(1)=x0+nx2
	tood_vy(1)=y0+ny2
	tood_vx(2)=x1+nx2
	tood_vy(2)=y1+ny2
	tood_vx(3)=x1-nx1
	tood_vy(3)=y1-ny1

	tood_simplePoly 4	

	; TODO: handle alpha + round
	If tood_EsStyle= TOOD_ES_Round
		tood_FilledCircle x0,y0 ,thickness
		tood_FilledCircle x1,y1 ,thickness
	EndIf

End Function



Function  tood_FilledCircle( x, y, r)

    Local result
    Local x1, y1, x2, y2;
    Local cx = 0
    Local cy = r
    Local ocx = $ffff
    Local ocy = $ffff
    Local df = 1 - r
    Local d_e = 3
    Local d_se = -2 * r + 5
    Local xpcx, xmcx, xpcy, xmcy
    Local ypcy, ymcy, ypcx, ymcx

    
    ;Sanity check radius 

    If r &lt; 0 Return -1


    ;Special Case For r=0 - draw a point 
    ;If r = 0 Return (pixelColor(dst, x, y, Color));
    

	If tood_dMode=TOOD_DmFilled Then
		tood_UnLock
		Oval x-r,y-r,r*2,r*2
	
	EndIf


    
    ;Test bounding box 
    
    x1 = x - r
    y1 = y - r
    x2 = x + r
    y2 = y + r

;    If (!(clipLine(dst, &amp;x1, &amp;y1, &amp;x2, &amp;y2))) {
;	Return (0);
    

	If tood_dmode=TOOD_DmSimpleHatch
	
		
		tood_Lock
    
	    ;Draw 
	     
	    While  cx &lt;= cy
	
			xpcx = x + cx
			xmcx = x - cx
			xpcy = x + cy
			xmcy = x - cy
			If ocy &lt;&gt; cy Then
			    If cy &gt; 0 Then
					ypcy = y + cy
					ymcy = y - cy
					tood_HlineClippedHatched  xmcx, ypcy,xpcx-xmcx
					tood_HlineClippedHatched  xmcx, ymcy,xpcx-xmcx
			    Else 
					tood_HlineClippedHatched  xmcx,y, xpcx -xmcx
			    EndIf
			    ocy = cy
			EndIf
			If ocx &lt;&gt; cx Then
			    If cx &lt;&gt; cy Then
					If cx &gt; 0 Then
					    ypcx = y + cx
					    ymcx = y - cx
					 	tood_HlineClippedHatched  xmcy, ymcx ,xpcy-xmcy
					    tood_HlineClippedHatched  xmcy, ypcx,xpcy-xmcy
					Else
					    tood_HlineClippedHatched  xmcy,y, xpcy-xmcy
					EndIf
			    EndIf
			    ocx = cx
			EndIf
		
			; Update 
		
			If df &lt; 0 Then
			    df =df + d_e
			    d_e =d_e + 2
			    d_se =d_se + 2
			Else
			    df =df + d_se
			    d_e = d_e + 2
			    d_se =d_se + 4
			    cy=cy-1
			EndIf
			cx=cx+1
	    Wend

	ElseIf tood_dmode=TOOD_DmAlpha
	
	
		tood_Lock
		
	    While  cx &lt;= cy
	
			xpcx = x + cx
			xmcx = x - cx
			xpcy = x + cy
			xmcy = x - cy
			If ocy &lt;&gt; cy Then
			    If cy &gt; 0 Then
					ypcy = y + cy
					ymcy = y - cy
					tood_HlineClippedAlpha  xmcx, ypcy,xpcx-xmcx
					tood_HlineClippedAlpha  xmcx, ymcy,xpcx-xmcx
			    Else 
					tood_HlineClippedAlpha  xmcx,y, xpcx -xmcx
			    EndIf
			    ocy = cy
			EndIf
			If ocx &lt;&gt; cx Then
			    If cx &lt;&gt; cy Then
					If cx &gt; 0 Then
					    ypcx = y + cx
					    ymcx = y - cx
					 	tood_HlineClippedAlpha  xmcy, ymcx ,xpcy-xmcy
					    tood_HlineClippedAlpha  xmcy, ypcx,xpcy-xmcy
					Else
					    tood_HlineClippedAlpha  xmcy,y, xpcy-xmcy
					EndIf
			    EndIf
			    ocx = cx
			EndIf
		
			; Update 
		
			If df &lt; 0 Then
			    df =df + d_e
			    d_e =d_e + 2
			    d_se =d_se + 2
			Else
			    df =df + d_se
			    d_e = d_e + 2
			    d_se =d_se + 4
			    cy=cy-1
			EndIf
			cx=cx+1
	    Wend
	
	
	ElseIf tood_dmode=TOOD_DmCacheBank
	
	
		tood_Lock
		
	    While  cx &lt;= cy
	
			xpcx = x + cx
			xmcx = x - cx
			xpcy = x + cy
			xmcy = x - cy
			If ocy &lt;&gt; cy Then
			    If cy &gt; 0 Then
					ypcy = y + cy
					ymcy = y - cy
					tood_HlineClippedCacheBank  xmcx, ypcy,xpcx-xmcx
					tood_HlineClippedCacheBank  xmcx, ymcy,xpcx-xmcx
			    Else 
					tood_HlineClippedCacheBank  xmcx,y, xpcx -xmcx
			    EndIf
			    ocy = cy
			EndIf
			If ocx &lt;&gt; cx Then
			    If cx &lt;&gt; cy Then
					If cx &gt; 0 Then
					    ypcx = y + cx
					    ymcx = y - cx
					 	tood_HlineClippedCacheBank  xmcy, ymcx ,xpcy-xmcy
					    tood_HlineClippedCacheBank  xmcy, ypcx,xpcy-xmcy
					Else
					    tood_HlineClippedCacheBank  xmcy,y, xpcy-xmcy
					EndIf
			    EndIf
			    ocx = cx
			EndIf
		
			; Update 
		
			If df &lt; 0 Then
			    df =df + d_e
			    d_e =d_e + 2
			    d_se =d_se + 2
			Else
			    df =df + d_se
			    d_e = d_e + 2
			    d_se =d_se + 4
			    cy=cy-1
			EndIf
			cx=cx+1
	    Wend	

	ElseIf tood_dmode=TOOD_DmCacheImg
	
	
		tood_UnLock
		
	    While  cx &lt;= cy
	
			xpcx = x + cx
			xmcx = x - cx
			xpcy = x + cy
			xmcy = x - cy
			If ocy &lt;&gt; cy Then
			    If cy &gt; 0 Then
					ypcy = y + cy
					ymcy = y - cy
					tood_HlineCacheImg  xmcx, ypcy,xpcx-xmcx
					tood_HlineCacheImg  xmcx, ymcy,xpcx-xmcx
			    Else 
					tood_HlineCacheImg  xmcx,y, xpcx -xmcx
			    EndIf
			    ocy = cy
			EndIf
			If ocx &lt;&gt; cx Then
			    If cx &lt;&gt; cy Then
					If cx &gt; 0 Then
					    ypcx = y + cx
					    ymcx = y - cx
					 	tood_HlineCacheImg  xmcy, ymcx ,xpcy-xmcy
					    tood_HlineCacheImg  xmcy, ypcx,xpcy-xmcy
					Else
					    tood_HlineCacheImg  xmcy,y, xpcy-xmcy
					EndIf
			    EndIf
			    ocx = cx
			EndIf
		
			; Update 
		
			If df &lt; 0 Then
			    df =df + d_e
			    d_e =d_e + 2
			    d_se =d_se + 2
			Else
			    df =df + d_se
			    d_e = d_e + 2
			    d_se =d_se + 4
			    cy=cy-1
			EndIf
			cx=cx+1
	    Wend

			
	EndIf


End Function

 </textarea><br><br>polytest.bb<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Include "tood.bb"


For n= 1 To CountGfxDrivers()

	drvname$=GfxDriverName(n)  
	
	If Instr(Upper$(drvname$),"OPENGL")
	;	SetGfxDriver n	
	EndIf

Next

Global w,h


If 2=2

	w=1024
	h=768
	;w=640
	;h=480

	Graphics w,h,0,1

Else

	w=640
	h=480
	w=512
	h=512
	Graphics w,h,0,2
EndIf



SetBuffer BackBuffer()


Global backimg=CreateImage(w,h)

;Print "Cache size: " + BankSize(tood_MainCache)
;Flip
;WaitKey()


; create a triangle
Global tri.tood_poly=tood_CreatePoly()
tri\x=GraphicsWidth()/2
tri\y=GraphicsHeight()/2

tood_AddPolyPoint tri,-1,-Sqr(3)/2.0
tood_AddPolyPoint tri,1,-Sqr(3)/2.0
tood_AddPolyPoint tri,0,Sqr(3)/2.0


; create a star
Global star.tood_poly=tood_CreatePoly()
star\x=GraphicsWidth()/2
star\y=GraphicsHeight()/2

star\scale=165
angstep=30
ang#=0

rad1#=1
rad2#=1

tog#=0







While ang&lt;360

	tood_AddPolyPoint star,Cos(ang)*Float(tog/2+.5),Sin(ang)*Float(tog/2+.5)
		
	ang=ang+angstep
	tog= tog Xor 1

Wend

ClsColor 10,0,0
Cls 

tood_SetTarget TOOD_SCREEN,0 

tood_DrawMode TOOD_DmFilled

tood_color 255,100,100,1

tood_Line 10,10-2,400,10-2,1



tood_quad 10,10,400,10,400,11,10,11

Line 10,10+2,400,10+2


tood_FilledCircle 100,100,100
;tood_FilledPolygon star,3

tood_color 255,255,255,1

tood_Polygon star,4





;Flip 
;WaitKey
;End




;Flip
;SetBuffer FrontBuffer()

;SetBuffer BackBuffer()




lay=0

Cls

dx=0
dy=h/2


;Print tood_mainCacheSize
;Flip
;WaitKey 

;End



While Not KeyHit(1)

	PrimsDemo
DrawStuff

tood_UnLock

GrabImage backimg,0,0	 

	ShadowDemo
	
		

	FadeToColor 0,0,0,5000	
	Delay 1000
	FadeToColor 255,255,255,1000
	
	;WaitKey()
Wend
	End

time=MilliSecs()

	;tood_Cache(TOOD_CmChacheResult)
	DrawBlock backimg,0,0
;Cls
	;tood_DrawCache 

	
	
	For n= 0 To 2

		mode = mode Xor Rand(0,2)
		Select mode
			Case 0
				tood_DrawMode TOOD_DmFilled
			Case 1
				tood_DrawMode TOOD_DmSimplehatch
			Case 2
				tood_DrawMode TOOD_DmAlpha
			
		End Select

		tood_DrawMode TOOD_DmCacheImg

	
		For pol.tood_poly=Each tood_poly
			pol\x=Rand(GraphicsWidth())
			pol\y=Rand(GraphicsHeight())
			pol\scale=Rnd(200)+10
			pol\angle=Rnd(360)
		Next
	


		r=10;w/2
		dx=dx+1
		
		If dx&gt;w+r Then dx=-r
		tood_color Rand(255) , Rand(255) , Rand(255) ,Rnd(1)	
;		tood_FilledCircle dx,dy,r
	
	
		
		tood_color Rand(255) , Rand(255) , Rand(255) ,Rnd(1)
	
		tood_FilledPolygon star
	
		
		tood_color Rand(255) , Rand(255) , Rand(255) ,Rnd(1)
	
		tood_FilledPolygon tri
	
		
		tood_color Rand(255) , Rand(255) , Rand(255) ,Rnd(1) 
		
;		tood_Triangle Rand(w),Rand(h),Rand(w),Rand(h),Rand(w),Rand(h)
	
	
		tood_color Rand(255) , Rand(255) , Rand(255),Rnd(1) 		
		
;		tood_Line Rand(w),Rand(h),Rand(w),Rand(h),Rand(2,10)	

	Next
		
	tood_UnLock		

	Color 255,255,255
	Text 10,10,MilliSecs()-time


	Flip False
	
;WaitKey()

	
	
	
	
	
;	Delay lay
	
	lay=lay-20
	If lay&lt;0 Then lay=0
	


;Wend





End

Function ShadowDemo()

	Local nowtime=0
	Local demotime#=20000

	Local starttime=MilliSecs()
	Local fac#

	Local oldx#,oldy#
	Local oldx2#,oldy2#
	Local n
	tood_SetTarget TOOD_SCREEN,0 

	tood_color 0 , 0 , 0 ,0.5
	
	DrawBlock backimg ,0,0
	
	tood_Cache(TOOD_CmChacheResultImg)


	

	Repeat
	
		If KeyHit (1) Then End
		nowtime=MilliSecs()
		fac=Float(nowtime-starttime)/demotime
		
		n=1
		For pol.tood_poly=Each tood_poly
			pol\scale=200;fac*100+200
			pol\angle=Float(n)*fac*360.0;pol\angle+1;0;fac*360.0*2.0*n
			n=n+1
		Next
	
		DrawBlock backimg,0,0
		star\x=w*fac


		tood_DrawMode TOOD_DmCacheImg
		oldx#=star\x
		oldy#=star\y
		oldx2#=tri\x
		oldy2#=tri\y


		tri\x=tri\x+20
		tri\y=tri\y+20
		
		star\x=star\x+20
		star\y=star\y+20		
		
		tood_FilledPolygon star
		tood_FilledPolygon tri
		
		If fac&lt;0.5
			tood_DrawMode TOOD_DmFilled
		Else
			tood_DrawMode TOOD_DmSimpleHatch

		EndIf

		star\x=oldx
		star\y=oldy		
		tri\x=oldx2
		tri\y=oldy2		


		tood_color 255 , 0 , 100 ,0.5
		tood_FilledPolygon star
		tood_color 0 , 200 , 0 ,0.5
		tood_FilledPolygon tri


		tood_UnLock		
		
		Flip 

	Until nowtime&gt;starttime + demotime
	
End Function


Function DrawStuff()

	ClsColor 0,0,0
	;Cls

	tood_SetTarget TOOD_SCREEN,0 

	tood_DrawMode TOOD_DmFilled

	tood_color 100,100,100

	lt=2

	For x=0 To w Step 30

	
		
		tood_Line x,0,x,h, lt
		
	Next 



	tood_DrawMode TOOD_DmAlpha
	tood_Color 255,0,0,0.25
	tood_FilledCircle w,h,h

	tood_Color 0,255,0,0.1
	tood_FilledCircle 0,0,w



	For n= 0 To 180
		tood_Color Rnd(255),Rnd(255),Rnd(255),0.05	
		tood_Line 0,h,w+40,w/10+Rand(h)-h/2,h/40
	Next 


	tood_UnLock



End Function



Function FadeToColor(r,g,b,demotime#)


	Local nowtime=0


	Local starttime
	Local fac#

	;SetBuffer FrontBuffer() 
	;GrabImage backimg ,0,0
	;SetBuffer BackBuffer()	

	tood_Cache TOOD_CmChacheDestBank
	

	tood_DrawMode TOOD_DmCacheAlpha

	starttime=MilliSecs()

	Repeat
			If KeyHit (1) Then End
			



		If fac&gt;1 Then fac=1
		tood_Color r,g,b,fac

		DrawBlock backimg,0,0
		
		tood_Quad 0,0,w,0,w,h,0,h
		tood_UnLock		
	;	Cls
	;WaitKey
		Flip
		
 
		nowtime=MilliSecs()
		fac=Float(nowtime-starttime)/demotime		
		
	Until   nowtime&gt;starttime + demotime
	

	tood_FreeCache

End Function


Function PrimsDemo()

	Local scalefac=Float(w)/42.0


	Restore logodata
	
	logo.tood_Poly=New tood_poly

	logo\x=w/2
	logo\y=h/2	
	
	For n= 1 To 47
		Read x,y
		tood_AddPolyPoint logo,x-15,10-y
	Next 


	
	ClsColor 255,255,255
	

	

	
		Cls
		
		tood_SetTarget TOOD_SCREEN,0 
		
		tood_color 0,0,0,0
		
		tood_DrawMode TOOD_DmFilled
		
		lt=1
		For x= 10 To 140 Step 14
			tood_line x,10,x,h-10,lt
			lt=lt+1
		Next
		
		lt=1
		For y= 10 To 140 Step 14
			tood_line 10,y,w-10,y,lt
			lt=lt+1
		Next
		
		tood_color 200,0,0,1
		tood_FilledCircle w/4,(h/3)*2,h/4
	
	
		tood_DrawMode TOOD_DmAlpha
	
	
		tood_color 0,0,255,0.25
		tood_FilledCircle w/2,(h/3)*2,h/2
	
	tood_UnLock
	GrabImage backimg  ,0,0
	
	tood_DrawMode TOOD_DmSimpleHatch
	tood_color 0,100,0,.75

	For ang#=0 To 360 
		
		If KeyHit(1) End
			
		logo\angle=ang


		logo\scale=scalefac*(ang/360.0)
			
		DrawBlock backimg ,0,0
			
		tood_FilledPolygon logo
		
		
		
		tood_UnLock
		Flip

	Next
	
	tood_DrawMode TOOD_DmFilled	
	For n= 0 To 50
		tood_color 0,Rand(255),0,1
		logo\scale=scalefac+Rnd(0.9)
		DrawBlock backimg ,0,0
			
		tood_FilledPolygon logo
	
		tood_UnLock
		Flip

	
	Next
	
	

End Function

.logodata
Data 0,0,2,10,1,10,1,15,3,15,4,20,4,15,5,15,5,10,4,10,4,1 ; 11
Data 6,1,6,9,9,9,9,7,8,7,8,2,11,2,11,7,10,7,10,9,13,9,13,1 ;12
Data 14,1,14,9,17,9,17,7,16,7,16,2,19,2,19,7,18,7,18,9,21,9,21,1 ; 12
Data 23,1,23,20,28,20,30,18,30,2,28,0,26,0,26,2,28,2,28,18,25,18,25,0 ; 12
</textarea> <br><br></td></tr></table><br>
<a name="997733"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Beaker</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I remember this! Good stuff. <br><br></td></tr></table><br>
<a name="997755"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ginger Tea</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> one of the fades was super slow and nigh on a slide show via my onboard gfx card, im guessing its not normally the case<br><br>other than that very nice <br><br></td></tr></table><br>
<a name="997764"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, nice man. Keep 'em coming .. :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
