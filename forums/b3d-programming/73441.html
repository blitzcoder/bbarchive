<!DOCTYPE html><html lang="en" ><head ><title >The 3D grid</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >The 3D grid</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >The 3D grid</a><br><br>
<a name="820510"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BLaBZ</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Im trying to create a 3d grid but unfortunately I haven't found the brain power to pull it off yet.<br><br>Each grid square needs the capability of being hidden/deleted/removed.<br><br>Im not sure how I would create a 1000x1000 grid with each square being independently removable.<br><br>Any Ideas?<br><br>Here's my start, there's not much to it.<br><br><pre class=code>
Graphics3D 400,400,16,2

SetBuffer BackBuffer()


;;;;Grid Array//Determines how many squares in the grid
Dim mapgrid(1000,1000)
;;;;The size of each square
Global gridindex = 20




cam1 = CreateCamera()
CameraViewport cam1,0,0,400,400
light1 = CreateLight()




;;;CreateGrid Function
CreateGrid()

While Not KeyHit(1)


UpdateWorld 
RenderWorld

FPS()

Flip
Cls


Wend


;;;;;;Creates a map mesh grid
Function CreateGrid()

shroud = CreateMesh()

   s = CreateSurface(shroud)
	AddVertex s,-10,+10,+100:AddVertex s,+10,+10,+100
	AddVertex s,-10,-10,+100:AddVertex s,+10,-10,+100
	AddTriangle s,0,1,2:AddTriangle s,2,1,3


End Function


;;;;;;//FPS
Global fpstime,fpscount,fps
Function FPS()
fpscount=fpscount+1
If MilliSecs()&gt;fpstime Then 
    fpstime=MilliSecs()+1000
    fps=fpscount
    fpscount=0
EndIf 
Text 10,0,"FPS: "+fps
End Function
;;;;;\\FPS
</pre> <br><br></td></tr></table><br>
<a name="820565"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PowerPC603</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Graphics3D 800,600,0,2

SetBuffer BackBuffer()

Const MapX = 100
Const MapZ = 100
Const GridSize = 2

;;;;Grid Array//Determines how many squares in the grid
Dim mapgrid(MapX,MapZ)

For z = 0 To MapZ-1
	For x = 0 To MapX-1
		mapgrid(x,z) = Rand(0,1)
	Next
Next

cam1 = CreateCamera()
AmbientLight 255, 255, 255
PositionEntity cam1, 0, 20, 0

gridmesh = CreateGrid()

While Not KeyHit(1)
	If KeyDown(200) Then MoveEntity cam1, 0, 0, 1
	If KeyDown(208) Then MoveEntity cam1, 0, 0, -1
	If KeyDown(203) Then TurnEntity cam1, 0, 1, 0
	If KeyDown(205) Then TurnEntity cam1, 0, -1, 0

	; If you want to test mesh-generating speed, replace keyhit(57) by keydown(57), then every frame a new random mesh will be generated
	; while holding down the spacebar
	If KeyHit(57) Then
		; Create a random tiled map (place new random numbers in the array)
		For z = 0 To MapZ-1
			For x = 0 To MapX-1
				mapgrid(x,z) = Rand(0,1)
			Next
		Next

		; Remove the mesh from memory
		FreeEntity gridmesh
		; Re-create the mesh
		CreateGrid()
	EndIf

	UpdateWorld 
	RenderWorld

	FPS()
	Text 10, 20, "Tris rendered: " + TrisRendered()

	Flip
	Cls
Wend



Function CreateGrid()
	Local mesh = CreateMesh()
	Local surf = CreateSurface(mesh)

	For z = 0 To MapZ-1
		For X = 0 To MapX-1
			If mapgrid(x,z) = 1 Then
				CreateTile(surf,x,z)
			EndIf
		Next
	Next

	Return mesh
End Function

Function CreateTile(surf, x, z)
	v0 = AddVertex(surf, (x*GridSize)+((GridSize/2)*(-1)), 0, (z*GridSize)+((GridSize/2)))
	v1 = AddVertex(surf, (x*GridSize)+((GridSize/2)), 0, (z*GridSize)+((GridSize/2)))
	v2 = AddVertex(surf, (x*GridSize)+((GridSize/2)), 0, (z*GridSize)+((GridSize/2)*(-1)))
	v3 = AddVertex(surf, (x*GridSize)+((GridSize/2)*(-1)), 0, (z*GridSize)+((GridSize/2)*(-1)))
	t0 = AddTriangle(surf, v0, v1, v3)
	t1 = AddTriangle(surf, v1, v2, v3)
End Function



;;;;;;//FPS
Global fpstime,fpscount,fps

Function FPS()
	fpscount=fpscount+1
	If MilliSecs()&gt;fpstime Then 
		fpstime=MilliSecs()+1000
		fps=fpscount
		fpscount=0
	EndIf 
	Text 10,0,"FPS: "+fps
End Function
;;;;;\\FPS
</pre><br>This code generates a mesh with size 100x100 (1000x1000 creates a MAV, because meshes are limited to 65535 vertices) as one big mesh.<br>It creates one mesh and adds vertices to it to generate the entire mesh, based on the value in the corresponding index in the array.<br>First the code places random values (0 or 1) in the array.<br>The the CreateGrid loops through the array and if it sees "1", it creates a tile in the big mesh.<br>If it's 0, do nothing.<br><br>You can move the camera using the up/down arrowkeys, and turn the camera using left/right arrowkeys.<br>When you hit the spacebar, the program places new random values in the array, frees the current big mesh and re-creates the mesh.<br>You will see that this works very fast, as continued hitting the spacebar doesn't even slow down.<br>If you want to create a map of size 1000x1000, I suggest that you create 100 big meshes, each of size 100x100, and place them next to eachother.<br>This way you reduce surface-count (this big mesh has only 1 surface).<br>If you where to create 1000x1000 seperate meshes, then you would have 1000.000 meshes and thus 1000.000 surfaces and your computer will crawl while rendering that much surfaces. <br><br></td></tr></table><br>
<a name="820580"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BLaBZ</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the code, you must have been doing this for awhile. <br><br>Some of the triangles appear backwards and show up black. I messed around for a few hours and managed to get what I was looking for.<br><br>Something like this<br><br><pre class=code>

Graphics3D 400,400,16,2

SetBuffer BackBuffer()

;;;;The size of each square
Global gridindex# = 20.0;;20.0 is the actual size, divided by 2 to represent the + side of the x/y axis and the - side of x/y
Global gridsize = (gridindex#/2.0)
Global gridx = 20,gridy = 10;;;;how many grid squares

;;;Use this to center the entire grid to the screen
Global centerx = (gridx*gridsize)/2
Global centery = (gridy*gridsize)/2

;;;;Grid Type
Type grid
	Field mesh
	Field surface
	Field ispicked
End Type 
;;;;Grid Array//Determines how many squares in the grid
Dim mapgrid(gridx,gridy)


;Environmental variables
Global cam1,light1
;setup camera and light
cam1 = CreateCamera()
CameraViewport cam1,0,0,400,400
light1 = CreateLight()




;;;CreateGrid Function
CreateGrid(gridsize)

While Not KeyHit(1)


PickAndKill()

UpdateWorld 
RenderWorld

FPS()

 
Flip
Cls

Wend

;;;;create grid according to dimensions
Function CreateGrid(size)
For x = 1 To (gridx - 1)
	For y = 1 To (gridy - 1)
		newgrid.grid = New Grid
		newgrid\mesh = CreateMesh()
		newgrid\surface = CreateSurface(newgrid\mesh)
	
		AddVertex newgrid\surface,-size,+size,+100:AddVertex newgrid\surface,+size,+size,+100
		AddVertex newgrid\surface,-size,-size,+100:AddVertex newgrid\surface,+size,-size,+100
		AddTriangle newgrid\surface,0,1,2:AddTriangle newgrid\surface,2,1,3
	
	
		PositionEntity newgrid\mesh,(x*gridsize) - centerx,(y*gridsize) - centery,100
		EntityPickMode newgrid\mesh,2
	Next
	
Next 

End Function 

;;Click and kill each grid square
Function PickAndKill()
CameraPick(cam1,MouseX(),MouseY())
PickedEnt = PickedEntity()
	If MouseDown(1)
		If PickedEnt &lt;&gt; 0 
			For tile.grid = Each grid
				If PickedEnt = tile\mesh
					FreeEntity tile\mesh				
			EndIf
		Next
	EndIf
EndIf
End Function

;;;;;;//FPS
Global fpstime,fpscount,fps
Function FPS()
fpscount=fpscount+1
If MilliSecs()&gt;fpstime Then 
    fpstime=MilliSecs()+1000
    fps=fpscount
    fpscount=0
EndIf 
Text 10,0,"FPS: "+fps
End Function
;;;;;\\FPS
</pre><br><br>Yours appears to render much faster then this code. I'm wondering if I could some how get the effect and visuals of this code with the speed of yours? <br><br></td></tr></table><br>
<a name="820592"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PowerPC603</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually, I began with this just a week ago. :-)<br><br>This code adds picking to the program.<br>It creates an infinity plane which is pickable, but entityalpha is set to 0 to make in invisible.<br>I did it this way to be able to add tiles to the big mesh too, instead of only removing them.<br><pre class=code>
Graphics3D 800,600,0,2

SetBuffer BackBuffer()

Const MapX = 100
Const MapZ = 100
Const GridSize = 2

;;;;Grid Array//Determines how many squares in the grid
Dim mapgrid(MapX,MapZ)

For z = 0 To MapZ-1
	For x = 0 To MapX-1
		mapgrid(x,z) = Rand(0,1)
	Next
Next

; Create an endless plane (used to do the picking), move it down a bit, set a different color and set pickmode
plane = CreatePlane()
MoveEntity plane, 0, -0.1, 0
EntityColor plane, 255, 0, 0
EntityPickMode plane, 2
EntityAlpha plane, 0

; Create camera and position it above the world
cam1 = CreateCamera()
AmbientLight 255, 255, 255
PositionEntity cam1, 100, 100, 100
TurnEntity cam1, 90, 0, 0

; Create a big mesh where each tile is created based on the value in the array
Global gridmesh = CreateGrid()
Global texture = LoadTexture("texture.jpg")
EntityTexture gridmesh, texture

While Not KeyHit(1)
	If KeyDown(203) Then MoveEntity cam1, -1, 0, 0
	If KeyDown(205) Then MoveEntity cam1, 1, 0, 0
	If KeyDown(200) Then MoveEntity cam1, 0, 1, 0
	If KeyDown(208) Then MoveEntity cam1, 0, -1, 0
	If KeyDown(201) Then MoveEntity cam1, 0, 0, 1
	If KeyDown(209) Then MoveEntity cam1, 0, 0, -1

	If MouseHit(1) Then
		PickedEnt = CameraPick(cam1, MouseX(), MouseY())
		; Calculate which tile to be switched on/off
		x = PickedX() / GridSize
		z = PickedZ() / GridSize
		If (x&gt;= 0) And (x&lt;=MapX) And (z&gt;=0) And (z&lt;=MapZ) Then
			; Toggle the tile on/off
			If mapgrid(x,z) = 1 Then mapgrid(x,z) = 0 Else mapgrid(x,z) = 1
			; Delete the big mesh
			FreeEntity gridmesh
			; Re-create the big mesh
			gridmesh = CreateGrid()
			; Apply the texture to it
			EntityTexture gridmesh, texture
		EndIf
	EndIf

	UpdateWorld 
	RenderWorld

	FPS()
	Text 10, 20, "Tris rendered: " + TrisRendered()

	Flip
	Cls
Wend



Function CreateGrid()
	Local mesh = CreateMesh()
	Local surf = CreateSurface(mesh)

	For z = 0 To MapZ-1
		For X = 0 To MapX-1
			If mapgrid(x,z) = 1 Then
				CreateTile(surf,x,z)
			EndIf
		Next
	Next

	Return mesh
End Function

Function CreateTile(surf, x, z)
	v0 = AddVertex(surf, (x*GridSize)+((GridSize/2)*(-1)), 0, (z*GridSize)+((GridSize/2)), 0, 0)
	v1 = AddVertex(surf, (x*GridSize)+((GridSize/2)), 0, (z*GridSize)+((GridSize/2)), 1, 0)
	v2 = AddVertex(surf, (x*GridSize)+((GridSize/2)), 0, (z*GridSize)+((GridSize/2)*(-1)), 1, 1)
	v3 = AddVertex(surf, (x*GridSize)+((GridSize/2)*(-1)), 0, (z*GridSize)+((GridSize/2)*(-1)), 0, 1)
	t0 = AddTriangle(surf, v0, v1, v3)
	t1 = AddTriangle(surf, v1, v2, v3)
End Function



;;;;;;//FPS
Global fpstime,fpscount,fps

Function FPS()
	fpscount=fpscount+1
	If MilliSecs()&gt;fpstime Then 
		fpstime=MilliSecs()+1000
		fps=fpscount
		fpscount=0
	EndIf 
	Text 10,0,"FPS: "+fps
End Function
;;;;;\\FPS
</pre><br><br>By a quick peek at your code, it seems that you create each tile as a seperate mesh, that's why your code renders slower, as it has much more seperate meshes and seperate surfaces.<br><br>My code creates one big mesh, and each tile's vertices and triangles are added to it.<br>It also adds UV coords to the vertices and applies a texture on the mesh.<br><br>Now the camera floats above the mesh, looking down.<br>Move the camera with up/down/left.right, and zoom in/out using pageup/pagedown.<br>Clicking will toggle the tile on/off. <br><br></td></tr></table><br>
<a name="820609"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BLaBZ</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is excellent, its taking me awhile to read through and understand everything but this is exactly what I need.<br><br>Out of curiosity, do you interpret code pretty quickly since you started coding? <br><br></td></tr></table><br>
<a name="820611"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PowerPC603</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> I programmed some stuff in Blitz3D and Visual Basic and I've been learning about programming since I was 8 years old (now 28).<br>I didn't use all my free time to code though, but I learn stuff quickly that makes sense.<br>Some things are chinese to me too, but after some testing, most of the time I start to grasp it.<br><br>And I'm thinking of creating a game which requires the same stuff as I've posted here, but a bit more difficult than this.<br>In my game, I need to generate the terrain while you play, same as the roads and buildings (dynamic loading like GTA does) as I hate loading screens. :-)<br><br>Imagine a game where you drive through a city and suddenly you get a loading screen in front of you, that would be annoying, don't you think?<br>When the loading has finished, the game goes on and you're momentarily distracted, that wouldn't make a good game I think.<br><br>Last week, I've been searching some options about how to dynamically load my terrain, road-system and much more and creating test code.<br>I've also been looking into pathfinding and came up with my own system to find a path via roads to my destination, but I need to convert the idea into code, which may take some time.<br><br>I'm a technician in RL, so analysing problems and coming up with a solution is what I do.<br>The same with programming.<br>I started with code like yours (lots of different meshes) and my framerate was down from 999 fps (1 tile generated) to 30 (2500 seperate tiles).<br>And then I only generated the terrain, no roads, buildings, trees or vehicles yet.<br>I remembered something about keeping your surface-count as low as you can to increase framerate, so I began thinking of merging several tiles into one bigger mesh.<br><br>My game also needs to give the player the ability to build new factories, which require and produce goods.<br>Then you need trucks to transport those goods from one factory to another.<br>The player must also be able to add/remove roads to connect the buildings to the existing roads.<br>Building tunnels and bridges must also be possible.<br>When you create a tunnel, then the terraintile where the tunnel entrance and exit are located, may not be generated, so I cannot create a map in a modelling package and just load it.<br>While generating the terrain, I must keep track if there is a tunnel entrance or exit on that tile, and if so, don't create a tile, as the terrain has collisions and would block the tunnel entrance/exit.<br>I have to have a system which holds the entire map into memory and create the models on-the-fly.<br>I also did find a way to reduce memory requirements.<br>I had an array: Map.Tile(1000,1000).<br>After creating all type-instances to fill the array, the program required 46Mb RAM. This was only after creating the array and the type-instances, no 3D-stuff or anything else was created.<br>The Tile type has only 3 fields which define each tile of the terrain, but only needs values from 0 - 200, so bytes would suffice.<br>But Blitz doesn't have byte-variables, only integers, floats and strings.<br><br>So for a map of size 1000x1000 and 3 bytes to define the tile, I tested memory-usage with banks, which I've never used before. The bank now has a size of 3.000.000 bytes, which is all I need to define all properties of each tile on the map.<br>Now memory-usage was only 12Mb RAM, so I'll be using 34Mb RAM less than before and it holds exactly the same data. <br><br></td></tr></table><br>
<a name="820746"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BLaBZ</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thats very interesting. So basically your terrain and environment are all going to be produced by built in algorithms?   <br><br>The thought of building a GTA style like game cramps my brain. I can't imagine........<br><br>Have you ever considered programming in c++? Just out of curiosity, do you think Blitz is much much faster to work with then c++? <br><br></td></tr></table><br>
<a name="820774"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PowerPC603</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, my entire 3D-world will be generated while you play using built-in algorithms.<br>Maybe that some buildings will be modelled in some modelling-tool, but I'm not sure about that yet.<br>But I'm 100% sure about the terrain-generating and road-construction, they will be generated by algorithms, as they need to be modified frequently.<br>If the game would not allow you to build your own roads, then I could load them from disk.<br>But now, when you have a straight road and place a road tile next to it, that straight road needs to be transformed into a T-junction, so I can't have pre-made models.<br><br>If you ever played Transport Tycoon, this is what I want to create, but in 3D where you can drive your own vehicle while other vehicles (that you own) transport goods from one factory to another.<br>Plus you can do GTA-style missions and a lot more.<br>It's actually a combination of Transport Tycoon, GTA and X2 (a spacesim from Egosoft).<br>Not that you'll be able to fly your own spacecraft, but the trading system (dynamic prices for goods based on stock) and missions will be in my game.<br>Those missions where given to you (in X2) in spacestations and required you to bring some goods (microchips for example) to another factory because of a computer-malfunction or something similar.<br>Or they wanted you to get some goods, because their stock is low. They paid you the maximum price per unit you delivered plus some bonus if you delivered the goods within the timelimit. Something like that.<br><br>I've seen a bit about C++ on highschool, but the syntax is quite complex for me.<br>Once on this forum I read that you need several pages of code in C++ just to generate a 3D-capable fullscreen engine, where Blitz only needs 1 command.<br>And that loading 3D-models is a lot more code too, unless you search the web for some finished code to do what you want.<br>If that's true, I don't know, but I know that C++ is something too complex for me.<br><br>A while back, they did some speed-comparisons between C++ and Blitz (like counting to 1.000.000) and the results where about the same, so I guess Blitz3D is fast enough and easy to use.<br><br>For me, working with Blitz is faster than c++, because I know a lot about Blitz3D and almost nothing about C++.<br>Converting to C++ would require me to learn another programming-style and syntax, while I'm already working with 2 programming environments (Blitz and Visual Basic). <br><br></td></tr></table><br>
<a name="820782"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BLaBZ</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thats cool. Our interests sound similar. I've actually been working quite a bit with Visual Basic Express 2005 lately. <br><br>Do you spend a lot of time pondering about problem solving and algorithms? <br><br>Have you started writing this game or are you in the planning stage?<br><br>I'm writing an rts engine thats completely customizable from a windows gui, which will be created in visual basic. <br>One thing I've been planning is.....<br>Visual Basic will write to a text file which will then be read by the engine. But I'd like to make something more "tamper proof." For instance create my own type of encryption. Or even better, create a file format thats encrypted. Any ideas?<br><br>If I were to change the extension to a text file, for example.. to .rts, would blitz still be able to read it as a text file??? <br><br></td></tr></table><br>
<a name="821004"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PowerPC603</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I spend a lot of time about problem solving, that's correct.<br>Even at work, when I'm doing my job, I'm busy thinking about how to do things in my game (and still being able to do my job good too).<br><br>My game is still in the planning stage, but I've started already with writing some test-code to see if the things I need can be done.<br>For now, everything I've tested works perfectly.<br><br>I've written some database programs within VB6, but they just use plain ASCII-text files, no encryption (yet), because all these programs will be used by myself only, so I can't help you with encryption algorithms.<br><br>Even if you removed the extension from a file, Blitz can read it as a normal text-file.<br>Blitz can read any file, just as long as you know how data is stored in the file to read it properly, otherwise you get some strange results (wrong data).<br>If you would write an integer to a file in binary, you write 4 bytes. If you were to read it as plain text, then your data is screwed up.<br><br>When writing data to a file, write it in binary.<br>When  people see some strange stuff in your file (not plain readable text), most people don't touch that file.<br>They would like to change some things when the file was a plain ASCII-file that can be read using Notepad and see how it impacts the game/program.<br><br>Image a game where you need money to buy stuff and you save the game as a plain text-file like this:<br>LocationX = 2569<br>LocationY = 4888<br>LocationZ = 471<br>Money = 19<br><br>Here most people will change the Money value to some greater value, load their game and say: hey, I've got lots of money now, easy.<br><br>But if you write the values in binary mode, then they would need a Hex-editor to figure out where the money-value is stored, how it's stored (most significant bit first or last) and then change the value in hex.<br>That's a lot more difficult, so most people don't even try it, because they need to do a lot of work, just to figure out IF they can change their money in the game. <br><br></td></tr></table><br>
<a name="840144"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FBEpyon</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is very interesting I also have been looking for code like this.<br><br>I was wondering if this could be used to make a 3D terrain system to make heightmaps..<br><br>If so could you make a example I have been trying to do this for weeks!!! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
