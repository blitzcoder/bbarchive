<!DOCTYPE html><html lang="en" ><head ><title >sending a long string in chunks to a type field</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >sending a long string in chunks to a type field</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >sending a long string in chunks to a type field</a><br><br>
<a name="1283645"></a>

<a name="1283646"></a>

<a name="1283647"></a>

<a name="1283648"></a>

<a name="1283649"></a>

<a name="1283667"></a>

<a name="1283668"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >stayne</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I need to send each part (string) of a long sentence, that will be word-wrapped, to a type field then draw them with Draw3D's Text3D function since the latest version doesn't offer word wrap.  I'm going to include the two functions I have lined up for this:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Type msg
	Field s$
	Field col
	Field timer
End Type

Function UpdateChat()

	If KeyDown(56) ;quick hack to drag the text around
		chatx = MouseX()-GraphicsWidth()/2
		chaty = -(MouseY()-GraphicsHeight()/2)
	EndIf

	tel = 0
	For m.msg = Each msg
		Text3D(Font,chatx,chaty-tel * textheight - scroll,m\s$,0)
		tel = tel + 1
		If tel = 14 Then Exit
	Next
	
	If scrollmode Then
		If scroll &lt;= chatscroll Then scroll = scroll + 8;speed
		If scroll &gt; chatscroll Then 
			scroll = 0
			Delete First msg
			scrollmode = 0
			chatscroll = 0
		End If
	Else
		If chatscroll = 0 Then If tel &gt; 13 Then chatscroll = chatscroll + textheight: scrollmode = 1
	End If
	
	;For m.msg = Each msg
	;	If m\timer &lt; MilliSecs() Then Delete m
	;Next

End Function

;-----------------------------------------------------------------------------------------------------
;													AddChat()
;-----------------------------------------------------------------------------------------------------
Function AddChat(s$)

	m.msg = New msg
	m\s$ = s$
	m\timer = MilliSecs() + 10000

End Function
</textarea><br><br>I'm adding sentences via AddChat and some of them are pretty long.  So...any ideas to break the strings up and wrap them?  Sorry this is a bit enigmatic. <br><br></td></tr></table><br>
<a name="1283716"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> With INSTR() you can look for a SPACE character With LEFT() and MID() you can divide the string.<br><br><pre class=code>Test$="abc def ghijk l mno"
Here%=0
Part$=""
Repeat 
    Here=Instr(Test," ")
    If Here=0
        Part=Test
    Else
       Part= Left(Test,Here-1)
    EndIf
    Print "PART=" + Part 
    Test=Mid(Test,Here+1)
Until Here=0</pre> <br><br>If you want to cut sentences longer than f.e. 30 characters, add a  OFFSET test:<br><pre class=code>Test$="abc def ghijk l mno  fjgj jgjr rjghj jjtiu djjgj djfjg jglvjbls  jjgj esk"
Here%=0
Offset%=30
Part$=""
Repeat 
    NextHere%=0
    Here=0
    Repeat
        Here=Instr(Test," ",Here+1)
        NextHere=Instr(Test," ",Here+1)
    Until NextHere&gt;Offset Or Here=0 Or NextHere=0
    If NextHere=0
        Part=Test
    Else
       Part= Left(Test,Here-1)
    EndIf
    Print "PART=" + Part + " (LEN=" + Len(Part) +")"
    Test=Mid(Test,Here+1)
Until NextHere=0</pre> <br><br></td></tr></table><br>
<a name="1283719"></a>

<a name="1283720"></a>

<a name="1283721"></a>

<a name="1283722"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> here is two methods to split sentence<br>the first use only the width of the chatbox<br>the second use logic splitters to split sentence while preserving words at best. (uses ., and space, but you can add :; etc ...)<br><br>You might want to fit MSG_MAX_LINES to your needs. Basically, it's the maximum number of lines available for a message.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Global lorem_ipsum1$	= "  Lorem ipsum dolor sit amet, consectetur adipiscing elit. Sed non risus. Suspendisse lectus tortor,"
lorem_ipsum1=lorem_ipsum1+" dignissim sit amet, adipiscing nec, ultricies sed, dolor. Cras elementum ultrices diam. Maecenas"
lorem_ipsum1=lorem_ipsum1+" ligula massa, varius a, semper congue, euismod non, mi. Proin porttitor, orci nec nonummy molestie,"
lorem_ipsum1=lorem_ipsum1+" enim est eleifend mi, non fermentum diam nisl sit amet erat. Duis semper. Duis arcu massa, scelerisque"
lorem_ipsum1=lorem_ipsum1+" vitae, consequat in, pretium a, enim. Pellentesque congue. Ut in risus volutpat libero pharetra tempor."
Global lorem_ipsum2$	=" Cras vestibulum bibendum augue. Praesent egestas leo in pede. Praesent blandit odio eu enim."
lorem_ipsum2=lorem_ipsum2+" Pellentesque sed dui ut augue blandit sodales. Vestibulum ante ipsum primis in faucibus orci luctus"
lorem_ipsum2=lorem_ipsum2+" et ultrices posuere cubilia Curae; Aliquam nibh. Mauris ac mauris sed pede pellentesque fermentum."
lorem_ipsum2=lorem_ipsum2+" Maecenas adipiscing ante non diam sodales hendrerit."
lorem_ipsum2=lorem_ipsum2+"Ut velit mauris, egestas sed, gravida nec, ornare ut, mi. Aenean ut orci vel massa suscipit pulvinar."
Global lorem_ipsum3$	=" Nulla sollicitudin. Fusce varius, ligula non tempus aliquam, nunc turpis ullamcorper nibh, in tempus"
lorem_ipsum3=lorem_ipsum3+" sapien eros vitae ligula. Pellentesque rhoncus nunc et augue. Integer id felis. Curabitur aliquet"
lorem_ipsum3=lorem_ipsum3+" pellentesque diam. Integer quis metus vitae elit lobortis egestas. Lorem ipsum dolor sit amet,"
lorem_ipsum3=lorem_ipsum3+" consectetuer adipiscing elit. Morbi vel erat non mauris convallis vehicula. Nulla et sapien. Integer"
lorem_ipsum3=lorem_ipsum3+" tortor tellus, aliquam faucibus, convallis id, congue eu, quam. Mauris ullamcorper felis vitae erat."
Global lorem_ipsum4$	=" Proin feugiat, augue non elementum posuere, metus purus iaculis lectus, et tristique ligula justo vitae"
lorem_ipsum4=lorem_ipsum4+" magna. Aliquam convallis sollicitudin purus. Praesent aliquam, enim at fermentum mollis, ligula massa"
lorem_ipsum4=lorem_ipsum4+" adipiscing nisl, ac euismod nibh nisl eu lectus. Fusce vulputate sem at sapien. Vivamus leo. Aliquam"
lorem_ipsum4=lorem_ipsum4+" euismod libero eu enim. Nulla nec felis sed leo placerat imperdiet. Aenean suscipit nulla in justo."
lorem_ipsum4=lorem_ipsum4+" Suspendisse cursus rutrum augue. Nulla tincidunt tincidunt mi. Curabitur iaculis, lorem vel rhoncus"
lorem_ipsum4=lorem_ipsum4+" faucibus, felis magna fermentum augue, et ultricies lacus lorem varius purus. Curabitur eu amet."

Const MSG_MAX_LINES% = 8
Type MSG
	Field s$[MSG_MAX_LINES]
	Field count%
End Type

; don't care about the words as words, just care about the width
Function addMsg(s$, chatwidth%)
	
	If Trim(s)="" Then Return;
	
	Local m.MSG = New MSG;
	Local LS% = Len(s);
	Local offsetL%=1;
	Local p1$="", pw, i%, c$, cs%
	
	Repeat
		p1 = "" : pw = 0;
		For offsetL = offsetL To LS
			c = Mid(s, offsetL,1) : cs% = StringWidth(c);
			If pw + cs&gt;=chatwidth Then Exit;
			pw = pw + cs : p1 = p1+c;
		Next
		
		m\count=m\count+1
		m\s[m\count-1]=p1;
		If m\count&gt;=MSG_MAX_LINES Then Exit;
		
	Until offsetL&gt;=LS;
	
End Function

; uses the ".", "," or " " as splitter for words
Function addMsg2(s$, chatwidth%)
	
	If Trim(s)="" Then Return;
	Local AOffset = chatwidth/StringWidth("A") - 1;
	Local BOffset = AOffset-15; '15' is an arbitrary number of chars at the end of the line to search for special characters.
	
	Local m.MSG = New MSG;
	
	While Len(s)&gt;AOffset
		
		Local partLeft$ = "";
		Local found% = False;
		Local ofs = 0;
		For c = AOffset To BOffset Step -1
			Select Mid(s, c,1)
			Case " ", ",", "."
				ofs = c;
				Exit;
			End Select
		Next
		
		If ofs&gt;0
			partLeft = Left(s, ofs-1);
			s = Right(s, Len(s)-ofs+1);
		Else
			partLeft = Left(s, AOffset-1)+"-";
			s = Right(s, Len(s)-AOffset+1);
		EndIf;
		
		m\count=m\count+1
		m\s[m\count-1]=partLeft;
		If m\count&gt;=MSG_MAX_LINES Then Return;
		
	Wend;
	
	m\count=m\count+1
	m\s[m\count-1]=s;
	
End Function

Const GW = 900, GH=600
Graphics GW,GH,0,2
SetBuffer BackBuffer()

Color 100,100,100
Rect 10,10,GW-20,GH-20,1
Color 255,128,000
Rect 10,10,GW-20,GH-20,0

Color 255,255,100

addMsg2(lorem_ipsum1, GW-40)
Local m.MSG
Local y% = 25
For m = Each MSG
	For n = 0 To m\count-1
		Text 20,y, m\s[n]
		y=y+15
	Next
Next;

Flip (True);

WaitKey();
End;
</textarea><br><br><br>[EDIT]<br>Damned ! 15 minutes too late :) <br><br></td></tr></table><br>
<a name="1283726"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Do you know how much room in pixels you have to display the text?<br>Do you know how wide the text string is in pixels?<br>If you know the string is 100 pixels wide but you have only 30 pixels display area for example simply find the first tab or space prior to '30/100' fraction of the string length in and cut it there and repeat with the remainder of the string.....basic idea/premise behind a solution. <br><br></td></tr></table><br>
<a name="1283734"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >stayne</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks a lot guys I'll dig into this later this afternoon. <br><br></td></tr></table><br>
<a name="1283743"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >stayne</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Midimaster I am having an issue with strings like this:<br><br>AddChat("&gt; "+"&lt;#f00&gt;"+c\name+"&lt;#fff&gt;"+" hit "+c\attacktargetname+" for "+c\damage+" points of damage!")<br><br>Is your code parsing +...+ strings as well? <br><br></td></tr></table><br>
<a name="1283766"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> If there is an "+" in the string it will be handled as a normal character...<br><br><br>Or do you mean, whether also the "+" can be the point, where the string should be divided? Yes, that is possible. <br><pre class=code>Here=Instr(Test,"+")</pre><br><br><br>But your sample strings will not contain any "+" at the end:<br>the command...<br><pre class=code>"&gt; "+"&lt;#f00&gt;"+c\name+"&lt;#fff&gt;"+" hit "+c\attacktargetname+" for "+c\damage+" points of damage!"</pre><br>becomes this strings....<br><pre class=code>"&gt; &lt;#f00&gt;STAYNE&lt;#fff&gt; hit PETER for 999 points of damage!"</pre><br><br>If you want to devide the strings on exaclty the old points, insert a new separator sign:<br><pre class=code>"&gt; |"+"&lt;#f00&gt;|"+c\name+"|&lt;#fff&gt;|"+" hit |"+c\attacktargetname+"| for |"+c\damage+"| points of damage!"</pre><br><br>now search for the "|"<br><pre class=code>Here=Instr(Test,"|")</pre> <br><br></td></tr></table><br>
<a name="1283783"></a>

<a name="1283787"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >stayne</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's wrapping the string like so:<br><br>Guard Stayne<br>hit soandso for 4 points of damage.<br><br>Still working at it...thanks man.  The &lt;#xxx&gt; entries are for Text3D color.  I have a feeling this function is parsing those as well. <br><br></td></tr></table><br>
<a name="1283788"></a>

<a name="1283789"></a>

<a name="1283790"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> both its code and mine don't care about "invisible" tags, so it's logical that the text is cut too early.<br>If you want to split only according to the "real text", you'll have to parse all the string and map the valid characters.<br>A simple tokenizer should do the job pretty well.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Global txt_1$	= " Let's Start with Some default color text, &lt;#F80&gt;Then switch to a nice orange one"
txt_1	=	txt_1+", which should continue on the next line !&lt;#08F&gt;And this is a fantastic light blue&lt;#FFF&gt; And end the first chapter with whyte text."
Global txt_2$	= "&lt;#f00&gt;|Marlon Brando|&lt;#fff&gt;|"+" hit &lt;#f00&gt;|a purple mouse|&lt;#fff&gt; for |0.002| points of damage!"

Const MSG_MAX_LINES% = 258
Type MSG
	Field s$[MSG_MAX_LINES]
	Field count%
End Type

Type tWord
	Field s$
End Type
Function TWord.tWord(v$)
	If v="" Then Return Null;
	Local w.tWord=New tWord:w\s=v:Return w;
End Function

; uses the ".", "," or " " as splitter for words
Function addMsg(s$, chatwidth%)
	
	If Trim(s)="" Then Return;
	Local AOffset = chatwidth/StringWidth("A") - 1;
	Local BOffset = AOffset-15; '15' is an arbitrary number of chars at the end of the line to search for special characters.
	
	Delete Each tWord;
	Local w.tWord;
	Local ln = Len(s):
	Local b%, c$
	Local cur$= ""
	For b = 1 To ln
		c = Mid(s, b,1)
		Select c
			Case "|"
				; skip this ... I don't know why you need this separator
			Case " ", ".", ":", ";", ",", "&gt;", "!", "?";, "[", "]", "(", ")"
				TWord(cur):cur=""; ad current word
				TWord(c); add special char
			Case "&lt;"
				TWord(cur):cur=""; add current word
				Local tag=False;
				If b&lt;ln-5
					If Mid(s,b+1,1)="#"
						If Mid(s,b+5,1)="&gt;"
							TWord(Mid(s,b,6));
							b=b+5;
							tag=True; add color tag
						EndIf;
					EndIf;
				EndIf;
				If Not(tag) Then cur=cur+c;
			Default
				cur=cur+c
		End Select
	Next;
	TWord(cur):cur="";
	
	Local m.MSG = New MSG;
	
	Local l%=0, curL$=""
	Local curtag$="";
	
	For w = Each tWord
		If Left(w\s,2) = "&lt;#"
			curtag = w\s;
			curL = curL + w\s;
		Else
			If l + Len(w\s) &gt; AOffset
				m\s[m\count] = curL;
				m\count=m\count+1;
				curL = curtag+w\s;
				l=Len(w\s);
			Else
				curL=curL+w\s;
				l = l + Len(w\s);
			EndIf;
		EndIf;
	Next;
	
	If curL&lt;&gt;""
		m\count=m\count+1
		m\s[m\count-1]=curL;
	EndIf;
	
End Function

Function HexInt%(h$)
	Local a=Asc(Lower(h));
	If a&gt;=Asc("a") And a&lt;=Asc("f") Then Return 10 + a-Asc("a");
	Return a-Asc("0");
End Function

Function tagColor(tag$)
	Color 16*HexInt(Left(tag,1)),16*HexInt(Mid(tag,2,1)),16*HexInt(Right(tag,1));
End Function

Function DrawText3D(x,y,t$)
	Local pos, px=0
	Local tx$=""
	For pos = 1 To Len(t)
		If Mid(t,pos,2)="&lt;#"
			If tx&lt;&gt;"" Then Text x+px,y, tx: px=px+StringWidth(tx): tx="";
			tagColor Mid(t, pos+2,3);
			pos=pos+5
		Else
			tx=tx+Mid(t,pos,1);
		EndIf;
	Next;
	If tx&lt;&gt;"" Then Text x+px,y, tx: px=px+StringWidth(tx): tx="";
End Function

Const GW = 600, GH=300
Graphics GW,GH,0,2
SetBuffer BackBuffer()

Color 50,50,50
Rect 10,10,GW-20,GH-20,1
Color 255,128,000
Rect 10,10,GW-20,GH-20,0

Color 255,255,100

addMsg(txt_1, GW-40)
addMsg(txt_2, GW-40)

Local m.MSG
Local y% = 25
For m = Each MSG
	For n = 0 To m\count-1
		DrawText3D (20,y, m\s[n])
		y=y+15
	Next
Next;

Flip (True);

WaitKey();
End;
</textarea><br><br>ps : Don't judge me !<br>Nobody like purple mice ! <br><br></td></tr></table><br>
<a name="1283795"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >stayne</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Bobysait you have some serious skills there my friend. <br><br></td></tr></table><br>
<a name="1283805"></a>

<a name="1283809"></a>

<a name="1283810"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, but I don't pretend to own real skills, it's just experience.<br>I've been programming with blitz3d for more than 10 years.<br>Like many others here (*), almost everything asked on the forum, we have already experienced it.<br><br>BTW : Hope the code will help<br><br><br><br><br>(*) except for Xylvan ... He was naturally born good. So, since he began to code, he keeps on doing the same candy world, again and again and again ^_^<br>private joke ... I could (and/or should) have skip this one as it's totally free and irelevant ... but, you know ...  :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
