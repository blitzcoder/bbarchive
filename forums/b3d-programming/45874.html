<!DOCTYPE html><html lang="en" ><head ><title >How Can I Select A Vertex With The Mouse</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >How Can I Select A Vertex With The Mouse</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >How Can I Select A Vertex With The Mouse</a><br><br>
<a name="509975"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Delerna</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> What happened to the PickedVertex command???<br><br>I can select a surface and a triangle with the mouse through PickedSurface and PickedTriangle but can someone please educate me on how to do the same with vertices<br><br>Thanks <br><br></td></tr></table><br>
<a name="509981"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DJWoodgate</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You find the minimum distance vertex for the picked triangle.  Tformpoint the picked x,y,z to local space and check each vertex of the picked triangle to find the closest vertex to this point. <br><br></td></tr></table><br>
<a name="512115"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Delerna</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK tried that David and it works fine, thanks for the input <br><br></td></tr></table><br>
<a name="512215"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just in case anyone else wants a way to do this:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; -----------------------------------------------------------------------------
; Vertex projection functions...
; -----------------------------------------------------------------------------

; -----------------------------------------------------------------------------
; WHAT'S THIS?
; -----------------------------------------------------------------------------
; 	Convert vertex co-ordinates within mesh into global co-ordinates.
; 	Convert vertex 3D co-ordinates to 2D co-ordinates.
; 	Pick nearest vertex when a mesh is clicked on.
; -----------------------------------------------------------------------------

; The main functions of interest here are:

; -----------------------------------------------------------------------------
; MakePickable		--	makes a mesh 'pickable' by PickNearestVertex (and the
; 						native Blitz 'pick' commands).
; -----------------------------------------------------------------------------

; -----------------------------------------------------------------------------
; PickNearestVertex --  gets surface and index of nearest vertex to requested
; 						2D x/y position if within requested mesh. If this returns
; 						True, you can then use the global variables V_PickedMesh,
; 						V_PickedSurface and V_PickedIndex to get the picked mesh,
; 						surface and vertex index respectively. Remember UpdateWorld!
; -----------------------------------------------------------------------------

; -----------------------------------------------------------------------------
; VertexProject		--  calculates 2D co-ords of given vertex. They are retrieved via
; 						VertexScreenX () and VertexScreenY () after calling this function.
; -----------------------------------------------------------------------------

; -----------------------------------------------------------------------------
; GlobalizeVertex	--  gets global 3D co-ords of given vertex. Retrieved via VertexGlobalX (),
; 						VertexGlobalY () and VertexGlobalZ () after calling this function.
; -----------------------------------------------------------------------------

; NOTE: The 'usecam' parameter in these functions is the camera currently in use.











; -----------------------------------------------------------------------------
; *** START OF INCLUDES...
; -----------------------------------------------------------------------------

; -----------------------------------------------------------------------------
; IMPORTANT: All globals are required!
; -----------------------------------------------------------------------------

Global V_PickedMesh							; Last mesh picked during PickNearestVertex ()
Global V_PickedSurface						; Last surface picked during PickNearestVertex ()
Global V_PickedIndex						; Last vertex index picked during PickNearestVertex ()

; -----------------------------------------------------------------------------
; IMPORTANT: All functions are required!
; -----------------------------------------------------------------------------

Function MakePickable (mesh, pickmode = 2, obscurer = True)
	EntityPickMode mesh, pickmode, obscurer
End Function

Function GlobalizeVertex (mesh, surface, index)
	vx# = VertexX (surface, index)
	vy# = VertexY (surface, index)
	vz# = VertexZ (surface, index)
	TFormPoint (vx, vy, vz, mesh, 0)
End Function

Function VertexGlobalX# ()
	Return TFormedX ()
End Function

Function VertexGlobalY# ()
	Return TFormedY ()
End Function

Function VertexGlobalZ# ()
	Return TFormedZ ()
End Function

Function VertexProject (usecam, mesh, surface, index)
	GlobalizeVertex (mesh, surface, index)
	CameraProject usecam, VertexGlobalX (), VertexGlobalY (), VertexGlobalZ ()
End Function

Function VertexScreenX ()
	Return ProjectedX ()
End Function

Function VertexScreenY ()
	Return ProjectedY ()
End Function

Function PickNearestVertex (usecam, x, y)
	picked = CameraPick (usecam, x, y)					; Find entity at pointer position
	If picked
		surf = PickedSurface ()							; Get selected surface
		tri = PickedTriangle ()							; Get selected triangle within surface
		t0 = TriangleVertex (surf, tri, 0)				; Find vertex 0 of triangle
		t1 = TriangleVertex (surf, tri, 1)				; Find vertex 1 of triangle
		t2 = TriangleVertex (surf, tri, 2)				; Find vertex 2 of triangle
		VertexProject (usecam, picked, surf, t0)		; Project vertex 0 screen position
		vx0 = VertexScreenX ()							; Get x position of vertex 0
		vy0 = VertexScreenY ()							; Get y position of vertex 0
		VertexProject (usecam, picked, surf, t1)		; Project vertex 1 screen position
		vx1 = VertexScreenX ()							; Get x position of vertex 1
		vy1 = VertexScreenY ()							; Get y position of vertex 1
		VertexProject (usecam, picked, surf, t2)		; Project vertex 2 screen position
		vx2 = VertexScreenX ()							; Get x position of vertex 2
		vy2 = VertexScreenY ()							; Get y position of vertex 2
		pd0# = Abs (Sqr ((x - vx0) ^ 2 + (y - vy0) ^ 2)) ; Distance from vertex 0 to pointer
		pd1# = Abs (Sqr ((x - vx1) ^ 2 + (y - vy1) ^ 2)) ; Distance from vertex 1 to pointer
		pd2# = Abs (Sqr ((x - vx2) ^ 2 + (y - vy2) ^ 2)) ; Distance from vertex 2 to pointer
		closest# = pd0: vindex = t0						; Assume vertex 0 is closest to pointer
		If pd1 &lt; closest								; See if vertex 1 is closer...
			closest = pd1								; ... if so, that's now 'closest'...
			vindex = t1									; ... and the vertex index is #1
		EndIf
		If pd2 &lt; closest								; See if vertex 2 is closer...
			vindex = t2									; ... if so, vertex index is #2
		EndIf
		V_PickedMesh	= picked
		V_PickedSurface = surf
		V_PickedIndex	= vindex
		Return True
	Else	
		V_PickedMesh	= 0
		V_PickedSurface = 0
		V_PickedIndex	= 0
		Return False
	EndIf
End Function

; -----------------------------------------------------------------------------
; *** END OF INCLUDES...
; -----------------------------------------------------------------------------












; -----------------------------------------------------------------------------
; Demo...
; -----------------------------------------------------------------------------

AppTitle "MOUSE: get nearest vertex | CURSORS &amp; A/Z: move"

Graphics3D 640, 480, 0, 2

; Our camera...

cam = CreateCamera ()
CameraClsColor cam, 32, 64, 96
MoveEntity cam, 0, 0, -5

; A cube, yesterday...

cube = CreateCube ()
EntityColor cube, 128, 128, 64

; Make cube 'pickable' -- IMPORTANT!

MakePickable (cube)

; A light...

light = CreateLight ()
PositionEntity light, -100, 20, -50
PointEntity light, cube

; The red 'cursor' ball...

piv = CreateSphere (8, cube)
ScaleEntity piv, 0.1, 0.1, 0.1
EntityColor piv, 255, 0, 0

HidePointer

Repeat
	
	If KeyDown (203) TurnEntity cube, 0, 1, 0, 1
	If KeyDown (205) TurnEntity cube, 0, -1, 0, 1
	If KeyDown (200) TurnEntity cube, 1, 0, 0
	If KeyDown (208) TurnEntity cube, -1, 0, 0
	If KeyDown (30) TranslateEntity cube, 0, 0, 0.1
	If KeyDown (44) TranslateEntity cube, 0, 0, -0.1

	mx = MouseX ()
	my = MouseY ()

	; If mouse button is clicked on a mesh, find the nearest vertex...
	
	If MouseHit (1)
	
		If PickNearestVertex (cam, mx, my)

			; -----------------------------------------------------------------
			; V_PickedMesh, V_PickedSurface and V_PickedIndex are now valid...
			; -----------------------------------------------------------------

			vx# = VertexX (V_PickedSurface, V_PickedIndex)
			vy# = VertexY (V_PickedSurface, V_PickedIndex)
			vz# = VertexZ (V_PickedSurface, V_PickedIndex)
			PositionEntity piv, vx, vy, vz

			; The 'piv' cursor is parented to the cube. VertexX/Y/Z return the
			; position of the vertex within its mesh, so the above line positions
			; the cursor at the picked vertex's position.

		EndIf

	EndIf

	UpdateWorld
	RenderWorld

	; Crosshair with shadow...
	
	Color 0, 0, 0: Line mx - 4, my, mx + 4, my: Line mx, my - 4, mx, my + 4
	Color 255, 255, 255: Line mx - 5, my - 1, mx + 3, my - 1: Line mx - 1, my - 5, mx - 1, my + 3

	; Plotting a crosshair over picked vertex to show off 2D vertex projection stuff...

	If V_PickedMesh ; Just checking a vertex has been picked...

		ShowEntity piv
			
		VertexProject (cam, V_PickedMesh, V_PickedSurface, V_PickedIndex)
		vx# = VertexScreenX ()
		vy# = VertexScreenY ()
		
		Color 0, 255, 0: Line vx - 4, vy, vx + 4, vy: Line vx, vy - 4, vx, vy + 4
		Color 255, 255, 255: Line vx - 5, vy - 1, vx + 3, vy - 1: Line vx - 1, vy - 5, vx - 1, vy + 3
	
	Else
	
		HideEntity piv
		
	EndIf

	Flip

Until KeyHit (1)

End
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
