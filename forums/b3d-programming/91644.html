<!DOCTYPE html><html lang="en" ><head ><title >Help</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Help</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Help</a><br><br>
<a name="1042958"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> hi, please someone kindly help me with this:<br><br>I need to put three cubes on the screen and mouse by lifting these boxes and put them elsewhere in the ground, something like when you play chess.<br><br>Please code as simple as possible.<br><br>A greeting. <br><br></td></tr></table><br>
<a name="1043170"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Axel Wheeler</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, CreateCube() is the simplest way to make a cube.  Use the EntityColor() command to color it and ScaleEntity() to make it the size you need.  Then PositionEntity() to put it where you want.<br><br>Keep in mind that CreateCube() creates a cube that is 2x2x2, so when using ScaleEntity() use half the size you really want it.  Also remember that the lower half of the cube will be below the location you put it, so adjust the Y value accordingly.<br><br>Clicking and dragging in 3D is considerably more tricky than the above.<br><br>The "Pick" commands can help to identify the cube being clicked.  They are: EntityPick(), LinePick(), CameraPick()<br><br>(I don't use these much myself so I can't tell you much more about them)<br><br>Then you will have to read the MouseX() and MouseY() values and somehow convert the 2D screen information they represent to a best guess at the 3D location.  Again, a Pick command can help because after every Pick command is run you can get PickedX(), PickedY() and PickedZ().  The X and Z values would tell you where on the chessboard the mouse pointer was when the user clicked (or released).  <br><br>These will also help:<br><br>MouseDown(1) detects a mouse button (left, I think) is currently being held down.<br>MouseHit(1) detects that it was hit since the last check.<br>MoveMouse() moves the mouse to a specified x and y position.  You may need this at some point.<br><br>Check the Blitz3D documentation for these commands and good luck Yue! <br><br></td></tr></table><br>
<a name="1043202"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fintan</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't know how exactly to lift something and put it down elsewhere, but I think I can point you in the right direction.  Here's a simple bit of code that will show you how to lift an object by clicking on it and drop it back to the ground when the mouse is released:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">		
         If MouseDown(1) And e=0
	        entitypicked=CameraPick(camera,MouseX(),MouseY())
		If GetEntityType(entitypicked)=type_cube
			e=1
		EndIf
	EndIf
	If MouseDown(1) And e=1
		PositionEntity entitypicked,EntityX(entitypicked),2,EntityZ(entitypicked)
	Else
	     e=0
	EndIf

	For cube.cube=Each cube
		TranslateEntity cube\cube,0,-0.2,0
	Next
</textarea><br><br>The variables "e", "entitypicked", and "type_cube" are obviously declared previously in the code as well as the type, "cube".<br>The "entitypicked" variable is not actually the entity handle, but a set of seemingly random numbers unless it is changed using "NameEntity", but I won't get into that.<br><br>You might be able to move the cube by using some sort of mathematical process to detemine where the mouse is in the game, provided you are using an overhead view of a stationary grid similar to chess.  You may also need to set the entitypickmode of everything in the scene to avoid an "enity does not exist" error.  The above code will make sure that it is a cube with the "getentitytype" command before lifting it.<br><br>I hope this at least helps you get started. <br><br></td></tr></table><br>
<a name="1043220"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've come up with a relatively simple result I'm pleased with.<br><br>Because a mouse only moves in x and y axis, and we want to move the picked object in x, y, and z, I used the mousewheel to control the z axis.<br><br>Holding the RMB down while hanging on to an object will rotate it.<br><br>One thing you may notice is that as the object gets closer to the camera it moves noticably faster in the x and y axis, and vise versa. Oh, and there is also collisions between cubes so they won't pass through each other, and will stack, etc.  They also won't fall through the ground, as gravity effects the cubes not held.<br><br>Here is the code:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
AppTitle "Drag'n Drop, by Drak.  8/24/2010"
SeedRnd MilliSecs()
Graphics3D 800,600,0,2
SetBuffer BackBuffer()

Const type_shape 	= 1
Const type_land		= 2

Collisions type_shape, type_land, 2, 3
Collisions type_shape, type_shape, 1, 3		

Global land=CreatePlane()
EntityColor land, 100,100,100
EntityType land,type_land

EntityColor land,32,32,32

Global camera = CreateCamera()		;create a camera
PositionEntity camera, 0,25,-50 	;move the camera up and backwards
TurnEntity camera,15,0,0
sun = CreateLight()					;create some light

;----------------------------------------------------------
;GLOBALS
;----------------------------------------------------------

;globals for picking commands and hud()
Global sensitivity# = 7
Global mx#
Global my#
Global mz#
Global pick_sent;this is a switch so we don't continually send picks

Global entity	;this global will hold the picked entity
Global name$	;this will hold the name of the picked entity
Global pos_x#	;this is used for the HUD function 
Global pos_y#	;as is this
Global pos_z#	;and this
Global rotation	;and also this

Type shape			;This sets up a type referred to as "shape"
	Field mesh		;mesh is the trait that actually stores the visible part of the shape
	Field name$		;we're going to name our shapes with this field
End Type

;create 3 cubes...
For a = -10 To 10 Step 10
	s.shape = New shape
		s\mesh = CreateCube()		;give it an actual mesh to click on
		s\name = "Cube " + Rand(50)	;this gives the cube a random number to identify it
		NameEntity(s\mesh, "" + s\name)
		EntityType s\mesh, type_shape
		EntityRadius s\mesh, 1
		EntityPickMode s\mesh,2,1
		PositionEntity s\mesh, a, 2,0	;the a comes from the top of the "for" loop, so a will equal -10,0, and last 10
		EntityColor s\mesh, Rand(255),Rand(255),Rand(255)
Next

;------------------------------------
;PROGRAM LOOP
;------------------------------------

While Not KeyHit(1)
get_input()	;grabs user input
gravity()	;self explanatory

RenderWorld
UpdateWorld

hud()		;writes text to the screen

Flip
Wend
;------------------------------------
;END PROGRAM LOOP
;------------------------------------




;------------------------------------
;FUNCTION GET_INPUT()   THIS FUNCTION GRABS ALL INPUT AND DECIDES WHAT SHOULD BE DONE WITH THAT INPUT
;------------------------------------
Function get_input()
	If MouseDown(1) = True
		
		;if we do not have an entity picked....
		If pick_sent = 0
			
			;shoot a camerapick out to the mouse coords
			entity = CameraPick(camera,MouseX(),MouseY())
				
				;if nothing is picked...
				If entity = 0
					name$ = "Missed. Bad shot!"
				End If
				
				;however, if we pick an object...
				If entity &lt;&gt; 0
					;grab the mouse coords and some info
					name$ = EntityName(entity)
					mx# = MouseX()
					my# = MouseY()
					mz# = MouseZ()
					pick_sent = 1	;this must be set to true now or else we'll keep sending picks every frame
				End If
				
		End If
		
		;now the code to move the object comes next...
		If pick_sent = 1
			
			If MouseX() &lt;&gt; mx
				TranslateEntity entity, (MouseX() - mx)/sensitivity,0,0
					mx# = MouseX()
					pos_x# = EntityX(entity)
			End If

			If MouseY() &lt;&gt; my
				TranslateEntity entity, 0,-(MouseY() - my)/sensitivity,0
					my# = MouseY()
					pos_y# = EntityY(entity)
			End If 
			
			If MouseZ() &lt;&gt; mz
				TranslateEntity entity, 0,0,(MouseZ() - mz)/(sensitivity/2)
					mz# = MouseZ()
					pos_z# = EntityZ(entity)
			End If
			
			If MouseDown(2) = True
				TurnEntity entity, 0,-1,0
			End If 
		
		End If
	End If
	
	If MouseDown(1) &lt;&gt; True
		entity = 0
		name$ = ""
		pick_sent = 0	;we have to reset this to 0 upon releasing the LMB
		pos_x = ""
		pos_y = ""
		pos_z = ""
		rotation = ""
	End If 
	
	;keyboard inputs...
	If KeyHit(13) Or KeyHit(78)
		sensitivity = sensitivity - .2
	End If
	
	If KeyHit(12) Or KeyHit(74)
		sensitivity = sensitivity + .2
	End If 
	
	
	
End Function 
					
;HEADS UP DISLPAY
Function hud()
	Text 0,0, "Entity: " + name$
	Text 0,20,"X pos: " + pos_x
	Text 0,40,"Y pos: " + pos_y
	Text 0,60,"Z pos: " + pos_z
	Text 0,80,"Yaw: " + rotation
	
	Text 260,540,"Hold RMB to rotate the held cube"
	Text 200,560,"Use + and - keys to adjust movement sensitivity."
	Text 60,580,"Select with LMB, move by holding LMB.  Use mousewheel to move object on the Z axis."
End Function 				

;GRAVITY FUNCTION 
;only applies gravity to unheld cubes
Function gravity()
For s.shape = Each shape
	If s\mesh &lt;&gt; entity 
		TranslateEntity s\mesh, 0,-.05,0
	End If 
Next
End Function 

</textarea> <br><br></td></tr></table><br>
<a name="1043278"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Axel Wheeler</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great work Drak!  I've modded it a bit for a different flavor.  Yue, normally I don't think people would actually write code for you like Drak did, so don't get too used to it!  :-) <br><br>Note: I'm trying to hide the mouse pointer while an object is picked, but it isn't working.  Oh well.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
AppTitle "Drag'n Drop, by Drak.  8/24/2010"
;Slight mods by Axel W. 8/25/2010
SeedRnd MilliSecs()
Graphics3D 800,600,0,2
SetBuffer BackBuffer()

Const type_shape 	= 1
Const type_land		= 2

Collisions type_shape, type_land, 2, 3
Collisions type_shape, type_shape, 1, 3		

Global land=CreateCube()
ScaleEntity(land,12,.01,12)
EntityColor land, 200,200,200
EntityType land,type_land

Global landTex=CreateTexture(32,32)
SetBuffer(TextureBuffer(landTex))
Color(0,128,0)
Rect(0,0,32,32,1)
Color(255,255,255)
Rect(0,0,32,32,0)
EntityTexture(land,landTex)
ScaleTexture(landTex,.1,.1)

SetBuffer(BackBuffer())


Global camera = CreateCamera()		;create a camera
PositionEntity camera, 0,10,-20 	;move the camera up and backwards
TurnEntity camera,25,0,0			;point it toward the gameboard
sun = CreateLight()					;create some light
LightColor(sun,255,255,128)			;Give the sun a bright yellow color
RotateEntity(sun,45,30,0)			; Put the sun over the right shoulder
AmbientLight(64,64,100)				; Add a bluish light from everywhere else.  This gives each facet of each mesh a slightly different color, which is realistic

;----------------------------------------------------------
;GLOBALS
;----------------------------------------------------------

;globals for picking commands and hud()
Global sensitivity# = 7
Global mx#
Global my#
Global mz#
Global pick_sent;this is a switch so we don't continually send picks

Global entity	;this global will hold the picked entity
Global name$	;this will hold the name of the picked entity
Global pos_x#	;this is used for the HUD function 
Global pos_y#	;as is this
Global pos_z#	;and this
Global rotation	;and also this

Type shape			;This sets up a type referred to as "shape"
	Field mesh		;mesh is the trait that actually stores the visible part of the shape
	Field name$		;we're going to name our shapes with this field
	Field shadow	;This is another mesh to be used as a shadow
End Type

;create 3 cubes...
For a = -10 To 10 Step 10
	For b = -10 To 10 Step 10
		s.shape = New shape
		s\mesh = CreateCube()		;give it an actual mesh to click on
		s\name = "Cube " + Rand(50)	;this gives the cube a random number to identify it
		NameEntity(s\mesh, "" + s\name)
		EntityType s\mesh, type_shape
		EntityRadius s\mesh, 1
		EntityPickMode s\mesh,2,1
		PositionEntity s\mesh, a, 5,b	;the a comes from the top of the "for" loop, so a will equal -10,0, and last 10
		EntityColor s\mesh, Rand(255),Rand(255),Rand(255)
		s\shadow = CreateCube()			;This is the shadow we spoke of earlier
		EntityColor(s\shadow,0,0,0)		;Shadows sure are dark, aren't they?
		EntityAlpha(s\shadow,.3)		;But you can still see things "through" them
		ScaleEntity(s\shadow,1,.1,1)	;They are skinny too!
	Next
Next

;------------------------------------
;PROGRAM LOOP
;------------------------------------

While Not KeyHit(1)
	
	get_input()	;grabs user input
	gravity()	;self explanatory
	
	RenderWorld
	UpdateWorld
	
	hud()		;writes text to the screen
	
	Flip
Wend
;------------------------------------
;END PROGRAM LOOP
;------------------------------------




;------------------------------------
;FUNCTION GET_INPUT()   THIS FUNCTION GRABS ALL INPUT AND DECIDES WHAT SHOULD BE DONE WITH THAT INPUT
;------------------------------------
Function get_input()
	ShowPointer()
	
	If MouseDown(1) = True
		HidePointer()		
		;if we do not have an entity picked....
		If pick_sent = 0
			
			
			;shoot a camerapick out to the mouse coords
			entity = CameraPick(camera,MouseX(),MouseY())
			
				;if nothing is picked...
			If entity = 0
				name$ = "Missed. Bad shot!"
			End If
			
				;however, if we pick an object...
			If entity &lt;&gt; 0
					;grab the mouse coords and some info
				name$ = EntityName(entity)
				mx# = MouseX()
				my# = MouseY()
				mz# = MouseZ()
				pick_sent = 1	;this must be set to true now or else we'll keep sending picks every frame
				TranslateEntity(entity,0,3,0); Just jump into the air
			End If
			
		End If
		
		;now the code to move the object comes next...
		If pick_sent = 1
			
			If MouseX() &lt;&gt; mx
				TranslateEntity entity, (MouseX() - mx)/sensitivity,0,0
				mx# = MouseX()
				pos_x# = EntityX(entity)
			End If
			
			If MouseY() &lt;&gt; my
				TranslateEntity entity, 0,0,-(MouseY() - my)/sensitivity
				my# = MouseY()
				pos_y# = EntityY(entity)
			End If 
			
			If MouseZ() &lt;&gt; mz
				TranslateEntity entity, 0,0,(MouseZ() - mz)/(sensitivity/2)
				mz# = MouseZ()
				pos_z# = EntityZ(entity)
			End If
			
			If MouseDown(2) = True
				TurnEntity entity, 0,-1,0
			End If 
			
		End If
	Else
		;ShowPointer()
	End If
	
	If MouseDown(1) &lt;&gt; True
		entity = 0
		name$ = ""
		pick_sent = 0	;we have to reset this to 0 upon releasing the LMB
		pos_x = ""
		pos_y = ""
		pos_z = ""
		rotation = ""
	End If 
	
	;keyboard inputs...
	If KeyHit(13) Or KeyHit(78)
		sensitivity = sensitivity - .2
	End If
	
	If KeyHit(12) Or KeyHit(74)
		sensitivity = sensitivity + .2
	End If 
	
	For s.shape=Each shape
		PositionEntity(s\shadow,EntityX(s\mesh),0,EntityZ(s\mesh))
	Next
	
End Function 

;HEADS UP DISLPAY
Function hud()
	Text 0,0, "Entity: " + name$
	Text 0,20,"X pos: " + pos_x
	Text 0,40,"Y pos: " + pos_y
	Text 0,60,"Z pos: " + pos_z
	Text 0,80,"Yaw: " + rotation
	
	Text 260,540,"Hold RMB to rotate the held cube"
	Text 200,560,"Use + and - keys to adjust movement sensitivity."
	Text 60,580,"Select with LMB, move by holding LMB.  Use mousewheel to move object on the Z axis."
End Function 				

;GRAVITY FUNCTION 
;only applies gravity to unheld cubes
Function gravity()
	For s.shape = Each shape
		If s\mesh &lt;&gt; entity And EntityY(s\mesh)&gt;1.25
			
				TranslateEntity s\mesh, 0,-.5,0
				
		End If 
	Next
End Function 

</textarea> <br><br></td></tr></table><br>
<a name="1043280"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yue</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> hello friends, thanks for your help, because my English is very bad, well the thing is I'm learning and the best way is to examine small codes that experienced users like you can write on the subject.<br><br><br>A greeting. <br><br></td></tr></table><br>
<a name="1043292"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well I love a challenge and I don't mind helping out. The way I learned b3d was by reading code.  To me it's the fastest way to learn, so long as the recipient learns why what happens, happens. <br><br></td></tr></table><br>
<a name="1043309"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Fintan</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow, Drak!<br><br>Okay, Yue.  Disregard my code that I posted earlier.  It was just the code that I used when I was first learning about picks.  Drak's code really is the way to go and it's very easy to understand, too. <br><br></td></tr></table><br>
<a name="1043364"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Drak</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Changing this:<br><pre class=code>

For s.shape=Each shape
		PositionEntity(s\shadow,EntityX(s\mesh),0,EntityZ(s\mesh))
Next
</pre><br><br>To this:<br><pre class=code>
For s.shape=Each shape
		PositionEntity(s\shadow,EntityX(s\mesh),0,EntityZ(s\mesh))
		RotateEntity s\shadow,0,EntityYaw(s\mesh),0
Next
</pre><br>allows the shadow to rotate with the object. <br><br></td></tr></table><br>
<a name="1043394"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Axel Wheeler</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>allows the shadow to rotate with the object. <br> <br></div><br>Oops ... it's the little things that get ya!  :-) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
