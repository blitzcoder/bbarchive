<!DOCTYPE html><html lang="en" ><head ><title >Pseudorandom Number Generator</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Pseudorandom Number Generator</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Pseudorandom Number Generator</a><br><br>
<a name="1078096"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ClayPigeon</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am creating a game that greatly relies on random numbers. I cannot use the random functions build into B3D because:<br>   - I have no control over the internal system of the B3D RNG<br>   - If I want to use a seed twice, it disrupts the randomness of anything else I use the RNG for<br>   - I need to supply the RNG with and x and y (maybe z) value to get my random number<br><br>The problem is that I have searched all over the internet for a good, yet simple random number generator, and have come across many. But, when I convert them to Blitz, the don't function correctly due to the differences in variable types that other languages use (eg. double-precision float). Does anyone know of a good PRNG for use with B3D that meets these criteria? <br><br></td></tr></table><br>
<a name="1078098"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> If I want to use a seed twice, it disrupts the randomness of anything else I use the RNG for <br></div><br>One thing you can do is get the current state of the RNG with RndSeed, and put that in a temporary variable. Set the seed to the value you want to get your set of predictable results, then once that's done, you can restore the RNG to the state extracted with RndSeed (by setting it as the new seed, with SeedRnd). The RNG will then continue exactly as it would have done, had you never executed the middle step.<br><br>I don't see what other control you could need over an RNG than to set and extract its state (any more than that and it's no longer generating random numbers...?). However, here's a recent thread on the same subject with a B3D code algorithm courtesy of Warpy (incidentally, it's still on the first forum page): <a href="http://www.blitzbasic.com/Community/posts.php?topic=93780" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=93780</a><br><br>As for your third criterion... I don't understand what you mean, sorry. <br><br></td></tr></table><br>
<a name="1078099"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> <a href="http://en.wikipedia.org/wiki/Linear_congruential_generator" target="_blank">http://en.wikipedia.org/wiki/Linear_congruential_generator</a><br><br>This one is similar to the one used in VBA (for older versions of Excel).<br><br>Very simple:<br><br>Xn+1 = (a * Xn + c) mod m<br><br>X(n=0) is the seed, and value from 0 to m-1<br>a is a multipler - typically some value between 1 and m<br>c is an increment  - typically some value between 1 and m<br><br>Very easy to use, and found from first google search. <br><br></td></tr></table><br>
<a name="1078112"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ClayPigeon</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Since 'n' is inside of the equation it's being set to, doesn't that make it an iterative equation? I want to be able to just give it the XY coords and it will return the same random number every time. Also I gave it a shot at implementing your equation and I couldn't get it to work. I get the feeling I missing something really obvious... <br><br></td></tr></table><br>
<a name="1078129"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> "n" refers to the "nth" iteration. "n" is not a variable...I'll write some code..hang on a moment...<br><br><pre class=code>
Graphics 512,512,0,2

Xn = 1 ;the seed
a = 120938 ;some number arbitrarily picked
c = 2430958 ;some other number abitrarily picked
m = 16777216 ;a large number - this is the range the values can be for


For n=1 To 10

Xn = (a * Xn + c) Mod m
Text 0,n*15,Xn

Next

Flip
WaitKey
End

</pre> <br><br></td></tr></table><br>
<a name="1078195"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ClayPigeon</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> But still, isn't that iterative? Meaning that if I want a number further into the sequence, I have to iterate it more times? <br><br></td></tr></table><br>
<a name="1078205"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warpy</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes, it's an iterative algorithm. The thread Yasha linked to contains a long explanation of why this has to be the case for any reasonable pseudorandom number algorithm.<br><br>In fact, you want to do exactly the same thing as Sacha wanted to do. <br><br></td></tr></table><br>
<a name="1078396"></a>

<a name="1078397"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ClayPigeon</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> The thread Yasha linked to contains a long explanation of why this has to be the case for any reasonable pseudorandom number algorithm. <br></div><br><br>Yes, I understand that iterative algorithms heed more true random numbers, but that means the further I get away from the origin, the more recursive iterations must be carried out, which is obviously not acceptable. Is there really no way to get a unique number for virtually any combination of x and y? I had something going a while back that worked okay, but it used B3D's built-in RNG:<br><br><pre class=code>Function Noise#(x%,y%,seed%)
	Local n = x+(y+512)*64
	n = n*(n)+24*(y)
	SeedRnd seed+n
	Return Rnd(-1,1)
End Function</pre><br><br>This also is obviously not acceptable, because it reseeds the RNG for every call, making its iterative-ness useless.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1078644"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Axel Wheeler</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Using Yasha's method (in his first post) you would add a couple of lines:<br><br><pre class=code>
Function Noise#(x%,y%,seed%)
	Local n = x+(y+512)*64
	n = n*(n)+24*(y)
        rs=RndSeed() ; Save the previous seed
	SeedRnd seed+n
	Result# = Rnd(-1,1)
        SeedRnd(rs) ; Set it back to that previous seed, like nothing ever happened
        Return Result#
End Function
</pre><br><br>Actually, if you just want to go back to uncontrolled random numbers after generating your controlled one, just use SeedRnd(millisecs()) again (assuming you did this at program start).  For ordinary use it doesn't really need to be the "next" random number.<br><br>However, that doesn't solve the problem of passing two or more seeds to get a unique result (especially as it appears from your code that you want these seeds to be floats)<br><br>What is the range of x and y?  <br><br>I would think it would be better to use integers if at all possible and if there is a reasonable limit to these values, then your general approach should work (I haven't checked your math, though).  Basically, if you can combine the two values into a unique single value that fits into an int, then you are good to go.<br><br>On the other hand, if these are large ints, could you maybe...<br><br><pre class=code>
SeedRnd(x)
ySeed=rand(y)
SeedRnd(ySeed)
Return Rnd(-1,1)
</pre><br><br>... and again you can restore the old seed at the end, or SeedRnd(millisecs()).<br><br>Are you really only looking for a single result for each x and y?  Or are you trying to produce a whole region of complexity filling each gridpoint?  I'm not sure it matters, but somehow it seems important.<br><br>In any event, see the following post (zoom in on a 500 million star galaxy), which may be one of the few (perhaps only?) successful attempts to do what (I think) you are talking about (in Blitz).  Anyway, it's really cool.  Uses an actual image as it's base, then seeds the rng with data from a given pixel.  Generates the star names too!<br><br><a href="http://www.blitzbasic.com/Community/posts.php?topic=78086" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=78086</a> <br><br></td></tr></table><br>
<a name="1078720"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ClayPigeon</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've created an infinite terrain generator using the code I posted above. It seems to work just fine, but it uses the B3D RNG, and I want one that is self-contained (meaning it IS a RNG, not using one). I tried that equation:<br><br>Xn+1 = (a * Xn + c) mod m<br><br>But, it generated an oscillating, repeating pattern that resembled a sawtooth wave. I really don't see how this could work.. <br><br></td></tr></table><br>
<a name="1078769"></a>

<a name="1078770"></a>

<a name="1078771"></a>

<a name="1078846"></a>

<a name="1078848"></a>

<a name="1078849"></a>

<a name="1078853"></a>

<a name="1078854"></a>

<a name="1078855"></a>

<a name="1078860"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmm I coded an improved perlin noise landscape generator, perhaps you can use this generation code:<br><br><img src="http://www.christianhart.de/bb/caribbean.jpg"><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 800,600,32,2

Global FreeLookXS#,FreeLookZS#,FreeLookRotXS#,FreeLookRotYS#

Const patches=8
Const scale#=32
Const detail#=512.0
Const octaves%=15
Const patchsize%=32
Const maxcols%=256
Const multi#=8.0
Const seed%=1

Global rseed%[2]
rseed[0]=seed
rseed[1]=seed
rseed[2]=seed


Dim p(512)
Dim grayd#(512)
;ms = MilliSecs()
Restore permutation
For i=0 To 256-1
	Read perm
	p(i) = ( perm )
	p(256+i)= ( perm )
	grayd#(i) = MyRND(0.7)
	grayd#(256+i) = MyRND(0.7)
Next

Global GameSpeed%=60
Global Screenwidth%=GraphicsWidth()
Global Screenheight%=GraphicsHeight()
Global FramePeriod%=1000/GameSpeed
Global FrameTime%=MilliSecs()-FramePeriod
Global DeltaTimeOld%



Global min%,max%

Global PlanetR%[maxcols],PlanetG%[maxcols],PlanetB%[maxcols]
Dim Red%(0),Green%(0),Blue%(0),Percent#(0)
Restore Temperate
CreateGradient(11,maxcols,True,PlanetR,PlanetG,PlanetB)

Dim patch(patches,patches)

xoff=MyRND(128)
yoff=MyRND(128)


For x=0 To patches-1
	
	For z=0 To patches-1
		
		patch(x,z)=CreatePatch(patchsize,seed,x,z,xoff,yoff,scale,multi)
		EntityFX patch(x,z),2
		
		pt=pt+1
		
		AppTitle "Generating Landscape: "+Int(pt*100.0/(patches^2))+"% [Seed: "+seed+"]"
		
		If KeyHit(1) Then End
		
	Next
	
Next

For x=0 To patches-2
	
	For z=0 To patches-1
		
		AlignMeshVertices(patch(x,z),patch(x+1,z),patchsize,3,1)
		
	Next
	
Next

For x=0 To patches-1
	
	For z=0 To patches-2
		
		AlignMeshVertices(patch(x,z),patch(x,z+1),patchsize,0,2)
		
	Next
	
Next


cam=CreateCamera()
CameraRange cam,0.001*scale,64*scale
PositionEntity cam,scale*patches/2.0,scale/1.0,-scale
CameraClsColor cam,150,200,255
CameraFogColor cam,150,200,255
CameraFogMode cam,1
CameraFogRange cam,1*scale,32*scale

sunpivot=CreatePivot()

sun=CreateSphere(8,sunpivot)
EntityFX sun,1+8
ScaleEntity sun,scale,scale,scale
EntityColor sun,255,255,0
PositionEntity sun,20*scale,20*scale,20*scale

light=CreateLight(2,sun)
PositionEntity light,0,0,0
LightRange light,30*scale
AmbientLight 32,32,32

water=CreateSprite()
SpriteViewMode water,2
PositionEntity water,scale*patches/2.0,0,scale*patches/2.0
RotateEntity water,90,0,0
ScaleSprite water,scale*patches/2.0,scale*patches/2.0
EntityFX water,1+16
EntityColor water,17,82,112
EntityBlend water,3

MoveMouse Screenwidth/2,Screenheight/2

While Not KeyHit(1)
	
	If KeyHit(57) Then wf=1-wf : WireFrame wf
	
	FreeLook(cam,0.01*scale)
	
	;TurnEntity sunpivot,0,1,0
	
	RenderWorld
	
	Flip
	
Wend

End


Function Tweakseed()
    
    Local temp%=(rseed[0]+rseed[1]+rseed[2]) Mod $10000
    rseed[0]=rseed[1]
    rseed[1]=rseed[2]
    rseed[2]=temp
    
End Function

Function MyRND#(scale#=1.0)
	
	Local n#=((((rseed[2] Shr 8) And $ff)*2)-$ff)/($ff*1.0/scale)
	Tweakseed()
	
	Return n
	
End Function

; creates a nice color gradient
Function CreateGradient(colors%,steps%,inverse=False,R%[maxcols],G%[maxcols],B%[maxcols])
	
	Dim Percent#(colors),Red%(colors),Green%(colors),Blue%(colors)
	
	Local i%,pos1%,pos2%,pdiff%
	Local rdiff%,gdiff%,bdiff%
	Local rstep#,gstep#,bstep#
	Local counter%=0
	
	If inverse Then
		For i=colors To 1 Step -1
			Read Percent(i),Red(i),Green(i),Blue(i)
			Percent(i)=100.0-Percent(i)
		Next
	Else
		For i=0 To colors-1 : Read Percent(i),Red(i),Green(i),Blue(i) : Next
	EndIf
	
    While counter&lt;colors
		
        pos1=Percent(counter)*steps*1.0/100
		pos2=Percent(counter+1)*steps*1.0/100
		
        pdiff=pos2-pos1
		
        rdiff%=Red(counter)-Red(counter+1)
		gdiff%=Green(counter)-Green(counter+1)
		bdiff%=Blue(counter)-Blue(counter+1)
		
        rstep#=rdiff*1.0/pdiff
		gstep#=gdiff*1.0/pdiff
		bstep#=bdiff*1.0/pdiff
		
		For i=0 To pdiff
			
			R[pos1+i]=Int(Red(counter)-(rstep*i))
			G[pos1+i]=Int(Green(counter)-(gstep*i))
			B[pos1+i]=Int(Blue(counter)-(bstep*i))
			
		Next
		
        counter=counter+1
		
	Wend
	
End Function

Function Normalize#(value#=128.0,value_min#=0.0,value_max#=255.0,norm_min#=0.0,norm_max#=1.0)
	
	Return ((value#-value_min#)/(value_max#-value_min#))*(norm_max#-norm_min#)+norm_min#
	
End Function

Function FreeLook(camera,movespeed#=1.0,smoothness#=0.8,rotspeed#=0.9,maxpitch#=85.0)
	
	Local p#,pitch#
	
	If movespeed&gt;0 Then
		FreeLookXS=(FreeLookXS+((KeyDown(205))-(KeyDown(203)))*movespeed)*smoothness
		FreeLookZS=(FreeLookZS+((KeyDown(200))-(KeyDown(208)))*movespeed)*smoothness
		MoveEntity camera,FreeLookXS,0,FreeLookZS
	EndIf
	
	FreeLookRotXS=((MouseXSpeed()-FreeLookRotXS)*0.2+FreeLookRotXS)*rotspeed
	FreeLookRotYS=((MouseYSpeed()-FreeLookRotYS)*0.2+FreeLookRotYS)*rotspeed
	
	p=EntityPitch(camera)+FreeLookRotYS
	If p&lt;-maxpitch Then pitch=-maxpitch ElseIf p&gt;maxpitch Then pitch=maxpitch Else pitch=p
	RotateEntity camera,pitch,-FreeLookRotXS+EntityYaw(camera),0
	
	MoveMouse GraphicsWidth()/2,GraphicsHeight()/2
	
End Function

Function CreatePatch(size%=32,seed%=1,xstart#=0.0,zstart#=0.0,xoff#=0.0,zoff#=0.0,scale#=1.0,yscale#=1.0)
	
	Local x%,z%,h#,c%
	Local vx#,vz#,u#,v#,vertex%
	Local v0%,v1%,v2%,v3%
	
	Local mesh%=CreateMesh()
	Local surf%=CreateSurface(mesh)
	
	For z=0 To size
		
		For x=0 To size
			
         ; calculate vertex coordinates / texture coordinates
			vx=1.0/size*x
			vz=1.0/size*z
			u=x*1.0/size
			v=z*1.0/size
			
			h=noise((x+((xstart+xoff)*size))*scale,(z+((zstart+zoff)*size))*scale,0,detail,seed,0,octaves)
			
			If h&lt;-1 Then h=-1 Else If h&gt;1 Then h=1
			
         ;If h&gt;-0.05 And h&lt;0.05 Then h=0.01
			
			If Not min Then min=0
			If Not max Then max=maxcols
			
			c=Floor(Normalize(h,-1,1,min,max))
			
			If c&lt;min Then min=c
			If c&gt;max Then max=c
			
			s#=Sqr(size)/(size/4.0)
			
         ; place vertex
			vertex=AddVertex(surf,vx*scale,Normalize(h,-1,1,-s,s)*yscale,vz*scale,u,v)
			
         ; set vertex color and texture coordinates
			VertexColor surf,vertex,PlanetR[Int(c)],PlanetG[Int(c)],PlanetB[Int(c)],1
			
         ; set triangles
			If z&lt;size And x&lt;size Then
				
				v0=x+((size+1)*z)
				v1=x+((size+1)*z)+(size+1)
				v2=(x+1)+((size+1)*z)
				v3=(x+1)+((size+1)*z)+(size+1)
				
				AddTriangle(surf,v0,v1,v2)
				AddTriangle(surf,v2,v1,v3)
				
			EndIf
			
		Next
		
	Next
	
   ;Goto skip
	
	For t=0 To CountTriangles(surf)-1
		
		v0=TriangleVertex(surf,t,0)
		v1=TriangleVertex(surf,t,1)
		v2=TriangleVertex(surf,t,2)
		
		vx0#=VertexX(surf,v0)
		vy0#=VertexY(surf,v0)
		vz0#=VertexZ(surf,v0)
		
		vx1#=VertexX(surf,v1)
		vy1#=VertexY(surf,v1)
		vz1#=VertexZ(surf,v1)
		
		vx2#=VertexX(surf,v2)
		vy2#=VertexY(surf,v2)
		vz2#=VertexZ(surf,v2)
		
		px#=vx1-vx0
		py#=vy1-vy0
		pz#=vz1-vz0
		
		qx#=vx2-vx0
		qy#=vy2-vy0
		qz#=vz2-vz0
		
		nx#=(py*qz)-(pz*qy)
		ny#=(pz*qx)-(px*qz)
		nz#=(px*qy)-(py*qx)
		
		VertexNormal surf,v0,nx,ny,nz
		VertexNormal surf,v1,nx,ny,nz
		VertexNormal surf,v2,nx,ny,nz
		
	Next
	
	.skip
	
   ;UpdateNormals mesh
	
	PositionEntity mesh,xstart*scale,0,zstart*scale
	
	Return mesh
	
End Function

Function SmoothNoise#( x#, y#, z#,Seed = 0)
	x = x + Seed: y = y + Seed: z = z + Seed
	Local x1#,y1#,z1#,u#,v#,w#,a#,aa#,ab#,b#,ba#,bb#
	Local g1#,g2#,g3#,g4#,g5#,g6#,g7#,g8#
	Local l1#,l2#,l3#,l4#,l5#,l6#,l7#
	x1 = ( Floor(x) And 255 );,                  // FIND UNIT CUBE THAT
	y1 = ( Floor(y) And 255 );,                  // CONTAINS POINT.
	z1 = ( Floor(z) And 255 );,
	x = x - Floor( x );                                // FIND RELATIVE X,Y,Z
	y = y - Floor( y );                                // OF POINT IN CUBE.
	z = z - Floor( z );
	u# = fade#(x);,                                // COMPUTE FADE CURVES
	v# = fade#(y);,                                // FOR EACH OF x,y,z.
	w# = fade#(z);
	a# = p(x1)+y1
	aa# = p(a)+z1
	ab# = p(a+1)+z1;,      // HASH COORDINATES OF
	b# = p(x1+1)+y1
	ba# = p(b)+z1
	bb# = p(b+1)+z1;      // THE 8 CUBE CORNERS,
	
	g1# = grayd#(bb+1)
	g2# = grayd#(ab+1)
	g3# = grayd#(ba+1)
	g4# = grayd#(aa+1)
	g5# = grayd#(bb)
	g6# = grayd#(ab)
	g7# = grayd#(ba)
	g8# = grayd#(aa)
	l1# = lerp#(u, g2#, g1#)
	l2# = lerp#(u, g4#, g3#)
	l3# = lerp#(v, l2#, l1#)
	l4# = lerp#(u, g6#, g5#)
	l5# = lerp#(u, g8#, g7#)
	l6# = lerp#(v, l5#, l4#)
	l7# = lerp#(w, l6#, l3#)
	Return l7#
End Function
Function fade#( t# ) : s# = t * t * t * (t * (t * 6 - 15) + 10):Return s#:End Function
Function lerp#( t#, a#, b#): z# = a + t * (b - a): Return z#:End Function
	
Function noise#(x#,y#,z#,size#=64,seed% = 0, MinOctaves = 0 , MaxOctaves = 9999)
	If seed = 0 Then seed = MilliSecs()
	x# = x# + seed
	y# = y# + seed
	z# = z# + seed
      ;//Set the initial value and initial size
	value# = 0.0: initialSize# = size#;
	
	For i = 1 To MinOctaves
		size = size/2
	Next
      ;//Add finer and finer hues of smoothed noise together
	While(size &gt;= 1.0) And MaxOctaves &gt; MinOctaves
		
		value# = value# + SmoothNoise#(x / size, y / size, z / size, seed) * size
		size = size / 2.0;
		MaxOctaves = MaxOctaves - 1
	Wend
	
      ;//Return the result over the initial size
	Return  (value# / Float initialSize);
	
End Function




Function AlignMeshVertices(mesh1%,mesh2%,size%,side1%=0,side2%=0,colors=True,normals=True,flipped=False,colored=False)
	
	Local surf1%=GetSurface(mesh1,1)
	Local surf2%=GetSurface(mesh2,1)
	
	Local i%,s%,t%,mag#
	Local r%,g%,b%,a#,nx#,ny#,nz#
	
	For i=0 To size
		
      ; 0 = upper side
      ; 1 = left side
      ; 2 = lower side
      ; 3 = right side
		
		If side1=0 Then s=(size^2)+size+i
		If side1=1 Then s=(size*i)+i
		If side1=2 Then s=i
		If side1=3 Then s=(size*i)+size+i
		
		If side2=0 Then t=(size^2)+size+i
		If side2=0 And flipped Then t=(size^2)+(2*size)-i
		
		If side2=1 Then t=(size*i)+i
		If side2=1 And flipped Then t=(size^2)+size-(size*i)-i
		
		If side2=2 Then t=i
		If side2=2 And flipped Then t=size-i
		
		If side2=3 Then t=(size*i)+size+i
		If side2=3 And flipped Then t=(size^2)+(2*size)-(size*i)-i
		
      ; align colors
		If colors Then
			
			r=(VertexRed(surf1,s)+VertexRed(surf2,t))/2
			g=(VertexGreen(surf1,s)+VertexGreen(surf2,t))/2
			b=(VertexBlue(surf1,s)+VertexBlue(surf2,t))/2
			a=(VertexAlpha(surf1,s)+VertexAlpha(surf2,t))/2.0
			
			VertexColor surf1,s,r,g,b,a
			VertexColor surf2,t,r,g,b,a
			
		EndIf
		
      ; draw sides
		If colored Then
			r=255:g=0:b=0:a=1.0
			VertexColor surf1,s,r,g,b,a
			VertexColor surf2,t,r,g,b,a
		EndIf
		
      ; align normals
		If normals Then
			
			nx=VertexNX(surf1,s)+VertexNX(surf2,t)
			ny=VertexNY(surf1,s)+VertexNY(surf2,t)
			nz=VertexNZ(surf1,s)+VertexNZ(surf2,t)
			
			mag=Sqr(nx*nx+ny*ny+nz*nz)
			
			nx=nx/mag
			ny=ny/mag
			nz=nz/mag
			
			VertexNormal surf1,s,nx,ny,nz
			VertexNormal surf2,t,nx,ny,nz
			
		EndIf
		
	Next
	
End Function

.Temperate
Data   0.0,255,255,255   ; icy mountains
Data   5.0,179,179,179   ; transition
Data  10.0,153,143, 92   ; tundra
Data  25.0,115,128, 77   ; high grasslands
Data  40.0, 42,102, 41   ; low grasslands
Data  45.0, 42,102, 41   ; low grasslands
Data  50.0,200,200,118   ; coast / should be a 0 height
Data  55.0, 17, 82,112   ; shallow ocean
Data  70.0, 17, 82,112   ; shallow ocean
Data  75.0,  9, 62, 92   ; ocean
Data 100.0,  9, 62, 92   ; deep ocean

.permutation
Data 151,160,137,91,90,15
Data 131,13,201,95,96,53,194,233,7,225,140,36,103,30,69,142,8,99,37,240,21,10,23
Data 190, 6,148,247,120,234,75,0,26,197,62,94,252,219,203,117,35,11,32,57,177,33
Data 88,237,149,56,87,174,20,125,136,171,168, 68,175,74,165,71,134,139,48,27,166
Data 77,146,158,231,83,111,229,122,60,211,133,230,220,105,92,41,55,46,245,40,244
Data 102,143,54, 65,25,63,161, 1,216,80,73,209,76,132,187,208, 89,18,169,200,196
Data 135,130,116,188,159,86,164,100,109,198,173,186, 3,64,52,217,226,250,124,123
Data 5,202,38,147,118,126,255,82,85,212,207,206,59,227,47,16,58,17,182,189,28,42
Data 223,183,170,213,119,248,152, 2,44,154,163, 70,221,153,101,155,167, 43,172,9
Data 129,22,39,253, 19,98,108,110,79,113,224,232,178,185, 112,104,218,246,97,228
Data 251,34,242,193,238,210,144,12,191,179,162,241, 81,51,145,235,249,14,239,107
Data 49,192,214, 31,181,199,106,157,184, 84,204,176,115,121,50,45,127, 4,150,254
Data 138,236,205,93,222,114,67,29,24,72,243,141,128,195,78,66,215,61,156,180</textarea><br><br>It is using a predictable pseudorandom number generator I derived from my elite planet name generator, very simple:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Global seed%[2]
Local min#,max#,i%,n#
seed[0]=$5A4A
seed[1]=$0248
seed[2]=$B753

Function Tweakseed()
    
    Local temp%=(seed[0]+seed[1]+seed[2]) Mod $10000
    seed[0]=seed[1]
    seed[1]=seed[2]
    seed[2]=temp
    
End Function

For i=0 To 20
	
	n=MyRND(0.7)
	
	If n&lt;min Then min=n
	If n&gt;max Then max=n
	
	Print n
	
Next

Print
Print "Min: "+min+" | Max: "+max

WaitKey

End

Function MyRND#(scale#=1.0)
	
	Local n#=((((seed[2] Shr 8) And $ff)*2)-$ff)/($ff*1.0/scale)
	Tweakseed()
	
	Return n
	
End Function</textarea><br><br>You should always get these values<br><br>0.304706<br>-0.595686<br>0.42549<br>0.13451<br>-0.0301961<br>0.529804<br>0.634118<br>-0.277255<br>-0.518824<br>-0.167451<br>0.441961<br>-0.244314<br>0.0356863<br>0.233333<br>0.0192157<br>0.288235<br>0.540784<br>-0.557255<br>0.266275<br>0.244314<br>-0.0466667<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1078925"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Axel Wheeler</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> For some reason I thought the Blitz3D rng was posted at some point.  If so I can't find it now.<br><br>Keep in mind though that the Blitz3D rng is compiled right into your binary, so there's no risk of it not working right on some processors or whatever.  The only thing that would break it is if Mark were to change it in an update and you were to recompile with that update, then you'd have a different terrain.  However, many other programs would break too so Mark is unlikely to do that. <br><br></td></tr></table><br>
<a name="1079792"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> Keep in mind though that the Blitz3D rng is compiled right into your binary, so there's no risk of it not working right on some processors or whatever. <br></div><br>If I'd klnown that a while ago it would have saved me having to ask a lot of people what the results of a Rnd were XD<br><br>--<br><br>Personally, I find the blitz generator good enough. <br><br></td></tr></table><br>
<a name="1080639"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MCP</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> An alternative to creating your own random number generator (and much faster in terms of compution time) would be to create a table of random numbers (say 4k's worth) either in the range<br>that your interested in or using floats in the range of 0.0 - 1.0 then scale the result to the range you want.<br>Your 'Seed' then would simply be an index into this array (modulo array size) to fetch the next number in sequence.<br><br>You could write a small program in Blitz to generate this table easily using the native functions or if you prefer 'true' randomness you could visit...<br><br><a href="http://www.random.org/integers/?mode=advanced" target="_blank"> www.random.org</a> and use their free random number generation service. <br><br></td></tr></table><br>
<a name="1080645"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Krischan</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Another example, this time with unique numbers:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Dim Array%(0)

CreateRandomArray(10)

For i=0 To 9
	
	Print Array(i)
	
Next

WaitKey

End

Function CreateRandomArray(size%)
	
	Local i%,n%,m%,z%
	
	Dim Array(size)
	
	For i=0 To size-1
		
		Array(i)=i
		
	Next
	
	For n=0 To size-2
		
		m=Rand(n,size-1)
		z=Array(n)
		
		Array(n)=Array(m)
		Array(m)=z
		
	Next
	
End Function</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
