<!DOCTYPE html><html lang="en" ><head ><title >SpiderMan Swinging</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >SpiderMan Swinging</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >SpiderMan Swinging</a><br><br>
<a name="988517"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Does anyone know how to setup swinging like in the spiderman games (and other games) using JV-ODE or PhysX? Using a ordinary joint limits you and doesn't allow slack. <br><br></td></tr></table><br>
<a name="988571"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is the demo - rope code from JV-ODE<br><br>However, for games, you may need to restrict the ANGLE each segment can move from the alignment of the segment higher up (this would effectively give a form of tension to the rope)<br>and perhaps, restrict the axis of movement too, allowing for the swinging to be only in x directions, depending on your needs.<br><br>It may even be preferable to use cylinders or  extended cubes instead of spheres too, to ensure that the rope doesn't get twisted. AGain, though, this is very dependent on your requrements.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; ###################################################################################################
; #                                       JV-ODE - Rope Demo                                        #
; #                                  Code by Jim Williams (VIP3R)                                   #
; #                              Devious Codeworks - Copyright © 2008                               #
; ###################################################################################################

Include "JV-ODE.bb"

AppTitle "JV-ODE - Rope Demo"

Graphics3D 800,600,0,2

Dim Sphere(10)
Dim Joint(10)

Type ODEGeom
	Field body
	Field geom
	Field mesh
End Type

; ###################################################################################################

; ### Setup ODE

dInitODE()

Global World=dWorldCreate()
Global Space=dHashSpaceCreate(0)
Global ContactGroup=dJointGroupCreate(0)

dWorldSetAutoDisableFlag(World,1)
dWorldSetGravity(World,0,-0.98,0)
dContactSetMode(dContactBounce)
dContactSetBounce(0.0)
dContactSetMu(48)

; ### Create Light

Global Light=CreateLight()
RotateEntity Light,50,-50,0
LightColor Light,255,255,255
AmbientLight 130,130,130

; ### Create Camera

Global Camera=CreateCamera()
CameraClsColor Camera,0,0,0
CameraRange Camera,1,1000
PositionEntity Camera,0,90,-100
RotateEntity Camera,30,0,0

; ### Create Plane

dCreatePlane(Space,0,1,0,0)

Global Plane=CreatePlane()
EntityAlpha Plane,0.8

EntityTexture Plane,PlaneTexture()

CreateMirror()

; ###################################################################################################

; ### Create Rope

For count=1 To 10
	ode.ODEGeom=New ODEGeom
	ode\body=dBodyCreate(World)
	Sphere(count)=ode\body
	dBodySetPosition(ode\body,-30,90-(count*8),0)
	dBodySetRotation(ode\body,0,0,0)
	dBodySetAutoDisableFlag(ode\body,1)
	ode\geom=dCreateSphere(Space,4)
	dGeomSetBody(ode\geom,ode\body)
	ode\mesh=CreateSphere()
	ScaleMesh ode\mesh,4,4,4
	EntityColor ode\mesh,Rand(0,255),Rand(0,255),Rand(0,255)
	EntityShininess ode\mesh,0.7
	Joint(count)=dJointCreateBall(World,0)
	If count=1
		dJointAttach(Joint(count),0,Sphere(count))
		Else
		dJointAttach(Joint(count),Sphere(count-1),Sphere(count))
		End If
	dJointSetBallAnchor(Joint(count),dBodyGetPositionX(Sphere(count)),dBodyGetPositionY(Sphere(count)),dBodyGetPositionZ(Sphere(count)))
Next

; ###################################################################################################

While Not KeyHit(1)

	UpdateKeys()

	If MilliSecs()-Timer&gt;700
		AddObject()
		Timer=MilliSecs()
		End If

	UpdateGeoms()

	PTime=MilliSecs()

	dSpaceCollide(Space,World,ContactGroup)
	dWorldQuickStep(World,0.1)
	dJointGroupEmpty(ContactGroup)

	PhysicsTime#=MilliSecs()-PTime

	RenderWorld

	Text 0,0,"JV-ODE Version "+dGetVersion()
	Text 0,15,"Physics Time:"+PhysicsTime
	Text 640,0,"Add Force To Rope"
	Text 640,15,"Using Arrow Keys"
	Text 340,0,"Demo Time:"+dDemoTime()
	If dDemoTime()=0 Then Text 240,80,"*** TIME EXPIRED PLEASE RESTART DEMO ***"

	Flip 1

Wend

dJointGroupDestroy(ContactGroup)
dSpaceDestroy(Space)
dWorldDestroy(World)
dCloseODE()

End

; ###################################################################################################

Function AddObject()

ode.ODEGeom=New ODEGeom
ode\body=dBodyCreate(World)
dBodySetPosition(ode\body,30,80,0)
dBodySetRotation(ode\body,0,0,0)
dBodySetAutoDisableFlag(ode\body,1)
ode\geom=dCreateBox(Space,10,5,10)
dGeomSetBody(ode\geom,ode\body)
ode\mesh=CreateCube()
ScaleMesh ode\mesh,5,2.5,5
EntityColor ode\mesh,Rand(0,255),Rand(0,255),Rand(0,255)
EntityShininess ode\mesh,0.7

End Function

; ###################################################################################################

Function UpdateKeys()

If KeyDown(200) Then dBodyAddForce(Sphere(10),0,0,10)
If KeyDown(208) Then dBodyAddForce(Sphere(10),0,0,-10)
If KeyDown(203) Then dBodyAddForce(Sphere(10),-10,0,0)
If KeyDown(205) Then dBodyAddForce(Sphere(10),10,0,0)

End Function

; ###################################################################################################

Function UpdateGeoms()

For ode.ODEGeom=Each ODEGeom
	PositionEntity ode\mesh,dGeomGetPositionX(ode\geom),dGeomGetPositionY(ode\geom),dGeomGetPositionZ(ode\geom)
	RotateEntity ode\mesh,dGeomGetPitch(ode\geom),dGeomGetYaw(ode\geom),dGeomGetRoll(ode\geom)
Next

End Function

; ###################################################################################################</textarea> <br><br></td></tr></table><br>
<a name="988647"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GIB3D</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is what I have so far for spiderman-like swinging. I didn't want to use the rope because you have to calculate how many joints you would need between where you clicked and where you are. What I wanted was one line that you can swing on and you can move freely between the distance the line was created. This example uses rope though.<br><br>PhysX<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Global Light
Global Camera
Global Player.TYPE_Physics
Global Sky

InitMedia()
MainLoop()

Function MainLoop()
	Local a.TYPE_Physics
	Local Yaw#
	
	While Not KeyDown(1)
		If MouseHit(1)
			If CameraPick(Camera,MouseX(),MouseY())
;				Local X# = (PickedX()-EntityX(Player\E_Mesh,1));(EntityX(Player\E_Mesh,1)-TFormedX());
;				Local Y# = (PickedY()-EntityY(Player\E_Mesh,1));((EntityY(Player\E_Mesh,1)+1)-TFormedY());
;				Local Z# = (PickedZ()-EntityZ(Player\E_Mesh,1));(EntityZ(Player\E_Mesh,1)-TFormedZ());
	
				;Local S = CreateSphere()
				;ScaleMesh S,6,6,6
				;PositionEntity S,PickedX(),PickedY(),PickedZ()
				
				FUNC_Delete_Rope()
				Create_Px_Rope(PickedX(),PickedY(),PickedZ(),16)
			EndIf
		EndIf
		
		If MouseHit(2)
			FUNC_Delete_Rope()
		EndIf
		
		pxBodyAddLocalForce(Player\E_Body,0,(KeyDown(18)-KeyDown(16))*10,(KeyDown(17)-KeyDown(31))*2,2)
		
		Yaw = Yaw + (KeyDown(30)-KeyDown(32))*9
		pxBodySetRotation(Player\E_Body,0,Yaw,0);pxBodyGetRotationYaw(Player\E_Body)
		
		pxRenderPhysic(60,0)
		
		FUNC_Update_Physics()
		
		TFormPoint 0,0,-70,Player\E_Mesh,0
		TranslateEntity Camera,(TFormedX()-EntityX(Camera))*.3,(TFormedY()-EntityY(Camera))*.3,(TFormedZ()-EntityZ(Camera))*.3
		
		PointEntity Camera,Player\E_Mesh
		
		Local Pitch# = EntityPitch(Camera,1)
		If Pitch &gt; 30 Then Pitch = 30
		If Pitch &lt; -30 Then Pitch = -30
		
		RotateEntity Camera,Pitch,EntityYaw(Camera,1),0,1
		
		UpdateWorld
		RenderWorld
		
		Flip
	Wend
End Function

Function InitMedia()
	InitGraphics()
	
	Light = CreateLight()
		RotateEntity Light,10,0,0
		
	Camera = CreateCamera()
		PositionEntity Camera,-30,30,-30
		RotateEntity Camera,30,-45,0
		CameraZoom Camera,.9
		CameraRange Camera,.01,1000
		CameraClsColor Camera,0,20,0
		CameraFogRange Camera,500,800
		CameraFogColor Camera,0,20,0
		CameraFogMode Camera,1
		
	pxCreateWorld(1,"key");;;;;;;;;;;;;;;;;;;;;;;;;
	pxSetGravity(0,-98,0);;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	
	EntityTexture CreatePlane(),GTexture()
	AmbientLight 80,80,100
	
	pxBodySetCollisionGroupFlag 1,1,0
	
	Local Model = CreateCube()
	ScaleMesh Model,6,2,6
	FUNC_Create_Physics(Model,0,2,0,0,0,0,False)
	Player = FUNC_Create_Physics(CreateCone(6),0,25,0,1,0,0,True,1)
	pxBodySetCollisionGroup Player\E_Body,1
	
	Sky = CreatePlane()
	EntityAlpha Sky,.8
	EntityColor Sky,170,170,255
	RotateEntity Sky,180,0,0
	PositionEntity Sky,0,400,0
	EntityPickMode Sky,2
	
	;[Block] XYZ
	Local XBeam = CreateCube()
		ScaleMesh XBeam,800,.5,.5
		EntityAlpha XBeam,.2
		EntityColor XBeam,255,0,0
		PositionEntity XBeam,800,0,0
	Local YBeam = CreateCube()
		ScaleMesh YBeam,.5,800,.5
		EntityAlpha YBeam,.2
		EntityColor YBeam,0,255,0
		PositionEntity YBeam,0,800,0
	Local ZBeam = CreateCube()
		ScaleMesh ZBeam,.5,.5,800
		EntityAlpha ZBeam,.2
		EntityColor ZBeam,0,0,255
		PositionEntity ZBeam,0,0,800
	;[End]
End Function

Function Create_Px_Rope(pos_x,pos_y,pos_z, amount%)
	Local a.TYPE_Physics = New TYPE_Physics
 	a\E_Mesh = CreateSphere()
	ScaleMesh a\E_Mesh,2,2,2
	EntityColor a\E_Mesh,Rand(0,255),Rand(0,255),Rand(0,255) 
  	a\E_Body=pxBodyCreateSphere(2,1000)
	pxBodySetCollisionGroup a\E_Body,1
	
	a\isRope = True
	
	pxBodySetPosition(a\E_Body, pos_x+0.1, pos_y,pos_z+0.1)
	pxKinematicSet a\E_Body
	
	For i=0 To amount%
	  	a.TYPE_Physics = New TYPE_Physics
		
		a\E_Mesh = CreateSphere()
		ScaleMesh a\E_Mesh,2,2,2
		EntityColor a\E_Mesh,Rand(0,255),Rand(0,255),Rand(0,255) 
		a\E_Body=pxBodyCreateSphere(2,.1)
		pxBodySetCollisionGroup a\E_Body,1
		
		a\isRope = True
		
		pos_y = pos_y-4
		pxBodySetPosition(a\E_Body,pos_x,pos_y,pos_z)
		
		Local t = a\E_Body
		a = Before a
		a\E_Joint = pxJointCreateSpherical(a\E_Body, t, pos_x,pos_y+2,pos_z,  0,-1,0)
		pxJointSphericalSetLimitSpring(a\E_Joint, 10, 10.5, 0)
	Next
	
	pxBodySetPosition a\E_Body,EntityX(Player\E_Mesh,1),EntityY(Player\E_Mesh,1),EntityZ(Player\E_Mesh,1)
	
	a\E_Joint = pxJointCreateSpherical(a\E_Body, Player\E_Body, EntityX(Player\E_Mesh,1),EntityY(Player\E_Mesh,1),EntityZ(Player\E_Mesh,1),  0,-1,0)
	pxJointSphericalSetLimitSpring(a\E_Joint, 10, 10.5, 0)
End Function

Function FUNC_Delete_Rope()
	Local a.TYPE_Physics
	
	For a.TYPE_Physics = Each TYPE_Physics
		If a\isRope
			pxDeleteJoint(a\E_Joint)
			FUNC_Delete_Physics(a)
		EndIf
	Next
End Function

Function Mesh_Capsule(height#, radius#)
 ;------------------------create Mesh-------------
capsule= CreateCylinder()
ScaleMesh capsule, radius, height/2, radius
PositionMesh capsule,0,0,0

sphere1 = CreateSphere() 
ScaleMesh sphere1,radius,radius,radius
PositionMesh sphere1,0, height/2,0
AddMesh sphere1,capsule

sphere2 = CreateSphere() 
ScaleMesh sphere2,radius,radius,radius
PositionMesh sphere2,0, -height/2,0
AddMesh sphere2,capsule

FreeEntity sphere1
FreeEntity sphere2

Return capsule
;----------------------------------------------------------------
 End Function

Function InitGraphics(w = 1024, h = 768,title$="Blitz3D Program",exit_message$="")
	Graphics3D w, h, 32, 2
	SetBuffer BackBuffer()
	SeedRnd MilliSecs()
	
	If exit_message &lt;&gt; ""
		AppTitle title,exit_message
	Else
		AppTitle title
	EndIf
End Function


Type TYPE_Physics
	Field E_Body,E_Mesh
	Field E_Joint
	Field isRope
End Type

Function FUNC_Create_Physics.TYPE_Physics(mesh,x#,y#,z#,pitch#,yaw#,roll#,is_hull=True,mass=10)
	Local a.TYPE_Physics = New TYPE_Physics
		a\E_Mesh = mesh
		ScaleMesh a\E_Mesh,5,5,5
		
		If is_hull
			a\E_Body = BodyCreateHull(a\E_Mesh,mass)
		Else
			a\E_Body = BodyCreateMesh(a\E_Mesh)
		EndIf
		
		pxBodySetCollisionGroup a\E_Body,2
		
		pxBodySetPosition a\E_Body,x,y,z
		pxBodySetRotation a\E_Body,pitch,yaw,roll
	Return a
End Function

Function FUNC_Update_Physics()
	Local a.TYPE_Physics
	
	For a.TYPE_Physics = Each TYPE_Physics
		pxBodySetEntity(a\E_Mesh,a\E_Body)
	Next
End Function

Function FUNC_Delete_Physics(a.TYPE_Physics)
	FreeEntity a\E_Mesh
	pxDeleteBody(a\E_Body)
	Delete a
End Function

Function GTexture() ; Green Checkered Texture
	Local T = CreateTexture(512,512)
	
	SetBuffer TextureBuffer(T)
		Color 38,148,0
		Rect 0,0,TextureWidth(T),TextureHeight(T)
		
		Color 63,164,0
		Rect TextureWidth(T)/2,0,TextureWidth(T)/2,TextureHeight(T)/2
		Rect 0,TextureHeight(T)/2,TextureWidth(T)/2,TextureHeight(T)/2
	SetBuffer BackBuffer()
	ScaleTexture T,40,40
	PositionTexture T,.25,.25
	
	Return T
End Function

Function BodyCreateHull(mesh, mass)
	Local nsurf = CountSurfaces(mesh)
	Local nvert = 0
	For ns = 1 To nsurf
		Local surf = GetSurface(mesh,ns)
		nvert = nvert + CountVertices(surf)
	Next
	vbank = CreateBank(nvert*4*3)
	nv = 0
	For ns = 1 To nsurf
		surf = GetSurface(mesh,ns)
		nvv = CountVertices(surf)
		For nvc = 0 To nvv - 1
			PokeFloat vbank,nv*12+0,VertexX(surf,nvc)
			PokeFloat vbank,nv*12+4,VertexY(surf,nvc)
			PokeFloat vbank,nv*12+8,VertexZ(surf,nvc)
			nv = nv+1
		Next
	Next
	Local bbb%= pxBodyCreateHull(vbank, nvert, mass)
	FreeBank vbank
	Return bbb
End Function

Function BodyCreateMesh(mesh%)
	Local nsurf = CountSurfaces(mesh)
	Local nvert = 0
	Local nface=0
	
	For ns = 1 To nsurf
		Local surf = GetSurface(mesh,ns)
		nface = nface+CountTriangles(surf)
		nvert = nvert +CountVertices(surf)
	Next
	
	Local fbank = CreateBank(nface*4*3)
	Local nf = 0
	Local vbank = CreateBank(nvert*4*3)
	Local nv = 0
	For ns = 1 To nsurf
		surf = GetSurface(mesh,ns)
		Local nfv = CountTriangles(surf)
		For nfc = 0 To nfv -1
			PokeInt fbank,nf*12,TriangleVertex(surf,nfc,0)
			PokeInt fbank,nf*12+4,TriangleVertex(surf,nfc,1)
			PokeInt fbank,nf*12+8,TriangleVertex(surf,nfc,2)
			nf=nf+1
		Next
		
		Local nvv = CountVertices(surf)
		For nvc = 0 To nvv - 1
			PokeFloat vbank,nv*12,VertexX(surf,nvc)
			PokeFloat vbank,nv*12+4,VertexY(surf,nvc)
			PokeFloat vbank,nv*12+8,VertexZ(surf,nvc)
			nv = nv+1
		Next
	Next
	
	bbb=pxCreateTriMesh(vbank, fbank, nvert, nface, 0)
	FreeBank vbank
	FreeBank fbank
	Return bbb
End Function

</textarea> <br><br></td></tr></table><br>
<a name="1148307"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nexinarus</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> GIB3D.. I cant seem to get your code to work am i missing something? <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
