<!DOCTYPE html><html lang="en" ><head ><title >Single surface help !</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Single surface help !</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Single surface help !</a><br><br>
<a name="679105"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi :)<br><br>Here is the code i have find : But i need two stuff<br><br>- Delete a sprite<br>- Hide a sprite<br><br>Anybody can help me ?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Global fpsindex#,fpstime#,fpsfold_millisecs#,fpsfps#,time,xyzturn

Type layer
	Field mesh
	Field count
	Field spritebank ; bank of sprites up to count
End Type

Type sprite
	Field x#,y#,z#
	Field r,g,b
	Field s#,a#
	Field verti[3]
	Field surf
End Type

;--------------------------------------------------------------------------------------------
Graphics3D 640,480,16,2
SetBuffer BackBuffer()
Global camera=CreateCamera()
CameraRange camera,1,9000
light=CreateLight(2)
PositionEntity light,1000,1000,-1000
;--------------------------------------------------------------------------------------------
fire=LoadTexture ("C:\Program Files\Blitz3D\samples\RobCummings\Bumpy\glow.bmp",2+48)

test=CreateLayer()
EntityTexture test,fire

;For i=0 To 1000
;	temp = AddSprite(test)
;	PositionSprite temp,Rnd(-500,500),Rnd(-500,500),Rnd(-500,500)
;Next

temp1 = AddSprite(test)
PositionSprite temp1,0,0,50

temp2 = AddSprite(test)
PositionSprite temp2,20,0,50



;--------------------------------------------------------------------------------------------
HidePointer
While Not KeyHit(1)


	;TransTex(fire,MilliSecs()*0.08)
	UpdateSprites()
	
	; Get the timer value
	T=MilliSecs() 
	
	; Mouse Look, every 25ms
	Time1=mouselook(camera,T,Time1,25)

	If MouseHit(1) Then
		AlphaSprite(temp1,0.1)
		AlphaSprite(temp2,0.1)
	EndIf
	
	; Movement
	If KeyDown(17) Then MoveEntity camera,0,0,1  ;W
	If KeyDown(31) Then MoveEntity camera,0,0,-1 ;S
	If KeyDown(30) Then MoveEntity camera,-1,0,0 ;A
	If KeyDown(32) Then MoveEntity camera,1,0,0  ;D

	UpdateWorld
	RenderWorld
	Text 0,0,fps()


	Flip 0
Wend
End

;--------------------------------------------------------------------------------------------

Function UpdateSprites()
;	For l.layer=Each layer
;		surf=GetSurface(l\mesh,1)
		
;		For i=0 To l\count-1
	
;			s.sprite = Object.sprite( PeekInt(l\spritebank,i*4) )


		For s.sprite=Each sprite
			TFormPoint s\x,s\y,s\z,0,camera
			
			If TFormedZ#()&gt;0
					
				TFormVector -s\s,-s\s,0,camera,0 
				VertexCoords s\surf,s\verti[0],s\x+TFormedX#(),s\y+TFormedY#(),s\z+TFormedZ#()
				
				TFormVector s\s,-s\s,0,camera,0 
				VertexCoords s\surf,s\verti[1],s\x+TFormedX#(),s\y+TFormedY#(),s\z+TFormedZ#()

				TFormVector -s\s,s\s,0,camera,0 
				VertexCoords s\surf,s\verti[2],s\x+TFormedX#(),s\y+TFormedY#(),s\z+TFormedZ#()
	
				TFormVector s\s,s\s,0,camera,0 
				VertexCoords s\surf,s\verti[3],s\x+TFormedX#(),s\y+TFormedY#(),s\z+TFormedZ#()
			
			EndIf
		Next
			
;	Next
End Function

;--------------------------------------------------------------------------------------------

Function CreateLayer()
	l.layer=New layer	
	l\mesh=CreateMesh() : surf=CreateSurface(l\mesh)
;	l\spritebank=CreateBank()
	NameEntity l\mesh,Handle(l)
	EntityColor l\mesh,255,255,255
	EntityFX l\mesh,1+32
	EntityBlend l\mesh,3
	Return l\mesh
End Function

;--------------------------------------------------------------------------------------------

Function AddSprite(mesh)
	l.layer=Object.layer(EntityName(mesh))
	surf=GetSurface(l\mesh,1)
;	l\count=l\count+1
	;ResizeBank l\spritebank,l\count*4
	
	;create a new sprite
	s.sprite=New sprite
	
	s\verti[0]=AddVertex(surf,0,0,0,0,1) ;topleft
	s\verti[1]=AddVertex(surf,0,0,0,1,1) ;topright
	s\verti[2]=AddVertex(surf,0,0,0,0,0) ;bottomleft
	s\verti[3]=AddVertex(surf,0,0,0,1,0) ;bottomright
	
	AddTriangle(surf,s\verti[1],s\verti[2],s\verti[3])
	AddTriangle(surf,s\verti[2],s\verti[1],s\verti[0])
	s\s=10 : s\a=1
	s\r=255 : s\g=255 : s\b=255
	s\x=0 : s\y=0 : s\z=0
	s\surf=surf
	
	;put the new sprite into the bank
;PokeInt l\spritebank,(l\count*4)-4,Handle(s)
	Return Handle(s)
End Function
;--------------------------------------------------------------------------------------------

Function AlphaSprite(sprite,Alpha#)
	s.sprite = Object.sprite(sprite)
	
	VertexColor s\surf,s\verti[0],s\r,s\g,s\b,Alpha#
	VertexColor s\surf,s\verti[1],s\r,s\g,s\b,Alpha#
	VertexColor s\surf,s\verti[2],s\r,s\g,s\b,Alpha#
	VertexColor s\surf,s\verti[3],s\r,s\g,s\b,Alpha#
End Function

;--------------------------------------------------------------------------------------------
Function PositionSprite(sprite,x#,y#,z#)
	s.sprite = Object.sprite(sprite)
	s\x=x
	s\y=y
	s\z=z
End Function

;--------------------------------------------------------------------------------------------

Function fps()
	fpsindex=fpsindex+1
	fpstime=fpstime+MilliSecs()-fpsfold_millisecs
	If fpstime=&gt;1000
		fpsfps=fpsindex
		fpstime=0
		fpsindex=0
	EndIf
	fpsfold_millisecs=MilliSecs()
	Return fpsfps
End Function

;--------------------------------------------------------------------------------------------

Function TransTex(texture,angle#,scale#=1)
	ScaleTexture texture,scale,scale
	RotateTexture texture,angle#
	x#=Cos(angle)/scale/2
	y#=Sin(angle)/scale/2
	PositionTexture texture,(x-.5)-y,(y-.5)+x
End Function



;--------------------------------------------
; M O U S E L O O K
;
; Parms: 
; Cam   - Camera
; T     - Current timer
; Time1 - Last time event ran
; Freq  - How often in milliseconds this function runs
Function Mouselook(Cam,T,Time1,Freq)

If T &gt; Time1 + Freq Then
	Time1=T
	
	If MouseDown(2) Then 

		TurnEntity cam,MouseYSpeed(),-MouseXSpeed(),0
		RotateEntity cam,EntityPitch(cam), EntityYaw(cam),0
		MoveMouse GraphicsWidth()/2,GraphicsHeight()/2

	EndIf
EndIf
Return Time1

End Function

</textarea> <br><br></td></tr></table><br>
<a name="679129"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well, i think in a single surface code, you shouldn't really try and delete sprites. Since, you will need to re-build the entire surfaces again. Best to just hide it and mark it for non-use, until you need to spawn a new sprite.<br><br>As for hiding a sprite, simply get all the four verticex and use vertex alpha on them, via the vertexcolor command :o) <br><br></td></tr></table><br>
<a name="679144"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have try to make this function but don't work :(<br><br>I haite single surface system :/<br><br>Function AlphaSprite(sprite,Alpha#)<br>	s.sprite = Object.sprite(sprite)<br>	<br>	VertexColor s\surf,s\verti[0],s\r,s\g,s\b,Alpha#<br>	VertexColor s\surf,s\verti[1],s\r,s\g,s\b,Alpha#<br>	VertexColor s\surf,s\verti[2],s\r,s\g,s\b,Alpha#<br>	VertexColor s\surf,s\verti[3],s\r,s\g,s\b,Alpha#<br>End Function <br><br></td></tr></table><br>
<a name="679151"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have find it's the entityfx the problem :/ must be 2+32 <br><br></td></tr></table><br>
<a name="679153"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh yeah, you must have that to enable the vertex alpha. Sorry ^_^ <br><br></td></tr></table><br>
<a name="679170"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> You should simply position the unused sprite outside the camera range. <br><br>Even when not visible vertex alpha 0 quads seems to slow down quite a bit when directly in front of the camera.  Strange I know but this is what I've experienced. <br><br>Stevie <br><br></td></tr></table><br>
<a name="679189"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok many thanks :) <br><br></td></tr></table><br>
<a name="679203"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Matty</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Or you can simply place all the vertices for the quad/sprite at a single point and it will not appear, or you could move them out of the view of the camera (behind). <br><br></td></tr></table><br>
<a name="679227"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Filax, now the z-order is all messed up by using flag 2! =( <br><br></td></tr></table><br>
<a name="679271"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok :) <br><br></td></tr></table><br>
<a name="679297"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> REMOVE THIS <br><br></td></tr></table><br>
<a name="679322"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Remove what? <br><br></td></tr></table><br>
<a name="679447"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> filax did you get the z-order thing fixed? <br><br></td></tr></table><br>
<a name="679621"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Defoc8</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> you might want to test alpha based hiding - im pretty sure<br>when i looked at this a while back - it only worked when you<br>used entity alpha - vertex alpha does not prevent the data<br>being processed..which results in card processing &amp; rejecting<br>or whatever it does with alpha 0 triangles..<br>Worst case - the card blends the quad triangles - wasting<br>gfx processor time on unseen geom..<br>Also transparent surfaces are subject to sorting..another<br>slow down..<br>I very much doubt that mark has a mechanism built in to <br>determine if all the verts have alpha 0!<br><br>the simplest solution as stated above - move the quad to<br>some insane position - far outside of the view range..<br>this will result in it being culled..quick n easy..<br><br><br> - dont assume anything ;) <br><br></td></tr></table><br>
<a name="679633"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> It won't be culled.  Single surface renders everything in that mesh no matter where it is.  I've tried it and the Tris count does not go down. <br><br></td></tr></table><br>
<a name="679635"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Defoc8</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Chroma - b3d will report the tri as being rendered yes..<br>but infact the card will cull it..b3d reports all the tris in a <br>mesh..when the mesh is in view..it does not and cannot<br>determine which tris are culled by the card without <br>intensive software processing..<br>When tris are rendered they will be clipped by the gfx<br>card based on the viewing frustrum...per tri..<br>Dont confuse whats going on in software with whats going<br>on in hardware.. <br><br></td></tr></table><br>
<a name="679651"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Chroma</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well that's excellent news then.  That means if you want to hide a single surface quad then you could place it at say -50000 on the y axis and it would not be drawn correct? <br><br></td></tr></table><br>
<a name="679655"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Defoc8</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> assuming the tri/quad is not within the viewing frustrum, it<br>will be culled by the hardware - so yes...<br><br>for large complicated meshes leaving this to gfx card is not<br>a good idea - blitz uses bounding volumes to ensure that<br>only meshes in view are sent to gfx card for drawing..the <br>card then determines which tris will be rendered..<br><br>Keeping this in mind - one single single surface for all your<br>particles may not be a good idea - instead you might<br>want to consider clusters - consider the max number<br>particles needed for your fx + create single surface <br>groups. When you alter the position of one quad in<br>mesh..the entire mesh is uploaded to the card..nastey..<br>this is not good, to be fair this will happen only once<br>per render when you are using a single mesh to store<br>your particles. BUT..if the number particles is too high,<br>you will cause a stall. Not that this wont happen with<br>clusters..but you lessen the chances of the stall, you<br>probably wont be using all the clusters at the same time.<br><br>Note:- you dont need to have create all your quads<br>           for a surface before you start - adding tris isnt<br>           expensive..3 points are uploaded..<br>           editing vertex data causes the entire surface/mesh<br>           to be uploaded..this can be expensive..<br><br>you might want to try clusters vs one large surface.. <br><br></td></tr></table><br>
<a name="679749"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Good advice. May even have a seperate surface for each emittor or area, depending on the particle count.<br><br>A good way to make sure the quad is out of view, is to take the cameras position and place the quad behind it, or just outside the camera views range, as long as your co-ords are relative to the cameras, to be doubly sure your camera won't see the hidden particles. I wasn't actually aware that the vertex alpha didn't stop the quad from being rendered. Thanks :o) <br><br></td></tr></table><br>
<a name="679750"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Good advice. May even have a seperate surface for each emittor or area, depending on the particle count.<br><br>A good way to make sure the quad is out of view, is to take the cameras position and place the quad behind it, or just outside the camera view range, as long as your co-ords are relative to the cameras, to be doubly sure your camera won't see the hidden particles. I wasn't actually aware that the vertex alpha didn't stop the quad from being rendered. Thanks :o) <br><br></td></tr></table><br>
<a name="681089"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi !<br><br>Thanks for reply's guys ! i have get a good french single <br>surface library here is the code : But i have alway my <br>Zorder problem :( i have try sort method from code archive<br>(fredborg etc) but nothing work with this library ???<br><br>If anybody can help me ? i'll give a TerraEd pro licence<br>to the guy wo can solve Zorder problem :)<br><br>Here is the particle sprite used :<br><img src="http://www.blitz3dfr.com/tempo/Particle.png"><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; --------------------------
; Create SSE layer
; --------------------------
Type SSE_Layer
	Field Surface
	Field Mesh
	Field Parent
	Field Effect
	Field Blend
End Type

; ---------------------------
; Create SSE emiter
; ---------------------------
Type SSE_Emitter
	Field Pivot
	
	Field Px#
	Field Py#
	Field Pz#
End Type

; --------------------------
; Create SSE entity
; --------------------------
Type SSE_Entity
	Field Surface
	Field Parent
	
	Field Px#
	Field Py#
	Field Pz#
	
	Field Rotate_Z#
	
	Field Scale_X#
	Field Scale_Y#
	
	Field Move_x#
	Field Move_y#
	Field Move_z#
	
	Field Red
	Field Green
	Field Blue
	
	Field Alpha#
	
	Field Show
	Field Draw

	Field UV_Ud#
	Field UV_Uf#
	Field UV_Vd#
	Field UV_Vf#
End Type

; --------------------
; Create a layer
; --------------------
Function SSE_CreateLayer(Texture$,Blend=3)
	L.SSE_Layer=New SSE_Layer

	L\Mesh=CreateMesh()
	L\Surface=CreateSurface(L\Mesh)
	L\Parent=0

	L\Effect=1+2+8+16+32
	L\Blend=Blend

	EntityFX L\Mesh,L\Effect
	EntityBlend L\Mesh,L\Blend

	Local_Texture=LoadTexture (Texture$,1+2+256)
	EntityTexture L\Mesh,Local_Texture,0,0
	FreeTexture Local_Texture

	Return Handle(L)
End Function

; -----------------------------------------------
; Retrieve the surface from a layer
; -----------------------------------------------
Function SSE_GetLayerSurface(layer)
	L.SSE_Layer=Object.SSE_Layer(layer)
	Return L\Surface
End Function

; --------------------------------------------
; Retrieve the mesh from a layer
; --------------------------------------------
Function SSE_GetLayerMesh(layer)
	L.SSE_Layer=Object.SSE_Layer(layer)
	Return L\Mesh
End Function

; ---------------------------------
; Create an SSE particle
; ---------------------------------
Function SSE_CreateParticule(Surface,Parent=False,ScaleX#=1,ScaleY#=1,Blend=3,Alpha#=1,Red=255,Green=255,Blue=255)
	SSE_Particle.SSE_Entity=New SSE_Entity
	SSE_Particle\Surface=Surface
	
	SSE_Particle\Show=1
	SSE_Particle\Draw=0
	
	SSE_Particle\Px#=0.0
	SSE_Particle\Py#=0.0
	SSE_Particle\Pz#=0.0
		
	SSE_Particle\Rotate_Z#=0.0
	
	SSE_Particle\Scale_X#=ScaleX#
	SSE_Particle\Scale_Y#=ScaleY#
	
	SSE_Particle\Red=Red
	SSE_Particle\Green=Green
	SSE_Particle\Blue=Blue
	SSE_Particle\Alpha#=Alpha#
	
	SSE_Particle\UV_Ud#=0.0
	SSE_Particle\UV_Uf#=1.0
	SSE_Particle\UV_Vd#=0.0
	SSE_Particle\UV_Vf#=1.0

	SSE_Particle\Parent=Parent
		
	Return Handle(SSE_Particle)
End Function

; -----------------------
; Get a particle X
; -----------------------
Function SSE_GetParticuleX#(Part)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	Return SSE_Particle\Px#
End Function

; -----------------------
; Get a particle Y
; -----------------------
Function SSE_GetParticuleY#(Part)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	Return SSE_Particle\Py#
End Function

; -----------------------
; Get a particle Z
; -----------------------
Function SSE_GetParticuleZ#(Part)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	Return SSE_Particle\Pz#
End Function

; --------------------------
; Position a particle
; --------------------------
Function SSE_PositionParticule(Part,Px#,Py#,pz#)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	SSE_Particle\Px#=Px#
	SSE_Particle\Py#=Py#
	SSE_Particle\Pz#=Pz#
End Function

; ---------------------------
; Translate a particle
; ---------------------------
Function SSE_TranslateParticule(Part,tx#,ty#,tz#)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	SSE_Particle\Px#=SSE_Particle\Px#+tx#
	SSE_Particle\Py#=SSE_Particle\Py#+ty#
	SSE_Particle\Pz#=SSE_Particle\Pz#+tz#
End Function

; ---------------------
; Move a particle
; ---------------------
Function SSE_MoveParticule(Part,Px#,Py#,Pz#)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	
	SSE_Particle\Px#=SSE_Particle\Px#+ ( Px#*Cos(SSE_Particle\Move_y#)*Cos(SSE_Particle\Move_z#) -Pz#*Sin(SSE_Particle\Move_y#) -Py#*Sin(SSE_Particle\Move_z#) )
	SSE_Particle\Py#=SSE_Particle\Py#+ ( PY#*Cos(SSE_Particle\Move_x#)*Cos(SSE_Particle\Move_z#) -Pz#*Sin(SSE_Particle\Move_x#) -Px#*Sin(SSE_Particle\Move_z#) )
	SSE_Particle\Pz#=SSE_Particle\Pz#+ ( Pz#*Cos(SSE_Particle\Move_y#)*Cos(SSE_Particle\Move_x#) -Px#*Sin(SSE_Particle\Move_y#) -Py#*Sin(SSE_Particle\Move_x#) )
End Function

; ----------------------
; Scale a particle
; ----------------------
Function SSE_ScaleParticule(Part,Scale_X#,Scale_Y#)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	SSE_Particle\Scale_X#=Scale_X#
	SSE_Particle\Scale_Y#=Scale_Y#
End Function

; --------------------
; Roll a particle
; --------------------
Function SSE_Turn2DParticule(Part,Rotate_Z#)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	SSE_Particle\Rotate_Z#=Rotate_Z#+SSE_Particle\Rotate_Z#
End Function

; --------------------------
; Rotate Z a particle
; --------------------------
Function SSE_Rotate2DParticule(Part,Rotate_Z#)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	SSE_Particle\Rotate_Z#=Rotate_Z#
End Function

; ------------------------------
; Rotate XYZ a particle
; ------------------------------
Function SSE_RotateParticule(Part,Rotate_X#,Rotate_Y#,Rotate_Z#)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	SSE_Particle\Move_X#=Rotate_X#
	SSE_Particle\Move_Y#=Rotate_Y#
	SSE_Particle\Move_Z#=Rotate_Z#
End Function

; --------------------
; Turn a particle
; --------------------
Function SSE_TurnParticule(Part,Rotate_X#,Rotate_Y#,Rotate_Z#)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	SSE_Particle\Move_x#=Rotate_X#+SSE_Particle\Move_x#
	SSE_Particle\Move_y#=Rotate_Y#+SSE_Particle\Move_y#
	SSE_Particle\Move_z#=Rotate_Z#+SSE_Particle\Move_z#
End Function

; ---------------------
; Free a particle
; ---------------------
Function SSE_FreeParticule(Part)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	Delete SSE_Particle
End Function

; ---------------------
; Color a particle
; ---------------------
Function SSE_ParticuleColor(Part,Red,Green,Blue)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	SSE_Particle\Red=Red
	SSE_Particle\Green=Green
	SSE_Particle\Blue=Blue
End Function

; --------------------
; Particle alpha
; --------------------
Function SSE_ParticuleAlpha(Part,Alpha#)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	SSE_Particle\Alpha#=Alpha#
End Function

; ------------------
; Particle hide
; ------------------
Function SSE_HideParticule(Part,State)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)

	Select State
	Case True
		SSE_Particle\Show=0
	Case False 
		SSE_Particle\Show=1
	End Select
End Function

; ----------------------------------------
; Define manualy particle UV
; ----------------------------------------
Function SSE_SetParticleUV(Part,UV_Ud#,UV_Uf#,UV_Vd#,UV_Vf#)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	SSE_Particle\UV_Ud#=UV_Ud#
	SSE_Particle\UV_Uf#=UV_Uf#
	SSE_Particle\UV_Vd#=UV_Vd#
	SSE_Particle\UV_Vf#=UV_Vf#
End Function

; ---------------------------------------------------------------------------------------------
; You can from a strip picture, define a piece of picture via number
; ---------------------------------------------------------------------------------------------
Function SSE_SetUVMatrix(Part,Size_X=1,Size_Y=1,Value=0)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	
	If Value&gt;((Size_X*Size_Y)-1) Then 
		Return 0
	Else 
		Local_Z#=Ceil((Value+1)/Float(Size_X))-1
		
		If Size_X&gt;1 Then
			SSE_Particle\UV_Vd#=Local_Z#/Float(Size_Y)
			SSE_Particle\UV_Vf#=(Local_Z#+1)/Float(Size_Y)
		Else
			SSE_Particle\UV_Vd#=0.0
			SSE_Particle\UV_Vf#=1.0
		EndIf
		
		If Size_Y&gt;1 Then
			SSE_Particle\UV_Ud#=(Value Mod Size_X)/Float(Size_X)
			SSE_Particle\UV_Uf#=(1+(Value Mod Size_X))/Float(Size_X)
		Else
			SSE_Particle\UV_Ud#=0.0
			SSE_Particle\UV_Uf#=1.0
		EndIf
	
		Return True
	EndIf
End Function

; -----------------------------------------------------------------------------------------------------------------
; Same from above but only for lines strip image, example with a 128*64 texture
;
; MyLayer2=SSE_CreateLayer("Particle.png")
; Surface2=SSE_GetLayerSurface(MyLayer2)
;
; Pivot2=CreatePivot() : PositionEntity pivot2,150,0,0
;
; For i=0 To 100
;	Temp3=SSE_CreateParticule(Surface2,pivot2)
;	SSE_PositionParticule(Temp3,Rnd(-100,100),Rnd(-100,100),Rnd(-100,100))
;	SSE_ScaleParticule(Temp3,20,20)
;	SSE_ParticuleColor(Temp3,Rnd(255),Rnd(255),Rnd(255))
;	SSE_SetUVLine(Temp3,2,1,Rand(0,1)) ; swap between image 1 and 2
; Next
; -----------------------------------------------------------------------------------------------------------------
Function SSE_SetUVLine(Part,Size_X,Size_Y,Value#)
	SSE_Particle.SSE_Entity=Object.SSE_Entity(Part)
	
	If (Size_X&gt;1 And Size_Y&gt;1) Then
		Return -1
	Else If Value#&gt;((Size_X*Size_Y)-1)Then
		Return 0
	EndIf
	
	If Size_X&gt;1 Then
		SSE_Particle\UV_Ud#=Value#/Float(Size_X)
		SSE_Particle\UV_Uf#=(Value#+1)/Float(Size_X)
		SSE_Particle\UV_Vd#=0.0
		SSE_Particle\UV_Vf#=1.0
	Else
		SSE_Particle\UV_Ud#=0.0
		SSE_Particle\UV_Uf#=1.0
		SSE_Particle\UV_Vd#=Value#/Float(Size_Y)
		SSE_Particle\UV_Vf#=(Value#+1)/Float(Size_Y)
	EndIf
	
	Return 1
End Function


; ----------------------------------
; Refresh particle system
; ----------------------------------
Function SSE_RefreshParticule(camera,zoom#=1)
	For L.SSE_Layer=Each SSE_Layer
		Local_Number=0
		
		ClearSurface L\Surface;on nettoie la Surface viser
	
		For SSE_Particle.SSE_Entity=Each SSE_Entity
	
			If  SSE_Particle\Show=1 And SSE_Particle\Surface=L\Surface Then 
				SSE_Particle\Draw=0
	
				If (SSE_Particle\parent=0) Then
						
				Local_X#=SSE_Particle\Px#
				Local_Y#=SSE_Particle\Py#
				Local_Z#=SSE_Particle\Pz#
			Else
				TFormPoint SSE_Particle\Px#,SSE_Particle\Py#,SSE_Particle\Pz#,SSE_Particle\Parent,0
				
				Local_X#=TFormedX()
				Local_Y#=TFormedY()
				Local_Z#=TFormedZ()
			EndIf
				
				
				Scale_X#=SSE_Particle\Scale_X#
				Scale_Y#=SSE_Particle\Scale_Y#
					
				Rotate_Z#=SSE_Particle\Rotate_Z#
				
				TFormPoint Local_X#,Local_Y#,Local_Z#,0,camera
				
				If TFormedZ#()&gt;0 Then ;verifie que la Particule se trouve devant la camera
					
					If TFormedX#()&lt;TFormedZ#()/zoom# And TFormedX#()&gt;-TFormedZ#()/zoom# And TFormedY#()&lt;TFormedZ#()*.75/zoom# And TFormedY#()&gt;-TFormedZ#()*.75/zoom# Then
						SSE_Particle\Draw=1
					Else
						;on teste la position des 4 vertices, si l'un deux, est visible, on dessine
						For p=0 To 3
							Local_Dx#=TFormedX#()+Scale_X#*(Cos(45+90*p + Rotate_Z#)/Cos(45))
							Local_Dy#=TFormedY#()+Scale_Y#*(Sin(45+90*p + Rotate_Z#)/Cos(45))	
							
							If Local_Dx#&lt;TFormedZ#()/zoom# And Local_Dx#&gt;-TFormedZ#()/zoom# And Local_Dy#&lt;TFormedZ#()*.7/zoom# And Local_Dy#&gt;-TFormedZ#()*.7/zoom# Then
								SSE_Particle\Draw=1
								Exit
							EndIf
						Next
						
					EndIf
					
						
				EndIf
				
							
				
				If SSE_Particle\Draw=1 Then
					Local_Tfx#=TFormedX#();savegarde de la position centrale
					Local_Tfy#=TFormedY#()
					Local_Tfz#=TFormedZ#()
									
					For p=0 To 3
						Select p
							Case 0
								Local_U#=SSE_Particle\UV_Uf#
								Local_V#=SSE_Particle\UV_Vd#
							Case 1
								Local_U#=SSE_Particle\UV_Ud#
								Local_V#=SSE_Particle\UV_Vd#
							Case 2
								Local_U#=SSE_Particle\UV_Ud#
								Local_V#=SSE_Particle\UV_Vf#
							Case 3
								Local_U#=SSE_Particle\UV_Uf#
								Local_V#=SSE_Particle\UV_Vf#
						End Select
						
						Local_Dx#=Scale_X#*(Cos(45+90*p + Rotate_Z#)/Cos(45))
						Local_Dy#=Scale_Y#*(Sin(45+90*p + Rotate_Z#)/Cos(45))	
						
						TFormPoint Local_Tfx#+Local_Dx#,Local_Tfy#+Local_Dy#,Local_Tfz#,camera,0
			
						AddVertex(SSE_Particle\Surface,TFormedX#(),TFormedY#(),TFormedZ#(),Local_U#,Local_V#)
						VertexColor(SSE_Particle\Surface,p+Local_Number,SSE_Particle\Red,SSE_Particle\Green,SSE_Particle\Blue,SSE_Particle\Alpha#) 	
					Next
	
					AddTriangle(SSE_Particle\Surface,1+Local_Number,0+Local_Number,2+Local_Number)
					AddTriangle(SSE_Particle\Surface,2+Local_Number,0+Local_Number,3+Local_Number)
					
					Local_Number=Local_Number+4
				EndIf
			
				
			EndIf

Next

	Next


	
	Return Local_Number/4
End Function
</textarea><br><br>And an example with Zorder problem :<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;=====================================================================
;Module "Single Surface"
;Pour BlitzKlan (http://blitzklan.free.fr)
;Par GIGNOPS (gignops@...) modifs par Filax
;Ce module sert à gerer une Scale_Ysteme de Particule en single Surface
;V1.0 05/05/2005
;======================================================================

Global fpsindex#,fpstime#,fpsfold_millisecs#,fpsfps#,time,xyzturn

Include "SSEngine.bb"

Graphics3D 1024,768,32,0
SetBuffer BackBuffer()


Global camera=CreateCamera()
CameraRange camera,1,9000
PositionEntity camera,0,0,-100

light=CreateLight(2)
PositionEntity light,100,100,100



MyLayer1=SSE_CreateLayer("Particle.png",1)
Surface1=SSE_GetLayerSurface(MyLayer1)
DebugLog "&gt; : "+Surface1

pivot1=CreatePivot()
PositionEntity pivot1,-250,0,0

For i=0 To 1000
Temp3=SSE_CreateParticule(Surface1,pivot1)
SSE_PositionParticule(Temp3,Rnd(-100,100),Rnd(-100,100),Rnd(-100,100))
SSE_ScaleParticule(Temp3,5,5)
SSE_ParticuleColor(Temp3,Rnd(255),Rnd(255),Rnd(255))
SSE_SetUVLine(Temp3,3,1,0)
Next


pivot2=CreatePivot() : PositionEntity pivot2,250,0,0

For i=0 To 1000
	Temp3=SSE_CreateParticule(Surface1,pivot2)
	SSE_PositionParticule(Temp3,Rnd(-100,100),Rnd(-100,100),Rnd(-100,100))
	SSE_ScaleParticule(Temp3,10,10)
	SSE_ParticuleColor(Temp3,Rnd(255),Rnd(255),Rnd(255))
	SSE_SetUVLine(Temp3,3,1,1)
Next

pivot3=CreatePivot() : PositionEntity pivot3,500,0,0

For i=0 To 1000
	Temp3=SSE_CreateParticule(Surface1,pivot3)
	SSE_PositionParticule(Temp3,Rnd(-100,100),Rnd(-100,100),Rnd(-100,100))
	SSE_ScaleParticule(Temp3,10,10)
	SSE_ParticuleColor(Temp3,Rnd(255),Rnd(255),Rnd(255))
	SSE_SetUVLine(Temp3,3,1,2)
Next



While Not KeyHit(1)

SSE_RefreshParticule(camera,1)

mouselook(camera)
	TurnEntity pivot1,0.1,0.1,0.1
	TurnEntity pivot2,-0.1,0.1,0.1

	If MouseHit(3) Then

	EndIf
	
	If KeyDown(17) Then MoveEntity camera,0,0,1  ;W
	If KeyDown(31) Then MoveEntity camera,0,0,-1 ;S
	If KeyDown(30) Then MoveEntity camera,-1,0,0 ;A
	If KeyDown(32) Then MoveEntity camera,1,0,0  ;D
	
	UpdateWorld
	RenderWorld

	Text 10,10,fps()
	Flip 0
Wend

End

Function fps()
	fpsindex=fpsindex+1
	fpstime=fpstime+MilliSecs()-fpsfold_millisecs
	If fpstime=&gt;1000
		fpsfps=fpsindex
		fpstime=0
		fpsindex=0
	EndIf
	fpsfold_millisecs=MilliSecs()
	Return fpsfps
End Function



;--------------------------------------------
; M O U S E L O O K
;
; Parms: 
; Cam   - Camera
; T     - Current timer
; Time1 - Last time event ran
; Freq  - How often in milliseconds this function runs
Function Mouselook(Cam)


	
	If MouseDown(2) Then 

		TurnEntity cam,MouseYSpeed(),-MouseXSpeed(),0
		RotateEntity cam,EntityPitch(cam), EntityYaw(cam),0
		MoveMouse GraphicsWidth()/2,GraphicsHeight()/2

	EndIf



End Function
</textarea> <br><br></td></tr></table><br>
<a name="681167"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >John Pickford</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> &gt; Delete a sprite<br>&gt; Hide a sprite<br><br>I switched to rebuilding the mesh each frame.  Didn't notice any performance issues and I didn't have to worry about hiding\removing sprites.   Nor did I have to set a maximum number of sprites (other than the limit to the number of triangle in a surface). <br><br></td></tr></table><br>
<a name="681325"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#23">[#23]</a></td></tr></table></td></tr><tr ><td class="posttext"> Any idea for Zorder problem ? (sprite behind other's) ? <br><br></td></tr></table><br>
<a name="681344"></a>

<a name="1134269"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RGR</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#24">[#24]</a></td></tr></table></td></tr><tr ><td class="posttext"> .<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="681353"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#25">[#25]</a></td></tr></table></td></tr><tr ><td class="posttext"> ?? Sorry i don't understand, my problem come from my<br>sprite Zorder, there are refresh from the order creation !<br><br>I think a solution is to sort the sprite list with a method<br>to sort them with a distance =&gt; camera (addfirst &amp; addlast)<br>but all my tests are bugged :( if anybody can help me with <br>this, i'll give to the winner a TerraEd Pro :) <br><br></td></tr></table><br>
<a name="682318"></a>

<a name="1134270"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RGR</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#26">[#26]</a></td></tr></table></td></tr><tr ><td class="posttext"> .<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1134271"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RGR</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#27">[#27]</a></td></tr></table></td></tr><tr ><td class="posttext"> .<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="682367"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Filax</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#28">[#28]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi :)<br><br>Thanks for reply ! But, if i understand your function<br>your quad are made with a function, and this function<br>return a 'mesh' (like a sprite) and i can't use this<br>because in a particle engine imagine you have 8000 quad<br>entity ! Under my lib you have only one mesh with dynamic<br>vertex and triangle creation, it's not the same thing !<br>i have only one entity ! All my vertex are stored under<br>the list, imo the solution is to sort the list from the<br>distance between my vertex =&gt; camera ?? Maybe i'm wrong ?<br>but i become crazy with this stuff :/ <br><br></td></tr></table><br>
<a name="682565"></a>

<a name="1134272"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RGR</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#29">[#29]</a></td></tr></table></td></tr><tr ><td class="posttext"> .<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1134274"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RGR</td><td align="right"><font class=tiny>(Posted 2006)</font>&nbsp;<a href="#30">[#30]</a></td></tr></table></td></tr><tr ><td class="posttext"> .<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1303505"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#31">[#31]</a></td></tr></table></td></tr><tr ><td class="posttext"> RGR the serial deleter... :\ Not good man ! <br><br></td></tr></table><br>
<a name="1303506"></a>

<a name="1303507"></a>

<a name="1303508"></a>

<a name="1303510"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RGR</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#32">[#32]</a></td></tr></table></td></tr><tr ><td class="posttext"> RemiD the 10 years old threads Spammer ;-) Even worse man!<br><br>It is amazing that nowadays everybody knows what other people should do ... <br><br></td></tr></table><br>
<a name="1303518"></a>

<a name="1303520"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#33">[#33]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sometimes some people say that i am harsh but i am an amateur compared to you... <br><br></td></tr></table><br>
<a name="1303531"></a>

<a name="1303532"></a>

<a name="1303533"></a>

<a name="1303534"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RGR</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#34">[#34]</a></td></tr></table></td></tr><tr ><td class="posttext"> You will improve ... no doubt ... nobody is a born professional ;-) <br><br></td></tr></table><br>
<a name="1303553"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#35">[#35]</a></td></tr></table></td></tr><tr ><td class="posttext"> I try to moderate myself to not be banned ;) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
