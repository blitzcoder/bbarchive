<!DOCTYPE html><html lang="en" ><head ><title >Help with vertices please</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Help with vertices please</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Help with vertices please</a><br><br>
<a name="406417"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is a code snippet from a small game I am in the process of coding. When you run the code you will notice that the flat areas are shaded grey. What I want is only complete squares shaded - you will notice the there are some individual triangles shaded (attached to squares and by themselves) - How do I stop those triangles from being shaded?<br><br>Please help.<br><br><br><pre class=code>
; level test

Graphics3D 640,480,16,2


Global width = 32
Global seed  = 3

Dim plasma#(width,width)
GeneratePlasma(width,width,1,seed)
SquarePlasma(width,0.005)

	
SetBuffer BackBuffer()

terr = CreateTerrainMesh(width,width)
b0 =  LoadBrush("efx_sample_05.jpg",1+8)
b1 =  LoadBrush("grass.bmp",1+8)

terr = GradePaintTerrainMesh(terr, 0.00001, 90, 90, 90, b0, b1, b2, b3, b4)
ScaleEntity terr,1,4,1
EntityFX terr,2
EntityColor terr,0,0,255


Global cam_camera = CreateCamera()
PositionEntity cam_camera,10,40,10
sun= CreateLight()
PositionEntity sun,10,40,10
RotateEntity sun,45,0,0


While Not KeyHit(1)

lev_docamera()

UpdateWorld
RenderWorld
Flip

Wend


End



Function LEV_docamera()

		CamSpd# = .50 
		MoveEntity(CAM_Camera, Float(KeyDown(205) - KeyDown(203)) * CamSpd, 0, Float(KeyDown(200) - KeyDown(208)) * CamSpd)

		If MouseDown(1) Then
			WireFrame 1
		Else
			WireFrame 0 
		EndIf

		If MouseDown(2)
			TurnSpeed# = 0.8
			TurnEntity(CAM_Camera, Float(MouseYSpeed())  * TurnSpeed#, 0, 0, False)
			TurnEntity(CAM_Camera, 0, -Float(MouseXSpeed()) * TurnSpeed#, 0, True)
		Else
			MouseXSpeed() : MouseYSpeed()
	 	EndIf
End Function





Function GeneratePlasma(width, height, grain#, seed)
   
     SeedRnd(seed)

    
     Dim plasma#(width, height)
    
     For j = 0 To width - 1
          For i = 0 To height - 1
               plasma#(j, i) = -1.0
          Next
     Next

     
     x_max = Ceil(Float(width - 1)/2.0)     ; mid-point x
     y_max = Ceil(Float(height - 1)/2.0)     ; mid-point y
     plasma#(0,      0)      = Rnd(0.0, 1.0)
     plasma#(x_max, 0)      = Rnd(0.0, 1.0)
     plasma#(0,      y_max) = Rnd(0.0, 1.0)
     plasma#(x_max, y_max) = Rnd(0.0, 1.0)

     
     RecursePlasma(0, 0, x_max, y_max, grain#, 1.0)
      
     For y = 0 To y_max
          plasma#(width - 1, y) = plasma#(0, y)
     Next
    
     RecursePlasma(x_max, 0, width - 1, y_max, grain#, 1.0)
     
     For x = 0 To width - 1
          plasma#(x, height - 1) = plasma#(x, 0)
     Next
     
     RecursePlasma(x_max, y_max, width - 1, height - 1, grain#, 1.0)
     
     For y = (y_max  + 1) To height - 1
          plasma#(0, y) = plasma#(width - 1, y)
     Next
    
     RecursePlasma(0, y_max, x_max, height - 1, grain#, 1.0)

End Function

;---------------------------------------------------------------------
Function MidPointDisplace#(x0, y0, x1, y1, x2, y2, grain#, level#)
     r# = Rnd(-grain#, grain#)/level#
     r# = (plasma#(x0, y0) + plasma#(x2, y2))/2.0 + r#
     If r# &lt; 0.0 Then r# = 0.0
     If r# &gt; 1.0 Then r# = 1.0
     plasma#(x1, y1) = r#
     
     Return r#
     
End Function

;---------------------------------------------------------------------
Function RecursePlasma(x0, y0, x2, y2, grain#, level#)
     
     If (x2 - x0 &lt; 2) And (y2 - y0 &lt; 2) Then Return
     
     level# = 2.0*level#
     
     x1 = Ceil((x0 + x2)/2.0)
     y1 = Ceil((y0 + y2)/2.0)
     
     v# = plasma#(x1, y0)
     If v# = -1.0 Then v# = MidPointDisplace#(x0, y0, x1, y0, x2, y0, grain#, level#)
     i# = v#
     
     v# = plasma#(x2, y1)
     If v# = -1.0 Then v# = MidPointDisplace#(x2, y0, x2, y1, x2, y2, grain#, level#)
     i# = i# + v#
     
     v# = plasma#(x1, y2)
     If v# = -1.0 Then v# = MidPointDisplace#(x0, y2, x1, y2, x2, y2, grain#, level#)
     i# = i# + v#

     v# = plasma#(x0, y1)
     If v# = -1.0 Then v# = MidPointDisplace#(x0, y0, x0, y1, x0, y2, grain#, level#)
     i# = i# + v#

     If plasma#(x1, y1) = -1.0 Then plasma#(x1, y1) = i#/4.0 
 
     RecursePlasma(x0, y0, x1, y1, grain#, level#)
     RecursePlasma(x1, y0, x2, y1, grain#, level#)
     RecursePlasma(x1, y1, x2, y2, grain#, level#)
     RecursePlasma(x0, y1, x1, y2, grain#, level#)
     
End Function

; ------------------------------------------------------------------------

Function SquarePlasma(width,value#)

yMax# = 0
tempx = 0
tempy = 0

For x = 0 To width - 1 
  	For y = 0 To width -1
		
		x1# = plasma(x,y)
		x2# = plasma(x+1,y)
		x3# = plasma(x,y+1)
		x4# = plasma(x+1,y+1)
		
		av# = (x1+x2+x3+x4) / Float(4)
		

			If Abs(av-x1) &lt;= value Then
			
								
				plasma(x,y)		= x1
						
				plasma(x+1,y) 	= x1
			
				plasma(x,y+1) 	= x1
			
				plasma(x+1,y+1) = x1
				
			EndIf
			
		
		
		If x1 &gt; yMax Then 
			yMax = x1
			tempx = x
			tempy = y
		EndIf
		
	Next
Next
plasma(tempx,tempy) = ymax
plasma(tempx+1,tempy) = ymax
plasma(tempx,tempy+1) = ymax
plasma(tempx+1,tempy+1) = ymax

     

End Function

;----------------------------------------------------------------------------

Function CreateTerrainMesh(x,y)
	;x = how many horizontal vertices
	;y = how many vertical vertices

	mesh = CreateMesh()
	surf = CreateSurface(mesh)	
	If x &lt; 2 Or y &lt; 2 Then Return mesh		;in case user gives a bad number simply return the mesh
	For tempx = 1 To x
		For tempy = 1 To y
			
				AddVertex(surf,tempx*4,plasma#(tempx,tempy)*10,tempy*4)
			
		Next	
	Next	

	For v0 = 0 To (x*y)-y-2
		For Z = 1 To x*y
			If v0 = z*y-1 Then v0 = v0 + 1
		Next
		v1 = V0 + 1
		v2 = v1 + y
		v3 = v0 + y
		AddTriangle(surf,v0,v1,v2)
		AddTriangle(surf,v0,v2,v3)
	Next

	UpdateNormals(mesh)
	Return mesh
End Function


; ----------------------------------- thanks to mrsnidesmin

Function GradePaintTerrainMesh%(m%, g1#, g2#, g3#, g4#, bGrad0%, bGrad1%, bGrad2%, bGrad3%, bGrad4%)	
	m2% = CreateMesh()
	
	s_g0% = CreateSurface(m2)
	s_g1% = CreateSurface(m2)

	
	For i% = 1 To CountSurfaces(m)
		s% = GetSurface(m, i)
		For it% = 0 To CountTriangles(s)-1
			v0% = TriangleVertex(s, it, 0)
			v1% = TriangleVertex(s, it, 1)
			v2% = TriangleVertex(s, it, 2)
			
			vt% = TriangleVertex(s,it+1,0)   ; error here?
			
			
			v0x# = VertexX(s, v0)
			v0y# = VertexY(s, v0)
			v0z# = VertexZ(s, v0)
			v0u# = VertexU(s, v0)
			v0v# = VertexV(s, v0)
			v0w# = VertexW(s, v0)
			v0nx# = VertexNX(s, v0)
			v0ny# = VertexNY(s, v0)
			v0nz# = VertexNZ(s, v0)
			v0red# = VertexRed(s, v0)
			v0green# = VertexRed(s, v0)
			v0blue# = VertexRed(s, v0)
			v0alpha# = VertexAlpha(s, v0)
			
			v1x# = VertexX(s, v1)
			v1y# = VertexY(s, v1)
			v1z# = VertexZ(s, v1)
			v1u# = VertexU(s, v1)
			v1v# = VertexV(s, v1)
			v1w# = VertexW(s, v1)
			v1nx# = VertexNX(s, v1)
			v1ny# = VertexNY(s, v1)
			v1nz# = VertexNZ(s, v1)
			v1red# = VertexRed(s, v1)
			v1green# = VertexRed(s, v1)
			v1blue# = VertexRed(s, v1)
			v1alpha# = VertexAlpha(s, v1)
			
			v2x# = VertexX(s, v2)
			v2y# = VertexY(s, v2)
			v2z# = VertexZ(s, v2)
			v2u# = VertexU(s, v2)
			v2v# = VertexV(s, v2)
			v2w# = VertexW(s, v2)
			v2nx# = VertexNX(s, v2)
			v2ny# = VertexNY(s, v2)
			v2nz# = VertexNZ(s, v2)
			v2red# = VertexRed(s, v2)
			v2green# = VertexRed(s, v2)
			v2blue# = VertexRed(s, v2)
			v2alpha# = VertexAlpha(s, v2)
						
			
			
		
			If (v0y = v2y And v0y = v1y And v0y = VertexY(s,vt)) Then  ; error here?
				s2 = s_g0
			Else
				s2 = s_g1
			EndIf
	

			v0% = AddVertex(s2, v0x, v0y, v0z, v0u, v0v, v0w)
			v1% = AddVertex(s2, v1x, v1y, v1z, v1u, v1v, v1w)
			v2% = AddVertex(s2, v2x, v2y, v2z, v2u, v2v, v2w)
			VertexNormal s2, v0, v0nx, v0ny, v0nz
			VertexNormal s2, v1, v1nx, v1ny, v1nz
			VertexNormal s2, v2, v2nx, v2ny, v2nz
			VertexColor s2, v0, v0red, v0green, v0blue, v0alpha
			VertexColor s2, v1, v1red, v1green, v1blue, v1alpha
			VertexColor s2, v2, v2red, v2green, v2blue, v2alpha
			AddTriangle s2, v0, v1, v2
		Next
	Next

	PaintSurface s_g0, bGrad0
	PaintSurface s_g1, bGrad1
	
	
	FreeEntity m
	Return m2
End Function
</pre> <br><br></td></tr></table><br>
<a name="406468"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Rigz</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I don't know if this is any good because i'm not entirely sure what you're trying to achieve in the long run, but I think this thing might be easier to work out if you do it on a quad by quad basis as opposed to tri tri as you seemed to be (?).<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; level test

Graphics3D 640,480,16,2


Global width = 32
Global seed  = 3

Dim plasma#(width,width)
Dim PVertices(width,width)
GeneratePlasma(width,width,1,seed)
SquarePlasma(width,0.005)

	
SetBuffer BackBuffer()

terr = CreateTerrainMesh(width,width)
b0 =  LoadBrush("efx_sample_05.jpg",1+8)
b1 =  LoadBrush("grass.bmp",1+8)

terr = GradePaintTerrainMesh(terr, 0.00001, 90, 90, 90, b0, b1, b2, b3, b4)
ScaleEntity terr,1,4,1
EntityFX terr,2
EntityColor terr,0,0,255


Global cam_camera = CreateCamera()
PositionEntity cam_camera,10,40,10
sun= CreateLight()
PositionEntity sun,10,40,10
RotateEntity sun,45,0,0


While Not KeyHit(1)

lev_docamera()

UpdateWorld
RenderWorld
Flip

Wend


End



Function LEV_docamera()

		CamSpd# = .50 
		MoveEntity(CAM_Camera, Float(KeyDown(205) - KeyDown(203)) * CamSpd, 0, Float(KeyDown(200) - KeyDown(208)) * CamSpd)

		If MouseDown(1) Then
			WireFrame 1
		Else
			WireFrame 0 
		EndIf

		If MouseDown(2)
			TurnSpeed# = 0.8
			TurnEntity(CAM_Camera, Float(MouseYSpeed())  * TurnSpeed#, 0, 0, False)
			TurnEntity(CAM_Camera, 0, -Float(MouseXSpeed()) * TurnSpeed#, 0, True)
		Else
			MouseXSpeed() : MouseYSpeed()
	 	EndIf
End Function





Function GeneratePlasma(width, height, grain#, seed)
   
     SeedRnd(seed)

    
     Dim plasma#(width, height)
    
     For j = 0 To width - 1
          For i = 0 To height - 1
               plasma#(j, i) = -1.0
          Next
     Next

     
     x_max = Ceil(Float(width - 1)/2.0)     ; mid-point x
     y_max = Ceil(Float(height - 1)/2.0)     ; mid-point y
     plasma#(0,      0)      = Rnd(0.0, 1.0)
     plasma#(x_max, 0)      = Rnd(0.0, 1.0)
     plasma#(0,      y_max) = Rnd(0.0, 1.0)
     plasma#(x_max, y_max) = Rnd(0.0, 1.0)

     
     RecursePlasma(0, 0, x_max, y_max, grain#, 1.0)
      
     For y = 0 To y_max
          plasma#(width - 1, y) = plasma#(0, y)
     Next
    
     RecursePlasma(x_max, 0, width - 1, y_max, grain#, 1.0)
     
     For x = 0 To width - 1
          plasma#(x, height - 1) = plasma#(x, 0)
     Next
     
     RecursePlasma(x_max, y_max, width - 1, height - 1, grain#, 1.0)
     
     For y = (y_max  + 1) To height - 1
          plasma#(0, y) = plasma#(width - 1, y)
     Next
    
     RecursePlasma(0, y_max, x_max, height - 1, grain#, 1.0)

End Function

;---------------------------------------------------------------------
Function MidPointDisplace#(x0, y0, x1, y1, x2, y2, grain#, level#)
     r# = Rnd(-grain#, grain#)/level#
     r# = (plasma#(x0, y0) + plasma#(x2, y2))/2.0 + r#
     If r# &lt; 0.0 Then r# = 0.0
     If r# &gt; 1.0 Then r# = 1.0
     plasma#(x1, y1) = r#
     
     Return r#
     
End Function

;---------------------------------------------------------------------
Function RecursePlasma(x0, y0, x2, y2, grain#, level#)
     
     If (x2 - x0 &lt; 2) And (y2 - y0 &lt; 2) Then Return
     
     level# = 2.0*level#
     
     x1 = Ceil((x0 + x2)/2.0)
     y1 = Ceil((y0 + y2)/2.0)
     
     v# = plasma#(x1, y0)
     If v# = -1.0 Then v# = MidPointDisplace#(x0, y0, x1, y0, x2, y0, grain#, level#)
     i# = v#
     
     v# = plasma#(x2, y1)
     If v# = -1.0 Then v# = MidPointDisplace#(x2, y0, x2, y1, x2, y2, grain#, level#)
     i# = i# + v#
     
     v# = plasma#(x1, y2)
     If v# = -1.0 Then v# = MidPointDisplace#(x0, y2, x1, y2, x2, y2, grain#, level#)
     i# = i# + v#

     v# = plasma#(x0, y1)
     If v# = -1.0 Then v# = MidPointDisplace#(x0, y0, x0, y1, x0, y2, grain#, level#)
     i# = i# + v#

     If plasma#(x1, y1) = -1.0 Then plasma#(x1, y1) = i#/4.0 
 
     RecursePlasma(x0, y0, x1, y1, grain#, level#)
     RecursePlasma(x1, y0, x2, y1, grain#, level#)
     RecursePlasma(x1, y1, x2, y2, grain#, level#)
     RecursePlasma(x0, y1, x1, y2, grain#, level#)
     
End Function

; ------------------------------------------------------------------------

Function SquarePlasma(width,value#)

yMax# = 0
tempx = 0
tempy = 0

For x = 0 To width - 1 
  	For y = 0 To width -1
		
		x1# = plasma(x,y)
		x2# = plasma(x+1,y)
		x3# = plasma(x,y+1)
		x4# = plasma(x+1,y+1)
		
		av# = (x1+x2+x3+x4) / Float(4)
		

			If Abs(av-x1) &lt;= value Then
			
								
				plasma(x,y)		= x1
						
				plasma(x+1,y) 	= x1
			
				plasma(x,y+1) 	= x1
			
				plasma(x+1,y+1) = x1
				
			EndIf
			
		
		
		If x1 &gt; yMax Then 
			yMax = x1
			tempx = x
			tempy = y
		EndIf
		
	Next
Next
plasma(tempx,tempy) = ymax
plasma(tempx+1,tempy) = ymax
plasma(tempx,tempy+1) = ymax
plasma(tempx+1,tempy+1) = ymax

     

End Function

;----------------------------------------------------------------------------

Function CreateTerrainMesh(x,y)
	;x = how many horizontal vertices
	;y = how many vertical vertices

	mesh = CreateMesh()
	surf = CreateSurface(mesh)	
	If x &lt; 2 Or y &lt; 2 Then Return mesh		;in case user gives a bad number simply return the mesh
	For tempx = 1 To x
		For tempy = 1 To y
			
				PVertices(tempx,tempy)=AddVertex(surf,tempx*4,plasma#(tempx,tempy)*10,tempy*4)
			
		Next	
	Next	

	For v0 = 0 To (x*y)-y-2
		For Z = 1 To x*y
			If v0 = z*y-1 Then v0 = v0 + 1
		Next
		v1 = V0 + 1
		v2 = v1 + y
		v3 = v0 + y
		AddTriangle(surf,v0,v1,v2)
		AddTriangle(surf,v0,v2,v3)
	Next

	UpdateNormals(mesh)
	Return mesh
End Function


; ----------------------------------- thanks to mrsnidesmin 

Function GradePaintTerrainMesh%(m%, g1#, g2#, g3#, g4#, bGrad0%, bGrad1%, bGrad2%, bGrad3%, bGrad4%) 
	m2% = CreateMesh() 
	
	s_g0% = CreateSurface(m2) 
	s_g1% = CreateSurface(m2) 
	
	
	For i% = 1 To CountSurfaces(m)
		s% = GetSurface(m, i)
		For tempx = 1 To width-1 
			For tempy = 1 To width-1 
				If plasma(tempx,tempy)=plasma(tempx,tempy+1) And plasma(tempx,tempy)=plasma(tempx+1,tempy) And plasma(tempx,tempy)=plasma(tempx+1,tempy+1)
					v0=AddVertex(s_g0,tempx*4,plasma#(tempx,tempy)*10,tempy*4)
					v1=AddVertex(s_g0,tempx*4,plasma#(tempx,tempy+1)*10,tempy*4+4)
					v2=AddVertex(s_g0,tempx*4+4,plasma#(tempx+1,tempy+1)*10,tempy*4+4)
					v3=AddVertex(s_g0,tempx*4+4,plasma#(tempx+1,tempy)*10,tempy*4)
					AddTriangle(s_g0,v0,v1,v2)
					AddTriangle(s_g0,v0,v2,v3)
					VertexNormal s_g0,v0,VertexNX(s,PVertices(tempx,tempy)),VertexNY(s,PVertices(tempx,tempy)),VertexNZ(s,PVertices(tempx,tempy))
					VertexNormal s_g0,v1,VertexNX(s,PVertices(tempx,tempy+1)),VertexNY(s,PVertices(tempx,tempy+1)),VertexNZ(s,PVertices(tempx,tempy+1))
					VertexNormal s_g0,v2,VertexNX(s,PVertices(tempx+1,tempy+1)),VertexNY(s,PVertices(tempx+1,tempy+1)),VertexNZ(s,PVertices(tempx+1,tempy+1))
					VertexNormal s_g0,v3,VertexNX(s,PVertices(tempx+1,tempy)),VertexNY(s,PVertices(tempx+1,tempy)),VertexNZ(s,PVertices(tempx+1,tempy))
					VertexColor s_g0,v0,VertexRed(s,PVertices(tempx,tempy)),VertexGreen(s,PVertices(tempx,tempy)),VertexBlue(s,PVertices(tempx,tempy))
					VertexColor s_g0,v1,VertexRed(s,PVertices(tempx,tempy+1)),VertexGreen(s,PVertices(tempx,tempy+1)),VertexBlue(s,PVertices(tempx,tempy+1))
					VertexColor s_g0,v2,VertexRed(s,PVertices(tempx+1,tempy+1)),VertexGreen(s,PVertices(tempx+1,tempy+1)),VertexBlue(s,PVertices(tempx+1,tempy+1))
					VertexColor s_g0,v3,VertexRed(s,PVertices(tempx+1,tempy)),VertexGreen(s,PVertices(tempx+1,tempy)),VertexBlue(s,PVertices(tempx+1,tempy))

				Else
					v0=AddVertex(s_g1,tempx*4,plasma#(tempx,tempy)*10,tempy*4)
					v1=AddVertex(s_g1,tempx*4,plasma#(tempx,tempy+1)*10,tempy*4+4)
					v2=AddVertex(s_g1,tempx*4+4,plasma#(tempx+1,tempy+1)*10,tempy*4+4)
					v3=AddVertex(s_g1,tempx*4+4,plasma#(tempx+1,tempy)*10,tempy*4)
					AddTriangle(s_g1,v0,v1,v2)
					AddTriangle(s_g1,v0,v2,v3)
					VertexNormal s_g1,v0,VertexNX(s,PVertices(tempx,tempy)),VertexNY(s,PVertices(tempx,tempy)),VertexNZ(s,PVertices(tempx,tempy))
					VertexNormal s_g1,v1,VertexNX(s,PVertices(tempx,tempy+1)),VertexNY(s,PVertices(tempx,tempy+1)),VertexNZ(s,PVertices(tempx,tempy+1))
					VertexNormal s_g1,v2,VertexNX(s,PVertices(tempx+1,tempy+1)),VertexNY(s,PVertices(tempx+1,tempy+1)),VertexNZ(s,PVertices(tempx+1,tempy+1))
					VertexNormal s_g1,v3,VertexNX(s,PVertices(tempx+1,tempy)),VertexNY(s,PVertices(tempx+1,tempy)),VertexNZ(s,PVertices(tempx+1,tempy))
					VertexColor s_g1,v0,VertexRed(s,PVertices(tempx,tempy)),VertexGreen(s,PVertices(tempx,tempy)),VertexBlue(s,PVertices(tempx,tempy))
					VertexColor s_g1,v1,VertexRed(s,PVertices(tempx,tempy+1)),VertexGreen(s,PVertices(tempx,tempy+1)),VertexBlue(s,PVertices(tempx,tempy+1))
					VertexColor s_g1,v2,VertexRed(s,PVertices(tempx+1,tempy+1)),VertexGreen(s,PVertices(tempx+1,tempy+1)),VertexBlue(s,PVertices(tempx+1,tempy+1))
					VertexColor s_g1,v3,VertexRed(s,PVertices(tempx+1,tempy)),VertexGreen(s,PVertices(tempx+1,tempy)),VertexBlue(s,PVertices(tempx+1,tempy))
				End If
			Next 
		Next
	Next
	PaintSurface s_g0, bGrad0 
	PaintSurface s_g1, bGrad1 
	
	FreeEntity m 
	Return m2 
End Function
</textarea><br><br>I added a new array (Pvertices) to store the vertex indexes created by the create terrain function and then used those to reference the terrain mesh. Most of the GradePaintTerrainMesh I changed, but I didn't copy the UVs of the terrain (i forgot :). Also, I don't know if it was intentional but u only store the colour red of each vertex in your function. <br><br></td></tr></table><br>
<a name="406469"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Rigz</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> . <br><br></td></tr></table><br>
<a name="406486"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Rigz,<br><br>You are a GOD....<br><br>I must now understand your code and give you homage.. BIG TIME... :)<br><br>ps. I am doing a variation (remake-ish) of the old C64 Game Sentinel... <br><br></td></tr></table><br>
<a name="406500"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Rigz</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> BTW, I don't know if you noticed but I posted some screenshots of my terrain engine using your lightmapper in the worklogs section if you're interested :) <br><br></td></tr></table><br>
<a name="406669"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> THat looks sweet. Let me know when you have finished it. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
