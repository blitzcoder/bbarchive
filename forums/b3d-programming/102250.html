<!DOCTYPE html><html lang="en" ><head ><title >Replacing Blitz3d collision with JV-ODE collisons?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Replacing Blitz3d collision with JV-ODE collisons?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Replacing Blitz3d collision with JV-ODE collisons?</a><br><br>
<a name="1220260"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I purchased the JV-ODE physics wrapper for Blitz3d in order to improved the quality of my games. My main purpose for buying the wrapper, however, was to replace Blitz3d's generally poor, unsophisticated, sold collision detection system with something more sophisticated and capable of handling dynamic collisions.<br>Asking anyone out there who may have used the wrapper for the same reasons, would it be possible to just use JV-ODE's dynamic collision functions purely without all the fancy stuff? Like, taking the game I have now, removing all of the Blitz3d collision functions, and replacing them with JV-ODE collisions so that characters can stand on moving platforms and jump into corners without getting stuck?<br>Any previous experience or help would be greatly appreciated. <br><br></td></tr></table><br>
<a name="1220274"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> With several linepicks or several colliders spheres for each moving character you can already do that...<br><br>Also the Bullet physics engine has a character controller (capsule collider). See : <a href="http://tools.mirage-lab.com/" target="_blank">http://tools.mirage-lab.com/</a> <br><br></td></tr></table><br>
<a name="1220276"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Cubed Inc.</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> What do you mean by the 'several linepicks or several colliders spheres'? I'm very interested. <br><br></td></tr></table><br>
<a name="1220291"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RGR</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Blitz Collision is not poor - not unsophisticated - it does its job and never forget - B3d is a 10 year old programming tool, not further developed since 6 years or so.<br>You said you bought JV-ODE Blitz Wrapper.<br>For the same reason said above, there are not many Users of B3d+Ode left (working daily with that software combination).<br>But there are many examples coming with Ode anwering exact your questions. I guess it is better to try them all out and learn to understand how to use it with your games than waiting for answers here.<br><br>If you want to replace your collision system in your games with realistic physics you made a good choice. Ode is in my view the easiest to learn and most useful tool.<br><br><br>............................................................................................................................ <br><br></td></tr></table><br>
<a name="1220328"></a>

<a name="1220329"></a>

<a name="1262116"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cubed inc &gt;&gt; The idea is to use linepicks or colliders spheres as sensors around a pivot.<br><br>Instead of using one collider sphere as the way to prevent your character to go through the environment or through others moving characters, you set each character mesh has a child of a pivot, you move the pivot, and you use several linepicks or several collider spheres (destroyed and recreated each frame, when necessary) to detect a collision with the environment or with others characters.<br>Then you can reposition each pivot at the wanted position by using the picked point infos or the collided point infos, the collider position and tformpoint.<br><br>The easier way would be to use several linepicks.<br><br>Some things to keep in mind :<br>If you use several linepicks, you want to have low tris "pickable" meshes, it will be faster.<br><br>If you use several colliders sphere you also want to have low tris "collidable" meshes, it will be faster.<br><br>Because the  linepicks or the colliders spheres are used to sense the environment and others characters and not to prevent the character to go through the environment and others characters, you must not move the character faster than the length of the linepick or than the radius of the collider sphere.<br><br>If you don't understand what i have written, you need to do more tests with linepicks and "pickables" meshes and with colliders and "collidables" meshes. Then reread this, it will make sense.<br><br>For moving platforms, i would use one linepick (to down) per character to detect the platform, and a state to know when to apply the gravity force or not. <br><br></td></tr></table><br>
<a name="1220424"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Charrua</td><td align="right"><font class=tiny>(Posted 2014)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> the following is pure blitz code, using a dynamic collision lib<br><br>just to show things blitz can do.<br><br>the lib<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; Dynamic collision lib by Simon Harrison (si@...)

; You are free to use this source code how you like, however a credit in any programs that use it would be appreciated.
; Feel free to email me any questions you might have about the code, however please don't necessarily expect a reply.

; Limitations:
; Only works with spheres as source entities
; Only works if the source entity is colliding with one dynamic entity at once

; Functions:
; DCO_SetPair(src_ent,des_ent,src_type,des_type,src_rad=1.0)
; DCO_UpdateA()
; DCO_UpdateB()
; DCO_EntityCollided()
; DCO_CollisionNX()
; DCO_CollisionNY()
; DCO_CollisionNZ()

; * See included example for demonstration of system.

Global DCO_pair_pos
Global DCO_ent
Global DCO_nx#
Global DCO_ny#
Global DCO_nz#

Type DCO_pair

	Field src_ent,src_col,src_type,src_rad
	Field des_ent,des_col,des_type
	
End Type

Function DCO_SetPair(src_ent,des_ent,src_type,des_type,src_rad=1.0)

	c.DCO_pair=New DCO_pair
	
	c\src_ent=src_ent
	c\src_type=src_type
	c\src_rad=src_rad
	
	c\des_ent=des_ent
	c\des_type=des_type
	
	c\src_col=CreatePivot() : EntityRadius c\src_col,src_rad
	c\des_col=CopyEntity(des_ent)
	EntityAlpha c\des_col,0
	
	DCO_pair_pos=DCO_pair_pos+(MeshWidth(c\des_col)*2)
	DCO_pair_pos=DCO_pair_pos+1000
	PositionEntity c\des_col,DCO_pair_pos,DCO_pair_pos,0
	
	EntityType c\src_col,src_type
	EntityType c\des_col,des_type
	
	ResetEntity c\src_col
	ResetEntity c\des_col
	
End Function

Function DCO_UpdateA()

	Local dx#,dy#,dz#,pivo,pivv

	For c.DCO_pair=Each DCO_pair

		dx#=EntityX#(c\src_ent)-EntityX#(c\des_ent)
		dy#=EntityY#(c\src_ent)-EntityY#(c\des_ent)
		dz#=EntityZ#(c\src_ent)-EntityZ#(c\des_ent)

		pivo=CreatePivot()
		pivv=CreatePivot(pivo)
		
		PositionEntity pivo,EntityX#(c\des_col),EntityY#(c\des_col),EntityZ#(c\des_col)
		
		RotateEntity pivo,0,0,0
		
		PositionEntity pivv,dx#,dy#,dz#,False

		RotateEntity pivo,-EntityPitch#(c\des_ent),-EntityYaw#(c\des_ent),-EntityRoll#(c\des_ent)

		PositionEntity c\src_col,EntityX#(pivv,True),EntityY#(pivv,True),EntityZ#(pivv,True)
		
		FreeEntity pivo
		
	Next

End Function

Function DCO_UpdateB()

	Local col,dx#,dy#,dz#,dx2#,dy2#,dz2#,ddx#=0,ddy#=0,ddz#=0,pivo,pivv,pivn
	
	DCO_ent=0
	For c.DCO_pair=Each DCO_pair
				
		col=EntityCollided(c\src_col,c\des_type)
		
		If col=c\des_col
	
			DCO_ent=c\des_ent
	
			dx#=EntityX#(c\src_ent)-EntityX#(c\des_ent)
			dy#=EntityY#(c\src_ent)-EntityY#(c\des_ent)
			dz#=EntityZ#(c\src_ent)-EntityZ#(c\des_ent)
		
			dx2#=EntityX#(c\src_col)-EntityX#(c\des_col)
			dy2#=EntityY#(c\src_col)-EntityY#(c\des_col)
			dz2#=EntityZ#(c\src_col)-EntityZ#(c\des_col)
	
			pivo=CreatePivot()
			pivv=CreatePivot(pivo)
			pivn=CreatePivot(pivo) ; piv used to translate collsion normals
			
			PositionEntity pivo,0,0,0
	
			RotateEntity pivo,-EntityPitch#(c\des_ent),-EntityYaw#(c\des_ent),-EntityRoll#(c\des_ent)
			
			PositionEntity pivv,dx2#,dy2#,dz2#,True
			PositionEntity pivn,CollisionNX#(c\src_col,1),CollisionNY#(c\src_col,1),CollisionNZ#(c\src_col,1),True
			
			RotateEntity pivo,0,0,0
			
			; platform normals
			DCO_nx#=EntityX#(pivn)
			DCO_ny#=EntityY#(pivn)
			DCO_nz#=EntityZ#(pivn)
			
			dx2#=EntityX#(pivv)
			dy2#=EntityY#(pivv)
			dz2#=EntityZ#(pivv)
	
			ddx#=dx2#-dx#
			ddy#=dy2#-dy#
			ddz#=dz2#-dz#
							
			FreeEntity pivo
		
			TranslateEntity c\src_ent,ddx#,ddy#,ddz#
		
			;Exit
		
		EndIf
				
	Next

End Function

Function DCO_EntityCollided()

	Return DCO_ent

End Function

Function DCO_CollisionNX#()

	Return DCO_nx#

End Function

Function DCO_CollisionNY#()

	Return DCO_ny#

End Function

Function DCO_CollisionNZ#()

	Return DCO_nz#

End Function
</textarea><br><br>an example<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; Dynamic Collisions Example
; --------------------------
; based on the test program made by Simon Harrison
; modified by Juan Ignacio Odriozola (Charrua)

Include "dcol_lib.bb" ; must be used at start of program

Graphics3D 640,480,0,2
SetBuffer BackBuffer()

Type platfrm
	Field entidad	;entity
End Type


cam=CreateCamera() : PositionEntity cam,0,15,-10 : RotateEntity cam,25,0,0
light=CreateLight()

; Create source entity
Global PlayerRadius# = 1.0
player=CreateSphere() : EntityColor player,0,0,255 : PositionEntity player,0,5,0
guide=CreateCube(player) : EntityColor guide,0,255,255 : ScaleEntity guide,.1,.1,1 : PositionEntity guide,0,.8,.3


; Give sphere a collision type for collisions with non-dynamic objects, i.e. the ground in this case
EntityType player,1 : EntityRadius player,1

room = CreateCube() : ScaleEntity room, 20,20,20 : PositionEntity room, 0,0,10
FlipMesh room
EntityType room,2

s=32
roomTexture=CreateTexture(s,s)
ClsColor 0,200,80
Cls

Color 255,255,255

Rect 0,0,s/2,s/2,1
Rect s/2,s/2,s,s,1

CopyRect 0,0,s,s,0,0,BackBuffer(),TextureBuffer(roomTexture)
ScaleTexture roomTexture,.1,.1

EntityTexture room,roomTexture,0,0

ground = CreatePlane() : EntityType ground,2 : EntityColor ground,0,0,128: EntityAlpha ground,0.8
Mirror=CreateMirror() 


Body1 = CreateCube() : EntityColor Body1,200,200,0 : ScaleEntity Body1,2,1,10 : PositionEntity Body1,-10,10,20
EntityType Body1,2

Body2 = CreateCube() : EntityColor Body2,200,0,200 : ScaleEntity Body2,4,.1,6 : PositionEntity Body2,10,3,18
RotateEntity Body2,-30,0,0
EntityType Body2,2




; NOW THE DYNAMIC STUFF - *** Important comments highlighted by stars ***

; Create first destination entity - platform 1
platform1=CreateCylinder(16) : ScaleEntity platform1,2,.2,2 : EntityColor platform1,255,255,0
PositionEntity platform1,-10,0,8
EntityPickMode platform1,2
plat.platfrm = New platfrm
plat\entidad = platform1


; Create second destination entity - platform 2
platform2=CreateCube() : ScaleEntity platform2,4,1,4 : EntityColor platform2,255,0,0
PositionEntity platform2,10,1,3
EntityPickMode platform2,2
plat.platfrm = New platfrm
plat\entidad = platform2

;another one
platform3=CreateCube() : ScaleEntity platform3,5,.2,5 : EntityColor platform3,0,0,255
PositionEntity platform3,-4,6,28
EntityPickMode platform3,2
plat.platfrm = New platfrm
plat\entidad = platform3

src_type=3 ; assign a collision type for source collision entity (sphere)
des_type=4 ; assign a collision type for destination collision entity (platforms)

; *** Set dynamic collision pairs ***
DCO_SetPair(player,platform1,src_type,des_type,PlayerRadius#) ; parameters - src_entity,des_entity,src_type,des_type,src_radius=1.0
DCO_SetPair(player,platform2,src_type,des_type,PlayerRadius#)
DCO_SetPair(player,platform3,src_type,des_type,PlayerRadius#)

;parar = False


; Set up normal sliding collisions between sphere and plane
;tipo1 = jugador, tipo 2 = sala, esto es para que el jugador choque contra las paredes y el piso
Collisions 1,2,2,2
; *** Enable collisions for dynamic pairs ***

Collisions src_type,des_type,2,2

Global txt1$,txt2$,txt3$
Global estoy=False
Global pvt=CreatePivot()

While Not KeyDown(1)
 
	sph_z#=0
	dy# = 0
	If KeyDown(203)=True Then dy =2
	If KeyDown(205)=True Then dy =-2
	If KeyDown(208)=True Then sph_z=-.4
	If KeyDown(200)=True Then sph_z=.4

	; Make our platforms dynamic - move them!
	If Not EstoyDebajoPlataforma(player) Then 
		MoveEntity platform1,0,Sin(angle#)*0.1,0
		angle#=angle#+1
	End If

	TurnEntity platform2,0,1,0
	
	TranslateEntity platform2,0,0,Sin(angle#)*0.1
	
	MoveEntity platform3,Sin(angle1#)*0.2,0,0 : angle1#=angle1#+1
	
	
	If EstoySobrePlataforma(player) Then
	;	;esto es un parche y no se por que se acelera...
		sph_z = sph_z/5
	Else
		DCO_UpdateA()
		UpdateWorld 0
		DCO_UpdateB()
		MoveEntity player,0,-.3,0
	EndIf

	MoveEntity player,0,sph_y,sph_z
	TurnEntity player,0,dy,0
	
	; *** Usual UpdateWorld call ***
	UpdateWorld
	
	; *** For best results, try experimenting with the order of the above four function calls ***
	
	RenderWorld
	
	; Output dynamic collision information - the dynamic collision system works best if you use these values to implement
	; proper bouncing physics between source entity and destination entity
	Text 0,  0,"EntityCollided: "+DCO_EntityCollided()
	Text 0, 20,"CollisionNX: "+DCO_CollisionNX#()
	Text 0, 40,"CollisionNY: "+DCO_CollisionNY#()
	Text 0, 60,"CollisionNZ: "+DCO_CollisionNZ#()
	
	Flip True

Wend


Function EstoySobrePlataforma(yo)	; player is over a platform

	algo = LinePick(EntityX(yo,True),EntityY(yo,True),EntityZ(yo,True),0,-PlayerRadius#,0)	;linea hacia abajo
	si = False

	If algo Then
		For plat.platfrm = Each platfrm
			If algo = plat\entidad Then
				EntityParent yo,plat\entidad
				si=True
			End If
		Next
	End If

	If Not si Then EntityParent yo,0
		
	Return si
	
End Function

Function EstoyDebajoPlataforma(yo)	; player is under a platform

	
	algo = LinePick(EntityX(yo,True),EntityY(yo,True),EntityZ(yo,True),0,+PlayerRadius#,0)	;linea hacia arriba
	si = False

	If algo Then
		For plat.platfrm = Each platfrm
			If algo = plat\entidad Then
				si=True
			End If
		Next
	End If

	
	Return si
	
End Function
</textarea><br><br><br>if you use ode, you have to just have to remember the following simple rules.<br><br>You have blitz entities (primitives or meshes) only as skin<br><br>for each blitz entity you may have a similar physics body created in ode<br>apply physics properties to it and let ode to calculate physics behavior, then in each loop of your program you have to replace and rotate each blitz entity linked to a physic body. <br><br>is as simple as is.<br><br>juan <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
