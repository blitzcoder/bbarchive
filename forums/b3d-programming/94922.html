<!DOCTYPE html><html lang="en" ><head ><title >Terrain Texture Layers - The Solution (and code)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Terrain Texture Layers - The Solution (and code)</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Terrain Texture Layers - The Solution (and code)</a><br><br>
<a name="1091308"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >John Blackledge</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Terrain Texture Layers - The Solution (and code)<br><br>I've finally cracked the Terrain Texture Layer question.<br>All of you will be exultant (well, two of you).<br><br>Granted that FastExt has already done this (see FastExt_Example_TextureBlends.bb example) with pre-supplied alphas.<br>What I wanted to do was<br>(a) Be able to draw to a newly created blank alpha texture<br>(b) Be able to save it, and later reload it.<br><br>Drawing was not a problem; I drew(?) to the Image and also the Textures stored in memory and reapplied them to the surface.<br>But I couldn't work out how to save an alpha image correctly.<br>(At least not the type that FastExt would load and implement properly.)<br><br>The included code shows how: make the alpha byte a 0(zero) not 255.<br><br>I've used two textures 'stone' and 'clay' to punch through the grass.<br>(I prefer 'punch'; I think 'texture splatting' tells no-one anything.)<br><br>The principle is:<br>Layer 0: Your usual grass texture.<br>Layer 1: The alpha texture which will allow the stone texture to 'punch' through.<br>Layer 2: The actual stone texture.<br>Layer 3: The alpha texture which will allow the clay texture to 'punch' through.<br>Layer 4: The actual clay texture.<br><br>See the code for the actual FastExt parameters to use for Load and TextureBlend.<br>You also need the FastExt and FreeImage additions<br><br>The crucial thing here is that we can save the results out of a Blitz program for later use, without having to reprocess through another program such as PSP.<br>Changes are made 'live', again without having to externally reprocess.<br><br>This uses a cube for simplicity, but should work just as well on a Blitz terrain or mesh terrain.<br>You will need your own 'grass.jpg', 'rock.jpg', 'clay.jpg', but alpha's are created for you if you haven't previously created and saved them.<br><br>This is as far as I want to take this example.<br>My next job is to build it into my own engine, so that I can stand on a hill and draw roads and paths directly onto the terrain.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;==================================
; MULTI-TEXTURED TERRAIN EXAMPLE ; John Blackledge 07/06/2011 based on FastExt examples.
;==================================
; The crucial thing here is that we can save the results out of a Blitz program
; for later use, without having to reprocess through another program like PSP.
; Changes are made 'live', again without having to externally reprocess.
;==================================
; This uses a cube for simplicity, but should work just as well on a Blitz terrain or mesh terrain.
; You will need your own 'grass.jpg', 'rock.jpg', 'clay.jpg',
; but alpha's are created for you if you haven't previously created and saved them.
;==================================

Include "FastExt.bb"
Include "FreeImage.bb"
Graphics3D 800,600,0,2

InitExt	; Initialize FastExt library after Graphics3D function
Global LayersSupported = HWTexUnits()

CreateLight()
cam = CreateCamera()
CameraClsColor cam,128,128,128
PositionEntity cam,0,0,-2.5
CameraRange cam,0.01,1000

cub = CreateCube() : PositionEntity cub,0,0,0

Global drawclr	= 238 ; produces light grey. Any darker will make 'paths' also appear darker.
Global eraseclr	= 255 ; produces white, allows layer 0 grass to not be overwritten.

; \/ layer 0. Top main layer that the others will 'punch' through.
grass=LoadTexture ("grass.jpg")
TextureBlend grass, FE_ALPHACURRENT
EntityTexture cub,grass,0,0

; \/ layer 1. 'Filter' layer for layer 2.
rockalphafile$ = "rockalpha.png"
If FileType(rockalphafile$) = 1
	rockalphaimg = LoadImage(rockalphafile$)
	rockalphatex = LoadTexture (rockalphafile$,1+2)

Else ; What - no alpha image? Let's create image and texture.
	rockalphaimg = CreateImage(256,256)
	SetBuffer ImageBuffer(rockalphaimg)
	Color eraseclr,eraseclr,eraseclr : Rect 0,0,256,256,1
	Color drawclr,drawclr,drawclr : Oval 64,64,128,128 
	SetBuffer BackBuffer()
	rockalphatex = CreateTexture(256,256,1+2)
EndIf

TextureBlend rockalphatex, FE_ALPHAMODULATE
EntityTexture cub,rockalphatex,0,1

; \/ layer 2
rocktex = LoadTexture ("rock.jpg")
TextureBlend rocktex, FE_ALPHACURRENT
EntityTexture cub,rocktex,0,2

; \/ layer 3
clayalphafile$ = "clayalpha.png"
If FileType(clayalphafile$) = 1
	clayalphaimg = LoadImage(clayalphafile$)
	clayalphatex = LoadTexture (clayalphafile$,1+2)

Else ; What - no alpha image? Let's create image and texture.
	clayalphaimg = CreateImage(256,256)
	SetBuffer ImageBuffer(clayalphaimg)
	Color eraseclr,eraseclr,eraseclr : Rect 0,0,256,256,1
	Color drawclr,drawclr,drawclr : Rect 20,110,220,30
	SetBuffer BackBuffer()
	clayalphatex = CreateTexture(256,256,1+2)
EndIf	
TextureBlend clayalphatex, FE_ALPHAMODULATE
EntityTexture cub,clayalphatex,0,3

; \/ layer 4
claytex = LoadTexture ("clay.jpg")
TextureBlend claytex, FE_ALPHACURRENT
EntityTexture cub,claytex,0,4

; \/ We're loading in alpha images, and converting them to textures.
RefreshAlphaFromImage(rockalphaimg, rockalphatex, 0,256,0,256)
RefreshAlphaFromImage(clayalphaimg, clayalphatex, 0,256,0,256)

While Not KeyHit(1)
	UpdateWorld()
	RenderWorld()
	;
	; ------ 2D IMAGE AND TEXT -------------
	Color 255,255,255
	Text GraphicsWidth()/2,4,"MULTI-TEXTURED TERRAIN EXAMPLE",True
	Text GraphicsWidth()/2,20,"(Your GFX supports "+LayersSupported+" Texture Layers)",True
	Color 0,0,0
	Text GraphicsWidth()/2,GraphicsHeight()-25,"[S] to SAVE both alpha textures",True
	If mode = 0
		DrawBlock rockalphaimg,0,0 : Text 1,6,"ROCK Alpha Layer"
	Else
		DrawBlock clayalphaimg,0,0 : Text 1,6,"CLAY Alpha Layer"
	EndIf
	Text 1,26,"SPACEBAR to toggle rock / clay"
	Text 1,196,"Draw in this rectangle:"
	Text 1,216,"Left Mouse to DRAW"
	Text 1,236,"Right Mouse to ERASE"
	; -----------------------------
	;
	; ------------ DRAW TO IMAGE, COPY TO TEXTURE ----------
	mx = MouseX() : my = MouseY() : brushsize = 10
	;
	If mx &lt; 256-(brushsize-1) And my &lt; 256-(brushsize-1) ; limit drawing area, don't want a MAV
		;
		If MouseDown(1) Or MouseDown(2)
			;	
			If mode = 0 ; rock mode
				SetBuffer ImageBuffer(rockalphaimg)
			Else 				; clay mode
				SetBuffer ImageBuffer(clayalphaimg)
			EndIf
			;
			If MouseDown(1) ; draw
				Color drawclr,drawclr,drawclr ; &lt;- draw/transparent colour
			Else 						; erase
				Color eraseclr,eraseclr,eraseclr ; &lt;- allow top layer texture
			EndIf
			Rect mx, my, brushsize, brushsize, 1
			;
			SetBuffer BackBuffer() 
			;
			If mode = 0 ; rock mode
				RefreshAlphaFromImage(rockalphaimg, rockalphatex, mx, mx+brushsize, my, my+brushsize)
				EntityTexture cub,rockalphatex,0,1
			Else 				; clay mode
				RefreshAlphaFromImage(clayalphaimg, clayalphatex, mx, mx+brushsize, my, my+brushsize)
				EntityTexture cub,clayalphatex,0,3
			EndIf
			;
		EndIf
		;
	EndIf
	; -------------------------------------------------------
	;
	Flip
	;
	If KeyDown(200) Then MoveEntity cam,0,0,+0.01 ; /\ zoom in
	If KeyDown(208) Then MoveEntity cam,0,0,-0.01 ; \/ zoom out
	;
	If KeyHit(57) ; Spacebar - swap between rock and clay
		mode = 1 - mode
	EndIf
	;
	If KeyHit(31) ; [S]ave both
		FiSaveImage(rockalphaimg,rockalphafile$)
		FiSaveImage(clayalphaimg,clayalphafile$)
	EndIf
	;
Wend
End

;=========================
Function RefreshAlphaFromImage(image, texture, xstart,xend,ystart,yend)
;=========================
Local ix, iy
Local rgb, redbyte, greenbyte, bluebyte, alphabyte
Local imgBuffer, imgwidth, imgheight
Local texBuffer, texwidth, texheight

	If image = 0 Or texture = 0 Then Return False

	imgBuffer = ImageBuffer(image)
	imgwidth	= ImageWidth(image)
	imgheight = ImageHeight(image)

	texBuffer = TextureBuffer(texture)
	texwidth	= TextureWidth(texture)
	texheight = TextureHeight(texture)

	LockBuffer(imgBuffer)
	LockBuffer(texBuffer)
	For iy = ystart To yend - 1
		For ix = xstart To xend - 1
			rgb = ReadPixelFast(ix,iy,imgBuffer) And $FFFFFF
			redbyte		= (RGB And $FF0000) Shr 16
			greenbyte = (RGB And $FF00) Shr 8
			bluebyte	= (RGB And $FF)
			If bluebyte = drawclr
				alphabyte = drawclr
			Else
				; This is the crucial bit....
				; for erasing it needs to be 0 not 255
				; and I've no idea why.
				alphabyte = 0
			EndIf
			newrgb = (alphabyte Shl 24) Or (redbyte Shl 16) Or (greenbyte Shl 8) Or bluebyte
			WritePixelFast(ix,iy,newrgb,texbuffer)
		Next
	Next
	UnlockBuffer(imgBuffer)
	UnlockBuffer(texBuffer)

End Function

;==================================================
;==================================================
</textarea> <br><br></td></tr></table><br>
<a name="1091377"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for sharing! I have no FastExt, but I'm sure it will be helpful for anyone who has. Good luck with your engine. The path painting sounds like a fun idea. <br><br></td></tr></table><br>
<a name="1091446"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >John Blackledge</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Warner.<br>FastExt seem to deliver what it promises, though until now I didn't have much use for what it does.<br>They really do seem to have extended B3D with calls such as TextureBlend, which calls their own version which then allows utilisation of the latest MS parameters such as TextureBlend rockalphatex, FE_ALPHAMODULATE.<br>Once I've got my engine where I want it I've promised myself that I will look through each call in turn in detail.<br>I suspect I will say a few times, "Oo- i wish I'd had that before." <br><br></td></tr></table><br>
<a name="1091459"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I say, hex numbers are evil! :) <br>I would never have thought of that. Thanks for sharing the success. <br><br></td></tr></table><br>
<a name="1091487"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >John Blackledge</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This forum has given so much to me over the last ten years.... it's nice to be able to give something back. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
