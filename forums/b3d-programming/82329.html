<!DOCTYPE html><html lang="en" ><head ><title >Collisions/Slope/Linepick help !!!!</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Collisions/Slope/Linepick help !!!!</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Collisions/Slope/Linepick help !!!!</a><br><br>
<a name="927925"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FlagDKT</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm coding a 3d platform game, but I had some troubles dealing with blitz collisions.<br>If using blitz standard collisions:<br>	1)I want the player to reach the peak of the slope without any effort, and I want the player to move freely in any direction without being 'adjusted' by the gravity/collision force.<br>	2)When the player goes down on the slope, sometimes it does not collide perfectly with it.<br>	3)When the player is near an edge of the platform it slides down slowly(due to the ellipsoid collision shape)<br><br>With linepick function I had less problems but:<br>	1)The player is able to jump on the platform even from below it<br><br>I need a good solution :(<br><br><pre class=code>
Graphics3D 800,600,32,2
Global cam=CreateCamera()
RotateEntity   cam,20,0,0
PositionEntity cam,0,10,-14
light=CreateLight(2)
PositionEntity light,0,4000,-700

Type char
	Field mesh
	Field name$
	Field pxDummy
	Field curdir_x#
	Field curdir_y#
	Field curdir_z#
	Field wanteddir_vx#
	Field wanteddir_vz#
	Field curPosY#
	Field moveSpeed#
	Field groundCollision
End Type

Const collType_Char=1
Const collType_Ground=2
Const collType_Platform=3

;-----------------------------------------------------
Global ch.char=New char
ch\moveSpeed#=0.08
ch\pxDummy=CreateCube()
EntityType ch\pxDummy,collType_Char
EntityRadius ch\pxDummy,1,2
EntityColor ch\pxDummy,255,0,0
ScaleEntity ch\pxDummy,1,2,1

d=CreateCone()
ScaleMesh d,.5,.5,.5
RotateMesh d,-90,0,0
PositionMesh d,0,0,-2
AddMesh d,ch\pxDummy
PositionEntity ch\pxDummy,0,4,0
ch\curPosY=4
FreeEntity d

Global Ground=CreateCube()
EntityType Ground,collType_Ground
ScaleMesh Ground,20,1,20
slope=CreateCube()
ScaleMesh slope,10,10,10
RotateMesh slope,0,0,30
PositionMesh slope,-12,-4,0
AddMesh slope,Ground
FreeEntity slope
EntityPickMode Ground,2

plt=CreateCube()
ScaleMesh plt,4,.5,4
PositionMesh plt,7,6,0
EntityType plt,collType_Platform
EntityPickMode plt,2

;-----------------------------------------------------
Global collMethod=1	;1&gt;Blitz Collisions, 2&gt;Ground LinePick
;-----------------------------------------------------

If collMethod=1 Then 
	Collisions collType_Char,collType_Ground,2,3
	Collisions collType_Char,collType_Platform,2,2
EndIf

timer=CreateTimer(60)
Repeat
	WaitTimer(timer)
	ch\groundCollision=False
	
	ch\wanteddir_vx=0:ch\wanteddir_vz=0
	If KeyDown(205) Then ch\wanteddir_vx=1
	If KeyDown(203) Then ch\wanteddir_vx=-1
	If KeyDown(200) Then ch\wanteddir_vz=1
	If KeyDown(208) Then ch\wanteddir_vz=-1
	TFormNormal ch\wanteddir_vx,0,ch\wanteddir_vz,0,0
	ch\wanteddir_vx=TFormedX()
	ch\wanteddir_vz=TFormedZ()
	If ch\wanteddir_vx&lt;&gt;0 Or ch\wanteddir_vz&lt;&gt;0 Then
		AlignToVector ch\pxDummy,-ch\wanteddir_vz,0,ch\wanteddir_vx,1,1
		TranslateEntity ch\pxDummy,((ch\wanteddir_vx)*ch\moveSpeed#),0,((ch\wanteddir_vz)*ch\moveSpeed#)
	EndIf
	
	If collMethod=2 Then	;Line GroundPick
		pick_length#=2.1
		If LinePick(EntityX#(ch\pxDummy), EntityY#(ch\pxDummy), EntityZ#(ch\pxDummy), 0, -pick_length, 0) &gt; 0
			ch\groundCollision=True
			PositionEntity ch\pxDummy,EntityX#(ch\pxDummy,True),PickedY()+2,EntityZ(ch\pxDummy,True),True
		EndIf
	EndIf
	
	
	;GRAVITY -----------------------------------
	ty#=EntityY(ch\pxDummy)
	y_vel#=(ty-ch\curPosY)
	ch\curPosY=ty
	If KeyHit(57)	;jump
		y_vel=1.1	
	Else
		y_vel=y_vel-.1	;2
	EndIf
	TranslateEntity ch\pxDummy,0,y_vel,0
	
	If CountCollisions( ch\pxDummy )
		If EntityCollided( ch\pxDummy,collType_Ground ) Or EntityCollided( ch\pxDummy,collType_Platform )
			ch\groundCollision=True
		EndIf
	EndIf
	
	UpdateWorld
	RenderWorld
	Text (610-FontWidth()),FontHeight()*tc,"Ground Collision: "+ch\groundCollision
	
	Flip (False)
	
Until KeyHit(1)
End

</pre> <br><br></td></tr></table><br>
<a name="927967"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pirate</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> answer to question #3<br><br>Collisions collType_Char,collType_Platform,2,2<br><br>in this line of code you are telling the char to slide down the slope by putting the response as 2 it tells it to use full sliding collision when on the platform...changing it to 3 will make the char stop and not slide down the slope...here is the help file...<br><br>src_type - entity type to be checked for collisions.<br>dest_type - entity type to be collided with.<br><br>method - collision detection method.<br>1: sphere-to-sphere collisions<br>2: sphere-to-polygon collisions<br>3: sphere-to-box collisions<br><br>response - what the source entity does when a collision occurs.<br>1: stop<br>2: slide1 - full sliding collision<br>3: slide2 - prevent entities from sliding down slopes <br><br><br>hope this helps...pirate <br><br></td></tr></table><br>
<a name="927984"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FlagDKT</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Pirate but it doesn't change much, the char should fall down directly without sliding at all on the edge!<br>And with stop type response it may stick at the border of the platform if u keep holding direction keys.<br>That's why I prefered collision type 2. <br><br></td></tr></table><br>
<a name="928020"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guy Fawkes</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Or you could use the toolbox upgrades. They have a sweet Collision system that outbests Blitz' commands.<br><br>~DarkShadowWing~ <br><br></td></tr></table><br>
<a name="928049"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Use both the blitz collisions and linepick then. just make sure your linepick is slightly bigger than your collision radius, that way your linepick can tell you what it's picking, before the blitz collision kicks in. The blitz collision will stop your character running through walls and jumping through platforms. <br><br></td></tr></table><br>
<a name="928191"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FlagDKT</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thx<br>I didn't notice that linepick would accept a radius paramenter :)<br>But why if I set radius to 1 it is much difficoult to pick surfaces??If I try to jump it falls down through the ground.<br>Can you show pls? :) <br><br></td></tr></table><br>
<a name="928244"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> When using linepicks it's always a good idea to start the pick slightly above your character, at least as much as the radius of the pick.  Also make sure the length of the pick is extended to include this offset :<br><br><pre class=code>
If LinePick(EntityX#(ch\pxDummy), EntityY#(ch\pxDummy)+2, EntityZ#(ch\pxDummy), 0, -pick_length-2, 0) &gt; 0
</pre> <br><br></td></tr></table><br>
<a name="928293"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >FlagDKT</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I see that you didn't use linepick radius?<br>And if I use linepick only, the player will still be able to jump the platform from below...I should blitz collisions for the ground and linepick for the platform..<br>But at the edge of the platform as u may see, it slides down slowly..how to avoid that?<br><br><pre class=code>
Graphics3D 800,600,32,2
Global cam=CreateCamera()
RotateEntity   cam,20,0,0
PositionEntity cam,0,10,-14
light=CreateLight(2)
PositionEntity light,0,4000,-700

Type char
	Field mesh
	Field name$
	Field pxDummy
	Field curdir_x#
	Field curdir_y#
	Field curdir_z#
	Field wanteddir_vx#
	Field wanteddir_vz#
	Field curPosY#
	Field moveSpeed#
	Field blitzCollision
	Field pickedGround
End Type

Const collType_Char=1
Const collType_Ground=2
Const collType_Platform=3

;-----------------------------------------------------
Global ch.char=New char
ch\moveSpeed#=0.08
ch\pxDummy=CreateCube()
EntityType ch\pxDummy,collType_Char
EntityRadius ch\pxDummy,1,2
EntityColor ch\pxDummy,255,0,0
ScaleEntity ch\pxDummy,1,2,1

d=CreateCone()
ScaleMesh d,.5,.5,.5
RotateMesh d,-90,0,0
PositionMesh d,0,0,-2
AddMesh d,ch\pxDummy
PositionEntity ch\pxDummy,0,4,0
ch\curPosY=4
FreeEntity d

Global Ground=CreateCube()
EntityType Ground,collType_Ground
ScaleMesh Ground,20,1,20
slope=CreateCube()
ScaleMesh slope,10,10,10
RotateMesh slope,0,0,30
PositionMesh slope,-12,-4,0
AddMesh slope,Ground
FreeEntity slope
EntityPickMode Ground,2

plt=CreateCube()
ScaleMesh plt,4,.5,4
PositionMesh plt,7,6,0
EntityType plt,collType_Platform

Collisions collType_Char,collType_Platform,2,2
	
timer=CreateTimer(60)
Repeat
	WaitTimer(timer)
	
	ch\wanteddir_vx=0:ch\wanteddir_vz=0
	If KeyDown(205) Then ch\wanteddir_vx=1
	If KeyDown(203) Then ch\wanteddir_vx=-1
	If KeyDown(200) Then ch\wanteddir_vz=1
	If KeyDown(208) Then ch\wanteddir_vz=-1
	TFormNormal ch\wanteddir_vx,0,ch\wanteddir_vz,0,0
	ch\wanteddir_vx=TFormedX()
	ch\wanteddir_vz=TFormedZ()
	If ch\wanteddir_vx&lt;&gt;0 Or ch\wanteddir_vz&lt;&gt;0 Then
		AlignToVector ch\pxDummy,-ch\wanteddir_vz,0,ch\wanteddir_vx,1,1
		TranslateEntity ch\pxDummy,((ch\wanteddir_vx)*ch\moveSpeed#),0,((ch\wanteddir_vz)*ch\moveSpeed#)
	EndIf
	ch\blitzCollision=False
	ch\pickedGround=False
	
	;GRAVITY -----------------------------------
	ty#=EntityY(ch\pxDummy)
	y_vel#=(ty-ch\curPosY)
	ch\curPosY=ty
	If KeyHit(57)	;jump
		y_vel=1.3
	ElseIf ch\blitzCollision=False
		y_vel=y_vel-.1	;2
	EndIf
	TranslateEntity ch\pxDummy,0,y_vel,0
	
	UpdateWorld
	pick_length#=2.1
	If LinePick(EntityX#(ch\pxDummy), EntityY#(ch\pxDummy)+2, EntityZ#(ch\pxDummy), 0, -pick_length-2, 0) &gt; 0
		ch\pickedGround=True
		y_vel#=0
		PositionEntity ch\pxDummy,EntityX#(ch\pxDummy,True),PickedY()+2,EntityZ(ch\pxDummy,True),True
	EndIf
	If CountCollisions( ch\pxDummy )
		If EntityCollided( ch\pxDummy,collType_Platform )
			ch\blitzCollision=True
			y_vel=0
		EndIf
	EndIf
	RenderWorld
	Text (610-FontWidth()),FontHeight()*1,"Blitz Collision: "+ch\blitzCollision
	Text (610-FontWidth()),FontHeight()*2,"Picked Ground   : "+ch\pickedGround
	
	Flip (False)
	
Until KeyHit(1)
End

</pre> <br><br></td></tr></table><br>
<a name="928908"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Buggy</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Just something to think about... if you're making a sidescroller, consider using coded collisions. Instead of having the CPU perform triangle collision checks on numerous triangles, if the sizes of things are relatively standard and you use a grid layout, you could do this much the same way as you would a 2D game - by checking the character's position against a map file or something similar. <br><br></td></tr></table><br>
<a name="929020"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> I would avoid the radius parameter if i were you tbh. It does slow things more. <br><br></td></tr></table><br>
<a name="933453"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> But at the edge of the platform as u may see, it slides down slowly..how to avoid that? <br></div><br>This fixes it. It causes some clipping problems though. But at least now you can drop down normally. Personally, I'd take a clipping problem over some weird sliding off edges problem any day!<br><pre class=code>EntityRadius ch\pxDummy,.05,2</pre><br><img src="http://h1.ripway.com/fox95871/fournarrowcolliders.JPG"><br><br>If there's a way to do this though, I think then it'd be perfect! Does anyone know a way to simulate a mesh having four colliders like this? I tried making copies of the character, repositioning them, and making them invisible, but I couldn't get them to lock together. One would always end up on the platform, and the others on the ground. <br><br></td></tr></table><br>
<a name="933492"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >jfk EO-11110</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>Not sure if fox95871 already said that: when you combine a linepick versus the ground that is slightly longer than the distance to the gound and then you apply gravity only when the linepick did not hit the ground, this will prevent the sliding artefact. The relation of the to parameters of Entityradius (of the additional sphere vs polygonal collision of char vs world) will then allow to set the angle the character starts sliding. So he starts sliding only on a very steep ground for example. If feels rather natural this way, not sure if this is what you want. <br><br></td></tr></table><br>
<a name="933696"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> I definitely didn't say that, I'm a sub-beginner! I just thought I'd sneak into this area and throw in an Occam's razor :) <br><br></td></tr></table><br>
<a name="934080"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >neoshaman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello do you want the character to climb any slope? or there is a limit to with it wouldnot climb at all (too steep)?<br><br>Here is the code i'm using at the moment, it is based on the mak/driver demo on the blitz3D sample, there is some physic for "sliding" temper by friction but you can disable it altogether.<br><br><pre class=code>
.physics
 ;-------------------------------------------------------------------------
 ; 
 ;-------------------------------------------------------------------------
	
;calculate char velocities	
	cx# = EntityX ( char )
	x_vel = cx - prev_x
	prev_x = cx
	
	cy# = EntityY ( char )
	y_vel = cy - prev_y
	prev_y = cy
	
	cz# = EntityZ ( char )
	z_vel = cz - prev_z
	prev_z = cz

.controls_States
 ;-------------------------------------------------------------------------
 ; 
 ;-------------------------------------------------------------------------

; HIT controls
	If Hit &gt; 0 Then Hit = Hit - 1
	If Hit &lt; 0 Then Hit = 0

; wiimote control
	If EnableWiimote = True Then 
		;nun roll
		B_NunRollLeft  = (JoyRoll(2)- WiiBlitz\NunRoll) &lt;  ANGsens;left
		B_NunRollRight = (JoyRoll(2)- WiiBlitz\NunRoll) &gt; -ANGsens;right
		;nunjoy
		B_NunJoyYUp    = ((JoyV(2) - WiiBlitz\NunJoyY) &lt; -DeadZone );up
		B_NunJoyYDown  = ((JoyV(2) - WiiBlitz\NunJoyY) &gt;  DeadZone );down
		B_NunJoyXLeft  = ((JoyU(2) - WiiBlitz\NunJoyX) &lt; -DeadZone );left
		B_NunJoyXright = ((JoyU(2) - WiiBlitz\NunJoyX) &gt;  DeadZone );right
	Else
		;nun roll
		B_NunRollLeft  = False
		B_NunRollRight = False
		;nunjoy
		B_NunJoyYUp    = False
		B_NunJoyYDown  = False
		B_NunJoyXLeft  = False
		B_NunJoyXright = False
	EndIf


; turn char
	If KeyDown ( 203 ) Or B_NunRollLeft  And (Not KeyDown (29)) Then
		TurnEntity char, 0,  3, 0 ;turn left
	EndIf
	
	If KeyDown ( 205 ) Or B_NunRollRight And (Not KeyDown (29)) Then
		TurnEntity char, 0, -3, 0 ;turn right
	EndIf

	
	;try to insert a "force to contact" to avoid jittery move on slope
		LinePick ( EntityX(char),EntityY(char),EntityZ(char),0,-Attract ,0,GRadius)
		PickTime#=PickedTime()
		PickGround  = PickedEntity()
		If (PickGround  = 0) And (y_vel &lt;&gt; 0)Then; no contact with ground
			contact = 0
		Else; no contact with ground, keep feet on slide (to optimize with contact normal to movement)
			TranslateEntity char,0,-PickedNY()*Delta,0
		EndIf

; on contact
If pickground &lt;&gt; 0 Then
		contact = 1
		
	; reset Y velocities
		y_vel# = 0 : prev_y# = EntityY ( char )


; //button test

	; key up pressed
		If KeyDown ( 200 ) Or B_NunJoyYUp   And (Not KeyDown (29))
			speed = speed + THRUST
		EndIf
	; key down pressed
		If KeyDown ( 208 ) Or B_NunJoyYDown And (Not KeyDown (29)) 
			speed = speed - THRUST	
		EndIf
	

	; strafe left button test
		If KeyDown ( 44 ) Or B_NunJoyXLeft And (Not KeyDown (29))
			MoveEntity char, -StrafeStr , 0, 0
		EndIf
	; strafe rigth button test
		If KeyDown ( 46 ) Or B_NunJoyXright And (Not KeyDown (29))
			MoveEntity char, StrafeStr , 0, 0
		EndIf


	; jump button test
		If ((KeyHit ( 45 ) Or JoyHit(5,1)) And PickGround &lt;&gt;0)
			MoveEntity char, 0, MaxSP * JUMP, 0
		EndIf

; //end button test

		;speed limiter
			If (speed &gt; MaxSP) speed = MaxSP
			If (speed &lt; -MinSP) speed = -MinSP
		;when no move
			If (Not KeyDown ( 200 )) And (Not KeyDown ( 208 )) Then
				speed = 0;speed * ( 1 - ( FRICTION  ) )
			EndIf

		
	;perform the movement
		MoveEntity char, 0, 0, speed
		x_vel = x_vel * ( 1 - FRICTION )
		y_vel = ( y_vel + GRAVITY ) * ( 1 - FRICTION )
		z_vel = z_vel * ( 1 - FRICTION )
		TranslateEntity char, x_vel, y_vel, z_vel
		; use static friction code here	; if speed under static then speed = 0 ; careful! static should be less strong than Thrust or maxSP/MinSP
			If (Abs(speed) &lt; STATIC) speed = 0
			If (Abs(x_vel) &lt; STATIC) x_vel# = 0
			If (Abs(y_vel) &lt; STATIC) y_vel# = 0
			If (Abs(z_vel) &lt; STATIC) z_vel# = 0


; no contact
Else

	contact = 0
	TranslateEntity char, x_vel, y_vel + GRAVITY, z_vel
		
EndIf

</pre> <br><br></td></tr></table><br>
<a name="935688"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Is there a way to load a mesh as a collider? Or is there any other way to make the collider be like in the picture I posted? <br><br></td></tr></table><br>
<a name="935696"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> hmmm it is usually a good idea to make an invisible much simpler mesh for collisions. if you make these right they will prevent sliding too much and speed up the collisions. <br><br></td></tr></table><br>
<a name="935805"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >neoshaman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> @fox95871<br><br>Regarding your picture... just look the mak/driver blitz3D sample, it has these king of collider (i have actually remove them when updating the code in mine while adding more physics)<br><br><pre class=code>	;align car to wheels
	zx#=(EntityX( wheels[2],True )+EntityX( wheels[4],True ))/2
	zx=zx-(EntityX( wheels[1],True )+EntityX( wheels[3],True ))/2
	zy#=(EntityY( wheels[2],True )+EntityY( wheels[4],True ))/2
	zy=zy-(EntityY( wheels[1],True )+EntityY( wheels[3],True ))/2
	zz#=(EntityZ( wheels[2],True )+EntityZ( wheels[4],True ))/2
	zz=zz-(EntityZ( wheels[1],True )+EntityZ( wheels[3],True ))/2
	AlignToVector car,zx,zy,zz,1
	
	zx#=(EntityX( wheels[1],True )+EntityX( wheels[2],True ))/2
	zx=zx-(EntityX( wheels[3],True )+EntityX( wheels[4],True ))/2
	zy#=(EntityY( wheels[1],True )+EntityY( wheels[2],True ))/2
	zy=zy-(EntityY( wheels[3],True )+EntityY( wheels[4],True ))/2
	zz#=(EntityZ( wheels[1],True )+EntityZ( wheels[2],True ))/2
	zz=zz-(EntityZ( wheels[3],True )+EntityZ( wheels[4],True ))/2
	AlignToVector car,zx,zy,zz,3
	
	;calculate car velocities	
	cx#=EntityX( car ):x_vel=cx-prev_x:prev_x=cx
	cy#=EntityY( car ):y_vel=cy-prev_y:prev_y=cy
	cz#=EntityZ( car ):z_vel=cz-prev_z:prev_z=cz
	
	;resposition wheels
	cnt=1
	For z=1.5 To -1.5 Step -3
	For x=-1 To 1 Step 2
;		PositionEntity wheels[cnt],0,0,0
;		ResetEntity wheels[cnt]
		PositionEntity wheels[cnt],x,-1,z
		cnt=cnt+1
	Next
	Next

	;move car
	If KeyDown(203) TurnEntity car,0,3,0
	If KeyDown(205) TurnEntity car,0,-3,0
	If EntityCollided( car,SCENE )
		If KeyDown(200)
			speed=speed+.02
			If speed&gt;.7 speed=.7
		Else If KeyDown(208)
			speed=speed-.02
			If speed&lt;-.5 speed=-.5
		Else
			speed=speed*.9
		EndIf
		MoveEntity car,0,0,speed
		TranslateEntity car,0,GRAVITY,0
	Else
		TranslateEntity car,x_vel,y_vel+GRAVITY,z_vel
	EndIf
</pre><br><br>This is the part of the code you are looking for, the "wheel" are the marker. It follow slope and don't slide... If you perform a test for individual collision on each wheel you can have extra info about the character position in the world (ie if near a edge). <br><br></td></tr></table><br>
<a name="935881"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, I'll check it out tonight.<br><br>Nate, that was so you could see what I was talking about, not to have them be visible in the actual game. One narrow collider in the center prevents sliding at the edges (yeah, I know about mode 3, but that doesn't always work, as the code in post 8 shows) so that characters fall more like in the old days, like in a nes game. But I was just showing that by having four of those, if there's some way to do that, you could also prevent the one side effect of having a narrow collider - the character's often half in a wall. Hopefully Tomago's code will show me some things I never thought of as a fix to that. <br><br></td></tr></table><br>
<a name="937821"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not saying there's actually lichen starting to grow on this topic, but I am getting the feeling it won't be long...<br><br>Is there a way to make mulitiply pointy collision meshes, or to import a custom mesh that looks like that and use it as a collider? Flag's character is now dropping down perfectly (see post 11) but it's clipping wrong as a side effect. Only multiple colliders or a custom collider mesh would fix that as far as I can tell. Does anyone know a way to achieve either?<br><br>I know it's not my topic, but I'm pretty curious now because his example is a lot like my game will be :)<br><br>Tomago, could you give me a more complete code sample than the one you gave? I mean like with Graphics3D and everything. I'm actually an infiltrator from the beginner's forum, so I'm not really sure exactly what to do with the code you posted. Sorry! D:<br><br>Thanks! <br><br></td></tr></table><br>
<a name="938451"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >neoshaman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Which one? <br><br></td></tr></table><br>
<a name="938502"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, a reply! I meant your last one, but I guess that's from the mak driver example. I looked at that last night, but man...I can't understand it at all! I understand the program running though, I guess each wheel is a collider? Thing is, when I do that I can never seem to get them to all stay attached to the character.<br><br>For example, in the picture I posted above, when I actually coded it to do that, if you jumped up onto the ledge so just your "foot" lands on it, the corresponding collider ends up on the ledge, and the other three drop down.<br><br>So for a beginner like me who starts to lose consciousness just looking at the mak code, what simple example could you give to lock four colliders together and to the character? Please wrap it if you can,  with Graphics3D and While Wend and all that. Thanks! Hope you can help :) <br><br></td></tr></table><br>
<a name="944193"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >fox95871</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#22">[#22]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm starting a new thread about this in the beginners area if anyone's interested:<br><br><a href="http://www.blitzbasic.com/Community/posts.php?topic=84103" target="_blank">http://www.blitzbasic.com/Community/posts.php?topic=84103</a> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
