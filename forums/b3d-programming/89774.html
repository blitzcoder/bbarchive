<!DOCTYPE html><html lang="en" ><head ><title >Falling When Running Into Two Walls At Once</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Falling When Running Into Two Walls At Once</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Falling When Running Into Two Walls At Once</a><br><br>
<a name="1020109"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Whats My Face</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Basically, I have a player and he runs along the ground and does it fairly well. However, when that player runs into two walls his ability to also run into the ground is somewhat impaired. So for several frames he will fall through the ground and then be reset for no good reason. Heres the code for the walking function<br><br>Heres some video of the problem if that helps:<br><a href="http://www.youtube.com/watch?v=QUtHQ61kOwo" target="_blank">http://www.youtube.com/watch?v=QUtHQ61kOwo</a><br><br>Don't ask me what happened to it near the end but you can get a clear view of the problem before the video looks bad.<br><br><pre class=code>
	charAction = IDLE
	
	;jumping + gravity
	Local playerCollided = EntityCollided(player,ENVIRONMENTTYPE)
	Local collisionIndex
	Local touchingGround = False
	
	TranslateEntity player,0,-.05,0 ;push the player down to make sure he is touching the ground
	
	;cycle through the player collisions
	If playerCollided &lt;&gt; 0 Then
		For collisionIndex = 1 To CountCollisions(player) 
			;this just gives a visual cue as to where the collision is
			PositionEntity colcube[collisionIndex],CollisionX#(player,collisionIndex),CollisionY#(player,collisionIndex),CollisionZ#(player,collisionIndex)
			ShowEntity colcube[collisionIndex]
			
			;if the collision is under the player then
			If CollisionY#(player,collisionIndex) &lt; EntityY#(player) Then
				touchingGround = True ;tell the computer hes touching the ground
				nx# = CollisionNX#(player,collisionIndex) ;get the normals
				ny# = CollisionNY#(player,collisionIndex)
				nz# = CollisionNZ#(player,collisionIndex)
				If playerYV# &lt; -.5 curHealth# = curHealth# + maxHealth*(playerYV#/2.50) ;subtract health if he fell too far
				RotateEntity character,0,0,0 ;make sure that the player is upright
				RotateEntity player,0,EntityYaw#(player),0
			
			;if the collision is over the player
			ElseIf CollisionY#(player,collisionIndex) &gt; EntityY#(player)+.6 Then
				If playerYV# &gt; 0 playerYV# = 0 ;stop his upward momentum
			EndIf 
		Next  
	EndIf
	
	;if the player not touching the ground
	If touchingGround = False Then
		TranslateEntity player,0,.05,0 ;move the player back down (we move him up above to smooth collisions)
		timeSinceLastGround = timeSinceLastGround + 1 ;add 1 to a counter that counts how many frames since the last collision
	Else
		timeSinceLastGround = 0
	EndIf 
	
	;keep player from getting stuck in corners (fixes a bug that kept player who were falling from a jump from getting stuck)
	If Abs((EntityY#(player)-oldPlayerY#)-playerYV#) &gt; .001 Then
		jumpXComp# = 0
		jumpZComp# = 0
	EndIf 
	
	;this rotates the player so hes facing on the lay of the land but save the pitch yaw and roll so we can put him back after hes done
	pitch# = EntityPitch#(player)
	yaw# = EntityYaw#(player)
	roll# = EntityRoll#(player)
	AlignToVector player,nx#,ny#,nz#,2
	
	;character movement
	If KeyHit(33) And chatOn = False autorun = 1-autorun
	Local currentlyMoving = 0
	
	;turns off autorun if the player presses a key
	If KeyDown(200) Or (KeyDown(17) And chatOn = False) Or (mouse1down And mouse2down) Or KeyDown(208) Or (KeyDown(31) And chatOn = False) autorun = 0
	
	;if the player should be going foward
	If ((KeyDown(200) Or (KeyDown(17) And chatOn = False) Or (mouse1down And mouse2down) Or autorun = 1)) And ((touchingGround = True) Or (timeSinceLastGround &lt; 10)) Then 
		;if the player is going up too steep a hill then face him down so he can't walk up it
		If (EntityPitch#(player)+Abs(EntityRoll#(player)) &lt; 60) Or (EntityPitch#(player) &lt;= 0) Then 
		Else
			RotateEntity player,-15,EntityYaw#(player),0
		EndIf
		
		;tell the computer that hes moving, move him, and set the anim seq to moving 
		currentlyMoving = 1
		If playerYV# &lt;= 0 MoveEntity player,0,0,-.2 
		charAction = MOVING 
	
	;if the player is supposed to be going backwards do much the same thing
	ElseIf (KeyDown(208) Or (KeyDown(31) And chatOn = False)) And (touchingGround = True Or timeSinceLastGround &lt; 10)
		If (EntityPitch#(player)-Abs(EntityRoll#(player)) &gt; -60) Or (EntityPitch#(player) &gt;= 0) Then 	
		Else
			RotateEntity player,15,EntityYaw#(player),0
		EndIf 
		currentlyMoving = -1
		If playerYV# &lt;= 0 MoveEntity player,0,0,.2
		charAction = MOVING
	EndIf 
	
	;rerotate the player to his original rotation
	RotateEntity player,pitch#,yaw#,roll#
	
	;turn the player if he's got the turn keys down
	If KeyDown(203) Or (KeyDown(30) And chatOn = False) Then
		TurnEntity player,0,2,0
	ElseIf KeyDown(205) Or (KeyDown(32) And chatOn = False)
		TurnEntity player,0,-2,0
	EndIf 
	
	;if the player isn't touching ground
	If touchingGround = False Then
		;and he hasn't been touching the ground for at least ten frames make him touch the ground
		If timeSinceLastGround &gt;= 10 charAction = JUMPING
		playerYV# = playerYV# - .025 ;move him downwards
		TranslateEntity player,jumpXComp#*.2,0,jumpZComp#*-.2 ;if he had any momentum move him that way
	Else
		playerYV# = 0
	EndIf 
 	
	;if the player is touching the ground then take away the momentum that he had when he was jumping
	If touchingGround = True Then
		jumpZComp# = 0 
		jumpXComp# = 0
	EndIf 
	
	;jumP + store momentum of jump
	If KeyHit(57) And touchingGround = True And chatOn = False Then
		playerYV# = .4
		jumpZComp# = Cos(EntityYaw#(player))*currentlyMoving
		jumpXComp# = Sin(EntityYaw#(player))*currentlyMoving
	EndIf 
	
	;gravity + save the playerY so that it can be used on the next loop
	If playerYV# &lt; -2.5 Then playerYV# = -2.5
	oldPlayerY# = EntityY#(player)
	TranslateEntity player,0,playerYV#,0
	
	;animation
	If charAction = IDLE Then
		If AnimSeq(character) &lt;&gt; 1 Animate character,1,.1,1
	ElseIf charAction = MOVING Then
		If AnimSeq(character) &lt;&gt; 2 Animate character,1,1,2
	ElseIf charAction = JUMPING Then
		If AnimSeq(character) &lt;&gt; 3 Animate character,2,1,3
	EndIf 
	
	;camera rotation (for when the user wants to pivot around the player mesh)
	If curPlayerKeyMode = RUNMODE Then 
		Local mxs = MouseXSpeed()
		Local mys = MouseYSpeed()
		
		If mouse1down And mouse2down=0 Then
			TurnEntity campiv,-mys/3.0,-mxs/3.0,0
		ElseIf mouse2down Then
			If mouse2hit Then
				RotateEntity player,0,EntityYaw#(campiv)+EntityYaw#(player),0
				RotateEntity campiv,EntityPitch#(campiv),0,0
			EndIf 
			
			TurnEntity player,0,-mxs/3.0,0
			TurnEntity campiv,-mys/3.0,0,0
		Else
			TurnEntity campiv,-EntityPitch#(campiv)/10,-EntityYaw#(campiv)/10,0
		EndIf 
	EndIf 
	
	If EntityPitch#(campiv) &gt; 80 RotateEntity campiv,80,EntityYaw#(campiv),0
	If EntityPitch#(campiv) &lt; -80 RotateEntity campiv,-80,EntityYaw#(campiv),0
	
	RotateEntity campiv,EntityPitch#(campiv),EntityYaw#(campiv),0
End Function
</pre> <br><br></td></tr></table><br>
<a name="1020170"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> If it's any help, i usually use blitz collisions, in conjunction with a short linepick downwards, to make sure the player is actually touching the ground. <br><br></td></tr></table><br>
<a name="1020171"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Looking at this further, i would say the align to vector command could be causing this, be feeding the wall vectors to the align command, and very briefly causing the player to be aligned to the wall, therefore making forward, be upwards. <br><br></td></tr></table><br>
<a name="1020198"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Whats My Face</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Ross C! Your line picking technique fixes 99% of the cases. <br><br></td></tr></table><br>
<a name="1020204"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> You might try setting up a different collision type for walls and floors.<br>If you want to walk on your model, you could split the model up into a 'walls' part and a 'floors' part. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
