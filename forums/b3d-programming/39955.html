<!DOCTYPE html><html lang="en" ><head ><title >Vector graphics - how to make the lines 'glow'?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Vector graphics - how to make the lines 'glow'?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Vector graphics - how to make the lines 'glow'?</a><br><br>
<a name="446702"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IronGiant</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm currently dabbling with the source code to a game coded by someone else, namely 'Vecteroids' (2D game) by Jim Pishlo.<br><br>I'm curious if it's possible to make the vector lines 'glow' and look as smooth as possible as they would<br>on an original vector monitor.<br><br>Naturally the monitors we are using on our PCs (ie raster) operate very differently to a true vector monitor, but even so, with todays' 2D/3D graphics cards and programming techniques there must be a way to simulate the smooth, bright and *glowing* lines that you'd normally see on a vector monitor.<br><br>Please note: I'm a relative beginner at this. I could have posted this in the beginners' area but I thought more people would see it here. :)<br><br><br>Thanks <br><br></td></tr></table><br>
<a name="446706"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlackD</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> A vector monitor? I don't think such a thing exists, except for the ones to trace waveforms, which are simply a light-beam moving over phosphor..<br><br>which is exactly what a CRT is, except at much higher resolution.. <br><br></td></tr></table><br>
<a name="446707"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IronGiant</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay then, an XY monitor to be precise. :)<br><br>They do exist, just not for use with PCs (I do though have one right behind me ....... in my Asteroids cab :-) <br><br></td></tr></table><br>
<a name="446716"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >John Pickford</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> The effect would probably be easier to recreate using B3D and drawing the lines with polyons.  The 'glow' could be drawn into the texture.   Also the texture could be rendered in 'additive' mode. <br><br></td></tr></table><br>
<a name="446717"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Like this :)<br><br>Personally I would use a single surface system instead of sprites and use Tris instead of whole quads. This is just a quick one I knocked up for you.<br><br>[EDIT]Or Use this texture for the line : <img src="http://www.morphlings.com/tim/line.png"> [/EDIT]<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640,480,32,2


LineTex = CreateTexture(32,32)
SetBuffer TextureBuffer(LineTex)
Line 16,0,16,32
Line 17,0,17,32
SetBuffer BackBuffer()


cam = CreateCamera()


pivot = CreatePivot()
PositionEntity pivot,0,0,20

LineSprite1 = CreateSprite(Pivot)
EntityTexture LineSprite1,LineTex
ScaleSprite Linesprite1,1,2
EntityBlend LineSprite1,3
RotateSprite LineSprite1,-20
SpriteViewMode LineSprite1,2
PositionEntity LineSprite1,-.7,0,0
EntityColor LineSprite1,0,255,255

LineSprite2 = CopyEntity(LineSprite1,pivot)
PositionEntity Linesprite2,.7,0,0
RotateSprite LineSprite2,20


LineSprite3 = CopyEntity(LineSprite1,pivot)
RotateSprite LineSprite3,100
ScaleSprite LineSprite3,1,0.75
PositionEntity LineSprite3,-0.60,-1.8,0

LineSprite4 = CopyEntity(LineSprite1,pivot)
RotateSprite LineSprite4,80
ScaleSprite LineSprite4,1,0.75
PositionEntity LineSprite4,0.75,-1.8,0

While Not KeyHit(1)

    TurnEntity pivot,0,0,1
	MoveEntity pivot,0,0,(Sin(MilliSecs()/2))
	
	RenderWorld
	Flip

Wend
</textarea> <br><br></td></tr></table><br>
<a name="446721"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IronGiant</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Indiepath.T - thanks very much for that, looks really nice. :-)<br><br>Is it possible to make it look even 'brighter'?<br><br>Also, given the ignorance of this here beginner, how would I go about using the texture you provided for the line?<br><br><br>Many thanks <br><br></td></tr></table><br>
<a name="446723"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Save the Texture as line.png and put it into the same directory as the code.<br><br>This is about as bright as you're gonna get without using some kind of filter. It would look cool with a bloom filter :)<br><br>Use the following code to utilise the texture :-<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640,480,32,2


LineTex = LoadTexture("line.png",2)

cam = CreateCamera()


pivot = CreatePivot()
PositionEntity pivot,0,0,30

LineSprite1 = CreateSprite(Pivot)
EntityTexture LineSprite1,LineTex
ScaleSprite Linesprite1,1,2
EntityBlend LineSprite1,3
RotateSprite LineSprite1,-20
SpriteViewMode LineSprite1,2
PositionEntity LineSprite1,-.7,0,0
EntityColor linesprite1,0,255,255
EntityFX linesprite1,1

LineSprite2 = CopyEntity(LineSprite1,pivot)
PositionEntity Linesprite2,.7,0,0
RotateSprite LineSprite2,20


LineSprite3 = CopyEntity(LineSprite1,pivot)
RotateSprite LineSprite3,100
ScaleSprite LineSprite3,1,0.75
PositionEntity LineSprite3,-0.60,-1.8,0

LineSprite4 = CopyEntity(LineSprite1,pivot)
RotateSprite LineSprite4,80
ScaleSprite LineSprite4,1,0.75
PositionEntity LineSprite4,0.75,-1.8,0

While Not KeyHit(1)

    TurnEntity pivot,0,0,1
	;MoveEntity pivot,0,0,(Sin(MilliSecs()/2))
	
	RenderWorld
	Flip

Wend
</textarea> <br><br></td></tr></table><br>
<a name="446756"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IronGiant</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Now that's looking very nice. Thanks very much for all your help so far. :-)<br><br>Of course, now you've mentioned that it would look even better with a bloom filter, so I just HAVE to ask the inevitable question - how would a bloom filter be added? :)<br><br>And would it make the lines look brighter/more intense? <br><br></td></tr></table><br>
<a name="446764"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is the bloom code - thanks to Bouncer.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;//////////////////////////////////////////////////////////////////////////////
;//	Bouncers Bloom Filter
;//////////////////////////////////////////////////////////////////////////////
;Bloom / Glow post-process filter by Bouncer
;Using SSwift's excellent texture blurring routine
;Use at your own risk :) may contain bugs 
;Warning ... this filter uses LOT of fillrate so it's not for the old gfx-cards 

;V1.2
;Fixed camera bug...

;---------------------------------------------------
;GLOW VARIABLES
Global GL_cam
Global GL_scenecam					;This stores the pointer to your scene camera
Global GL_offset = 65536			;Offset for the glow camera
Global GL_texsize = 256			;Glow texture resolution (128 or 256 recommended)
Global GL_camzoom# = 1.0		;Change this value if your camzoom differs from the default

Global GL_sprite
Global GL_tex,GL_texbuffer

Global GL_sprite_scene
Global GL_timer=1

;SSwifts blur routine ---------------------
; This is the location in the world where the camera will reside.
; This location should be far from any other geometry you have in your world.
Const BLUR_CAM_X# = 65536.0
Const BLUR_CAM_Y# = 65536.0
Const BLUR_CAM_Z# = 0.0

;Modified to additive blend in the same time as blur.
;Also modified the copyrecting... so that multiple passes don't cause texture drifting.
;this is just a quick fix... don't actually get why tex texture drifts...
Function BlurTexture(Texture, Blur_Quality, Blur_Radius#)

; -------------------------------------------------------------------------------------------------------------------
; This function blurs a texture using a technique that takes advantage of 3D acceleration.  
;
; * You MUST hide all other cameras before calling this function!
; * You MUST reset your texture's blending mode, scale, and position after calling this function!
;
; Texture is the texture you want blurred.
;
; Blur_Quality defines the quality of the blur.  1 = 4 passes, 2 = 8 passes, 3 = 12 passes, etc.
;
; (The reason that the passes are in multiples of four is because interference artifacts are created when
; the number of passes is not a multiple of four... meaning that ten passes will actually look far worse
; than eight.)
;
; Blur_Radius# defines the radius of the blur, in pixels, assuming a map size of 256x256.
;
;(Ie, a radius of 16 will be the same width regardless of whether the texture is 16x16 or 512x512.  It will
; only be exactly 16 pixels wide if the map is 256x256.)
; -------------------------------------------------------------------------------------------------------------------

; This is used for temporary storage of the meshes used for soft shadow blurring.
Local BlurMesh[16*4]


; If blurring is enabled...
If Blur_Quality &gt; 0

Blur_Cam = CreateCamera()

; Set the camera's range to be very small so as to reduce the possiblity of extra objects making it into the scene.
CameraRange Blur_Cam, 0.1, 100

; Set the camera to zoom in on the object to reduce perspective error from the object being too close to the camera.
CameraZoom Blur_Cam, 16.0

; Aim camera straight down.
RotateEntity Blur_Cam, 90, 0, 0, True

; Set the camera viewport to the same size as the texture.
CameraViewport Blur_Cam, 0, 0, TextureWidth(Texture), TextureHeight(Texture)

; Set the camera so it clears the color buffer before rendering the texture.
CameraClsColor Blur_Cam, 0,0,0
CameraClsMode  Blur_Cam, True, True

; Position the blur camera far from other entities in the world.
PositionEntity Blur_Cam, BLUR_CAM_X#, BLUR_CAM_Y#, BLUR_CAM_Z#

; Create the sprites to use for blurring the shadow maps.
For Loop = 0 To (Blur_Quality*4)-1
BlurMesh[Loop] = CreateSprite()
EntityBlend BlurMesh[Loop],3
Next

; Scale the texture down because we scale the sprites up so they fill a larger area of the
; screen.  (Otherwise the edges of the texture are darker than the middle because they don't
; get covered.
ScaleTexture    Texture, 0.5, 0.5
PositionTexture Texture, 0.5, 0.5

; Blur texture by blitting semi-transparent copies of it on top of it.
BlurRadius# = Blur_Radius# * (1.0 / 256.0)
BlurAngleStep# = 360.0 / Float(Blur_Quality*4)

; Normally we would just divide 255 by the number of passes so that adding all the passes
; together would not exceed 256.  However, if we did that, then we could not have a number of
; passes which does not divide 256 evenly, or else the error would result in the white part of
; the image being slightly less than white.  So we round partial values up to ensure that
; white will always be white, even if it ends up being a little whiter than white as a result
; when all the colors are added, since going higher than white just clamps to white.
BlurShade = Ceil(255.0 / Float(Blur_Quality*4))

; Place each of the blur objects around a circle of radius blur_radius.
For Loop = 0 To (Blur_Quality*4)-1

EntityTexture BlurMesh[Loop], Texture
EntityFX BlurMesh[Loop], 1+8
EntityAlpha BlurMesh[Loop], 1.0 / Float(Loop+1)
ScaleSprite BlurMesh[Loop], 2, 2

BlurAngle# = BlurAngleStep# * Float(Loop) + 180.0*(Loop Mod 2)

Xoff# = BlurRadius# * Cos(BlurAngle#)
Yoff# = BlurRadius# * Sin(BlurAngle#)

PositionEntity BlurMesh[Loop], BLUR_CAM_X# + Xoff#, BLUR_CAM_Y# - 16.0, BLUR_CAM_Z# + Yoff#, True

Next

; Render the new texture.
RenderWorld

; Copy the new texture from the screen buffer to the texture buffer.
CopyRect 1, 1, TextureWidth(Texture)-1, TextureHeight(Texture)-1, 0, 0, BackBuffer(), TextureBuffer(Texture)

; Free the blur entities.
For Loop = 0 To (Blur_Quality*4)-1
FreeEntity BlurMesh[Loop]
Next

; Free the blur camera.
FreeEntity Blur_Cam

EndIf

End Function


;Initializes the filter
;--------------------------

;camera - your main scene camera
;NOTE - do not change camera rotation before calling this init function.
Function InitGlow(camera)
GL_scenecam = camera

;SET GLOW CAMERA
GL_cam = CreateCamera()
PositionEntity GL_cam,GL_offset,0,0
CameraProjMode GL_cam,2
CameraRange GL_Cam, 1, 100
CameraClsMode GL_cam,0,1
CameraViewport GL_cam,0,0,GL_texsize,GL_texsize
HideEntity GL_cam

;SET GLOW SPRITE 1 (FOR DARKENING PASSES)
GL_sprite = CreateSprite()
GL_tex = CreateTexture(GL_texsize,GL_texsize,256+16+32)
GL_texbuffer = TextureBuffer(GL_tex)
EntityTexture GL_sprite,GL_tex
PositionEntity GL_sprite,GL_offset,0,1
EntityOrder GL_sprite,-999
EntityFX GL_sprite,1

;SET GLOW SPRITE 2  (FINAL  OVERLAY)
GL_sprite_scene = CreateSprite()
EntityTexture GL_sprite_scene,GL_tex
ScaleSprite GL_sprite_scene,2*(1.0/GL_camzoom),2*(1.0/GL_camzoom)
PositionEntity GL_sprite_scene,EntityX(GL_scenecam),EntityY(GL_scenecam),EntityZ(GL_scenecam)+2
EntityParent GL_sprite_scene,GL_scenecam
EntityOrder GL_sprite_scene,-999
EntityFX GL_sprite_scene,1
End Function



;Renders the filter
;--------------------------

;fade - initial fade value for the scene
;dark_passes - how many multiply passes (darkening)
;glow_passes - how many blur / glow passes
;glare_size - blur radius

;just play with the values until you find a good combination.


;IMPORTANT NOTES
;* 		The scene camera will be hidden automatically...
;		but you must hide every other camera you might be using Before calling this Function.

;*		Also if you want to turn the blurring off you MUST HIDE the GL_sprite_scene object and stop calling this function (obviously :)

;* 		The more dark_passes and glow_passes - the slower the function is ... so beware.
;		Also switching to smaller texture size will boost performance.

Function RenderGlow(fade#=0.15,dark_passes=2,glow_passes=3,glare_size#=4)
;RENDER THE SCENE WITH MAIN CAMERA AND COPY TO TEXTURE
;Also apply the fade filter
HideEntity GL_sprite
EntityBlend GL_sprite_scene,1
EntityColor GL_sprite_scene,0,0,0
EntityAlpha GL_sprite_scene,fade

CameraViewport GL_scenecam,0,0,GL_texsize,GL_texsize
RenderWorld
CopyRect 1,1,GL_texsize-1,GL_texsize-1,0,0,BackBuffer(),GL_texbuffer
;CameraViewport GL_scenecam,0,0,GraphicsWidth(),GraphicsHeight()
CameraViewport GL_scenecam,0,GraphicsHeight()/8,GraphicsWidth(),GraphicsHeight()-((GraphicsHeight()/8)*2)

ShowEntity GL_sprite
EntityColor GL_sprite_scene,255,255,255
EntityAlpha GL_sprite_scene,1


;SWITCH TO GLOW CAMERA AND HIDE MAIN CAMERA
HideEntity GL_scenecam
ShowEntity GL_cam


;MULTIPLY BLEND WITH BACKBUFFER TO MAKE THE CONTRAST HIGHER
;(RESERVE THE LUMINATED AREAS)
EntityBlend GL_sprite,2		
For i=1 To dark_passes
	RenderWorld 
Next
CopyRect 1,0,GL_texsize-1,GL_texsize,0,0,BackBuffer(),GL_texbuffer
EntityBlend GL_sprite,1
HideEntity GL_sprite

;BLUR AND PUT SOME GLOW ON THE TEXTURE
;MORE PASSES - MORE GLOW
HideEntity GL_cam
For i=1 To glow_passes
blurtexture(GL_tex,1,glare_size)
Next
ScaleTexture GL_tex, 1,1
PositionTexture GL_tex,0,0
TextureBlend GL_tex, 2;2

;SWITCH BACK TO NORMAL CAMERA
ShowEntity GL_scenecam

;BLEND WITH ADDITIVE BLENDING WITH THE SCENE
EntityBlend GL_sprite_scene,3
End Function
</textarea><br><br>and here is the code to use it &gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;&gt;<br><br>BEWARE this will slow everything down.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640,480,32,2

Include "bbloom.bb"

LineTex = LoadTexture("line.png",2)

cam = CreateCamera()

initglow(cam)

pivot = CreatePivot()
PositionEntity pivot,0,0,20

LineSprite1 = CreateSprite(Pivot)
EntityTexture LineSprite1,LineTex
ScaleSprite Linesprite1,1,2
EntityBlend LineSprite1,3
RotateSprite LineSprite1,-20
SpriteViewMode LineSprite1,2
PositionEntity LineSprite1,-.7,0,0
EntityColor linesprite1,0,255,255
EntityFX linesprite1,1

LineSprite2 = CopyEntity(LineSprite1,pivot)
PositionEntity Linesprite2,.7,0,0
RotateSprite LineSprite2,20


LineSprite3 = CopyEntity(LineSprite1,pivot)
RotateSprite LineSprite3,100
ScaleSprite LineSprite3,1,0.75
PositionEntity LineSprite3,-0.60,-1.8,0

LineSprite4 = CopyEntity(LineSprite1,pivot)
RotateSprite LineSprite4,80
ScaleSprite LineSprite4,1,0.75
PositionEntity LineSprite4,0.75,-1.8,0

While Not KeyHit(1)

    TurnEntity pivot,0,0,1
	;MoveEntity pivot,0,0,(Sin(MilliSecs()/2))
	renderglow(0.3,1,4,3)			; Play with these Numbers to alter effect

	RenderWorld
	Flip

Wend
</textarea><br><br>Neat Eh? <br><br></td></tr></table><br>
<a name="446767"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IronGiant</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow, that IS nice. Lovely stuff - thanks. :-)<br><br>As you say, it does slow things down (understandably so) - if incorporated into a game like Asteroids, would there likely be any problems running it on your 'below average' PC ('below average' being perhaps 1GHz) but perhaps coupled with, say, an older video card (older being a year old perhaps). <br><br></td></tr></table><br>
<a name="446789"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I would include it in the game and give the user the option to turn it on or off. <br><br></td></tr></table><br>
<a name="446817"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Alos, bear in mind that brightness is kinda relative to the rest of the screen. Don't ever use pure white (255 255 255) as a colour on screen. (Unless I suppose if you have a nuclear-explosion white-out!)Have a dark background, keep colours light but not bright so that lights will really show! <br><br></td></tr></table><br>
<a name="446834"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey I can feel a remake of the old battlezone coming on... <br><br></td></tr></table><br>
<a name="446837"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IronGiant</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Indiepath.T - agreed, an option to turn it on and off does sound like the best solution.<br><br>Have you see the Vectoroids source code? I ask as I'm curious how easy (or hard) it would be to incorporate not only the 'bloom' function but also the very nice smooth lines and brightness in the way that you showed me. Remember that I'm a relative beginner - is it likely to be beyond me?<br><br><br>Malice - I heed your warning, however, as Asteroids is purely bright white vectors on a black background then presumably it will look 'right' if implemented as per the above code examples? <br><br></td></tr></table><br>
<a name="446849"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> IronGiant - Can't say I have seen the source. It should be possible to convert it to use similar code to the stuff I have posted. If the program uses the 2D line command you could convert it using the following snippet. (from Fredborg)<br><pre class=code>
Function Line3D(mesh,x0#,y0#,z0#,x1#,y1#,z1#) 

	If mesh = 0 
		mesh = CreateMesh() 
		surf = CreateSurface(mesh) 
		EntityFX mesh,1+16 
	Else 
		lastsurf = CountSurfaces(mesh)
		surf = GetSurface(mesh,lastsurf)
		If CountVertices(surf)&gt;30000
			surf = CreateSurface(mesh)
		EndIf 
	End If 

	v0 = AddVertex(surf,x0,y0,z0) 
	v1 = AddVertex(surf,x1,y1,z1)  
	v2 = AddVertex(surf,(x0+x1)*0.5,(y0+y1)*0.5,(z0+z1)*0.5) 
	AddTriangle surf,v0,v1,v2 

	Return mesh 

End Function
</pre> <br><br></td></tr></table><br>
<a name="446855"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>Malice - I heed your warning, however, as Asteroids is purely bright white vectors on a black background then presumably it will look 'right' if implemented as per the above code examples? <br> <br></div><br><br>I hope so! In theory yes, but I think that your problem mainly comes into that of hardware. Newer monitors (especially flat-screen types) have a much sharper definition now, so the luminance/refractive stuff that you'd get from a CRT screen is greatly reduced. So instead of having a bright  'light', you would get the actual, sharp, defined line.<br><br>I'd certain recommend the Faded Texture, the bloom stuff, if you can (bit techy for me!) and good luck! <br><br></td></tr></table><br>
<a name="446873"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IronGiant</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Indiepath.T - It does indeed appear to be using the Line command, at least in this portion of the code:<br><br>Function render_object(obj.vector_obj)<br>	Color 255,255,255<br>	If obj\visible Then<br>		an# = obj\angle<br>		For i = 1 To obj\vector_count - 1<br>			ox# = obj\x<br>			oy# = obj\y<br>			x1# = ox + Sin((obj\vectors[i-1]\angle) + an) * ((obj\vectors[i-1]\radius))<br>			y1# = oy + Cos((obj\vectors[i-1]\angle) + an) * ((obj\vectors[i-1]\radius))<br>			x2# = ox + Sin((obj\vectors[i]\angle) + an) * ((obj\vectors[i]\radius))<br>			y2# = oy + Cos((obj\vectors[i]\angle) + an) * ((obj\vectors[i]\radius))<br>			Line x1,y1,x2,y2<br>		Next<br>	End If<br>End Function<br><br><br>BTW, how do I make inserted code (ie inserted into a message here) appear as scrollable green text on a black background)? <br><br></td></tr></table><br>
<a name="446874"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Damien Sturdy</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> [ code ]<br>[ /code ]<br><br>(remove spaces) <br><br></td></tr></table><br>
<a name="446875"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IronGiant</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> Malice - thanks, yes, I see what you mean. No doubt some experimentation will be called for. :-) <br><br></td></tr></table><br>
<a name="446886"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >TartanTangerine (was Indiepath)</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#20">[#20]</a></td></tr></table></td></tr><tr ><td class="posttext"> Then it SHOULD be pretty easy to convert to 3d. BUT without seeing the rest of the code I could be wrong.<br><br>Email me the code it you like. I'd like to have a go some time. <br><br></td></tr></table><br>
<a name="446934"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IronGiant</td><td align="right"><font class=tiny>(Posted 2004)</font>&nbsp;<a href="#21">[#21]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Cygnus. <br><br>Indiepath.T - YHM. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
