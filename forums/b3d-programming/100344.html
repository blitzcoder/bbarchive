<!DOCTYPE html><html lang="en" ><head ><title >Get Drives Data &amp; system info (for Blitz3D)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Get Drives Data &amp; system info (for Blitz3D)</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Get Drives Data &amp; system info (for Blitz3D)</a><br><br>
<a name="1184040"></a>

<a name="1184041"></a>

<a name="1184042"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >virtlands</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Blitzers, <br><br>  Here is some crazy code that I did around March 2012.  <img src="http://i411.photobucket.com/albums/pp194/aeDream/wunderland%20emoticons/default61.gif"><br> <br> The purpose of this is to determine various windows system info <br> including listing some drives information.<br><br> I allowed for the possibility of 3 different methods to get drive data:<br><br>;;           method 0 =  GetLogicalDriveStrings(..,..)<br>;;           method 1 =  GetLogicalDrives()  <br>;;           method 2 =  ..the ancient way..<br>;; <br>Function GetDrivesData(method) { .... }<br><br>This program requires <b>BlitzSys.DLL</b> and <b>BlitzSys.BB</b> , which you can get from this link:<br><a href="http://uploadingit.com/file/2ni14zc2vf4nzyt7/BlitzSys_DLL%20%26%20BlitzSys_BB%20_V1.05.zip" target="_blank">http://uploadingit.com/file/2ni14zc2vf4nzyt7/BlitzSys_DLL%20%26%20BlitzSys_BB%20_V1.05.zip</a><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80"> ;; This program was created by Virtlands,  Andy
 ;;  March 2012

Include "C:\Blitz3d projects\BlitzSys V1.05\blitzsys.bb"

Type SystemInfo                               ;; total size = 36 bytes
       Field OemID                         ;; DWORD
       Field pagesize                      ;; DWORD
       Field minapplicationaddress     ;; LPVoid :: DWORD
       Field maxapplicationaddress    ;; LPVoid :: DWORD
       Field ActiveProcessorMask      ;; DWORD_PTR
       Field numberofprocessors       ;; DWORD
       Field processortype              ;; DWORD
       Field allocationgranularity      ;; DWORD
       Field processorlevel              ;; WORD
       Field processorrevision          ;; WORD 
End Type 


;; This structure is used with the GetVersionEx(..) function.
Type OSVersionInfo       
        Field OSVerInfoSize     ;;=148,      DWORD    (DWORD=4 bytes)   
        Field Version                               ;; = a combination of majorversion and minorversion
        Field majorversion        ;; DWORD
        Field minorversion        ;; DWORD
        Field buildnumber         ;; DWORD
        Field platformID           ;; DWORD
        Field CSDVersion$        ;; 128 byte string
End Type 

;; This structure is used with the GetVersionEx(..) function.
Type OSVersionInfoEx        
        Field OSVerInfoSize      ;;=156,      DWORD    (DWORD=4 bytes)   
        Field Version                               ;; = a combination of majorversion and minorversion
        Field majorversion        ;; DWORD
        Field minorversion        ;; DWORD
        Field buildnumber         ;; DWORD
        Field platformID           ;; DWORD
        Field CSDVersion$        ;; 128 byte string
        Field ServicePackMajor ;; WORD     (WORD=2 bytes)
        Field ServicePackMinor ;; WORD
        Field SuiteMask           ;; WORD
        Field ProductType        ;; BYTE
        Field Reserved            ;; BYTE
End Type 

Global systeminfo                               ;;  a bank     to hold the system info
Global systeminfo1.SystemInfo             ;;  the object to hold the system info

Global winversion                                ;;  a bank      to hold the user's Windows version 
Global winversion1.OSVERSIONINFOex    ;;   the object to hold the user's Windows version

WinVersion = CreateBank(156)    ;; To be used with OSVersionInfo1 or OSVersionInfo2
SystemInfo = CreateBank(36)     ;; To be used with FindSystemInfo

systeminfo1 = New systeminfo
winversion1 = New OSVERSIONINFOex
Global ProcessorCount 

Global DrivesAvail                 ;;  a special variable whose bits represents drives
Global Drive.drive[26]           ;; There are a maximum of 26 possible drives, A:\ to Z:\
Global DriveType$[6]            ;; descriptions of drive types

;Global AllowCDROMs = False   ;; This program shall ignore CDROM drives by default

Global HexT%[256]   ; This array allows for quick conversion of Hex to Decimal


Global fname$[10]             ;; = a list of embedded filenames
Global fbytes[10]              ;; = file bytes, (=the size in bytes of each attached file.)
Global foffset[10]              ;; = the offset into the main file where the data is
Global ffound[10]              ;; Set to TRUE if found, else set to FALSE
Global Extract_ini = False   ;; Set to TRUE if the Extract function was already run before


;;

Global c34$ = Chr$(34)

Type drive
        Field n$             ;  drive name, such as "C:\"
        Field dt             ;  drive type
        Field sn             ;  drive serial number
        Field vol$          ;   drive volume name
        Field fs$            ;  drive file system, such as "NTFS", "FAT", ...
End Type 


Global Appdir$, TempDir$, CLine$
Global AppSelf$                          ;; the path to THIS running program
Global Envar$[20], Enval$[20]       ;; some environment vars and values

Global useLinkParser = True         ;; Shall we use a link parser?   I think so...
Global LinkParserPath$         ;; absolute path of the link parser
Global LinkParserPathR$       ;; relative path of the link parser

Global tBank = CreateBank(256)
Global tDrive.drive               ;; = a temporary drive variable

Global WinFontsDir$             ;; Windows default fonts
Global ComputerName$,HomeDrive$,HomePath$,PCUserName$   

Global iSHpathIGNORE=False ;; Disable the iSHpath function if we choose to...

Global BBanksize  ;; ииииии start with a generic bank that can be used over And over again
Global BBank     


;    Enval[10] = DLLGetSpecialFolder$(CSIDL_DESKTOP)

;;_____program starts here__________

;Graphics 800,600,32,2

Graphics 1024,600,32,2

winversion = FindWindowsVersion(0) 
ProcessorCount = FindNumberOfProcessors()

GetEnvars() 
GetDrivesData(0)  

ComputerName = Enval[0]
HomeDrive       = Enval[1]
HomePath        = Enval[2]
PCUserName     = Enval[8]
WinFontsDir     = Enval[11]

;;; ---- Now Let's display some data ---- ;)

Print
Print"  SystemInfo demonstration program _by VirtLands "
Print 
Print"  Windows major version = "+ winversion1\majorversion
Print"  Windows minor version = "+ winversion1\minorversion
Print
Print"  Number of processors = "+ProcessorCount
Print
Print"  Computer name     = "+computername 
Print"  HomeDrive         = "+homedrive
Print"  HomePath          = "+homepath
Print"  PCUserName        = "+pcusername
Print"  Windows Fonts Dir = "+winfontsdir
Print 
Print"  SystemProperty("+Q("os")+") = "+ SystemProperty("os") 
Print 
Print"  The drives are listed as follows: "
Print 

 t.drive = Null 

 For z=0 To 26
     t = drive[z] 
     If t&lt;&gt;Null 
               Print" Drive "+t\n+ "  Type= "+ drivetype[t\dt]+ " : serial= "+Hex$(t\sn)+ "  : Volume name= "+Q(t\vol)+"   : File System= "+t\fs 
     End If 
 Next 

WaitKey():End 
  


Function Q$(Q$)  ;; same as function c34$
             Return Chr$(34)+Q$+Chr$(34)
End Function 

Function c34$(q$)   ;; returns  Q$ in quotes
             Return Chr$(34)+Q$+Chr$(34)
End Function 

Function we()
    WaitKey(): End
End Function 

Function GetEnvars()   ;;  Reads in some evironment variables
                               ;;  (There may be some duplication.)   
   Restore ENV_Var

   For z = 0 To 15
        Read Envar[z]        
   Next 
   For z = 0 To 9
        Enval[z] = GetEnv$(Envar[z])
   Next 

   Appdir$   = SystemProperty$("AppDir")
   Tempdir$ = SystemProperty$("TempDir")
   CLine$ = CommandLine$()

   ;; Now read additional special folders

   Enval[10] = DLLGetSpecialFolder$(CSIDL_DESKTOP)
   Enval[11] = DLLGetSpecialFolder$(CSIDL_FONTS)
   Enval[12] = DLLGetSpecialFolder$(CSIDL_MYPICTURES)
   Enval[13] = DLLGetSpecialFolder$(CSIDL_DESKTOPDIRECTORY)
   Enval[14] = DLLGetSpecialFolder$(CSIDL_PERSONAL)
   Enval[15] = DLLGetSpecialFolder$(CSIDL_PROGRAM_FILES)

   Enval[4] = LastSlash(Enval[4])
   Enval[6] = LastSlash(Enval[6])

   For z = 9 To 15
          Enval[z] = LastSlash(Enval[z])
   Next 

End Function

;; This function gets drive data only, such as "A:\", "B:\", "C:\",...
;; (does not compute directories or paths)
;;
;; There are about three basic methods for retrieving drives data.  (0,1)
;;
;;           method 0 =  GetLogicalDriveStrings(..,..)
;;           method 1 =  GetLogicalDrives()  
;;           method 2 =  ..the ancient way..
;;
Function GetDrivesData(method=0)   
     Local ptr=0,c,d=0,r=0                 ;; r = return value = number of drives found
     Local dz.drive=Null
     Local a$,vol$,z=0
     Local vn,sn,mcl,flags,fs

  If DriveType[z] =""              ; determine whether the "DriveType" array is initialized
             Restore DriveTypes
             For z=0 To 6
                  Read DriveType[z]
             Next 
  End If  

  If (winversion&lt;$0501) Or method=2     ;;  If the user's Windows version is less than _Windows XP_,
                                                        ;;   then we'll find the available drives this way:
              d = 0
              For z = 67 To 90                    ;; loop through drives C..Z
                     a$ = Chr$(z)+":\nul"
                     If FileType( a$ )
                              If drive[d]=Null Then drive[d] = New drive                              
                              r = r +1                ;; count the number of drives                            
                     End If 
                     d = d +1                     
              Next   
  Else 
     ;; the following methods 1 and 2 require _Windows XP_ (5.1.2600 SP3)  to work
     ;;
     Select method
     Case 0
                  ;;   number of drives = length of "tBank" string divided by 4   
                r = GetLogicalDriveStrings(255,tBank) Shr 2             

                For z= 1 To r                            
                         d  = PeekByte(tBank,ptr)-65                    ;; convert 'A' to 0
                         ptr = ptr +4
                         If drive[d]=Null Then drive[d] = New drive  ;; create new drive object                         
                Next 

     Case 1
                drives = GetLogicalDrives()   
                drives = drives And $3FFFFFF     ;; = 67108863 = 11111111111111111111111111 (binary)
                DrivesAvail = drives                  ;; (make a copy of that value)

                While drives
                         If  (drives And 1)
                                  r = r +1                   ;; count the number of drives
                                  If drive[d]=Null Then drive[d] = New drive   
                         End If
                         drives = drives Shr 1
                         d = d+1
               Wend  
     End Select 

  End If 

  If (r&gt;0) And (winversion&gt;=$0501)  ;; requires _Windows XP_ or greater to run "GetVolumeInformation(...)"

               vn=CreateBank(256)    ;volume name
               sn=CreateBank(4)       ;serial number
               mcl=CreateBank(4)        
               flags=CreateBank(4) 
               fs=CreateBank(256)    ;file system

               For z = 0 To 25
                        dz = drive[z]
                        If dz&lt;&gt;Null
                                 a$ = Chr$(z+65)+":\"
                                 GetVolumeInformation( a$,vn,255,sn,mcl,flags,fs,255 )  
                                 dz\n   = a$                                    
                                 dz\sn  = PeekInt(sn,0)
                                 dz\vol = BankToString(vn,256)  
                                 dz\fs  = BankToString(fs,256)      
                                 dz\dt = GetDriveType(a$)                                 
                        End If
               Next   

               FreeBank vn
               FreeBank sn
               FreeBank mcl
               FreeBank flags
               FreeBank fs
  End If

  FreeBank tBank 

  Return r

End Function 


; This function converts a string-BANK to a Blitz3D string
;
Function BankToString$(bank,bytecount)
      Local a$="", ptr = 0, byte 

      bc1 = bytecount-1
      For z = 0 To bc1
             byte = PeekByte(bank,z)
             If byte=0 Then Exit 
             a$ = a$ + Chr$(byte)
      Next 

      Return a$
End Function 

;; ииииииии Uses the generic bank known as "BBank" ииииииииии
;; ииииииии "BBanksize" And "BBank" are Global variables ииииии
;; иииииииииииииииии иииииииииииииииии иииииииииииииииии иииииииииии
Function UseBank(size)
       If bbanksize=0
                bbanksize = size
                bbank = CreateBank(size)                                                  
       End If 

       If bbanksize &lt; size
                bbanksize = size
                ResizeBank bbank, size                
       End If 
End Function 

Function LastSlash$(s$)    ; Gives a last slash to any string that is missing one
L = Len(s$)
If L=0 Then Return "\"

If Mid$(s$,L,1) = "\" 
       Return s$
Else  Return s$+"\"

End If
End Function


;Global WinVersion     ;; Shall hold windows version 
;
;Type WinVersion 
;        Field OSVerInfSize
;        Field majorversion
;        Field minorversion
;        Field buildnumber
;        Field platformID
;        Field CSDVersion$
;End Type 
;WinVersion = CreateBank(148)
;;
;WinVersion = CreateBank(148)

;Global WinVers_T1.WinVersion     ;; create two copies of WinVersion 
;Global WinVers_T2.WinVersion 


;Global OSVersionInfo1.OSVERSIONINFO
;Global OSVersionInfo2.OSVERSIONINFOex
;Global SystemInfo1.SystemInfo
;Global WinVersion                      ;; Shall hold windows version 
;Global SystemInfo

;WinVersion = CreateBank(156)    ;; To be used with OSVersionInfo1 or OSVersionInfo2
;SystemInfo = CreateBank(36)     ;; To be used with FindSystemInfo



;  "mode" has the following values
;
;   0 = find windows version the ancient way
;   1 = find windows version the advanced way
;
Function FindWindowsVersion(mode=1)
             Local ver1,ver2
             Local w.OSVERSIONINFOex = winversion1             
             Local ptr, s$, infosize 
             Local in, v1,v2,v3, L, max=0

             If w=Null Then w = New OSVERSIONINFOex

                                   ;; Options: 
                                   ;; Use 148 bytes for struct OSVERSIONINFO
                                   ;; Use 156 bytes for struct OSVERSIONINFOex  
             infosize = 148
             w\OSVerInfoSize = infosize
             PokeInt winversion, 0, infosize       ;; shall read in 148 bytes of data ..

             If mode = 0  ;; do it the hard way 
                      ChangeDir appdir      

                      If FileType("winversion.txt")=0   ;; if the file does not exist...              

                              ExecFile("ver &gt; winversion.txt")                   
                              Delay(100)   ;; give the system time to output to "winversion.txt"

                              Repeat 
                                     Delay(100)
                                     in = ReadFile("winversion.txt")
                                     If in&lt;&gt;0 Then Exit
                                     max = max +1                           
                              Until max =10   ;; if the file can not be read within 1 sec, then abort. 
                      Else 
                              in = ReadFile("winversion.txt")
                      End If 

                      If in=0       ;; if for some reason mode 0 fails, then try mode 1                                
                                Return FindWindowsVersion(1)
                      End If 

                      For z = 1 To 4
                             s = ReadLine(in)
                             If Len(s) Then Exit 
                      Next 

                      s = Lower$(s)    
                      L = Len(s)              
                      CloseFile(in)                      
                      
                      v1 = Instr(s,"version") +7
                      v2 = Instr(s,".",v1) +1
                      v3 = Instr(s,".",v2) +1

                      w\majorversion = Int(Mid$(s, v1,v2-v1-1))   
                      w\minorversion = Int(Mid$(s, v2,v3-v2-1))   
                      w\buildnumber =  Int(Mid$(s, v3,L-v3))      
                      w\version = (w\majorversion Shl 8) Or w\minorversion 
                      Return w\version
             End If 

             ver1 = GetVersionEx(WinVersion)
             ver2 = GetVersion()                 ;; = the older-style function, just in case
             ptr = 4

             If (ver1&gt;0)                         
                       w\majorversion = PeekInt(winversion,ptr) : ptr = ptr +4
                       w\minorversion = PeekInt(winversion,ptr) : ptr = ptr +4
                       w\buildnumber = PeekInt(winversion,ptr)  : ptr = ptr +4
                       w\platformID   = PeekInt(winversion,ptr)  : ptr = ptr +4

                       w\version = (w\majorversion Shl 8) Or w\minorversion                      
                       y = ptr +128

                       For z = 1 To 128
                               byte = PeekByte(winversion,ptr)
                               If byte=0 Then Exit 
                               s$ = s$ + Chr$(byte)
                               ptr = ptr +1                                 
                       Next 

                       w\CSDVersion$ = s$  ; A null-terminated string, such as "Service Pack 3", 
                                                    ; that indicates the latest Service Pack installed on the system                                                    
                       If infosize&gt;148
                           ptr = y
                           w\ServicePackMajor = PeekShort(winversion,ptr) : ptr = ptr +2
                           w\ServicePackMinor = PeekShort(winversion,ptr) : ptr = ptr +2
                           w\SuiteMask          = PeekShort(winversion,ptr) : ptr = ptr +2
                           w\ProductType       = PeekByte(winversion,ptr) : ptr = ptr +1
                           w\reserved            = PeekByte(winversion,ptr)
                       End If 
             Else    
                      ;;; We're assuming at this point that "GetVersionEx()" didn't work,
                      ;;; so assume that "GetVersion()" still works...
                      If (ver2&gt;0)
                                v = ver2
                                w\majorversion = v And 255
                                v = v Shr 8
                                w\minorversion = v And 255
                                v = v Shr 8 
                                w\buildnumber = v
                                w\version = (w\majorversion Shl 8) Or w\minorversion
                      Else 
                                Return FindWindowsVersion(0)
                      End If 
             End If 

            FreeBank Winversion  ;; winversion can now hold Windows Version 

            Return w\version 
End Function 


;Type System_Info                               ;; total size = 36 bytes
;       Field OemID                         ;; DWORD
;       Field pagesize                      ;; DWORD
;       Field minapplicationaddress     ;; LPVoid :: DWORD
;       Field maxapplicationaddress   ;; LPVoid :: DWORD
;       Field ActiveProcessorMask      ;; DWORD_PTR
;       Field numberofprocessors       ;; DWORD
;       Field processortype              ;; DWORD
;       Field allocationgranularity      ;; DWORD
;       Field processorlevel              ;; WORD
;       Field processorrevision          ;; WORD 
;End Type 


;; simply returns the number of processors in the PC
;;
Function FindNumberOfProcessors()
     Local s.systeminfo = systeminfo1 
     Local n, ptr =20
     
     If winversion &lt; $0500 
              s\numberofprocessors  = 1   ;; ASSUME it is 1
              Return 1
     End If 
                                            ;; function requires  Windows 2000 Professional (5.0 SP4 ) or greater
     GetSystemInfo(systeminfo)   ;; $0501

     n = PeekInt(systeminfo,ptr)
     s\numberofprocessors  = n

     Return n
End Function 

Function FindSystemInfo()
     Local s.systeminfo = systeminfo1 
     Local ptr

     GetSystemInfo(systeminfo)

     s\OemID    = PeekInt(systeminfo,ptr) : ptr = ptr +4
     s\pagesize = PeekInt(systeminfo,ptr) : ptr = ptr +4
     s\minapplicationaddress = PeekInt(systeminfo,ptr) : ptr = ptr +4
     s\maxapplicationaddress = PeekInt(systeminfo,ptr) : ptr = ptr +4
     s\activeprocessormask   = PeekInt(systeminfo,ptr) : ptr = ptr +4
     s\numberofprocessors    = PeekInt(systeminfo,ptr) : ptr = ptr +4
     s\processortype           = PeekInt(systeminfo,ptr) : ptr = ptr +4
     s\allocationgranularity   = PeekInt(systeminfo,ptr) : ptr = ptr +4
     s\processorlevel           = PeekShort(systeminfo,ptr) : ptr = ptr +2
     s\processorrevision       = PeekShort(systeminfo,ptr) 

End Function 


.DriveTypes
Data "Unknown Drive Type  "
Data "Drive has no Root Dir "
Data "Drive is Removable  "
Data "Drive is Fixed      "
Data "Drive is Remote      "
Data "CDRom Drive         "
Data "RamDrive               "

.monthdata
Data "Jan","Feb","Mar","Apr","May","Jun","Jul","Aug","Sep","Oct","Nov","Dec"

.ENV_Var   ; Environment Variables
Data "COMPUTERNAME", "HOMEDRIVE", "HOMEPATH", "PATH"
Data "PROGRAMFILES", "PROMPT", "TMP", "USERDOMAIN", "USERNAME"
Data "USERPROFILE"
;; Extended special folders
Data "CSIDL_DESKTOP"                 ;0
Data "CSIDL_FONTS"                    ;20
Data "CSIDL_MYPICTURES"            ;39
Data "CSIDL_DESKTOPDIRECTORY"  ;$10
Data "CSIDL_PERSONAL"                ;5
Data "CSIDL_PROGRAM_FILES"        
</textarea><br><br>The program can also retrieve the Windows version.<br>Whether this program works with Windows 8, well, I have no idea.<br><br>If anyone wants to download the complete <b>BlitzSys</b> package, then here it is for <u>version 1.05</u><br><a href="http://uploadingit.com/file/oufitjhfvxgvj1tk/BlitzSys%20V1.05%20(complete).zip" target="_blank">http://uploadingit.com/file/oufitjhfvxgvj1tk/BlitzSys%20V1.05%20(complete).zip</a><br><br>The following links display charts relating Windows OS names with version numbers:<br><a href="http://msdn.microsoft.com/en-us/library/windows/desktop/ms724832%28v=vs.85%29.aspx" target="_blank">http://msdn.microsoft.com/en-us/library/windows/desktop/ms724832%28v=vs.85%29.aspx</a> <br><a href="http://en.wikipedia.org/wiki/Comparison_of_Microsoft_Windows_versions" target="_blank">http://en.wikipedia.org/wiki/Comparison_of_Microsoft_Windows_versions</a> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
