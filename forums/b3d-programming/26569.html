<!DOCTYPE html><html lang="en" ><head ><title >Fade to black</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Fade to black</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Fade to black</a><br><br>
<a name="277107"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Boiled Sweets</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br><br>been lookikng in the forums and although many people have posted questions and answers regarding how to fade to black (and for that matter fade from black to show your scene) there doesn't seem to be anything that works well like you see in commercial games.<br><br>Has anyone got any thoughts/code to show how to do it smoothly and successfully?<br><br>Thanks. <br><br></td></tr></table><br>
<a name="277133"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> If it's full screen, you could use the gamma commands.<br><br>Or you could put a black sprite or quad covering the whole screen and slowly change it's transparecny. Might be slow on some computers tho, because the sprite is filling the whole screen. <br><br></td></tr></table><br>
<a name="277191"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Boiled Sweets</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well image you have 8000+ entities to alpha fade - that would be slow noh?  Cos thats what I have.<br><br>Also a black sprite or quad in front of the camera seems a little brutal somehow, I guess that whilst it has an alpha of 0 then it is not sent to the render anyway.  Would it take a lot of to draw a black quad in front of the camera, it would only be having to draw 12 tris. <br><br></td></tr></table><br>
<a name="277212"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >darklordz</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> check out my screentransition lib... composed out of demos i gathered and wrote...<br><br><a href="http://www.balpoint.com/files//tek%20demos/ScreenTransition.Archive.Demo.exe" target="_blank">http://www.balpoint.com/files//tek%20demos/ScreenTransition.Archive.Demo.exe</a><br><br>Source included....<br><br>also this is simple source for fading<br><br><br>P.S *For the smart ones...* I loved the album!<br><pre class=code>Function Fader(Image$,FadeToRGB,Pause)

		Local Camera = CreateCamera()
		CameraClsMode Camera,0,1
		Local FadeSprite = CreateSprite()
		EntityColor FadeSprite,Left$(FadeToRGB,3),Left$(Mid$(FadeToRGB,4),3),Right$(FadeToRGB,3)
		MoveEntity FadeSprite,0,0,1
		EntityOrder FadeSprite,-100

		Local Logo =  LoadImage(Image$)
		Local FadeIn = False
		Local FadeOut = False
		Local FadeLevel# = 1
		Local Done = False
		Local Timer = 0
	
		While Done = False
			Timer = Timer + 1
		
			If Timer = 100
				FadeIn = True
			EndIf
			
			Cls

			DrawImage Logo,(GraphicsWidth()/2)-(ImageWidth(Logo)/2),(GraphicsHeight()/2)-(ImageHeight(Logo)/2)
			
			If FadeIn = True
				EntityAlpha FadeSprite,FadeLevel#
				FadeLevel# = FadeLevel# - .01
				If FadeLevel# &lt;= 0
					FadeLevel# = 0
					FadeIn = False
				EndIf
			EndIf
		
			If FadeOut = True
				EntityAlpha FadeSprite,FadeLevel#
				FadeLevel# = FadeLevel# + .01
				If FadeLevel# =&gt; 1
					FadeLevel# = 1
					Done = True
				EndIf
			EndIf		
		
			If GetKey() Or GetJoy() Or Timer &gt;= Pause
				FadeIn = False
				FadeOut = True
			EndIf
		
			RenderWorld
			Flip
		Wend	
		FreeEntity FadeSprite
		FreeEntity Camera
		FreeImage Logo
	End Function</pre> <br><br></td></tr></table><br>
<a name="277213"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >GitTech</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>P.S *For the smart ones...* I loved the album! <br> <br></div><br><br>Me too :D <br><br></td></tr></table><br>
<a name="277238"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Boiled Sweets</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> That cod eis nice but doesn't help fading a 3d scene. <br><br></td></tr></table><br>
<a name="277246"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Eh, a quad is made up of 2 triangles :)<br><br>And you right, it doesn't get rendered when alpha is zero. Best hiding it tho. Also try using the gamma commands. <br><br></td></tr></table><br>
<a name="277304"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Boiled Sweets</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Err, what gamma commands? <br><br></td></tr></table><br>
<a name="277327"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rottbott</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Does your scene have to continue moving while fading in/out? <br><br></td></tr></table><br>
<a name="277339"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> <a href="http://www.blitzbasic.com/b3ddocs/command.php?name=SetGamma&amp;ref=2d_a-z" target="_blank">http://www.blitzbasic.com/b3ddocs/command.php?name=SetGamma&amp;ref=2d_a-z</a><br><br>Try that. there's an example to in blitz. <br><br></td></tr></table><br>
<a name="277340"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Boiled Sweets</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Rottbott, yes would be nice... <br><br></td></tr></table><br>
<a name="277355"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey, try this. Press 1 to fade in, and 2 to fade out. Adjust fade_speed to change how quick the fade is.<br><br><pre class=code>
Graphics3D 800,600
SetBuffer BackBuffer()

Global light=CreateLight()


Global camera=CreateCamera()
PositionEntity camera,0,0,-10
CameraRange camera,0.1,1000

Global sphere=CreateSphere()
PositionEntity sphere,2,0,0

Global cube=CreateCube()
PositionEntity cube,-2,0,0

Global fade_quad=CreateMesh()

surf=CreateSurface(fade_quad)
v0 = AddVertex (surf, -2,-2,0,  0  ,0)
v1 = AddVertex (surf,  2,-2,0,  0  ,0)
v2 = AddVertex (surf, -2, 2,0,  0  ,1)
v3 = AddVertex (surf,  2, 2,0,  0  ,1)

tri  = AddTriangle (surf,v0,v2,v1)
tri1 = AddTriangle (surf,v1,v2,v3)

EntityParent fade_quad,camera
PositionEntity fade_quad,0,0,0.5
EntityColor fade_quad,0,0,0
Global fade_alpha#=1
Global fade_dir=1; 1 to fade out, 0 to fade in, -1 for no change
Global fade_speed#=0.01; adjust for fade speed

While Not KeyHit(1)


	If KeyHit(2) Then fade_in()
	If KeyHit(3) Then fade_out()



	fade()

	UpdateWorld
	RenderWorld
	Flip

Wend
End

Function fade()
	If fade_dir=0 Then
		fade_alpha=fade_alpha-fade_speed
		If fade_alpha&lt;0 Then
			fade_alpha=0
			fade_dir=-1
		End If
	ElseIf fade_dir=1 Then
		fade_alpha=fade_alpha+fade_speed
		If fade_alpha&gt;1 Then
			fade_alpha=1
			fade_dir=-1
		End If
	End If
	EntityAlpha fade_quad,fade_alpha
End Function

Function fade_out()
	fade_dir=1
End Function

Function fade_in()
	fade_dir=0
End Function
</pre><br><br>should do what you want :) <br><br></td></tr></table><br>
<a name="277711"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >darklordz</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> @dewwzil my code does fade 3d scenes....and there is lots of code in my arc. It's an alternative to teh gamma commands. <br><br>My code creates a sprite of any color and uses entityalpha to fadeinto view or out...... <br><br></td></tr></table><br>
<a name="277760"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tracer</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> I just whack a black quad in front of the camera, then alpha it from 1 to 0 to fade in and from 0 to 1 to fade out. Works great and is the simple solution.<br><br>Tracer <br><br></td></tr></table><br>
<a name="277768"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> As my code demonstrats and i think dark lords does too. :) <br><br></td></tr></table><br>
<a name="277810"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Anthony Flack</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yep, that's what I do too. It seems excessive, but for most people, it should be a lot faster than continually updating the gamma table, which is actually pretty slow if you haven't noticed. <br><br></td></tr></table><br>
<a name="277910"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2003)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the tip Anthony. Never knew that one :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
