<!DOCTYPE html><html lang="en" ><head ><title >Align With Normals</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Align With Normals</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Align With Normals</a><br><br>
<a name="1295779"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi<br><br>I am working on a simple editor where I have a errain mesh and need to identify the angles of the normals to a selected triangle.<br><br>Triangle selection is all working well enough, and the position correctly marks the centre of a triangle.<br><br>However, when I try to align a 'marker' cylinder to show the direction vector of the normal, it doesn't appear to be correct.<br><br>here's the offending function:<br><br><pre class=code>
	Local Surf=GetSurface(TERRAIN,1)
	
	Local v0=TriangleVertex(Surf,Tri,0)
	Local v1=TriangleVertex(Surf,Tri,1)
	Local v2=TriangleVertex(Surf,Tri,2)
	
	VertexColor Surf,v0,0,0,255
	VertexColor Surf,v1,0,0,255
	VertexColor Surf,v2,0,0,255
	
	EntityFX TERRAIN,2
	
	Local x0#=VertexX(Surf,v0)
	Local x1#=VertexX(Surf,v1)
	Local x2#=VertexX(Surf,v2)
	
	Local y0#=VertexY(Surf,v0)
	Local y1#=VertexY(Surf,v1)
	Local y2#=VertexY(Surf,v2)
	
	Local z0#=VertexZ(Surf,v0)
	Local z1#=VertexZ(Surf,v1)
	Local z2#=VertexZ(Surf,v2)
	
	Local nx0#=VertexNX(Surf,v0)
	Local nx1#=VertexNX(Surf,v1)
	Local nx2#=VertexNX(Surf,v2)
	
	Local ny0#=VertexNY(Surf,v0)
	Local ny1#=VertexNY(Surf,v1)
	Local ny2#=VertexNY(Surf,v2)
	
	Local nz0#=VertexNZ(Surf,v0)
	Local nz1#=VertexNZ(Surf,v1)
	Local nz2#=VertexNZ(Surf,v2)
	
	Local vX#=(x0+x1+x2)/3
	vX=vX+EntityX(TERRAIN,True)
	Local vY#=(y0+y1+y2)/3
	vY=vY+EntityY(TERRAIN,True)
	Local vZ#=(z0+z1+z2)/3
	vZ=vZ+EntityX(TERRAIN,True)
	
	Local nX#=(nx0+nx1+nx2)/3
	Local nY#=(ny0+ny1+ny2)/3
	Local nZ#=(nz0+nz1+nz2)/3
	
	RotateEntity NORMALMARKER,VectorPitch(nX,nY,nZ),VectorYaw(nX,nY,nZ),0
	
	PositionEntity NORMALMARKER,vX,vY,vZ,True
	UpdateNormals NORMALMARKER
	ShowEntity NORMALMARKER
End Function
</pre> <br><br></td></tr></table><br>
<a name="1295785"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> First, a mesh has only vertex normals and not surface normals. Averaging the three vertex normals should usually be a reasonable approximation. But for accuracy is it better to compute the surface normal directly. <br><br>Consider two edges of the triangle to be vectors. The cross product of these is perpendicular to both and is thus a normal for the triangle. This could point to the front or back, depending on the order in which the edges are considered. If you end up with the wrong one then swap the two edges, i.e. change (edge1 cross edge2 ) to (edge2 cross edge1).<br><br>Once you have a normal vector you can orient the marker with AlignToVector. Here is something I wrote to illustrate TFormNormal on a scaled entity. That part is irrelevant for your problem. But near the bottom of the code you can see how alignment works once you have position and normal information.<br><br><pre class=code>; This shows how to find vertex positions and normals with a scaled entity.
; Left/Right arrow keys move around the scaled sphere. Escape quits

Graphics3D 800, 600, 0, 2
AmbientLight 50,50,50

lt = CreateLight() : RotateEntity lt, 0, 90, 0

sph = CreateSphere( 16 )

cam = CreateCamera() : PositionEntity cam, 0,0, -10 : CameraZoom cam, 4

cone = CreateCone()
ScaleMesh cone, 0.1, .25, 0.1
PositionMesh cone, 0, 0.26, 0

EntityColor cone, 0, 100, 100

surf = GetSurface(sph,1)
maxV = CountVertices( surf ) - 1

ScaleEntity sph, 2, .8, 2

v = 150  ; start at a vertex we can easily see

While Not KeyDown(1)

	PositionMarker( sph, 1, v, cone )
	RenderWorld
	Text 340, 500, "Vertex: " + v	
	Flip

	v = v + KeyDown(205) - KeyDown(203)   :   Delay 50
	
	If v &gt; maxV Then v = 0
	If v &lt; 0 Then v = maxV	

Wend


Function PositionMarker( mesh, surface_index, vert, marker )

	surf = GetSurface( mesh, surface_index )

	x# = VertexX( surf, vert )
	y# = VertexY( surf, vert )
	z# = VertexZ( surf, vert )

	nx# = VertexNX( surf, vert )
	ny# = VertexNY( surf, vert )
	nz# = VertexNZ( surf, vert )
	
	; Position and normal are correct for the underlying mesh, but
	; not for the scaled entity. Here's how to transform them.
		
	TFormPoint x,y,z, mesh, 0    ; the 0 means world coordinates
	x = TFormedX()
	y = TFormedY()
	z = TFormedZ()

	TFormNormal nx,ny,nz, mesh, 0 
	nx = TFormedX()
	ny = TFormedY()
	nz = TFormedZ()

	PositionEntity marker,  x,  y,  z	
	AlignToVector  marker, nx, ny, nz, 2
	
End Function</pre> <br><br></td></tr></table><br>
<a name="1295787"></a>

<a name="1295788"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RemiD</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> A method that i have used in the past :<br>create a mesh (for example a cube) to represent your normal :<br>normalmesh = createcube()<br>scalemesh(normalmesh,0.01/2,0.01/2,1.0/2)<br>positionmesh(normalmesh,0,0,1.0/2)<br><br>then once you have your picked/collided point position, position this normalmesh at this position.<br>then create a temporary pivot<br>then position the temporary pivot at this position<br>then move or translate (i don't remember) the temporary pivot with the normal values<br>then pointentity normalmesh to temporarypivot<br>then free the temporarypivot <br><br></td></tr></table><br>
<a name="1295801"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> First, a mesh has only vertex normals and not surface normals. Averaging the three vertex normals should usually be a reasonable approximation. But for accuracy is it better to compute the surface normal directly. <br> <br></div><br>Thanks for this! Considering the available commands, this makes a lot more sense now!<br><br>I had just made a little progress using<br><br><pre class=code>AlignToVector NORMALMARKER,nX,nY,nZ,2</pre> but still wasn't entirely accurate.<br><br>I can see from your example now, how to use the TForm commands, at least this is all a great way to learn some of these commands that I have rarely used and are a little intimidating!<br><br><br><div class="quote"> then once you have your picked/collided point position, position this normalmesh at this position.<br>then create a temporary pivot<br>then position the temporary pivot at this position<br>then move or translate (i don't remember) the temporary pivot with the normal values<br>then pointentity normalmesh to temporarypivot<br>then free the temporarypivot  <br></div><br><br>Hey RemiD, thanks for this, I THINK, if I'm following correctly, that that suggestion is in principle what I am actually trying to do, only my difficulty is with aligning the normalmesh (or pivot, whichever is responsible for determining the actual orientation) to match the normal Terrain Mesh's vector's direction<br><br>Anyhow, with my newly acquired knowledge of the TFormNormal command as above, I am on my way again :) Thanks! <br><br></td></tr></table><br>
<a name="1295804"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> The TForm commands aren't really needed here. My original example was mainly to illustrate how to deal with scaled entities.<br><br>Copying your code and doing the cross product I get the following. It is untested, just visualizing how it should work.<br><br><pre class=code>	Local x0#=VertexX(Surf,v0)
	Local x1#=VertexX(Surf,v1)
	Local x2#=VertexX(Surf,v2)
	
	Local y0#=VertexY(Surf,v0)
	Local y1#=VertexY(Surf,v1)
	Local y2#=VertexY(Surf,v2)
	
	Local z0#=VertexZ(Surf,v0)
	Local z1#=VertexZ(Surf,v1)
	Local z2#=VertexZ(Surf,v2)
	
	Local xe1#, ye1#, ze1#  ; edge 1 will point from v0 to v1
	Local xe2#, ye2#, ze2#  ; edge 2 from v0 to v2
	
	xe1 = x1 - x0  :  ye1 = y1 - y0  :  ze1 = z1 - z0
	xe2 = x2 - x0  :  ye2 = y2 - y0  :  ze2 = z2 - z0
	
	; Normal vector will be edge 1 crossed with edge 2. Don't need to scale to length 1.
	; If this goes the opposite way from what is needed then interchange edge 1 and edge 2.
	
	Local nx#, ny#, nz#
	
	nx = ye1 * ze2 - ye2 * ze1
	ny = xe2 * ze1 - xe1 * ze2
	nz = xe1 * ye2 - xe2 * ye1
	
	; Position the marker and then align with 
	
	AlignToVector NORMALMARKER, nx, ny, nz, 2</pre> <br><br></td></tr></table><br>
<a name="1295821"></a>

<a name="1295822"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Flanker</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> A little example with normals functions from simonh in the code archives :<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 1280,720,32,2
SetBuffer BackBuffer()

Global camera = CreateCamera()
CameraRange camera,0.1,1000
PositionEntity camera,0,0,-2

light = CreateLight()
TurnEntity light,70,30,0

mesh = CreateSphere()
EntityPickMode mesh,2

Global triangleMesh = CreateMesh()
surf = CreateSurface(triangleMesh)
AddVertex(surf,0,0,0)
AddVertex(surf,0,0,0)
AddVertex(surf,0,0,0)
AddTriangle(surf,0,1,2)
EntityColor triangleMesh,255,0,0
EntityFX triangleMesh,1
HideEntity triangleMesh

wire = 0

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
While Not KeyHit(1)

	; freelook
	yaw# = -MouseXSpeed()*0.1
	pitch# = MouseYSpeed()*0.1
	TurnEntity camera,pitch,yaw,0
	RotateEntity camera,EntityPitch(camera),EntityYaw(camera),0
	MoveMouse GraphicsWidth()/2,GraphicsHeight()/2
	
	; camera move
	If KeyDown(200) Then MoveEntity camera,0,0,0.01
	If KeyDown(208) Then MoveEntity camera,0,0,-0.01
	If KeyDown(203) Then MoveEntity camera,-0.01,0,0
	If KeyDown(205) Then MoveEntity camera,0.01,0,0
	
	; mousepick
	If MouseHit(1) Then CameraPick(camera,GraphicsWidth()/2,GraphicsHeight()/2)
	
	; wirefrale
	If KeyHit(59) Then wire = Not wire:Wireframe wire

	UpdateWorld()
	RenderWorld()
	
	; draw triangle + 2d normal
	If PickedEntity()
		DrawPickedTriangle()
	Else ; or hide the triangle mesh
		HideEntity triangleMesh
	EndIf
	
	; help
	Color 100,200,255
	Text GraphicsWidth()/2,10,"F1 for wireframe",1
	Text GraphicsWidth()/2,25,"Mouse for freelook",1
	Text GraphicsWidth()/2,40,"Arrows to move camera",1
	Text GraphicsWidth()/2,55,"Mouse left to pick a triangle",1	
	
	Flip

Wend

End

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
Function DrawPickedTriangle()

	v0 = TriangleVertex(PickedSurface(),PickedTriangle(),0)
	v1 = TriangleVertex(PickedSurface(),PickedTriangle(),1)
	v2 = TriangleVertex(PickedSurface(),PickedTriangle(),2)
	
	ShowEntity triangleMesh
	surf = GetSurface(triangleMesh,1)

	VertexCoords(surf,0,VertexX(PickedSurface(),v0),VertexY(PickedSurface(),v0),VertexZ(PickedSurface(),v0))
	VertexCoords(surf,1,VertexX(PickedSurface(),v1),VertexY(PickedSurface(),v1),VertexZ(PickedSurface(),v1))
	VertexCoords(surf,2,VertexX(PickedSurface(),v2),VertexY(PickedSurface(),v2),VertexZ(PickedSurface(),v2))
	
	cx# = (VertexX(PickedSurface(),v0) + VertexX(PickedSurface(),v1) + VertexX(PickedSurface(),v2) ) / 3.0
	cy# = (VertexY(PickedSurface(),v0) + VertexY(PickedSurface(),v1) + VertexY(PickedSurface(),v2) ) / 3.0
	cz# = (VertexZ(PickedSurface(),v0) + VertexZ(PickedSurface(),v1) + VertexZ(PickedSurface(),v2) ) / 3.0
	
	nx# = TriangleNX(PickedSurface(),PickedTriangle())
	ny# = TriangleNY(PickedSurface(),PickedTriangle())
	nz# = TriangleNZ(PickedSurface(),PickedTriangle())

	CameraProject(camera,cx,cy,cz)
	x1 = ProjectedX()
	y1 = ProjectedY()
	CameraProject(camera,cx+nx,cy+ny,cz+nz)
	x2 = ProjectedX()
	y2 = ProjectedY()
		
	Color 0,255,0
	Oval x2-2,y2-2,5,5,1
	Line x1,y1,x2,y2
	
End Function

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; function from simonh in the code archives
Function TriangleNX#(surf,tri_no)

	v0=TriangleVertex(surf,tri_no,0)
	v1=TriangleVertex(surf,tri_no,1)
	v2=TriangleVertex(surf,tri_no,2)
	
	ax#=VertexX#(surf,v1)-VertexX#(surf,v0)
	ay#=VertexY#(surf,v1)-VertexY#(surf,v0)
	az#=VertexZ#(surf,v1)-VertexZ#(surf,v0)
	
	bx#=VertexX#(surf,v2)-VertexX#(surf,v1)
	by#=VertexY#(surf,v2)-VertexY#(surf,v1)
	bz#=VertexZ#(surf,v2)-VertexZ#(surf,v1)
	
	nx#=(ay#*bz#)-(az#*by#)
	
	Return nx#

End Function
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; function from simonh in the code archives
Function TriangleNY#(surf,tri_no)

	v0=TriangleVertex(surf,tri_no,0)
	v1=TriangleVertex(surf,tri_no,1)
	v2=TriangleVertex(surf,tri_no,2)
	
	ax#=VertexX#(surf,v1)-VertexX#(surf,v0)
	ay#=VertexY#(surf,v1)-VertexY#(surf,v0)
	az#=VertexZ#(surf,v1)-VertexZ#(surf,v0)
	
	bx#=VertexX#(surf,v2)-VertexX#(surf,v1)
	by#=VertexY#(surf,v2)-VertexY#(surf,v1)
	bz#=VertexZ#(surf,v2)-VertexZ#(surf,v1)
	
	ny#=(az#*bx#)-(ax#*bz#)
	
	Return ny#

End Function
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; function from simonh in the code archives
Function TriangleNZ#(surf,tri_no)

	v0=TriangleVertex(surf,tri_no,0)
	v1=TriangleVertex(surf,tri_no,1)
	v2=TriangleVertex(surf,tri_no,2)
	
	ax#=VertexX#(surf,v1)-VertexX#(surf,v0)
	ay#=VertexY#(surf,v1)-VertexY#(surf,v0)
	az#=VertexZ#(surf,v1)-VertexZ#(surf,v0)
	
	bx#=VertexX#(surf,v2)-VertexX#(surf,v1)
	by#=VertexY#(surf,v2)-VertexY#(surf,v1)
	bz#=VertexZ#(surf,v2)-VertexZ#(surf,v1)
	
	nz#=(ax#*by#)-(ay#*bx#)
	
	Return nz#

End Function</textarea><br>If you use a mesh to draw the normal instead of a 2d line, you just have to position it in the DrawPickedTriangle() function, at point cx#,cy#,cz#, then align to vector nx#,ny#,nz#. <br><br></td></tr></table><br>
<a name="1296264"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's really great, thanks Flanker! Just what I needed.<br><br>I did manage to get something that seemed to work but the code you've provided is very clean and the TriangleN_ functions are nicely separated out.<br><br>Thanks for providing the info to convert the 2D lines too. <br><br></td></tr></table><br>
<a name="1296279"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think the order that the vertices are defined in the surface can be used to always know how to order the cross product.<br>For each triangle, the vertices are defined in a clockwise order --with the face of the clock pointing in the same direction as the "visible" normal of the triangle.<br><br>- A triangle with vertex one, two and three;<br>- With vector "a" being from vertex one to two, and vector "b" being from vertex two to three;<br>- The cross product should always be ordered ( a x b ) to find the normal that matches the one used for the triangle visibility.<br><br>(Based on the "right-hand rule" from Wikipedia: <a href="https://en.wikipedia.org/wiki/Cross_product#Definition)" target="_blank">https://en.wikipedia.org/wiki/Cross_product#Definition)</a> <br><br></td></tr></table><br>
<a name="1296302"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> These things always manage to confuse me. The problem is that I learned mathematics long before computer graphics, so my brain is hardwired with mathematical conventions. That includes coordinate systems being right handed. But Direct3D is left handed.<br><br>In the early days of Blitz3D I visualized things "backward" so often that I finally gave up and adopted a lazy approach. I know that I need a x b or b x a. I don't even attempt to figure out which it should be. Just try one of them. If that fails then use the other one. <br><br></td></tr></table><br>
<a name="1296364"></a>

<a name="1296488"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Kryzon</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> If i understand what you mean, the cross order should be ( b x a ) then (since it's the opposite of right-hand), under that description in my post.<br><br>It occurred to me that he's got some triangle selection happening.<br>How is he doing that? Because if he's using one of the Pick commands (camera, line etc.), the normal of the picked triangle is already calculated for you, given by PickedNX\NY\NZ. <br><br></td></tr></table><br>
<a name="1296437"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> If i understand what you mean, the cross order should be ( b x a ) then (since it's the opposite of right-hand) <br></div><br>You were probably right the first time.<br><br>Think about the X,Y,Z axes, and imagine three unit vectors in the positive direction. In traditional mathematics those are called i,j,k but I will use the names X,Y,Z. They are mutually perpendicular.<br><br>It is also traditional that they form a right handed system, so X x Y = Z. Start with the fingers of your right hand pointing in the X direction. Curl your fingers toward Y. Your thumb will then point toward Z.<br><br>If you do this with Blitz3D it doesn't work. X points right, Y up and Z into the screen. That's left handed. Using the intuitive physical definition we should now get X x Y = -Z, or equivalently Y x X = Z. <br><br>But if you blindly calculate X x Y using the standard coordinate formulas you still get Z.<br><br>Anyway, I stopped worrying about it. Here is another example. Pitch, Yaw, Roll are rotations about the three axes. Which direction is a positive turn? If you stand at the origin and look down an axis then the traditional convention is that a positive turn ( like +10 degrees ) is counterclockwise. That's for a right handed system.<br><br>What about a left handed system? Well, something has to change. The Direct3D designers chose to flip the direction on one axis. I think it's the Y-axis, i.e. Yaw. But again I don't depend on my memory. I just try it and see. <br><br></td></tr></table><br>
<a name="1296524"></a>

<a name="1296530"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>It occurred to me that he's got some triangle selection happening.<br>How is he doing that? Because if he's using one of the Pick commands (camera, line etc.), the normal of the picked triangle is already calculated for you, given by PickedNX\NY\NZ. <br> <br></div><br><br>I think I began trying to use these, but it wasn't working right for me- I might go back and see if I can do it now I have a better understanding, since it's got to be a better way :)<br><br>Incidentally, under-the-hood, there's no way for B3D (Or directX rather) to obtain the vertex normals WITHOUT 'knowing' the associated triangle plane angles and therefore, the triangle's vectors - so it makes more sense if I can just grab those values rather than reinvent wheels!<br><br>_________________<br><br><br>I think if the triangles were created the reverse direction, they would not be visible (without setting entityFX to diasable Backface culling) from the same side - Flanker's (simonh's) code seems to work for me, so the cross-product order he's using at least suits however I happen to have built my triangles :)<br>_________________<br><br>Thanks again!<br>__________________<br>__________________<br><br>Update, I substituted in PickedX, Y and Z values for the TriangleNX functions from Flanker's code in my actual project, and the results appear identical, so everything's working as intended/expected for now at least, and a whole lot cleaner :D <br><br></td></tr></table><br>
<a name="1296538"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Flanker</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> I didn't even remember the commands PickedNX(), PickedNY(), PickedNZ(), if it suits your need then yes you'd better use them, probably faster and makes the code cleaner.<br><br>BTW, I don't understand the discussion about the cross order. When you call TriangleVertex for example, 0, 1, 2 returns the vertices used for the triangle, in the order the triangle was created. So you don't have to worry about the cross product order. <br><br></td></tr></table><br>
<a name="1296541"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2016)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think the dsicussion was just with regards to which GLOBAL direction (+ or - on the given axis) the triangle normal would be, since it would point from the 'front face' which is reversed if the order of created verts are? <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
