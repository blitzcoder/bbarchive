<!DOCTYPE html><html lang="en" ><head ><title >Problem with Types ... Guru needed please!</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Problem with Types ... Guru needed please!</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Problem with Types ... Guru needed please!</a><br><br>
<a name="538665"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hed</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Aaaaarggghhhhh!<br>Help me please... !<br><br>After I thought I got it with types - I must admit that I *don't*<br><br>This is my Problem:<br><br>I have some characters moving around my gamespace (animMeshes)<br>with bones animating...<br><br>I want to be able to record their movement in space<br>using a simple event system, that stores a characters<br>x,y,z, rotation and some other data on a "stack".<br>Each character has its own stack.<br><br>This is what I would like to have... in a very short version :D<br><br>First I tried types within types like this:<br><br><pre class=code>
type character
   field id$
   ...
   eventstack.evnt
end type
</pre><br><br>Then I figured out, that they obviously dont work...<br>I used a workaround that I found somewhere on the net<br>but that was to complicated/confusing in the end...<br><br>--&gt; So I came to this idea:<br>I simply use an array of types and every character gets<br>one of these arrayentries as their eventlist, when I create it<br>So I can do something like this to loop through all events:<br><br><pre class=code>
for eventlist(myCharacter\eventlist_id).evnt = each evnt
   ...
next
</pre><br><br>And I tried out this little code to see if it is possible at all<br><pre class=code>
Type evnt
	Field a
	Field b
End Type

Dim eventlist.evnt(5)

For i = 1 To 5

	eventlist(i) = New evnt

	eventlist(i)\a = i
	eventlist(i)\b = i * 10	
		
Next

For i = 1 To 5

	Print "Eventlist " + i + ":"
	
	For eventlist.evnt(i) = Each evnt
		Print eventlist(i)\a
		Print eventlist(i)\b
	Next

	Print "---"

Next

WaitKey()
</pre><br><br>The program should output this:<br>(At least I think it should...)<br><br>Eventlist 1:<br>1<br>10<br>---<br>Eventlist 2:<br>2<br>20<br>---<br>Eventlist 3:<br>3<br>30<br>--- etc...<br><br>But it outputs:<br><br>Eventlist 1:<br>1<br>10<br>2<br>20<br>3<br>30<br>4<br>40<br>5<br>50<br>--- etc.<br><br>Am I doing something wrong? If yes, what?<br><br>I have also read this in the Language Reference:<br><br>"You can create variables or arrays of custom types using a '.' type tag followed by the type name. For example: <br><br>Global mine.MyType Dim all_mine.MyType( 100 ) "<br><br>But how do I access this array then...<br><br><br>******* Please help me folks! *******<br>It's probably just a little typo in my code... But I dont<br>get it...<br><br>And sorry, if this has been asked 1000 times before...<br>No search at the moment...<br><br>Nice day, and thanks for your attention<br><br>hed <br><br></td></tr></table><br>
<a name="538666"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Tiger</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>
Type evnt
	Field ID
	Field a
	Field b
End Type

Dim eventlist.evnt(5)

For i = 1 To 5

	eventlist(i) = New evnt

	eventlist(i)\ID=i
	eventlist(i)\a = i
	eventlist(i)\b = i * 10	
		
Next

For i = 1 To 5

	Print "Eventlist " + i + ":"
	
	For eventlist.evnt(i) = Each evnt
		If eventlist(i)\ID=I
			Print eventlist(i)\a
			Print eventlist(i)\b
		EndIf
	Next

	Print "---"

Next

WaitKey()
</pre><br><br>For each always count all the records of the type. <br><br></td></tr></table><br>
<a name="538670"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rhyolite</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yeah, types are confusing!! Your output loop should be like this:<br><br>For i = 1 To 5<br><br>	Print "Eventlist " + i + ":"<br>	<br>		Print eventlist(i)\a<br>		Print eventlist(i)\b<br><br>	Print "---"<br><br>Next<br><br>Rhy :) <br><br></td></tr></table><br>
<a name="538684"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Or, you could use a blitz array in a type collection:<br>BUT, you would be limited. You would have to say how many array slots you want before hand.<br><br><pre class=code>

Type Character
   Field x,y,z
   Field mesh
   Field health
   Field Prev_X_Pos[100]
   Field Prev_Y_Pos[100]
   Field Prev_Z_Pos[100]
End Type
</pre><br><br>But, that way, you could simply loop round once you got to 100, that way, keeping the record smaller, and keep a pointer saying the last record. <br><br></td></tr></table><br>
<a name="538815"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ezbe</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> The types within types must be fields, and have to be created separately. You could also use banks within types to save the handles (Handle()) for the types within types, and get them with Object(). Note that if you use For Each with the types within types, it'll go through every instance of the type, no matter are they in arrays or within other types.<br><br><pre class=code>
Const Max_Events% = 50

Type Event
	Field a%
	Field b%
End Type


Type Character
	Field id$
	;...
	Field Eventstack.Event[Max_Events%]		;'Blitz' array of types
End Type

;Blitz arrays can be used as local arrays in functions
;or as arrays within types. Use a[n] instead of Dim a(n)
;B-arrays have the following limitations:
;-Number of cells must be constant (Here I've used a constant (Const Max_Events), but you can use normal number (Event[50])
;-Just one dimension  (can't do [50, 50])


;Function to create a new Character-instance and the Event-types within it
Function CreateCharacterType.Character()
	Local C.Character	;Local type instance-handle, returned by the function
	Local i%			;Loop-index

	;Create a new instance of Character named C
	C.Character = New Character

	;Create the Event-types within the Character-type
	For i = 0 To Max_Events
		C\EventStack.Event[i] = New Event
	Next

	Return C.Character
End Function



;Test program:

;Create 21 instances of type Character
For i = 0 To 20
	C.Character = CreateCharacterType.Character()		;.Character can be omitted
	;Fill the id
	C\id$ = "Character " + i
	
	;Fill the Event-types for each
	For j = 0 To Max_Events
		C\EventStack[j]\a% = j * i
		C\EventStack[j]\b% = j + (i*i)
	Next
Next

;Print out the arrays
For C.Character = Each Character
	Print "Character ID:" + C\id$
	
	;Go through the event-types
	For i = 0 To Max_Events%
		;Locate 20, 0
		Print "C\EventStack["+i+"]\A% = "+C\EventStack[i]\a% + "  ...\B% = " + C\EventStack[i]\b%
		Delay 5
	Next
Next

WaitKey
</pre> <br><br></td></tr></table><br>
<a name="539120"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hed</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks guys for your replies! And all the ideas!<br><br>But I think it just can't be done in Blitz :(<br>At least not this way...<br><br>@Ezbe: You idea works pretty good - thanks a ton for the<br>work on the code. The only problem is that this wouldnt be<br>a "stack" - what I would need in my app. It's limited<br>by MaxEvents%... And character can have 1 event or 100.000 (worst case - most unlikely... but possible)<br><br>But I had a better idea today: I simply write "event-tracks"<br>to the harddisk and read them in some sort of a cache with<br>a fixed number of events ala Ezbe's idea. Would be also more<br>flexible to me for the app I have in mind.<br><br>But thanks a ton again for your replies!<br><br>hed <br><br></td></tr></table><br>
<a name="539146"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ezbe</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> As I posted earlier, you could use banks to hold the handles to subtypes (types within types), this way you could have any number of event-types within the another type. I'll edit the example code to use banks for the event-type handles.<br><br><br>Edit: Here's the example with banks:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

Type Event
	Field a%
	Field b%
End Type


Type Character
	Field id$

	Field SubTypeBank%			;Bank to hold subtype handles
End Type


;Function to create a new Character-instance and the bank to hold the subtype-handles
Function CreateCharacterType.Character()
	Local C.Character	;Local type instance-handle, returned by the function
	Local i%			;Loop-index

	;Create a new instance of Character named C
	C.Character = New Character

	;Create the bank
	C\SubTypeBank% = CreateBank(0)
	
	Return C.Character
End Function


;Function to add a event-type within the character-type, and store the handle in a bank
Function AddEvent(C.Character, A% = 0, B% = 0)
	
	Local Size%, Index%
	Local Event.Event

	;Check that we got valid character-type
	If C.Character = Null Then
		DebugLog "AddEvent: Character-parameter is null"
		Return -1
	EndIf
	
	;Get the current bank size
	Size% = BankSize(C\SubTypeBank%)
	
	;Get the index
	Index% = (Size% + 4) / 4
	
	;Add one byte to bank size
	ResizeBank C\SubTypeBank, Size% + 4
	
	;Create new event-instance
	Event.Event = New Event
	
	;Set the values for Event-fields
	Event\A% = A%
	Event\B% = B%

	;Save the type-handle to bank
	PokeInt C\SubTypeBank, Size, Handle(Event.Event)

	Return Index% 	;Returns the index of event
End Function


;Get event from within Character-type
Function GetEvent.Event(C.Character, Index%)

	Local Size%, TypeHandle%
	Local Event.Event

	;Check that we got valid character-type
	If C.Character = Null Then
		DebugLog "GetEvent: Character-parameter is null"
		Return Null
	EndIf
	
	
	;Get the current bank size
	Size% = BankSize(C\SubTypeBank%)

	;Change the index to match offset
	Index% = (Index%-1) * 4

	If (Size%-4) &lt; Index% Then
		DebugLog "GetEvent: Index out of range: " + (Index/4 +1)
		Return Null
	EndIf

	TypeHandle% = PeekInt(C\SubTypeBank, Index%)
	

	Event.Event = Object.Event(TypeHandle%)

	Return Event.Event

End Function


;Get number of event-types within characters subtype-bank
Function GetNumberOfEvents(C.Character)

	;Check that we got valid character-type
	If C.Character = Null Then
		DebugLog "GetNumberOfEvents: Character-parameter is null"
		Return 0
	EndIf

	Return (BankSize(C\SubTypeBank) / 4)

End Function



;Function to delete a 4-byte value from bank and move all the values after it one step 'backwards'
;Originally by Noel Cower, I think(?), made some changes
Function EraseObject(Bank, Index%)
	Local Size%, N%, R%
	Local Event.Event

	Size% = BankSize(Bank)

	Index% = Index% - 1

    If Size &gt; Index*4 Then

    	If Index &lt;= Size Then
    	   	R% =  PeekInt(Bank,Index*4)

			;Get the event
			Event.Event = Object.Event(R%)
			;Delete the event
			Delete Event.Event

			;Get rid of the handle in the bank
	       	If (Index * 4) = Size-4 Then
	       		PokeInt(Bank,(Index)*4,0)
		      	ResizeBank(Bank,Size-4)
       		Else
	       		n = Index * 4
		      	While n &lt; Size - 4
			     	PokeInt(Bank,n,PeekInt(Bank,n+4))
    				n = n + 4
	       		Wend
		      	ResizeBank(Bank,Size-4)
    		EndIf
   	    EndIf

        Return R
    Else
        DebugLog("EraseObject: Index out of range: " + Index%)
        Return 0
    EndIf
End Function



;Test program:

;Create 20 instances of type Character
For i = 1 To 20
	C.Character = CreateCharacterType.Character()		;.Character can be omitted
	;Fill the id
	C\id$ = "Character " + i
	
	;Create 10 events to each
	For j = 1 To 10
		AddEvent(C.Character, i*i, i+j)
	Next
Next

;Let's remove event number 5 from the last characters event list
EraseObject(C\SubTypeBank%, 5)

;Now, when we print out the events, the last character has only 9 events, and others have 10
;The event number 5 was removed, so event nr 6 = 5, 7 = 6, 8 = 7 etc. as the events after the deleted
;were rearranged 'one step backwards' within the bank (Thanks to Noel's EraseObject -function)
;You can notice the change in the B%-value after the 4th event


;Print out the arrays
For C.Character = Each Character
	Print "Character ID:" + C\id$

	For i = 1 To GetNumberOfEvents(C.Character)
		Event.Event = GetEvent(C.Character, i%)
		;After you get the Event-type, you can change the fields, pass it to functions, etc.
		;If the index is out of range, GetEvent will return a null-type, in which case the print-line
		;will cause an error message about 'object not existing'
		Print i+". Event\A% = "+Event\a% + "  ...\B% = " + Event\b%
	Next

Next

WaitKey

</textarea><br><br><br>Edit #2: Whoopsie, forgot to make the EraseObject-function delete the erased event-type instance too, fixed now  ;) <br><br></td></tr></table><br>
<a name="539401"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> hed, if you really want to store your events for each character in a stack (LIFO - Last In First Out) then you can do something like the following:<br><br><pre class=code>
Type eventT
	Field a
	Field b
	Field next_event.eventT
End Type

Type characterT
	Field ID
	Field stack.eventT
End Type

; Create a character.
my_char.characterT = New characterT
my_char\ID = 1

; Create an event.
event.eventT = New eventT
event\a = 1
event\b = 2

; Push (add) event to character's stack.
push_event(my_char,event)

; Pop (get) event from top of character's stack.
event.eventT = pop_event(my_char)

If event &lt;&gt; Null
	; Do stuff with the event.
	Print event\a
	Print event\b
	
	; Delete it.
	Delete event
EndIf

;
; Place specified event on top of character's stack.
;
Function push_event(c.characterT, e.eventT)

	e\next_event = c\stack
	c\stack = e
	
End Function

;
; Retrieve event from top of character's stack.
;
Function pop_event.eventT(c.characterT)

	e.eventT = c\stack

	If e &lt;&gt; Null Then c\stack = e\next_event
	
	Return e

End Function
</pre><br><br>[EDIT] oops. had a couple of typos. <br><br></td></tr></table><br>
<a name="541285"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >hed</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Ezbe: Yes, I thought about using banks too...<br>The only thing that makes me some trouble (personally)<br>are the good old peek and poke commands... Not that I have<br>any fear into going verrrry deep into the cpu (ok the memory<br>chips in this case) - but I think thats horror to debug...<br><br>But your code looks really great! Thanks a ton for it...<br><br>@big10p: Not bad the idea of using a pointer to the next<br>event within one event! I think about it...<br>Thanks!<br><br>[cunfusion]Oh man... I had the feeling I fully understand types...<br>Now I have the certain feeling I know nothing about it...<br>[/confusion]<br><br>:D <br><br>nice day @ all,<br>hed <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
