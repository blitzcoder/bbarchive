<!DOCTYPE html><html lang="en" ><head ><title >best way to copy a type to a bank?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >best way to copy a type to a bank?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >best way to copy a type to a bank?</a><br><br>
<a name="547133"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guan</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, is there a speedy way to copy a tpye to a created bank? <br><br></td></tr></table><br>
<a name="547156"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nicstt</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> try this wrote it a few days ago for someone.<br><br>most of the code is for checks etc.<br><br>I've just finishing restoring a backup, so hope its ok:)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; copy and checking data
; copyright Nicholas Tindall 2005
; *******************************

Graphics 800, 600, 16, 2
SeedRnd MilliSecs()
SetBuffer BackBuffer()

;setup something to test
Type badguy
	Field posx
	Field posy
	Field speed#
	Field number
End Type

For a = 1 To 1000
	bad.badguy = New badguy
		bad\posx = Rnd (0, GraphicsWidth()) ; randomly enter some data
		bad\posy = Rnd (0, (GraphicsHeight() / 2) ) + (GraphicsHeight() / 2)
		bad\speed# = Rnd (0, 10) / 100
		bad\number = a
Next

Type newbadguy	; type the data will be transfered too
	Field posx
	Field posy
	Field speed#
	Field number
End Type

;we will need 2 bytes for some of posx and posy values, 4 bytes for the float and 2 bytes for the number * by 1000:
; (2+2+4+2)= 10 * 1000 = 10000

bankname = CreateBank(10000)

offbank = 0 ; offbank is the offset to poke each value to
For bad.badguy = Each badguy
	PokeShort bankname, offbank, bad\posx
	offbank = offbank + 2	; increment offbank by the bytes allowed for it, ie 2,2,4 then 2
	PokeShort bankname, offbank, bad\posy 
	offbank = offbank + 2
	PokeFloat bankname, offbank, bad\speed#
	offbank = offbank + 4
	PokeShort bankname, offbank, bad\number
	offbank = offbank + 2
Next

Global dirPath$ = "" ; change it to a specific directory if you wish

Repeat
	bad.badguy = First badguy
	
	If KeyHit(88) ;F12
		savingdata = WriteFile(dirPath$ + "checktype.txt")
		For bad.badguy = Each badguy
			savedata$ = Str bad\posx +"  "+ Str bad\posy +"  "+ Str bad\speed# +"  "+ Str bad\number
			WriteLine savingdata, savedata$
		Next
		CloseFile(savingdata)
	EndIf

	If KeyHit(67) ;F9 
		savingdata = WriteFile(dirPath$ + "checkbank.txt")
		offbank = 0
		For a = 1 To 1000
			x = PeekShort (bankname, offbank) ;reading a bank is almost the same as writing to one, poke To place Data in a bank - peek To Read that Data
			offbank = offbank + 2	; increment offbank by the bytes allowed for it, ie 2,2,4 then 2
			y = PeekShort (bankname, offbank)
			offbank = offbank + 2
			speed# = PeekFloat (bankname, offbank)
			offbank = offbank + 4
			number = PeekShort (bankname, offbank)
			offbank = offbank + 2
			savedata$ = Str x +"  "+ Str y +"  "+ Str speed# +"  "+ Str number
			WriteLine savingdata, savedata$
		Next
		CloseFile(savingdata)
	EndIf

	If KeyHit(66) ;F8
		savingdata = WriteFile(dirPath$ + "checknewtype.txt")
		For newbad.newbadguy = Each newbadguy
			savedata$ = Str newbad\posx +"  "+ Str newbad\posy +"  "+ Str newbad\speed# +"  "+ Str newbad\number
			WriteLine savingdata, savedata$
		Next
		CloseFile(savingdata)
	EndIf


	If KeyHit(63) ; F5
		offbank = 0 ; offbank is the offset when peeking, should be the same as when poking
		For a = 1 To 1000
			newbad.newbadguy = New newbadguy ; create a new type then read the data into the type variables
			newbad\posx = PeekShort (bankname, offbank) ;reading a bank is almost the same as writing to one, poke To place Data in a bank - peek To Read that Data
			offbank = offbank + 2	; increment offbank by the bytes allowed for it, ie 2,2,4 then 2
			newbad\posy = PeekShort (bankname, offbank)
			offbank = offbank + 2
			newbad\speed# = PeekFloat (bankname, offbank)
			offbank = offbank + 4
			newbad\number = PeekShort (bankname, offbank)
			offbank = offbank + 2
		Next
	EndIf

	If KeyHit(62) ; F4
		bad.badguy = Last badguy ;we want the Last Type item so we know how many there are - placed in testcheck
		testx = 0 : testy = 0 : testspeed = 0 : testnumber = 0 : testcheck = bad\number
		For bad.badguy = Each badguy
			If bad\number = 1
				newbad.newbadguy = First newbadguy
			Else
				newbad = After newbad
			EndIf
			If newbad\posx = bad\posx Then testx = testx + 1
			If newbad\posy = bad\posy Then testy = testy + 1
			If newbad\speed# = bad\speed# Then testspeed = testspeed + 1
			If newbad\number = bad\number Then testnumber = testnumber + 1
		Next
	EndIf

	; i sometimes save data to a file then spot check to see if its correct, can also compare the data 
	; with code - looking at what i get in a file sometimes helps me debug
	Text GraphicsWidth() / 2, 20, "Press F12 to save Type data to a file", True, False
	Text GraphicsWidth() / 2, 40, "Press F9 to save Bank data to a file for checking purposes", True, False
	Text GraphicsWidth() / 2, 60, "Press F8 to save NEW Type data to a file for checking purposes", True,False
	Text GraphicsWidth() / 2, 80, "Press F5 to copy Bank data to the new type 'newbadguy' ", True,False
	Text GraphicsWidth() / 2, 100, "Press F4 to check old and new type info", True,False
	Text GraphicsWidth() / 2, 120, "'Esc' exits", True,False

	If testx &gt; 0 And testx &lt; 1000
		Text GraphicsWidth() / 2, 160, "posx had Errors in the copying!"
	ElseIf testx = 1000
		Text GraphicsWidth() / 2, 160, "posx had No Errors in the copying:)"
	EndIf
	If testy &gt; 0 And testy &lt; 1000
		Text GraphicsWidth() / 2, 170, "posy had Errors in the copying!"
	ElseIf testy = 1000
		Text GraphicsWidth() / 2, 170, "posy had No Errors in the copying:)"
	EndIf
	If testspeed &gt; 0 And speed &lt; 1000
		Text GraphicsWidth() / 2, 180, "speed# had Errors in the copying!"
	ElseIf testspeed = 1000
		Text GraphicsWidth() / 2, 180, "speed# had No Errors in the copying:)"
	EndIf
	If testnumber &gt; 0 And testnumber &lt; 1000
		Text GraphicsWidth() / 2, 190, "number had Errors in the copying!"
	ElseIf testnumber = 1000
		Text GraphicsWidth() / 2, 190, "number had No Errors in the copying:)"
	EndIf

	If testx = 1000 And testy = 1000 And testspeed = 1000 And testnumber = 1000
		Text GraphicsWidth() / 2, 220, "ALL fields copied correctly", True, False
	EndIf

	Flip
Until KeyHit(1)

Delete Each newbadguy
Delete Each badguy
FreeBank bankname		; free bank when finished with it, so it frees up the memory
EndGraphics
</textarea> <br><br></td></tr></table><br>
<a name="547162"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Guan</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, nicoust, but it's a typical way in blitz to copy a type to a bank. Is there a way just like c++ does:<br>memcpy(&amp;bank,&amp;type,sizeof(type))? <br><br></td></tr></table><br>
<a name="547172"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nicstt</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> nope nothing so straightforward.<br><br>Although its not as bad as my code looks, is only a few lines:) <br><br></td></tr></table><br>
<a name="547482"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Storm8191</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Guan: there isn't an easy way to copy type data to a bank.  However, there are ways to store type references to banks or plain integers.  The Object and Handle keywords are probably the least properly explained tools in the Blitz3D language.  Basically you can use Handle() to return an integer for your type instance, i.e. hand% = handle(mybaddie.typeObj).  You can then use the object keyword to regain access to your type instance, i.e. yourbaddie.typeObj = object.typeObj(hand%).  Since each type instance has its own unique number returned from Handle(), you'll always get the same object back.<br><br>Now, there's nothing stopping you from storing each of these integers in a bank, and so then you'll have your type instances listed in your bank.  But don't go deleting the actual type instance, as then you'll loose all the data anyway.  Granted this may not be anythig you're looking for,  but I thought it was worth a mention anyway.<br><br>If you want to store the actual contents of your type instance within a bank, you'll have to do it manually, variable by variable, and write a function to do the reverse operation, and build a type instance.  I think one of the reasons Blitz doesn't do deep copies is that it's hard to say how deep you can go, when one type may reference countless others of major importance. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
