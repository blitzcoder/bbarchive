<!DOCTYPE html><html lang="en" ><head ><title >Collisions (bah!)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Collisions (bah!)</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Collisions (bah!)</a><br><br>
<a name="594338"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Im having real difficulty trying to get EntityBox to be of any use. Whatever values I use, don't seem to represent the effeccts I see in the program.<br><br>Here's what I have anyway (I removed references to Loading texture files so it should just run as-is.)<br><br>Hopefully it should be clear enough whats going on, the EntityBox lines are withing the CreateWall function and at the Initialisation stage where the camera is set.<br><br>Any help offered here will be immensely appreciated! <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Dungeon 

 
Graphics3D 800,600,32,2

SetBuffer BackBuffer()


;*****************************INITIALISE******************************

SeedRnd MilliSecs()

AmbientLight 160,160,160

Global cam=CreateCamera()

EntityType cam,2,0
EntityBox cam,0,0.5,0,0.5,1,0.5


CameraViewport cam,0,0,800,600
CameraRange cam,0.01,10

ScaleEntity cam,0.5,1,0.5

CameraFogMode cam,1
CameraFogColor cam,1,1,1
CameraFogRange cam,-12,8

camlight=CreateLight(2)
EntityParent camlight,cam,1

LightRange camlight,1


Global fpcounter#
Global timer

Global poison=0
Global tempPoison#=0
Global tpadd#=0.01
;****************************BUILD ARRAYS *****************************

Dim vpos#(1,1)
Dim Map(200)

;***************************CREATE QUICK RANDOM MAP**********************


Fl=CreatePlane()
MoveEntity fl,0,-1,0


	For f= 1 To 200

		Map(f)=CreateWall()
			
		ScaleMesh Map(f),1,2,1
		
			If f&gt;1
				AddMesh Map(f),Map(f-1)
			EndIf
		
		
		Morph(Map(f))
						
		PositionEntity Map(f),Rand(-15,15),0,Rand(-15,15)
		
	Next
	
	
;******************************* SET COLLISIONS ******************

Collisions 3,2,3,1
Collisions 2,3,3,1

;*************************** MAIN LOOP ****************************

Global fps#=0

timer=MilliSecs()

While Not KeyDown(1)

PositionEntity cam,Int(EntityX(cam)),0,Int(EntityZ(cam))
RotateEntity cam,0,Int(EntityYaw(cam)),0

;*************************CONTROLS*****************************




If KeyDown(200) Then walkforward(cam)
If KeyDown(208) Then walkbackward(cam)

If KeyDown(203) Then turnleft(cam)
If KeyDown(205) Then turnright(cam)

If KeyDown(25)

			Delay 150
			
			poison = (1-poison)
			
			If poison=1 Then AmbientLight 160,220,160
			
			If poison=0 
				CameraZoom cam,1
				AmbientLight 160,160,160
				tempPoison=0
			EndIf
EndIf
			

;*************************TORCHLIGHT*******************************
If poison=0 Then LightColor camlight,Rand(fpcounter*5),Rand(170,220),100
If poison=1 Then LightColor camlight,0,50,0
		render()











Wend
End

;*******************************************FUNCTIONS************************




Function walkforward(entity)

	If poison=1	Then CameraClsMode cam,0,0

For f= 1 To 5 

MoveEntity entity,0,0.02,0.1

	

render()

Next

For f= 1 To 5 

MoveEntity entity,0,-0.02,0.1

	

render()

Next

	If poison=1	Then CameraClsMode cam,1,1

End Function




Function walkbackward(entity)

	If poison=1	Then CameraClsMode cam,0,0

For f= 1 To 5 

MoveEntity entity,0,0.02,-0.1

	

render()

Next

For f= 1 To 5 

MoveEntity entity,0,-0.02,-0.1

	

render()

Next

	If poison=1	Then CameraClsMode cam,1,1

End Function


Function turnleft(entity)

For f=1 To 20

TurnEntity entity,0,4.5,0

	
render()

Next

End Function


Function turnright(entity)

For f=1 To 20

TurnEntity entity,0,-4.5,0

render()

Next

End Function







Function CreateWall()

;*****************************CREATE SEGMENTED CUBE FROM 6 SIDES **************

	mesh=CreateMesh()
	
	segs=3
	
	For scnt=0 To 3
		surf=CreateSurface( mesh )
		stx#=-.5
		sty#=stx
		stp#=Float(1)/Float(segs)
		y#=sty
		For a=0 To segs
			x#=stx
			v#=a/Float(segs)
			For b=0 To segs
				u#=b/Float(segs)
				AddVertex(surf,x,y,0.5,u,v)
				x=x+stp
			Next
			y=y+stp
		Next
		For a=0 To segs-1
			For b=0 To segs-1
				v0=a*(segs+1)+b:v1=v0+1
				v2=(a+1)*(segs+1)+b+1:v3=v2-1
				AddTriangle( surf,v0,v1,v2 )
				AddTriangle( surf,v0,v2,v3 )
			Next
		Next
		RotateMesh mesh,0,90,0
	Next

	UpdateNormals mesh
	
	EntityType Mesh,3,1
	
	EntityBox mesh,0,0,0,0.3,2,0.3

Return mesh
End Function


;**************************MORPH VERTS*****************************

Function Morph(mesh)

surftotal=CountSurfaces(mesh)

For xx= 1 To surftotal
as1=GetSurface(mesh,xx)

; record the locations of the verts
Dim vpos#(CountVertices(as1)-1,3)
For n=0 To CountVertices(as1)-1
	vpos(n,0)=VertexX(as1,n)
	vpos(n,1)=VertexY(as1,n)
	vpos(n,2)=VertexZ(as1,n)
	vpos(n,3)=0
Next

For n=0 To CountVertices(as1)-1

; change these to make it more or less messy
xm#=Rnd(-.2,.2)
ym#=Rnd(-.0,.0)
zm#=Rnd(-.2,.2)

	For nn=0 To CountVertices(as1)-1
	
	; if the vert has not been monkeyed with monkey away
	If vpos(nn,3)=0
		If vpos(n,0)=vpos(nn,0) And vpos(n,1)=vpos(nn,1) And vpos(n,2)=vpos(nn,2)
			VertexCoords as1,nn,vpos(nn,0)+xm,vpos(nn,1)+ym,vpos(nn,2)+zm
			vpos(nn,3)=1
			EndIf
		EndIf
		
	Next
Next

Next

Return mesh

End Function


Function render()

;*************************MAINTAIN FACING*******************

RotateEntity cam,0,EntityYaw(cam),0

	If poison=1

		tempPoison#=tempPoison#+tpadd#
		If TempPoison#&gt;1 Then tpadd#=(0-0.01)
		If TempPoison#&lt;0.8 Then tpadd#=0.01
			CameraZoom cam,tempPoison#
	EndIf

UpdateWorld
RenderWorld

Text 0,100,"POLYS: "+TrisRendered(),0,1

Text 0,200,"FPS: "+fps#,0,1

If poison=1 Text 0,250,"POISON AFFLICTION APPLIED",0,1

fpcounter#=fpcounter#+1

	If (MilliSecs()-timer)&gt;=1000
		timer=MilliSecs()
		fps#=fpcounter#
		fpcounter#=0
	EndIf

Flip

End Function


; COLLISIONS INDECES

;1 = PLAYER
;2 = CAMERA
;3 = WALL
</textarea> <br><br></td></tr></table><br>
<a name="594352"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Note that the EntityBox command should be used in a similar way to FitMesh ... so for a centered cube of length 1 unit size if would be EntityBox, entity,-.5,-.5,-.5,1,1,1.<br><br>I haven't really looked at your code so I'm not sure if that helps.  Also, I don't think that the entitybox aligns with the entity during rotations ... could be wrong though as I never use them.<br><br>Stevie <br><br></td></tr></table><br>
<a name="594354"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well although all the wall meshes are slightly deformed, tey still fit roughly to blitz-unit coords. This is all I need for collisions, just the blitz units, Each wall segment has an approximate square base, and are not rotatetd anyway, so rotations shouldnt matter.<br><br>I gathered that EntityBox sizes should represent blitz units as the coords, but in practice it doesnt seem so - thats my problem. I wonder if the EntityBox coordinates relate to the centre of me wall segemnts.. this might have something to do with it... <br><br></td></tr></table><br>
<a name="594391"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Of course EntityBox uses blitz units - what else would it use? :)<br><br>To elaborate on what Stevie G showed you, the first 3 params of EntityBox are used to say where a corner (any corner) of the box is, relative to the centre of the mesh. The final 3 params specify where the opposite corner of the box is, relative to the first corner.<br><br>Well, that's how I remember it working, anyway - it's been a while. The docs for EntityBox are sparse and somewhat misleading, it has to be said. <br><br></td></tr></table><br>
<a name="594579"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This bit of code is a bit strange and makes very little sense to me as you arent freeing the added mesh.  If your going to add all the meshes together then you must use the sphere to poly collision as sphere to box will definately not work.  You have unused meshes all over the place?!<br><br><br><pre class=code>
If f&gt;1
				AddMesh Map(f),Map(f-1)
			EndIf
</pre><br><br><br><br>Anyway, I fiddled about with this for a bit but got nowhere as the code is a wee bit messy ;)  I changed and simplified a few things and this works.  Hope it helps.<br><br><br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
;Dungeon 

Graphics3D 800,600,32,2

; COLLISIONS INDECES
Const C_PLAYER = 1
Const C_WALL = 3
Const C_CAMERA = 2

SetBuffer BackBuffer()


;*****************************INITIALISE******************************

SeedRnd MilliSecs()

AmbientLight 160,160,160

Global cam=CreateCamera()
EntityType cam,C_CAMERA
EntityRadius cam,.5

CameraViewport cam,0,0,800,600
CameraRange cam,0.01,10
CameraFogMode cam,1
CameraFogColor cam,1,1,1
CameraFogRange cam,-12,8

camlight=CreateLight(2)
EntityParent camlight,cam,1

LightRange camlight,1


Global fpcounter#
Global timer

Global poison=0
Global tempPoison#=0
Global tpadd#=0.01
;****************************BUILD ARRAYS *****************************

Dim vpos#(1,1)
Dim Map(200)

;***************************CREATE QUICK RANDOM MAP**********************

Fl=CreatePlane()
MoveEntity fl,0,-1,0

For f= 1 To 200
	Map(f) = CreateCube()
	ScaleEntity Map(f), 1, 2, 1
	EntityBox Map(f), -1,0,-1,2,4,2
	EntityType Map(f), C_Wall
	;Map(f)=CreateWall()
	;Morph( Map(f) )
	PositionEntity Map(f),Rand(-15,15)*2,0,Rand(-15,15)*2
Next
	
;******************************* SET COLLISIONS ******************

Collisions C_CAMERA, C_WALL,3,1

;*************************** MAIN LOOP ****************************

Global fps#=0

timer=MilliSecs()
ActionTimer = 0

While Not KeyDown(1)

;PositionEntity cam,Int(EntityX(cam)),0,Int(EntityZ(cam))
;RotateEntity cam,0,Int(EntityYaw(cam)),0

;*************************CONTROLS*****************************

If ActionTimer = 0
	Move# = KeyDown(200) - KeyDown(208 ) 
	Turn# = KeyDown(203) - KeyDown(205 ) 
	If Move &lt;&gt; 0 ActionTimer = 10
	If Turn &lt;&gt; 0 ActionTimer = 20 : Move = 0
Else
	If Move &lt;&gt; 0 
		MoveEntity Cam,0,0, Move * .2
	Else
		TurnEntity Cam , 0, Turn * 4.5 , 0
	EndIf
	ActionTimer = ActionTimer - 1
EndIf

If poison=1 Then CameraClsMode cam,0,0

If KeyDown(25)
	Delay 150
	poison = (1-poison)
	If poison=1 Then AmbientLight 160,220,160
	If poison=0 
		CameraZoom cam,1
		AmbientLight 160,160,160
		tempPoison=0
	EndIf
EndIf
			

;*************************TORCHLIGHT*******************************
If poison=0 Then LightColor camlight,Rand(fpcounter*5),Rand(170,220),100
If poison=1 Then LightColor camlight,0,50,0
		render()


Wend
End

;*******************************************FUNCTIONS************************

Function CreateWall()

;*****************************CREATE SEGMENTED CUBE FROM 6 SIDES **************

	mesh=CreateMesh()
	
	segs=3
	
	For scnt=0 To 3
		surf=CreateSurface( mesh )
		stx#=-.5
		sty#=stx
		stp#=Float(1)/Float(segs)
		y#=sty
		For a=0 To segs
			x#=stx
			v#=a/Float(segs)
			For b=0 To segs
				u#=b/Float(segs)
				AddVertex(surf,x,y,0.5,u,v)
				x=x+stp
			Next
			y=y+stp
		Next
		For a=0 To segs-1
			For b=0 To segs-1
				v0=a*(segs+1)+b:v1=v0+1
				v2=(a+1)*(segs+1)+b+1:v3=v2-1
				AddTriangle( surf,v0,v1,v2 )
				AddTriangle( surf,v0,v2,v3 )
			Next
		Next
		RotateMesh mesh,0,90,0
	Next

	UpdateNormals mesh
	
	EntityType Mesh,C_WALL
	
	EntityBox mesh,-.15,0,-.15,1.5,2,1.5 

Return mesh
End Function


;**************************MORPH VERTS*****************************

Function Morph(mesh)

surftotal=CountSurfaces(mesh)

For xx= 1 To surftotal
as1=GetSurface(mesh,xx)

; record the locations of the verts
Dim vpos#(CountVertices(as1)-1,3)
For n=0 To CountVertices(as1)-1
	vpos(n,0)=VertexX(as1,n)
	vpos(n,1)=VertexY(as1,n)
	vpos(n,2)=VertexZ(as1,n)
	vpos(n,3)=0
Next

For n=0 To CountVertices(as1)-1

; change these to make it more or less messy
xm#=Rnd(-.2,.2)
ym#=Rnd(-.0,.0)
zm#=Rnd(-.2,.2)

	For nn=0 To CountVertices(as1)-1
	
	; if the vert has not been monkeyed with monkey away
	If vpos(nn,3)=0
		If vpos(n,0)=vpos(nn,0) And vpos(n,1)=vpos(nn,1) And vpos(n,2)=vpos(nn,2)
			VertexCoords as1,nn,vpos(nn,0)+xm,vpos(nn,1)+ym,vpos(nn,2)+zm
			vpos(nn,3)=1
			EndIf
		EndIf
		
	Next
Next

Next

Return mesh

End Function


Function render()

;*************************MAINTAIN FACING*******************

RotateEntity cam,0,EntityYaw(cam),0

	If poison=1

		tempPoison#=tempPoison#+tpadd#
		If TempPoison#&gt;1 Then tpadd#=(0-0.01)
		If TempPoison#&lt;0.8 Then tpadd#=0.01
			CameraZoom cam,tempPoison#
	EndIf

UpdateWorld
RenderWorld

Text 0,100,"POLYS: "+TrisRendered(),0,1

Text 0,200,"FPS: "+fps#,0,1

If poison=1 Text 0,250,"POISON AFFLICTION APPLIED",0,1

fpcounter#=fpcounter#+1

	If (MilliSecs()-timer)&gt;=1000
		timer=MilliSecs()
		fps#=fpcounter#
		fpcounter#=0
	EndIf

Flip

End Function

</textarea> <br><br></td></tr></table><br>
<a name="594583"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >octothorpe</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> [codebox] <br><br></td></tr></table><br>
<a name="594591"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oops ... <br><br></td></tr></table><br>
<a name="595049"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hey thanks Stevie, yeah it is messy, because it started out just as quick little something whic developed quite well.. I hope to incorporate it as a game engine soon - <br><br>Well I had to re-do my head-bobbing code (hence the funcs for walking) and add in again the morphing of the walls (whicch I know caused a lot of collision headaches with irregular verts and camera bbbing), but thanks because of using the entityradius and different collision methods really made a difference.<br><br>Ive got it running now with the colisions working fine thanks again.<br><br>Thanks again! <br><br></td></tr></table><br>
<a name="595072"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Damien Sturdy</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> hehe, one thing:<br><br><div class="quote"> <br> wonder if the EntityBox coordinates relate to the centre of me wall segemnts.. this might have something to do with it... <br> <br></div><br><br>They do. <br><br>Glad its fixed :) <br><br></td></tr></table><br>
<a name="595079"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> thanks Cygnus! That makes a lot of sense now ;) <br><br></td></tr></table><br>
<a name="595111"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Damien Sturdy</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> No prob. ANother note is that they DO rotate with the mesh.<br><br>Spherical/eliptical collisions don't. <br><br></td></tr></table><br>
<a name="595161"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> Your welcome mate.  Good luck with your Dungeon Master clone?! <br><br></td></tr></table><br>
<a name="595573"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yes it is! A kinda Dungeon Master/Bloodwych type game (to be!) Hopefully with a few little extra's thrown in!<br><br>Cheers! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
