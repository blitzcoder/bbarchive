<!DOCTYPE html><html lang="en" ><head ><title >isosceles trapezoid circle</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >isosceles trapezoid circle</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >isosceles trapezoid circle</a><br><br>
<a name="1122049"></a>

<a name="1122086"></a>

<a name="1133978"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I got that to work, now i need it trapezoids in cube like form to make a sphere.[&lt;- not doing that any more]<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,32,2
SeedRnd MilliSecs()
SetBuffer BackBuffer()

Global gw = GraphicsWidth()/2
Global gh = GraphicsHeight()/2
Global zoom# = 1

Type trap
	Field ent
	Field x1#,y1#
	Field x2#,y2#
	Field x3#,y3#
	Field x4#,y4#
End Type

Local z1#,z2#,z3#,z4#
Local mindist# = 10.0
Local multi# = 10.0
Local layerstep# = 10
Local Segments = 120.0
Local p = CreatePivot()

For m# = 1 To multi
	For i# = 1 To Segments
		Local a# = i * (360.0/Segments)
		
		Local x1# = Sin(a)*(mindist+m*layerstep)
		Local y1# = Cos(a)*(mindist+m*layerstep)
		
		Local x2# = Sin(a)*(mindist+(m+1)*layerstep)
		Local y2# = Cos(a)*(mindist+(m+1)*layerstep)
		
		a# = (i+1)* (360.0/Segments)
		
		Local x3# = Sin(a)*(mindist+(m+1)*layerstep)
		Local y3# = Cos(a)*(mindist+(m+1)*layerstep)
		
		Local x4# = Sin(a)*(mindist+m*layerstep)
		Local y4# = Cos(a)*(mindist+m*layerstep)
		
		DebugLog x1+"  "+y1+" | "+x2+"  "+y2+" | "+x3+"  "+y3+" | "+x4+"  "+y4
		
		Local t.trap = CreateTrapizoid(x1,y1,z1,x2,y2,z2,x3,y3,z3,x4,y4,z4,p)
		
		EntityColor t\ent,Rand(255),Rand(255),Rand(255)
	Next
Next


cam = CreateCamera()
PositionEntity cam,0,0,-10
AmbientLight 255,255,255

While Not KeyHit(1)
	Cls
	Local ms1 = MilliSecs()
	;TurnEntity p,0,0,1
	RenderWorld()
	;Trapizoid_Draw()
	Locate 0,0
	Print (MilliSecs()-ms1)
	Flip
Wend

Function CreateTrapizoid.trap(x1,y1,z1,x2,y2,z2,x3,y3,z3,x4,y4,z4,parent = 0 )
	Local f.trap = New trap
	f\x1 = x1
	f\y1 = y1
	f\x2 = x2
	f\y2 = y2
	f\x3 = x3
	f\y3 = y3
	f\x4 = x4
	f\y4 = y4
	Local m = CreateMesh(parent)
	Local s = CreateSurface(m)
	Local t = AddVertex(s,x1,y1,z1)
	AddVertex(s,x2,y2,z2)
	AddVertex(s,x3,y3,z3)
	AddVertex(s,x4,y4,z4)
	AddTriangle(s,t,t+2,t+3)
	AddTriangle(s,t,t+1,t+2)
	f\ent = m
	ScaleMesh m,.025,.025,.025
	Return f
End Function


Function Trapizoid_Draw()
	Local f.trap,z# = zoom
	For f = Each trap
		Line gw+(f\x1/z),gh+(f\y1/z),gw+(f\x2/z),gh+(f\y2/z)
		Line gw+(f\x2/z),gh+(f\y2/z),gw+(f\x3/z),gh+(f\y3/z)
		Line gw+(f\x3/z),gh+(f\y3/z),gw+(f\x4/z),gh+(f\y4/z)
		Line gw+(f\x4/z),gh+(f\y4/z),gw+(f\x1/z),gh+(f\y1/z)
	Next
End Function
</textarea><br><br><font class="tiny">Last edited 2012</font><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1133866"></a>

<a name="1133871"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> How can a circular snap to grid integrate with the above code, were each snap is done in real-time, so i do not have to create objects to create the above circle.  <br><br>Also, how do i separate each layer so that each trapezoid is closer to that of a square, meaning it doesn't look like a rectangle at any layer.<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1134008"></a>

<a name="1134009"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> could you please descripe more detailed what you want to do? Perhaps a picture would help?<br><br>what do you mean with "snap"?, Which "grid"? Are you talking about mouse? <br><br>How should look "closer to square"? Same size of all trapezoids in the whole circle? Or same size within the same layer?<br><br>Do you need it for 2D or 3D? What is your target with this code?<br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1134034"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> well i got most of it figured out, all that is left is to adjust the snap outward so that the farther the distance from the center the larger the next snap will be so that the relative size of the bases and sides are as close to the same as possible, so that it doesn't look squished.<br><br>here is what i have so far.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,300,32,2
SetBuffer BackBuffer()

Global GW# = GraphicsWidth()/2
Global GH# = GraphicsHeight()/2

Local px# = 0
Local py# = 0
Global seg# = 10
Global snap# = 25

While Not KeyHit(1)
	Cls
	Color 255,0,0
	;circular snap
	Local Ang# = (Angle2D(px,py,MouseX()-GW,MouseY()-GH))
	Local ang1# = Floor(Ang / (360/seg))*((360/seg))+(360/seg)/2
	
	;distance from center snap
	Local dis# = Distance(px,py,MouseX()-GW,MouseY()-GH) 
	Local dist# = Floor(dis / (snap))* (snap) + (snap) /2
	
	;Line px+GW,py+GH,GW+Sin(ang1#)*dist,GH+Cos(ang1#)*dist 
	Oval GW+Sin(ang1)*dist-3,GH+Cos(ang1)*dist-3,6,6,1
	
	Construct_Trap(ang1#,dist#,px,py)
	
	Flip
Wend

Function Construct_Trap(a#,d#,px#,py#)
	Line px+GW+Sin(a-(360/seg)/2)*(d-snap/2),py+GH+Cos(a-(360/seg)/2)*(d-snap/2),px+GW+Sin(a+(360/seg)/2)*(d-snap/2),py+GH+Cos(a+(360/seg)/2)*(d-snap/2)
	Line px+GW+Sin(a-(360/seg)/2)*(d+snap/2),py+GH+Cos(a-(360/seg)/2)*(d+snap/2),px+GW+Sin(a+(360/seg)/2)*(d+snap/2),py+GH+Cos(a+(360/seg)/2)*(d+snap/2)

End Function

Function Angle2D#( x1#,y1#,x2#,y2# )
	Return ATan2(x2-x1,y2-y1)
End Function


Function Distance#(x#,y#,x2#,y2#)
	Return Sqr((x#-x2#)*(x#-x2#)+(y#-y2#)*(y#-y2#))
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1134138"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> k, my problem is that the dot should stay in the center of the trapezoid.  I guess what i need to know is how to find a triangle from 2 angles and a length, i think. cause when i call construct_trap(angle,distance,cx,cy) i can find the surrounding angles, by adding half the snap value, but the distance isn't correct for each corner of the trapezoid, i think.  any help would be awesome.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,300,32,2
SetBuffer BackBuffer()

Global GW# = GraphicsWidth()/2
Global GH# = GraphicsHeight()/2

Local px# = 50
Local py# = 0
Global seg# = 4
Global snap# = 25

While Not KeyHit(1)
	Cls
	Color 255,0,0
	;circular snap
	Local Ang# = (Angle2D(px,py,MouseX()-GW,MouseY()-GH))
	Local ang1# = Floor(Ang / (360/seg))*((360/seg))+(360/seg)/2
	
	;distance from center snap
	Local dis# = Distance(px,py,MouseX()-GW,MouseY()-GH) 
	Local dist# = (Floor(dis / snap)* snap) + (snap /2)
	
	;Line px+GW,py+GH,GW+Sin(ang1#)*dist,GH+Cos(ang1#)*dist 
	Oval px+GW+(Sin(ang1)*dist-3),py+GH+(Cos(ang1)*dist-3),6,6,1
	
	Construct_Trap(ang1#,dist#,px,py)
	
	Flip
Wend

Function Construct_Trap(a#,d#,px#,py#)
	
	Local x# = px + GW
	Local y# = py + GH
	Local as# = 360.0/seg
	Local ad# = snap/2.0
	Local a_x# = Sin(a-(as/2))
	Local a_y# = Cos(a-(as/2))
	Local ax# = Sin(a+(as/2))
	Local ay# = Cos(a+(as/2))
	Color 0,255,0
	Oval x+Sin(a)*d-3,y+Cos(a)*d-3,6,6,1
	Locate x+Sin(a)*d,y+Cos(a)*d
	Print "x: "+(x+Sin(a)*d)+"   y: "+(y+Cos(a)*d)
	Locate 0,0
	Print "Angle: " + a
	
	Line x+(a_x*(d-ad)),y+(a_y*(d-ad)),x+(ax*(d-ad)),y+(ay*(d-ad))
	Line x+(a_x*(d+ad)),y+(a_y*(d+ad)),x+(ax*(d+ad)),y+(ay*(d+ad))
	Line x+(a_x*(d-ad)),y+(a_y*(d-ad)),x+(a_x*(d+ad)),y+(a_y*(d+ad))
	Line x+(ax*(d-ad)),y+(ay*(d-ad)),x+(ax*(d+ad)),y+(ay*(d+ad))
End Function

Function Angle2D#( x1#,y1#,x2#,y2# )
	Return ATan2(x2-x1,y2-y1)
End Function


Function Distance#(x#,y#,x2#,y2#)
	Return Sqr((x#-x2#)*(x#-x2#)+(y#-y2#)*(y#-y2#))
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1134205"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Floyd</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's is the essence of what I think is the problem; find the appropriate distance for a point x,y and various angles. I worked it out on paper as follows:<br><br><pre class=code>Step 1 ( you have already done this step )

Draw the point x,y and the ray from the center to the point.
Determine and draw the two rays on either side of the point.

Step 2
 
Draw the "mid ray" bisecting these two sides
Draw the perpendicular line from the point to this mid ray.
Determine the distance of the intersection point from the center.

Step 3
Extend the perpendicular line from Step 2 until it hits the sides.
Determine the length of these new side lines.</pre><br><br>The lengths (dMid and dSide) of the newly constructed lines are easily derived from the right triangles of steps 2 and 3.<br><br>Here is a small test program illustrating the procedure. At the end dSide is the length of the two white sides. Use this to construct the trapezoid, rather than the original dPoint.<br><br><pre class=code>
Graphics 800, 600, 0, 2
SetBuffer FrontBuffer()

side1_angle# = 10
side2_angle# = 60
half_angle#  = (side2_angle - side1_angle) / 2
mid_angle# = (side1_angle + side2_angle) / 2

x# = 500
y# = 200

; Step 1

Color 255,255,255
Text 20,500,"Step 1"

Color 0,255,0
Oval x-3,y-3, 6,6
Line 0,0, x,y

Color 80,80,80
Line 0,0, Cos(side1_angle) * 1000, Sin(side1_angle) * 1000
Line 0,0, Cos(side2_angle) * 1000, Sin(side2_angle) * 1000

Delay 5000

; Step 2

Color 255,255,255
Text 20,530,"Step 2"

Color 80,80,0
;Line 0,0, Cos(side1_angle + half_angle) * 1000, Sin(side1_angle + half_angle) * 1000
Line 0,0, Cos(mid_angle) * 1000, Sin(mid_angle) * 1000
dPoint# = Sqr( x*x + y*y )

point_angle# = ATan2( y, x )
dMid# = dPoint * Cos( point_angle - mid_angle )

Color 255,255,0
Line 0,0, Cos(mid_angle) * dMid, Sin(mid_angle) * dMid

Color 0,80,0
Line x, y,    Cos(mid_angle) * dMid, Sin(mid_angle) * dMid

Delay 5000

; Step 3

Color 255,255,255
Text 20,560,"Step 3"

dSide# = dMid / Cos( half_angle )

Color 255,255,255

Line 0,0, Cos(side1_angle) * dSide, Sin(side1_angle) * dSide
Line 0,0, Cos(side2_angle) * dSide, Sin(side2_angle) * dSide

Color 0,80,0

Line Cos(side1_angle) * dSide, Sin(side1_angle) * dSide,  Cos(side2_angle) * dSide, Sin(side2_angle) * dSide 

WaitKey</pre> <br><br></td></tr></table><br>
<a name="1134629"></a>

<a name="1134634"></a>

<a name="1134635"></a>

<a name="1134637"></a>

<a name="1134638"></a>

<a name="1134640"></a>

<a name="1134641"></a>

<a name="1134642"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks Floyd, but I didn't quite understand what you were describing. But I did figure it out using the method to solve triangle type AAS.  which is very simple because each angle is pretty much given by 360/segs and 90, all i needed to do is plug these angle's in as well as the length of a side into the sine law and voila , done.<br><br>Now the second part of my question, has to deal with trapezoid thickness.  I have something that is close to what I desire, but it's not perfect.  I want the snap to expand so that each trapezoid is as close to square as possible, if that makes sense, and to snap at these said gaps.  what i have now snaps correctly but the ratio of base's to the trapezoid height still looks rectangular.  Also there seems to be a correlation between the number of segments the circle is comprised of and this ratio of base's to height.  anyway the code.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,300,32,2
SetBuffer BackBuffer()

Global GW# = GraphicsWidth()/2
Global GH# = GraphicsHeight()/2

Local px# = 50
Local py# = 0
Global seg# = 19*2
Local LStep = 1
Local snap# = 0

While Not KeyHit(1)
	Local ms1 = MilliSecs()
	Cls
	Color 255,0,0
	;circular snap
	Local Ang# = (Angle2D(px,py,MouseX()-GW,MouseY()-GH))
	Local ang1# = Floor(Ang / (360/seg))*((360/seg))+(360/seg)/2
	
	;distance from circle center
	Local dis# = Distance(px,py,MouseX()-GW,MouseY()-GH) 
	
	;were the adjustment of snap length is altered to resize the height of the trapizoid
	St = Int(dis/LStep)
	LStep = Int(LStep * st)-(Int(LStep*st/2))
	snap = Floor(LStep/2)
	Print snap
	
	Local dist# = (Floor(dis / snap)* snap) + (snap /2) ;basic snap formula
	
	;Line px+GW,py+GH,GW+Sin(ang1#)*dist,GH+Cos(ang1#)*dist 
	Oval px+GW+(Sin(ang1)*dist-3),py+GH+(Cos(ang1)*dist-3),6,6,1
	
	Construct_Trap(snap,ang1#,dist#,px,py)
	Locate 0,0
	Print (MilliSecs()-ms1)
	Flip
Wend

Function Construct_Trap(snap#,a#,d#,px#,py#)
	;d = length of side c
	
	Local x# = px + GW
	Local y# = py + GH
	
	Local as# = (360.0/seg)
	
	Local a1# = as/2  ;angle A
	Local b1# = 90    ;angle B
	Local c1# = 180-(a1+b1);angle C
	
	;use the sine law to find the length of side a
	Local l1# = (d *Sin(b1))/Sin(c1)
	
	Local ad# = snap/2.0
	
	Line x+Sin(a-a1)*(l1-ad),y+Cos(a-a1)*(l1-ad),x+Sin(a+a1)*(l1-ad),y+Cos(a+a1)*(l1-ad)
	Line x+Sin(a-a1)*(l1+ad),y+Cos(a-a1)*(l1+ad),x+Sin(a+a1)*(l1+ad),y+Cos(a+a1)*(l1+ad)
	Line x+Sin(a-a1)*(l1-ad),y+Cos(a-a1)*(l1-ad),x+Sin(a-a1)*(l1+ad),y+Cos(a-a1)*(l1+ad)
	Line x+Sin(a+a1)*(l1-ad),y+Cos(a+a1)*(l1-ad),x+Sin(a+a1)*(l1+ad),y+Cos(a+a1)*(l1+ad)
	
End Function

Function Angle2D#( x1#,y1#,x2#,y2# )
	Return 90-ATan2(y2-y1,x2-x1)
End Function


Function Distance#(x#,y#,x2#,y2#)
	Return Sqr((x#-x2#)*(x#-x2#)+(y#-y2#)*(y#-y2#))
End Function


</textarea><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1134852"></a>

<a name="1134853"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> something like this?<br><br><pre class=code>Graphics 800,600
SetBuffer BackBuffer()
Radius#=100
Sectors#=36
MiddleX#=300
MiddleY#=300
Repeat
	Length#=2*Pi*Radius
	Part#=Length/Sectors
	Print length + " " + part
	For i=0 To 360 Step 10
		xIn#=Sin(i)*Radius
		YIn#=Cos(i)*Radius
		xOut#=Sin(i)* (Radius+Part)
		yOut#=Cos(i)* (Radius+Part)
		Line MiddleX+xIn, MiddleY+yIn, MiddleX+xOut, MiddleY+yOut
		xNext#=Sin(i+10)*Radius
		YNext#=Cos(i+10)*Radius
		Line MiddleX+xIn, MiddleY+yIn, MiddleX+xNext, MiddleY+yNext
	Next
	Flip 1
	
	Delay 1000
	Radius= Radius+Part
Until KeyHit(1)

</pre><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1134936"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Midimaster: Well, yes and no...  You have the correct ratio that I want, but I would like for it to work with a changeable number of sectors. ie.. 1600 sectors or more or less. <br><br></td></tr></table><br>
<a name="1134953"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> there are only a few step to complete variable parameters.<br><br>I did this example to ask, whether this is the design you are looking for: <br><br><i>"if distance from the circle center rises, also 'size of the rings' rises, but number of sectors keep the same in all rings"</i><br><br><br>so feel free to enter a number of sectors here:<br><br><pre class=code>Graphics 800,600
SetBuffer BackBuffer()
Radius#=100
Sectors#=90
AnglePerSector#=360.0/Sectors ;new!
MiddleX#=300
MiddleY#=300
Repeat
	Length#=2*Pi*Radius
	Part#=Length/Sectors
	Print length + " " + part
	i=0
	While i&lt;360
		i=i+AnglePerSector ;new
		xIn#=Sin(i)*Radius
		YIn#=Cos(i)*Radius
		xOut#=Sin(i)* (Radius+Part)
		yOut#=Cos(i)* (Radius+Part)
		Line MiddleX+xIn, MiddleY+yIn, MiddleX+xOut, MiddleY+yOut
		xNext#=Sin(i+AnglePerSector)*Radius ;new
		YNext#=Cos(i+AnglePerSector)*Radius ;new
		Line MiddleX+xIn, MiddleY+yIn, MiddleX+xNext, MiddleY+yNext
	Wend
	Flip 1
	
	Delay 300
	Radius= Radius+Part
Until KeyHit(1)
</pre> <br><br></td></tr></table><br>
<a name="1135013"></a>

<a name="1135016"></a>

<a name="1135029"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> @MidiMaster: very good, that helped out a lot! Thank you.<br><br>Thank you both!<br><br>Final code:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,300,32,2
SetBuffer BackBuffer()

Global GW# = GraphicsWidth()/2
Global GH# = GraphicsHeight()/2

Local px# = 50
Local py# = 0
Global seg# = 32
Global segA# = 360/seg

Global MaxLayers = 500
Dim part#(MaxLayers)
Local LStep = 1
Global Radius# = 1

For j = 1 To MaxLayers
	Local length#=2*Pi*Radius
	part(j)=length/seg
	Radius = Radius + part(j)
Next

Radius = 1

While Not KeyHit(1)
	Local ms1 = MilliSecs()
	Cls
	Color 255,0,0
	;circular snap
	Local Ang# = (Angle2D(px,py,MouseX()-GW,MouseY()-GH))
	Local ang1# = Floor((Ang / segA)+.5)*segA
	
	Oval px+GW+(Sin(ang1)*Radius-3),py+GH+(Cos(ang1)*Radius-3),6,6,1
	
	Construct_Trap(part(LStep+1),ang1#,Radius,px,py)
	
	;distance from circle center
	Local dis# = Distance(px,py,MouseX()-GW,MouseY()-GH) 
	If dis &gt;= Radius+ (part(LStep)/2) Then LStep = LStep + 1:Radius = Radius + part(LStep)
	If dis &lt;= Radius- part(LStep)/2 Then Radius = Radius - part(LStep):LStep= LStep - 1
	
	
	Locate 0,0
	Print (MilliSecs()-ms1)
	Flip
Wend

Function Construct_Trap(snap#,a#,d#,px#,py#)
	;d = length of side c
	
	Local x# = px + GW
	Local y# = py + GH
	
	Local as# = segA
	
	Local a1# = as/2  ;angle A
	Local b1# = 90    ;angle B
	Local c1# = 180-(a1+b1);angle C
	
	;use the sine law to find the length of side a
	Local l1# = (d *Sin(b1))/Sin(c1)
	
	Local ad# = snap/2
	
	Line x+Sin(a-a1)*(l1-ad),y+Cos(a-a1)*(l1-ad),x+Sin(a+a1)*(l1-ad),y+Cos(a+a1)*(l1-ad)
	Line x+Sin(a-a1)*(l1+ad),y+Cos(a-a1)*(l1+ad),x+Sin(a+a1)*(l1+ad),y+Cos(a+a1)*(l1+ad)
	Line x+Sin(a-a1)*(l1-ad),y+Cos(a-a1)*(l1-ad),x+Sin(a-a1)*(l1+ad),y+Cos(a-a1)*(l1+ad)
	Line x+Sin(a+a1)*(l1-ad),y+Cos(a+a1)*(l1-ad),x+Sin(a+a1)*(l1+ad),y+Cos(a+a1)*(l1+ad)
	
End Function

Function Angle2D#( x1#,y1#,x2#,y2# )
	Return 90-ATan2(y2-y1,x2-x1)
End Function


Function Distance#(x#,y#,x2#,y2#)
	Return Sqr((x#-x2#)*(x#-x2#)+(y#-y2#)*(y#-y2#))
End Function


</textarea><br><br><font class="tiny">Last edited 2012</font><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1135021"></a>

<a name="1135023"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Midimaster</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> there is a mistake in your code: the rectangle are not 100% squares. Hasn't this been your target?<br><br>I will have a look on the code...<br><br>well here is a sample,that shows the problem more dramatic (because of only 12 segments). And you will see my solution in yellow dots:<br><br><pre class=code>Graphics3D 800,300,32,2
SetBuffer BackBuffer()

Global GW# = GraphicsWidth()/2
Global GH# = GraphicsHeight()/2

Local px# = 50
Local py# = 0
Global seg# = 12

Global segA# = 360/seg

Global MaxLayers = 500
Dim part#(MaxLayers)
Local LStep = 1
Global Radius# = 1

For j = 1 To MaxLayers
	Local length#=2*Pi*Radius
	part(j)=length/seg
	Radius = Radius + part(j)
Next

Radius = 1

While Not KeyHit(1)
	Local ms1 = MilliSecs()
	Cls
	Color 255,0,0
	;circular snap
	Local Ang# = (Angle2D(px,py,MouseX()-GW,MouseY()-GH))
	Local ang1# = Floor((Ang / segA)+.5)*segA
	
;	Oval px+GW+(Sin(ang1)*Radius-3),py+GH+(Cos(ang1)*Radius-3),3,3,1
	
	
	;distance from circle center
	Local dis# = Distance(px,py,MouseX()-GW,MouseY()-GH) 
	
	Radius#=0 
	For lstep=0 To 999
		Radius=Radius+part(lStep)
		If Radius&gt;Dis Then Exit
	Next
	Radius=Radius-part(lstep)/2
	
	DebugLog lstep + " " + dist + " " + radius + " " + ang1
	
	Construct_Trap((part(LStep-1)+part(lStep))/2,ang1#,Radius,px,py)
	Color 255,255,0
	Oval px+GW+(Sin(ang1)*Radius-3),py+GH+(Cos(ang1)*Radius-3),6,6,1
	Radius=Radius-part(lstep)/2
	Oval px+GW+(Sin(ang1-segA/2)*Radius-1),py+GH+(Cos(ang1-segA/2)*Radius-1),3,3,1
	Oval px+GW+(Sin(ang1+segA/2)*Radius-1),py+GH+(Cos(ang1+segA/2)*Radius-1),3,3,1
	Oval px+GW+(Sin(ang1+segA/2)*(Radius+part(lstep))-1),py+GH+(Cos(ang1+segA/2)*(Radius+part(lstep))-1),3,3,1
	Oval px+GW+(Sin(ang1-segA/2)*(Radius+part(lstep))-1),py+GH+(Cos(ang1-segA/2)*(Radius+part(lstep))-1),3,3,1
	
	
	;If dis &gt;= Radius+ (part(LStep)/2) Then LStep = LStep + 1:Radius = Radius + part(LStep)
	;If dis &lt;= Radius- part(LStep)/2 Then Radius = Radius - part(LStep):LStep= LStep - 1
	
	
	Locate 0,0
	Print (MilliSecs()-ms1)
	Flip
Wend

Function Construct_Trap(snap#,a#,d#,px#,py#)
	;d = length of side c
	
	Local x# = px + GW
	Local y# = py + GH
	
	Local as# = segA
	
	Local a1# = as/2  ;angle A
	Local b1# = 90    ;angle B
	Local c1# = 180-(a1+b1);angle C
	
	;use the sine law to find the length of side a
	Local l1# = (d *Sin(b1))/Sin(c1)
	
	Local ad# = snap/2
	
	Line x+Sin(a-a1)*(l1-ad),y+Cos(a-a1)*(l1-ad),x+Sin(a+a1)*(l1-ad),y+Cos(a+a1)*(l1-ad)
	Line x+Sin(a-a1)*(l1+ad),y+Cos(a-a1)*(l1+ad),x+Sin(a+a1)*(l1+ad),y+Cos(a+a1)*(l1+ad)
	Line x+Sin(a-a1)*(l1-ad),y+Cos(a-a1)*(l1-ad),x+Sin(a-a1)*(l1+ad),y+Cos(a-a1)*(l1+ad)
	Line x+Sin(a+a1)*(l1-ad),y+Cos(a+a1)*(l1-ad),x+Sin(a+a1)*(l1+ad),y+Cos(a+a1)*(l1+ad)
	
End Function

Function Angle2D#( x1#,y1#,x2#,y2# )
	Return 90-ATan2(y2-y1,x2-x1)
End Function


Function Distance#(x#,y#,x2#,y2#)
	Return Sqr((x#-x2#)*(x#-x2#)+(y#-y2#)*(y#-y2#))
End Function
</pre><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1135028"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> oh yea, simple enough fix though: <br><br>currently:<br>Construct_Trap(part(LStep),ang1,Radius,px,py)<br><br>fix:<br>Construct_Trap(part(LStep+1),ang1,Radius,px,py) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
