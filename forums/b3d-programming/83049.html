<!DOCTYPE html><html lang="en" ><head ><title >Weird issw with TurnEntity (Possibly Float issue)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Weird issw with TurnEntity (Possibly Float issue)</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Weird issw with TurnEntity (Possibly Float issue)</a><br><br>
<a name="937199"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> The following should be run-able as is. Check out the Yaw values, though, I dunno why I can't seem to fix them to stay to 90 degree increments. :S<br><br><pre class=code>
;Dungeon 

 
Graphics3D 800,600,32,2

SetBuffer BackBuffer()

Global screenbk=CreateImage(GraphicsWidth(),GraphicsHeight())
Global underw_a
;*****************************INITIALISE******************************

SeedRnd MilliSecs()

AmbientLightR=128
AmbientLightG= 128
AmbientLightB= 128

MasterFogRangeNear=-12
MasterFogRangeFar=8

Global cam=CreateCamera()

EntityType cam,2,0

CameraViewport cam,0,0,800,600

ScaleEntity cam,0.5,1,0.5

CameraFogMode cam,1
CameraFogColor cam,AmbientLightR /8,AmbientLightG /8,AmbientLightB /8
CameraFogRange cam,MasterFogRangeNear,MasterFogRangeFar
CameraRange cam,0.01,MasterFogRangeFar+2

ViewFar=MasterFogRangeFar
ViewNear=MasterFogRangeNear

camlight=CreateLight(2)
EntityParent camlight,cam,1

LightRange camlight,4
EntityRadius cam,0.3

Global fpcounter#
Global timer

Global TORCH=0
Global poison=0
Global tempPoison#=0
Global tpadd#=0.01
;****************************BUILD ARRAYS *****************************

Dim vpos#(1,1)
Dim Map(200)

;***************************CREATE QUICK RANDOM MAP**********************


Fl=CreatePlane()
MoveEntity fl,0,-1,0
;ftx=LoadTexture("Floor.bmp")
;EntityTexture Fl,ftx
;FreeTexture ftx

	For f= 1 To 200

		Map(f)=CreateWall()
					
		ScaleMesh Map(f),1,4,1
		
				Morph(Map(f))
						
		PositionEntity Map(f),Rand(-15,15),0,Rand(-15,15)
				
		Next
	
	
;******************************* SET COLLISIONS ******************

Collisions 2,3,3,1

;*************************** MAIN LOOP ****************************

Global fps#=0

timer=MilliSecs()

While Not KeyDown(1)

;*************************CONTROLS*****************************

PositionEntity cam,Int(EntityX(cam)),0,Int(EntityZ(cam)),1
RotateEntity cam,0,Floor(EntityYaw(cam)),0,1



If KeyDown(200) Then walkforward(cam)
If KeyDown(208) Then walkbackward(cam)

If KeyDown(203) Then turnleft(cam)
If KeyDown(205) Then turnright(cam)

If KeyDown(25)

			Delay 150
			
			poison = (1-poison)
			
			If poison=1
				AmbientLightR=AmbientLightR-64
				AmbientLightB=AmbientLightB-64
			End If	
					
			If poison=0 
				CameraZoom cam,1
				AmbientLightR=AmbientLightR+64
				AmbientLightB=AmbientLightB+64
			EndIf

		AmbientLight AmbientLightR,AmbientLightG,AmbientLightB

EndIf


If KeyDown(20) 
			
			Delay 150
			
			TORCH=(Not TORCH)
			
			If TORCH
				AmbientLightR=AmbientLightR+64
				AmbientLightG=AmbientLightG+64
				AmbientLightB=AmbientLightB+32
			
			FogRangeFar=MasterFogRangeFar+2
			CameraRange cam,0.01,FogRangeFar+2
			CameraFogRange cam,FogRangeNear,Rand(FogRangeFar,FogRangeFar+4)

			LightColor camlight,Rand(32)+16+AmbientLightR,Rand(32)+16+AmbientLightG,AmbientLightB+16

			ShowEntity camlight
	
			ViewFar=(ViewFar*2)
	
			EndIf	
			
			If Not TORCH			
				AmbientLightR=AmbientLightR-64
				AmbientLightG=AmbientLightG-64	
				AmbientLightB=AmbientLightB-32
					
				CameraFogRange cam,FogRangeNear,MasterFogRangeFar
				CameraRange cam,0.01,MasterFogRangeFar-2	
					
				HideEntity camlight					
			EndIf
				
		AmbientLight AmbientLightR,AmbientLightG,AmbientLightB
		CameraFogColor cam,AmbientLightR/8,AmbientLightG/8,AmbientLightB/8
	
CameraFogColor cam,AmbientLightR /8,AmbientLightG /8,AmbientLightB /8
CameraFogRange cam,FogRangeNear,FogRangeFar
CameraRange cam,0.01,FogRangeFar+2
	
			
EndIf

If TORCH
EndIf

;*************************TORCHLIGHT*******************************

		render()


Wend
End

;*******************************************FUNCTIONS************************




Function walkforward(entity)

	If poison=1
		CameraClsMode cam,0,0
	End If

For f= 1 To 5 

MoveEntity entity,0,0.05,0.1


render()

Next

For f= 1 To 5 

MoveEntity entity,0,-0.05,0.1


render()

Next

	If poison=1	Then CameraClsMode cam,1,1

End Function




Function walkbackward(entity)

	If poison=1
		CameraClsMode cam,0,0
	End If

For f= 1 To 5 

MoveEntity entity,0,0.1,-0.1


render()

Next

For f= 1 To 5 

MoveEntity entity,0,-0.1,-0.1

render()

Next

	If poison=1
		CameraClsMode cam,1,1
	End If

End Function


Function turnleft(entity)
yw=Int(Floor(EntityYaw(entity)))

For f=0 To 20

TurnEntity entity,0,4.5,0,True
	render()
Next

RotateEntity entity,0,yw+90,0,True
;q=EntityYaw(entity,True) Mod 90
;If q&lt;&gt;0 Then TurnEntity entity,0,-q,0,True

End Function


Function turnright(entity)
yw=Int(Floor(EntityYaw(entity)))
For f=0 To 20
TurnEntity entity,0,-4.5,0,True
	render()
Next

;RotateEntity entity,0,yw-90,0,True

;q=EntityYaw(entity,True) Mod 90
;If q&lt;&gt;0 Then TurnEntity entity,0,q,0,True
End Function







Function CreateWall()

;*****************************CREATE SEGMENTED CUBE FROM 6 SIDES **************

	mesh=CreateMesh()
	
	segs=3
	
	For scnt=0 To 3
		surf=CreateSurface( mesh )
		stx#=-.5
		sty#=stx
		stp#=Float(1)/Float(segs)
		y#=sty
		For a=0 To segs
			x#=stx
			v#=a/Float(segs)
			For b=0 To segs
				u#=b/Float(segs)
				AddVertex(surf,x,y,0.5,u,v)
				x=x+stp
			Next
			y=y+stp
		Next
		For a=0 To segs-1
			For b=0 To segs-1
				v0=a*(segs+1)+b:v1=v0+1
				v2=(a+1)*(segs+1)+b+1:v3=v2-1
				AddTriangle( surf,v0,v1,v2 )
				AddTriangle( surf,v0,v2,v3 )
			Next
		Next
		RotateMesh mesh,0,90,0
	Next

	UpdateNormals mesh
	
	EntityType Mesh,3,1
	
	EntityBox mesh,-0.5,0,-0.5,1,4,1

;wtx=LoadTexture("Wall.bmp")
;EntityTexture Mesh,wtx
;FreeTexture wtx

Return mesh
End Function


;**************************MORPH VERTS*****************************

Function Morph(mesh)

surftotal=CountSurfaces(mesh)

For xx= 1 To surftotal
as1=GetSurface(mesh,xx)

; record the locations of the verts
Dim vpos#(CountVertices(as1)-1,3)
For n=0 To CountVertices(as1)-1
	vpos(n,0)=VertexX(as1,n)
	vpos(n,1)=VertexY(as1,n)
	vpos(n,2)=VertexZ(as1,n)
	vpos(n,3)=0
Next

For n=0 To CountVertices(as1)-1

; change these to make it more or less messy
xm#=Rnd(-.1,.1)
ym#=Rnd(-.1,.1)
zm#=Rnd(-.1,.1)

	For nn=0 To CountVertices(as1)-1
	
	; if the vert has not been monkeyed with monkey away
	If vpos(nn,3)=0
		If vpos(n,0)=vpos(nn,0) And vpos(n,1)=vpos(nn,1) And vpos(n,2)=vpos(nn,2)
			VertexCoords as1,nn,vpos(nn,0)+xm,vpos(nn,1)+ym,vpos(nn,2)+zm
			vpos(nn,3)=1
			EndIf
		EndIf
		
	Next
Next

Next

Return mesh

End Function


Function render()

;*************************MAINTAIN FACING*******************

RotateEntity cam,0,EntityYaw(cam),0,True

	If poison=1
		tempPoison#=tempPoison#+tpadd#
		If TempPoison#&gt;1 Then tpadd#=(0-0.01)
		If TempPoison#&lt;0.8 Then tpadd#=0.01
			CameraZoom cam,tempPoison#
	EndIf

UpdateWorld
RenderWorld

Text 600,50,EntityX(cam,True)+" , "+EntityZ(cam,True),1,1
Text 600,70,EntityYaw(cam,True)

Text 0,100,"POLYS: "+TrisRendered(),0,1

Text 0,200,"FPS: "+fps#,0,1

If poison Text 0,250,"POISON AFFLICTION APPLIED",0,1
If TORCH Text 0,280,"TORCHLIGHT ACTIVE",0,1

fpcounter#=fpcounter#+1

	If (MilliSecs()-timer)&gt;=1000
		timer=MilliSecs()
		fps#=fpcounter#
		fpcounter#=0
	EndIf

If poison=1
	CaptureScreen()
	WobbleView()
End If
	Flip
End Function


; COLLISIONS INDECES

;1 = PLAYER
;2 = CAMERA
;3 = WALL

















Function capturescreen()
 CopyRect 0,0,GraphicsWidth(),GraphicsHeight(),0,0,BackBuffer(),ImageBuffer(screenbk)
End Function

Function WobbleView()
 gw#=GraphicsWidth()
 gh#=GraphicsHeight()
 underw_a=(underw_a+4)
 steph#=gh/32
 mu8#=gh/60
 If underw_a&gt;359 Then underw_a=0
  For iif#=0 To gh-4  Step .001
   wsin#=(Sin((underw_a+iif)Mod 360.0)*mu8#)
   CopyRect 0,  iif,         gw,steph+4, 0,iif+wsin#, ImageBuffer(screenbk),BackBuffer()
   iif=iif+steph
  Next
End Function
</pre> <br><br></td></tr></table><br>
<a name="937205"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Sledge</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Dude, try it this way:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Const DIR_RIGHT% = 1
Const DIR_LEFT% = -1

Type TPlayer
	Field orientationModifier%
	Field geom%
End Type

Function TPlayer_Make.TPlayer(x_arg%, y_arg%, z_arg%)
	newTPlayer.TPlayer = New TPlayer
	newTPlayer\orientationModifier = 0
	newTPlayer\geom = CreateCamera()
	PositionEntity newTPlayer\geom,x_arg, y_arg, z_arg
	Return newTPlayer
End Function

Function TPlayer_IsTurning%(player_arg.TPlayer)
	If player_arg\orientationModifier &lt;&gt; 0 Return True
	Return False
End Function

Function TPlayer_SetTurn(player_arg.TPlayer, direction_arg%)
	player_arg\orientationModifier = 90*direction_arg
End Function


Graphics3D 800,600,0,2
SetBuffer BackBuffer()
light = CreateLight(): PositionEntity light,100,100,-100
currentPlayer.TPlayer = TPlayer_Make(0,0,0)

For i% = 1 To 100
	newScenePiece.TScenePiece = New TScenePiece
	newScenePiece\geom = CreateCube()
	While EntityDistance(currentPlayer\geom, newScenePiece\geom) &lt; 6
		PositionEntity newScenePiece\geom,Rand(-50,50),0,Rand(-50,50)
	Wend
Next

While Not KeyHit(1)
	
	If TPlayer_IsTurning(currentPlayer) = False
		If KeyHit(203)
			TPlayer_SetTurn(currentPlayer, DIR_LEFT)
		ElseIf KeyHit(205)
			TPlayer_SetTurn(currentPlayer, DIR_RIGHT)
		EndIf
	Else
		If currentPlayer\orientationModifier&gt;0
			RotateEntity currentPlayer\geom,0,Int(EntityYaw(currentPlayer\geom))+(DIR_LEFT*5),0
			currentPlayer\orientationModifier = currentPlayer\orientationModifier-5
		Else
			RotateEntity currentPlayer\geom,0,Int(EntityYaw(currentPlayer\geom))+(DIR_RIGHT*5),0
			currentPlayer\orientationModifier = currentPlayer\orientationModifier+5		
		EndIf
	EndIf

	RenderWorld
	Text 10,10,EntityYaw(currentPlayer\geom)
	Flip
Wend



Type TScenePiece
	Field geom%
End Type</textarea><br>All it's doing is giving the player an orientation modifier, which is an order to rotate by so many degrees. Then what happens is that the orientation modifer is chipped away each frame/loop as the entity is rotated, until it gets back to zero and is ready to take another order. You shouldn't have too much trouble integrating it into your own code.<br><br>EDIT: Oh, here's a slightly extended version with movement in. You know how it is when you get tapping away :)<textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Const DIR_RIGHT% = 1
Const DIR_LEFT% = -1
Const DIR_FORWARDS% = 1
Const DIR_BACKWARDS% = -1

Type TPlayer
	Field orientationModifier%
	Field movementModifier%
	Field geom%
End Type

Function TPlayer_Make.TPlayer(x_arg%, y_arg%, z_arg%)
	newTPlayer.TPlayer = New TPlayer
	newTPlayer\orientationModifier = 0
	newTPlayer\movementModifier = 0
	newTPlayer\geom = CreateCamera()
	CameraRange newTPlayer\geom,.1,100
	CameraFogMode newTPlayer\geom,1 
	CameraFogRange newTPlayer\geom,1,50
	PositionEntity newTPlayer\geom,x_arg, y_arg, z_arg
	EntityRadius newTPlayer\geom,0.99
	EntityType newTPlayer\geom,1
	Return newTPlayer
End Function

Function TPlayer_GetMutuallyExclusiveCommand(player_arg.TPlayer)
	If KeyDown(203)
		player_arg\orientationModifier = DIR_LEFT*90
		Return
	ElseIf KeyDown(205)
		player_arg\orientationModifier = DIR_RIGHT*90
		Return
	ElseIf KeyDown(200)
		player_arg\movementModifier = DIR_FORWARDS*10
		Return
	ElseIf KeyDown(208)
		player_arg\movementModifier = DIR_BACKWARDS*10		
	EndIf
End Function

Function TPlayer_IsTurning%(player_arg.TPlayer)
	If player_arg\orientationModifier &lt;&gt; 0 Return True
	Return False
End Function

Function TPlayer_IsMoving%(player_arg.TPlayer)
	If player_arg\movementModifier &lt;&gt; 0 Return True
	Return False
End Function



Graphics3D 800,600,0,2
SetBuffer BackBuffer()
light = CreateLight(): PositionEntity light,100,100,-100
currentPlayer.TPlayer = TPlayer_Make(0,0,0)

For i% = 1 To 100
	newScenePiece.TScenePiece = New TScenePiece
	newScenePiece\geom = CreateCube()
	EntityType newScenePiece\geom,2
	While EntityDistance(currentPlayer\geom, newScenePiece\geom) &lt; 6
		PositionEntity newScenePiece\geom,Rand(-25,25)*2,0,Rand(-25,25)*2
	Wend
Next
Collisions 1,2,2,1

While Not KeyHit(1)
	
	If TPlayer_IsTurning(currentPlayer) = False
		If TPlayer_IsMoving(currentPlayer) = False
			PositionEntity currentPlayer\geom,Int(EntityX(currentPlayer\geom)),0,Int(EntityZ(currentPlayer\geom))
			TPlayer_GetMutuallyExclusiveCommand(currentPlayer)
		Else
			If currentPlayer\movementModifier&gt;0
				MoveEntity currentPlayer\geom,0,0,Float(DIR_FORWARDS)*.2
				currentPlayer\movementModifier = currentPlayer\movementModifier-1
			Else
				MoveEntity currentPlayer\geom,0,0,Float(DIR_BACKWARDS)*.2
				currentPlayer\movementModifier = currentPlayer\movementModifier+1			
			EndIf
		EndIf
	Else
		If currentPlayer\orientationModifier&gt;0
			RotateEntity currentPlayer\geom,0,Int(EntityYaw(currentPlayer\geom))+(DIR_LEFT*5),0
			currentPlayer\orientationModifier = currentPlayer\orientationModifier-5
		Else
			RotateEntity currentPlayer\geom,0,Int(EntityYaw(currentPlayer\geom))+(DIR_RIGHT*5),0
			currentPlayer\orientationModifier = currentPlayer\orientationModifier+5		
		EndIf
	EndIf

	UpdateWorld
	RenderWorld
	Color 90,90,220
	Text 10,10,"Yaw: "+EntityYaw(currentPlayer\geom)
	Text 10,30,"  X: "+EntityX(currentPlayer\geom)
	Text 10,42,"  Y: "+EntityY(currentPlayer\geom)
	Text 10,54,"  Z: "+EntityZ(currentPlayer\geom)
	Color 255,255,255
	Flip
Wend



Type TScenePiece
	Field geom%
End Type</textarea> <br><br></td></tr></table><br>
<a name="937253"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Try using rotateentity instead of turnentity. Personally, i've never seen the need for turnentity. As pointed out above, it carries floating point inaccuracies, which will add up. <br><br></td></tr></table><br>
<a name="937271"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> The above stuff was modified so that I could give an example of the issue.<br>If you look, there's a coupla uses of rotateentity but these too didn't fix the inaccuracy :(<br><br>I'l work with your suggestion and see how I get on :)<br><br>The idea is that movement, though restricted to "steps" is displayed so the movement is a little smoother on-screen and also helps with the "POISON" SCREEN EFFECTS. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
