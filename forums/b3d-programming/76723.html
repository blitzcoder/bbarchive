<!DOCTYPE html><html lang="en" ><head ><title >If/EndIf for IDE autoindent</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >If/EndIf for IDE autoindent</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >If/EndIf for IDE autoindent</a><br><br>
<a name="858036"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Curtastic</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> After using IDEal, and finding it's the best IDE I've ever used, I noticed it can't tell which If's need an endif and which don't. So I made this function. It was hard as HELL!<br><br>This takes a line of blitz3d code that begins with "If" and tells if it needs and EndIf (and thus the next line should be indented.)<br><br>Anyone want to test it? There could be bugs.<br>Does anyone have a better way of doing it or a way of optimizing it?<br>Trixx: wanna put it in IDEal?<br><br><br>Here's a test program.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 800,600

Dim tests$(20)
;should need endif:
tests(1)="If a Then"
tests(2)="If 3And b"
tests(3)="If Not x_"
tests(4)="If a12.go"
tests(5)="If array[get( -14+1 )]&gt; 5 -b"
tests(6)="If First bullet &lt;&gt; Null"
tests(7)="If guy.guy\target\x# + basex=10.5"
tests(8)="If x$="+Chr(34)+"a Then b"+Chr(34)
tests(9)="If Andy&gt;Joe Or(will_not)"
tests(10)="If(jim.person)"
;should not need endif:
tests(11)="If a Thenn"
tests(12)="If a3And b"
tests(13)="If Not2 x_"
tests(14)="If 12.go"
tests(15)="If array[get( -14+1 )]&gt; 5 b"
tests(16)="If Not(car\moving)go(car)"
tests(17)="If(x#&gt;5)x#=5"
tests(18)="If b_52(52, 12) Then x=10"
tests(19)="If x2#=(15 Xor %10) Or y% End"
tests(20)="If RectsOverlap(t2\x-t2\sizex/2,t2\y-t2\sizey/2,t2\sizex,t2\sizey, b\x-gl\tnow\sizex/2,b\y-gl\tnow\sizey/2,gl\tnow\sizex,gl\tnow\sizey) die(1)"

For i=1 To 20
	Write "Test "+i+" "
	If needsendif(tests(i)) Then
		Write "needs EndIf: "
	Else
		Write "is okay    : "
	EndIf
	Print tests(i)
Next

WaitKey
End



;Returns 1 if the string is a If statment that would require and EndIf
;Assumes s$ is a string that starts with "If"
;Assumes no syntax errors.
Function NeedsEndIf(s$)
	
	;How it works:
	;If "a" refers to any letter or number or underscore.
	;The only possible ways for it to not need an endif are:
	;a a
	;)a
	;]a
	;"a
	
	Local lastchar$
	Local char$
	Local space
	Local add
	Local quote
	Local i
	Local lastwordwasnumber
	
	s=Lower(s)
	
	
	For i=3 To Len(s)
		char=Mid(s,i,1)
		
		;Keep track of whether we are in quotes.
		If char=Chr(34) Then
			quote=Not quote
			lastchar=char
			
		ElseIf quote=0 Then
			
			;Ignore optional symbols and decimal points.
			If char&lt;&gt;"%" And char&lt;&gt;"#" And char&lt;&gt;"$" And char&lt;&gt;"." Then
				
				If lastchar&lt;&gt;"" Then
					;Check for a separation of words.
					If space=1 Or lastchar=")" Or lastchar="]" Or lastchar=Chr(34) Or (lastwordwasnumber And isletter(char)) Then
						;Check for starting of a new word.
						If isletter(char) Or isnumber(char) Then
							;Check if it's a "Then"
							If char="t" Then
								If Len(s)&gt;=i+3 Then
									If Mid(s,i,4)="then" Then
										If Len(s)=i+3 Then Return True
										If Mid(s,i+4)=" " Then Return True
									EndIf
								EndIf
							EndIf
							;Check if it's ok to have 2 words in a row as one line.
							add=isoperator(s,i)
							If add=0 Then
								Return False
							Else
								i=i+add
							EndIf
						EndIf
					EndIf
				EndIf
				
				space=0
				If char=" " Then
					space=1
					lastwordwasnumber=0
					
					;Ignore unary operators.
					If isunary(s,i) Then
						lastchar=""
					EndIf
				ElseIf isletter(char) Or isnumber(char) Or char="_" Or char=")" Or char="]" Then
					If isnumber(char) Then
						;for the rare case of "If 4go()" or "If 2And a"
						If lastchar="" Then
							lastwordwasnumber=1
						EndIf
					Else
						lastwordwasnumber=0
					EndIf
					lastchar=char
				Else
					lastchar=""
				EndIf
			EndIf
		EndIf
	Next
	
	Return True
	
End Function


;Checks if there is a text binary operator here
Function IsOperator(s$,i)
	Local add
	
	If Mid(s,i,3)="and" Then
		add=3
	ElseIf Mid(s,i,2)="or" Then
		add=2
	ElseIf Mid(s,i,3)="xor" Then
		add=3
	Else
		Return 0
	EndIf
	
	;Make sure it isnt actually a variable like android.
	If Mid(s,i+add,1)=" " Or Mid(s,i+add,1)="(" Then
		Return add
	Else
		Return 0
	EndIf
End Function

;Checks if there is a text unary operator on the left
Function IsUnary(s$,i)
	Local add
	
	If i&lt;6 Then Return
	
	If Mid(s,i-5,5)="first" Then
		add=5
	ElseIf Mid(s,i-4,4)="last" Then
		add=4
	ElseIf Mid(s,i-3,3)="not" Then
		add=3
	Else
		Return 0
	EndIf
	
	;Make sure it isnt actually a variable like cannot.
	Local m$
	m=Mid(s,i-add-1,1)
	If isletter(m)=0 And isnumber(m)=0 And m&lt;&gt;"_" Then
		Return 1
	Else
		Return 0
	EndIf
End Function

Function IsNumber(char$)
	Return char="0" Or Int(char)&gt;0
End Function

Function IsLetter(char$)
	Return (char&gt;="a" And char&lt;="z")
End Function
</textarea> <br><br></td></tr></table><br>
<a name="858040"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rob Farley</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wrote this a while back, you might be able to use it?<br><br><a href="http://www.blitzbasic.com/codearcs/codearcs.php?code=1229" target="_blank">http://www.blitzbasic.com/codearcs/codearcs.php?code=1229</a> <br><br></td></tr></table><br>
<a name="858067"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Curtastic</td><td align="right"><font class=tiny>(Posted 2008)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually you should be able to use my code to help your indent program.<br><div class="quote"> Times it will NOT work:<br>If you've got if statements without 'then' formed like 'If a=true a=false' <br></div><br>My code solves that problem. That is the only purpose of my code. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
