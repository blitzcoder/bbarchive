<!DOCTYPE html><html lang="en" ><head ><title >Separating a line of cubes?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Separating a line of cubes?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Separating a line of cubes?</a><br><br>
<a name="829718"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JA2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi everyone, I'm in need of a bit of help...<br><br>I have a line of pickable cubes. I want to be able to click any of the cubes and have the others know that the line is broken. For example, say I click cubes 4 and 8, then cubes 1+2+3 will become a separate line, 5+6 will become a separate line, and cubes 9+10 will become a separate line.<br><br>The line doesn't have to do anything, just know that it's now separate. Just changing the colours of each cube in a line to match would be great :)<br><br>Thanks in advance for any help on this. Here's what I have so far:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 640,480,0,2
SetBuffer BackBuffer()
SeedRnd MilliSecs()
HidePointer

Global Camera	= CreateCamera()
Global Light	= CreateLight()
Global Plane	= CreatePlane()
Global PlaneTex	= CreateTexture (64,64)

Color 0,0,100
Rect 0,0,64,64
Color 0,0,150
Rect 0,0,32,32
Rect 32,32,32,32
CopyRect 0,0,64,64,0,0,BackBuffer(),TextureBuffer(PlaneTex)

EntityTexture Plane,PlaneTex

Type Cube

	Field X,Y,Z,Mesh,Mark,Picked,Num

End Type

Local CE.Cube,A

For A = 1 To 10

	CE.Cube = New Cube
	CE\Mesh	= CreateCube()
	CE\Mark	= CreateCone()
	CE\X	= -10+A*2
	CE\Y	= 0
	CE\Z	= 15
	CE\Num	= A
	
	PositionEntity CE\Mesh,CE\X,CE\Y,CE\Z
	ScaleEntity CE\Mesh,0.9,0.9,0.9
	EntityColor CE\Mesh,Rnd(100,200),Rnd(100,200),Rnd(100,200)
	EntityPickMode CE\Mesh,2

	PositionEntity CE\Mark,CE\X,CE\Y+2,CE\Z
	RotateEntity CE\Mark,180,0,0
	ScaleEntity CE\Mark,0.2,0.5,0.2
	EntityColor CE\Mark,0,255,0
	
Next

While Not KeyDown (1)

	If MouseHit (1)

		CameraPick (Camera,MouseX(),MouseY())
		
		If PickedEntity() &lt;&gt; 0
		
			For CE.Cube = Each Cube
			
				If PickedEntity() = CE\Mesh
				
					CE\Picked = True
				
				EndIf
			
			Next
		
		EndIf

	EndIf

	For CE.Cube = Each Cube

		If CE\Picked = True
		
			EntityAlpha CE\Mesh,0.1
			EntityAlpha CE\Mark,0.1
		
		Else
		
			EntityAlpha CE\Mesh,1
			EntityAlpha CE\Mesh,1
		
		EndIf
	
	Next

	UpdateWorld
	RenderWorld
	
	Color 0,0,255
	
	For CE.Cube = Each Cube
	
		CameraProject Camera,CE\X,CE\Y,CE\Z
		
		Text ProjectedX(),ProjectedY(),CE\Num,True,True
	
	Next
	
	Color 255,0,0
	Line MouseX()-10,MouseY(),MouseX()+10,MouseY()
	Line MouseX(),MouseY()-10,MouseX(),MouseY()+10
	
	Flip
	
Wend
End</textarea> <br><br></td></tr></table><br>
<a name="829722"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Vertigo</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> If these are built in a chain, why not have two type fields, Previous_cube_state and Next_Cube_state.  Point the handle's of the other objects to the prev and next fields.  Then do a check for each cube in the chain.  Have it check the previous then the next.  Have a type field for each, Prev_broken and Next_broke true/false.  Checking object x, if object x's next_cube_state's object type field for Broken = true then make the current object's type field broken = true.  If that makes sense.  If an entire chain never returns a broken =true then globally that chain is still intact.<br><br>Hope that helps.<br>Scott <br><br></td></tr></table><br>
<a name="829751"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Like this?<br><br><pre class=code>
Graphics3D 640,480,0,2
SetBuffer BackBuffer()
SeedRnd MilliSecs()
HidePointer

Global Camera	= CreateCamera()
Global Light	= CreateLight()
Global Plane	= CreatePlane()
Global PlaneTex	= CreateTexture (64,64)

Color 0,0,100
Rect 0,0,64,64
Color 0,0,150
Rect 0,0,32,32
Rect 32,32,32,32
CopyRect 0,0,64,64,0,0,BackBuffer(),TextureBuffer(PlaneTex)

EntityTexture Plane,PlaneTex

Type Cube

	Field X,Y,Z,Mesh,Mark,Picked,Num
	Field Active

End Type

Local CE.Cube,A

For A = 1 To 10

	CE.Cube = New Cube
	CE\Mesh	= CreateCube()
	CE\Mark	= CreateCone()
	CE\X	= -10+A*2
	CE\Y	= 0
	CE\Z	= 15
	CE\Num	= A
	CE\Active = True
	
	PositionEntity CE\Mesh,CE\X,CE\Y,CE\Z
	ScaleEntity CE\Mesh,0.9,0.9,0.9
	EntityColor CE\Mesh,Rnd(100,200),Rnd(100,200),Rnd(100,200)
	EntityPickMode CE\Mesh,2

	PositionEntity CE\Mark,CE\X,CE\Y+2,CE\Z
	RotateEntity CE\Mark,180,0,0
	ScaleEntity CE\Mark,0.2,0.5,0.2
	EntityColor CE\Mark,0,255,0
	
Next


While Not KeyDown (1)

	If MouseHit (1)

		CameraPick (Camera,MouseX(),MouseY())
		
		If PickedEntity() &lt;&gt; 0
		
			For CE.Cube = Each Cube
			
				If PickedEntity() = CE\Mesh
				
					CE\Picked = True
					CE\Active = False
				
				EndIf
			
			Next
		
		EndIf

	EndIf

	For CE.Cube = Each Cube

		If CE\Picked = True
		
			EntityAlpha CE\Mesh,0.1
			EntityAlpha CE\Mark,0.1
		
		Else
		
			EntityAlpha CE\Mesh,1
			EntityAlpha CE\Mesh,1
		
		EndIf
	
	Next
	
	UpdateWorld
	RenderWorld
	
	For CE.Cube = Each Cube
	
		Color 0,0,255
	
		CameraProject Camera,CE\X,CE\Y,CE\Z
		Tx = ProjectedX()
		Ty = ProjectedY()
		
		Text Tx, Ty,CE\Num,True,True
		
		
		Color 0,128,0
		;link active
		If CE\Active
			;last
			l.cube = Before CE
			If l &lt;&gt; Null
				If l\Active 
					CameraProject Camera, l\X, l\Y, l\Z
					Line Tx, Ty, ProjectedX(), ProjectedY()
				EndIf
			EndIf
			;next
			n.cube = After CE 
			If n &lt;&gt; Null
				If n\Active 
					CameraProject Camera, n\X, n\Y, n\Z
					Line Tx, Ty, ProjectedX(), ProjectedY()
				EndIf
			EndIf
		EndIf
	
	Next
	
	Color 255,0,0
	Line MouseX()-10,MouseY(),MouseX()+10,MouseY()
	Line MouseX(),MouseY()-10,MouseX(),MouseY()+10
	
	Flip
	
Wend
End
</pre> <br><br></td></tr></table><br>
<a name="829752"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >JA2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Exactly what I wanted. Thanks guys :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
