<!DOCTYPE html><html lang="en" ><head ><title >Pom Pom Using Cylinders As Lines.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Pom Pom Using Cylinders As Lines.</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Pom Pom Using Cylinders As Lines.</a><br><br>
<a name="956732"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Clyde</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hello there Blitz 3D Fans, nice to see you all again, boy it's been a while since posting a code help topic here. I havent forgotten about good old Blitz! :)<br><br>Right, here's my problem the formula used for working out the orientation of the Lines aka Cylinders, does not work properly. try altering any of the X / Y / Z Amounts ( Z must equal 1 in my code, or it'll MAV and crash )<br><br>And I wondered if anybody around here knew the correct way of putting it right.<br><br>If you dont get what Im trying to achieve. The to best describe what Im after is to imagine a Dandilion thats got the white stuff you can blow, to make wishes with. Or as I called it, a Pom Pom. Or think of a plotball with lines projecting out. <br><br>It's supposed to equally spread out the lines so they point in the correct direction.<br><br><pre class=code>
Const XRES=640,YRES=480
Graphics3D XRES,YRES,32
SetBuffer BackBuffer()

Const XAMOUNT=4;8
Const YAMOUNT=4;8
Const ZAMOUNt=1;4

Global Cam=CreateCamera()
Global Light=CreateLight()

PositionEntity Cam,0,0,-5

Const MAX_LINES=(XAMOUNT*YAMOUNT*ZAMOUNT)

Dim Cylinder( MAX_LINES )

Global Control=CreatePivot()

Global SpaceIncX#=Float(360/XAMOUNT)
Global SpaceIncY#=Float(360/YAMOUNT)
Global SpaceIncZ#=Float(360/ZAMOUNT)


SetupPomPom()
Main()
End


Function Main()
	
	Local Angle#
	
	While Not KeyHit(1)
		
		TurnEntity ( Control, 0.5, 0.7, Sin( Angle# )*1.5 )
		
		Angle#=Angle#+0.45
		
		RenderWorld()
		Flip
	
	Wend
	
End Function



Function SetupPomPom()
	
	;
	; Setup Cylinder Lines.
	;

	For Z=0 To ZAMOUNT-1
		For Y=0 To YAMOUNT-1
			For X=0 To XAMOUNT-1
				
				
				Cylinder( Count )=CreateCylinder(16,True,Control)
				EntityColor( Cylinder( Count ),Rand(64,255),Rand(64,255),Rand(64,255) )
				
				ScaleEntity ( Cylinder( Count ),0.04,2,0.04 )
				
				;
				; Set Orientation.
				;
				TurnEntity ( Cylinder( Count ), OrientX#, OrientY#, OrientZ# )
				
				Count=Count+1
				OrientX#=OrientX#+SpaceIncX#

			Next
			OrientY#=OrientY#+SpaceIncY#

		Next
		OrientZ#=OrientZ#+SpaceIncZ#

	Next
	
End Function 
</pre><br><br>Cheers and all the very best to you,<br>Clyde. <br><br></td></tr></table><br>
<a name="956752"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Your using the wrong approach.  Only a Geosphere ( isocohedron ) as a starting object will give you uniform spacing.<br><br>Once you have this, place each cylinder at the vertex and point it back to the center to create the pom pom effect.   For example:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 640,480,16,1

Global CAMERA = CreateCamera()
PositionEntity CAMERA, 0,0,-5

Global CONTROL = CreatePivot()

POMPOMcreate( 2 , CONTROL )

While Not KeyHit(1)

	TurnEntity CONTROL,0.5, 0.7, Sin( Angle# ) * 1.5
	Angle#=Angle#+0.45
	RenderWorld()
	
	Text 0,0,CountChildren( CONTROL )
	
	Flip

Wend

;================================================================================
;================================================================================
;================================================================================

Function POMPOMcreate( Detail , Parent )

	Tmp = GEOcreate( Detail, CONTROL )
	
	TmpCyl = CreateCylinder( 16 , True )
	FitMesh TmpCyl, -.02,-.02,-1,.04,.04,1
	HideEntity TmpCyl
	
	s = GetSurface( Tmp, 1 )
	For v = 0 To CountVertices(s)-1
		Cylinder = CopyEntity( TmpCyl , Parent )
		EntityColor Cylinder,Rand(64,255),Rand(64,255),Rand(64,255)
		PositionEntity Cylinder, VertexX(s,v), VertexY(s,v), VertexZ(s,v)
		PointEntity Cylinder, Tmp
		PositionEntity Cylinder, 0,0,0		
	Next
	
	FreeEntity Tmp
	
End Function

;================================================================================
;================================================================================
;================================================================================

Function GEOcreate( Detail, Parent = 0 )

	Mesh = CreateMesh( Parent )
	S = CreateSurface( Mesh )
	a#=2.0/(1.0+Sqr(5.0))					
	b#=1.0/Sqr((3.0+Sqr(5.0)) / (1.0+Sqr(5.0)))
	v01=AddVertex(S,  0,  a,  b)
	v02=AddVertex(S,  0,  a, -b)
	v03=AddVertex(S,  0, -a,  b)
	v04=AddVertex(S,  0, -a, -b)
	v05=AddVertex(S,  a,  b,  0)
	v06=AddVertex(S,  a, -b,  0)
	v07=AddVertex(S, -a,  b,  0)
	v08=AddVertex(S, -a, -b,  0)
	v09=AddVertex(S,  b,  0,  a)
	v10=AddVertex(S,  b,  0, -a)
	v11=AddVertex(S, -b,  0,  a)
	v12=AddVertex(S, -b,  0, -a)
	GEOsub( S, v02,v05,v07,Detail )
	GEOsub( S, v01,v07,v05,Detail )
	GEOsub( S, v01,v03,v11,Detail )
	GEOsub( S, v01,v09,v03,Detail )
	GEOsub( S, v02,v04,v10,Detail )
	GEOsub( S, v02,v12,v04,Detail )
	GEOsub( S, v03,v06,v08,Detail )
	GEOsub( S, v04,v08,v06,Detail )
	GEOsub( S, v07,v11,v12,Detail )
	GEOsub( S, v08,v12,v11,Detail )
	GEOsub( S, v05,v10,v09,Detail )
	GEOsub( S, v06,v09,v10,Detail )
	GEOsub( S, v01,v11,v07,Detail )
	GEOsub( S, v01,v05,v09,Detail )
	GEOsub( S, v02,v07,v12,Detail )
	GEOsub( S, v02,v10,v05,Detail )
	GEOsub( S, v04,v12,v08,Detail )
	GEOsub( S, v04,v06,v10,Detail )
	GEOsub( S, v03,v08,v11,Detail )
	GEOsub( S, v03,v09,v06,Detail )

	Return mesh

End Function

;================================================================================
;================================================================================
;================================================================================

Function GEOsub( S, v1 , v2 , v3 , Detail )
	
	If Detail &gt; 0 
		nx#=(VertexX(S,v1)+VertexX(S,v2))/2
		ny#=(VertexY(S,v1)+VertexY(S,v2))/2
		nz#=(VertexZ(S,v1)+VertexZ(S,v2))/2
		l#=Sqr(nx*nx+ny*ny+nz*nz)
		n1=AddVertex(S,nx/l,ny/l,nz/l)
		nx#=(VertexX(S,v2)+VertexX(S,v3))/2
		ny#=(VertexY(S,v2)+VertexY(S,v3))/2
		nz#=(VertexZ(S,v2)+VertexZ(S,v3))/2
		l#=Sqr(nx*nx+ny*ny+nz*nz)
		n2=AddVertex(S,nx/l,ny/l,nz/l)
		nx#=(VertexX(S,v3)+VertexX(S,v1))/2
		ny#=(VertexY(S,v3)+VertexY(S,v1))/2
		nz#=(VertexZ(S,v3)+VertexZ(S,v1))/2
		l#=Sqr(nx*nx+ny*ny+nz*nz)
		n3=AddVertex(S,nx/l,ny/l,nz/l)
		GEOsub( S, v1,n3,n1,Detail-1)
		GEOsub( S, v2,n1,n2,Detail-1)
		GEOsub( S, v3,n2,n3,Detail-1)
		GEOsub( S, n3,n2,n1,Detail-1)
	Else
		AddTriangle(S,v1,v2,v3)
	End If

End Function
</textarea><br><br>I'm not sure of the formula to get the number of vertices based on detail but you can see the number created by the child count.<br><br>Stevie <br><br></td></tr></table><br>
<a name="956759"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Clyde</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cheers Stevie :) <br><br></td></tr></table><br>
<a name="956880"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Clyde</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Is there a way to do this without using fit mesh?<br>Plus say, I want a total of just 32 strands.<br><br>Thanks a zillion,<br>Clyde. <br><br></td></tr></table><br>
<a name="956943"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> This is the best I can do.  Note that if you set the detail to 6 you would normally create 36 strands ( 6 x 6 ) , however, at a pitch of 90 degrees you will have 6 strands which overlap each other, 5 of which are never seen.  I have added a condition which removes them so total strands = Detail * Detail - ( Detail - 1 ).<br><br>Just comment out the 'y = 0 or Angle &lt;&gt; 90' + 'endif' to see what I mean.<br><br>p.s. What is this for?<br><br>Stevie<br><br><pre class=code>
Graphics3D 640,480,16,1

Global CAMERA = CreateCamera()
PositionEntity CAMERA, 0,0,-5

Global CONTROL = CreatePivot()
Dim Cylinder(0)

POMPOMcreate( 6 , CONTROL )

While Not KeyHit(1)

	TurnEntity CONTROL,0.5, 0.7, Sin( Angle# ) * 1.5
	Angle#=Angle#+0.45
	RenderWorld()
	
	Text 0,0,CountChildren( CONTROL )
	
	Flip

Wend

;================================================================================
;================================================================================
;================================================================================

Function POMPOMcreate( Detail, Parent )

	No = Detail * Detail - ( Detail - 1 )
	Dim Cylinder( No-1 )

	Yaw = CreatePivot()
	Pitch = CreatePivot( Yaw )
	cnt = 0
	
	For y = 0 To Detail-1

		Angle# = y * ( 180.0 / Float( Detail ) )
		RotateEntity Yaw, 0 , Angle , 0
		
		For p = 0 To Detail-1
		
			Angle# = p * ( 180.0 / Float( Detail ) )
			RotateEntity Pitch, Angle , 0 , 0
			
			If y = 0 Or Angle &lt;&gt; 90
				
				Cylinder( cnt ) = CreateCylinder( 16, True , Parent )
				RotateMesh Cylinder( cnt ), 90,0,0
				ScaleMesh Cylinder( cnt ), .04, .04, 2
				EntityColor Cylinder( cnt ),Rand(64,255),Rand(64,255),Rand(64,255)
				RotateEntity Cylinder( cnt ), EntityPitch( Pitch, 1 ) , EntityYaw( Pitch, 1 ) , 0 , 1
				cnt = cnt + 1
				
			EndIf
				
		Next
	Next	

	FreeEntity Yaw
	
End Function
</pre> <br><br></td></tr></table><br>
<a name="957124"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Clyde</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks again Stevie.<br><br>This is for an effect idea for a new Gravity production.<br><br>If yourself or others reading this are interested in gfx demos, or wonder whats a gfx demo then see the link in my signature. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
