<!DOCTYPE html><html lang="en" ><head ><title >Simulating a hanging chain</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Simulating a hanging chain</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Simulating a hanging chain</a><br><br>
<a name="770456"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PoliteProgrammer</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Okay, I'm trying to simulate a chain hanging under the influence of gravity. The chain is made up of discrete links. As in a real chain, the links cannot stretch, so each link can vary from the next in rotation only. <br><br>I've tried a few different methods to get this working (only in 2d at the moment) but nothing's really worked. Does anyone have a suggested algorithm to get this working? <br><br></td></tr></table><br>
<a name="770468"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IPete2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> James,<br><br>There is a Flash book called Actionscript Animation - Making Things Move! by Keith Peters.<br><br>You'll have to translate to Blitz, from something like this book, or you could invest some time in Vipr's ODE for B3d or BMax, which ever language you are using.<br><br>EasyTok also allow you to build this sort of thing in B3d too.  Otherwise I think there may be some code in the archive to do with such things.<br><br>IPete2. <br><br></td></tr></table><br>
<a name="770535"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> No need for fancy 3rd party physics engines for this kind of thing.<br><br>Here's an old thrust demo by Wiebo which uses verlet integration .. <br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
; quick thrust game, by Wiebo de Wit
; Using verlet integration for movement and stick constraints for the ball and chain =]
; green line is your ship. I'm sorry but i didn't have the strength to include my new
; vector object engine. a and s to rotate, r-shift to thrust

Const TIMESTEP# = 0.05
Const GRAVITYANGLE = 0			; 0 = straight down, 90 = right, etc
Const GRAVITYFORCE# = 10
Const DRAG# = 0.995
Const THRUST# = 40
Const NUM_PARTICLES = 5
Const RESTLENGTH# = 20

Type Particle
	Field x#, y#
	Field old_x#, old_y#
	Field a_x#, a_y#
	Field mass#
	Field nxt.particle
End Type

Type Player
	Field angle#
	Field thrust#
	Field point.particle
End Type

Global player.player, p.particle

Graphics 640,480
SetBuffer BackBuffer()

; create player
player.player = New player

; player is a particle too, but you can control it
player\point = New particle
p.particle = player\point
p\x = 200 : p\y = 200
p\old_x = 200 : p\old_y = 200
p\a_x = 0 : p\a_y = 0

; make ship heavy
p\mass =1

For count = 1 To NUM_PARTICLES

	p\nxt = New particle
	p = p\nxt

	p\x = 200 : p\y = 200
	p\old_x = Rand(200,205) 
	p\old_y = Rand(200,205)
	p\a_x = 0 : p\a_y = 0
	p\mass = 60
Next

; last particle is ball. make heavy
p\mass = 1

; loop
While KeyHit( 1 ) = False

	GetInput()
	ApplyPhysics()
	Verlet()
	ConstrainParticlesMass()
	DrawParticles()
	Flip
	Cls

Wend

; ------------------------------------------------------------

Function GetInput()

	If KeyDown( 203 )
		;rotate
		player\angle = player\angle + 5
	EndIf

	If KeyDown( 205 )
		;rotate
		player\angle = player\angle - 5
	EndIf

	If KeyDown( 200 )
		;thrust, adjust player particle acceleration
		p.particle = player\point
		p\a_x = p\a_x - Sin( player\angle ) * THRUST
		p\a_y = p\a_y - Cos( player\angle ) * THRUST
	EndIf

End Function


Function ApplyPhysics()

	; apply gravity to particles
	For p.particle = Each particle
		p\a_x = p\a_x + Sin( GRAVITYANGLE ) * GRAVITYFORCE / p\mass
		p\a_y = p\a_y + Cos( GRAVITYANGLE ) * GRAVITYFORCE / p\mass
	Next

End Function


Function ConstrainParticlesMass()

	p.particle = player\point

	While p\nxt &lt;&gt; Null
		dx# = p\nxt\x - p\x
		dy# = p\nxt\y - p\y

		deltalength# = Sqr( dx*dx + dy*dy )
		diff# = ( deltalength - RESTLENGTH ) / (deltalength * ( -p\mass + -p\nxt\mass ) )

		p\x = p\x + -p\mass * dx * diff
		p\y = p\y + -p\mass * dy * diff
		p = p\nxt
		p\x = p\x - -p\mass * dx * diff
		p\y = p\y - -p\mass * dy * diff
	Wend

End Function


Function DrawParticles()

	p.particle = player\point

	; draw player, green line

	Color 0,255,0
	dx# = Sin( player\angle) 
	dy# = Cos( player\angle)
	Line p\x-dx*20, p\y-dy*20 , p\x + dy*5, p\y - dx*5
	Line p\x-dx*20, p\y-dy*20 , p\x - dy*5, p\y + dx*5
	Line p\x + dy*5, p\y - dx*5, p\x - dy*5, p\y + dx*5

	; draw line
	Color 255,0,0
	While p\nxt &lt;&gt; Null
		Line p\x, p\y, p\nxt\x, p\nxt\y
		p = p\nxt
	Wend

	; draw ball at end
	Color 255,255,0
	Oval p\x -14, p\y-14, 28,28, 0

	; white dots, for clarity
	Color 255,255,255
	For p.particle =Each particle
		Plot p\x, p\y
	Next

End Function


Function Verlet()

	; eg: move particles, introducing drag

	For p.particle = Each particle

		x# = p\x
		y# = p\y

		tempx# = p\x
		tempy# = p\y

		oldx# = p\old_x
		oldy# = p\old_y

		p\x = p\x + DRAG * x - DRAG * oldx + p\a_x * TIMESTEP * TIMESTEP
		p\y = p\y + DRAG * y - DRAG * oldy + p\a_y * TIMESTEP * TIMESTEP

		p\old_x = tempx
		p\old_y = tempy

		; reset acceleration after moving particle
		p\a_x = 0 : p\a_y = 0
	Next

End Function
</textarea> <br><br></td></tr></table><br>
<a name="770537"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >IPete2</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I wish I understood Verlets Stevie!  I think it would be so useful for all sorts of things.<br><br>IPete2. <br><br></td></tr></table><br>
<a name="770539"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>I think it would be so useful for all sorts of things.<br> <br></div><br><br>It definately is very useful but can consume many months of your life due to some of it's limitations.  <br><br>I found this page a while back which has loads of links and examples ... you should have a butchers.<br><br><a href="http://movil.be/index.php?s=delicious.p&amp;tag=verlet" target="_blank">http://movil.be/index.php?s=delicious.p&amp;tag=verlet</a> <br><br></td></tr></table><br>
<a name="770562"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PoliteProgrammer</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank you Stevie G, that was exactly what I was looking for; I'll learn how that works and modify it to my needs. Thanks! <br><br></td></tr></table><br>
<a name="770647"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PoliteProgrammer</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, I've been trying out the code, and i'm fairly confident on how it's working, but could anyone explain this line:<br><br><br><pre class=code>
diff# = ( deltalength - RESTLENGTH ) / (deltalength * ( -p\mass + -p\nxt\mass ) )
</pre><br><br>What physical quantity does diff# represent?<br><br>I've also attempted to modify the code quite significantly (you'll notice my use of arrays, because I'll need them for something else I want to do). It's not working, would anyone mind pointing out anything they see that's wrong with it?<br><br><pre class=code>

Graphics 640, 480, 16, 0
SetBuffer BackBuffer()
SeedRnd MilliSecs()
TFormFilter False
frameTimer = CreateTimer(60)

timeStep# = 0.016666
g# = 9.81
drag# = 0.995
numLinks = 100
massOfEachLink = 60
restLength# = 20


Type playerType
Field x#, y#, vX#, vY#, aX#, aY#
Field mass#
End Type

player.playerType = New playerType
player\x = 100
player\y = 250
player\mass = 1


Type clawType
Field x#, y#, aX#, aY#
End Type


Type chainLinkType
Field x#, y#, oldX#, oldY#, aX#, aY#, mass#
End Type

Dim chainArray.chainLinkType(numLinks - 1)
For i = 0 To numLinks - 1

  chainArray(i) = New chainLinkType
  chainArray(i)\x = 50 + i * 540 / (numLinks - 1)
  chainArray(i)\y = 50
  chainArray(i)\oldX = chainArray(i)\x
  chainArray(i)\oldY = chainArray(i)\y
  chainArray(i)\mass = massOfEachLink

Next




While Not KeyHit(1)
WaitTimer frameTimer
Cls


  ; Controls



  For i = 0 To numLinks - 1
    ; Apply gravity to particles
    chainArray(i)\aY = chainArray(i)\aY + g / chainArray(i)\mass

    ; Verlet Integration
    tempX# = chainArray(i)\x
    tempY# = chainArray(i)\y

    chainArray(i)\x = chainArray(i)\x * (1 + drag) - drag * chainArray(i)\oldX + chainArray(i)\aX * timeStep * timeStep
    chainArray(i)\y = chainArray(i)\y * (1 + drag) - drag * chainArray(i)\oldY + chainArray(i)\aY * timeStep * timeStep
    chainArray(i)\oldX = tempX
    chainArray(i)\oldY = tempY

    ; Reset acceleration after moving particle
    chainArray(i)\aX = 0 : chainArray(i)\aY = 0
  Next



  ; Make each link experience a force from the surrounding ones
  For i = 0 To numLinks - 2

    temp_dx# = chainArray(i + 1)\x - chainArray(i)\x
    temp_dy# = chainArray(i + 1)\y - chainArray(i)\y
    temp_Separation# = Sqr(temp_dx * temp_dx + temp_dy * temp_dy)
    temp_Extension# = temp_Separation - restLength

    ; Start 
    temp_SpringForce# = temp_Extension / ( temp_Separation * (chainArray(i)\mass + chainArray(i + 1)\mass) )
    chainArray(i)\x = chainArray(i)\x + chainArray(i)\mass * temp_dx * temp_SpringForce
    chainArray(i)\y = chainArray(i)\y + chainArray(i)\mass * temp_dy * temp_SpringForce
    chainArray(i + 1)\x = chainArray(i + 1)\x - chainArray(i + 1)\mass * temp_dx * temp_SpringForce
    chainArray(i + 1)\y = chainArray(i + 1)\y - chainArray(i + 1)\mass * temp_dy * temp_SpringForce



  Next



  chainArray(0)\x = 50
  chainArray(0)\y = 50
  chainArray(numLinks - 1)\x = 590
  chainArray(numLinks - 1)\y = 50





  ; Draw Links
  For i = 0 To numLinks - 1
    Rect chainArray(i)\x, chainArray(i)\y, 1, 1, 1
  Next



Flip
Wend
End

</pre><br><br><br>[Edit] Oops, forgot that I was the one that posted last. <br><br></td></tr></table><br>
<a name="770805"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yo! Wazzup?</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'd try taking out the code and seeing what happens...<br>That's one of the fun parts of Blitz. <br><br></td></tr></table><br>
<a name="770844"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> The diff is a distance measurement which tells you how far from it's restlength a link is.  It uses this to adjust both points proportionate to their inversemass so that a heavier particle isn't moved as far as a lighter one but they still maintain their distance appart.<br><br>Really the 'Mass' field should be 'InverseMass'.  Note if you set this to 0 the particle is immovable automatically.<br><br>I've changed a couple of things.  Your initial particle positions were causing major energy in the system so I fixed this.  I also made the inversemasses lower so that gravity has a bigger effect.   It seems to work ..<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics 640, 480, 16, 0
SetBuffer BackBuffer()
SeedRnd MilliSecs()
TFormFilter False
frameTimer = CreateTimer(60)

timeStep# = 0.05 ;0.016666
g# = 9.81
drag# = 0.995
numLinks = 100
massOfEachLink = 1
restLength# = 4
Iterations = 1



Type playerType
Field x#, y#, vX#, vY#, aX#, aY#
Field mass#
End Type

player.playerType = New playerType
player\x = 100
player\y = 250
player\mass = 1


Type clawType
Field x#, y#, aX#, aY#
End Type


Type chainLinkType
Field x#, y#, oldX#, oldY#, aX#, aY#, mass#
End Type

Dim chainArray.chainLinkType(numLinks - 1)
For i = 0 To numLinks - 1

  chainArray(i) = New chainLinkType
  chainArray(i)\x = 120 + i * 4 ;i * 540 / (numLinks - 1)
  chainArray(i)\y = 100
  chainArray(i)\oldX = chainArray(i)\x
  chainArray(i)\oldY = chainArray(i)\y
  chainArray(i)\mass = massOfEachLink

  ;set end points to have infinite mass
  If i = 0 Or i = ( numlinks-1 )
 	  chainArray(i)\mass = 0 
  EndIf	

Next




While Not KeyHit(1)
WaitTimer frameTimer
Cls


  ; Controls



  For i = 0 To numLinks - 1

    ; Apply gravity to particles which don't have an infinite mass
	If chainArray(i)\Mass &gt; 0   chainArray(i)\aY = chainArray(i)\aY + g / chainArray(i)\mass

    ; Verlet Integration
    tempX# = chainArray(i)\x
    tempY# = chainArray(i)\y

    chainArray(i)\x = chainArray(i)\x * (1.0 + drag) - drag * chainArray(i)\oldX + chainArray(i)\aX * timeStep * timeStep
    chainArray(i)\y = chainArray(i)\y * (1.0 + drag) - drag * chainArray(i)\oldY + chainArray(i)\aY * timeStep * timeStep
    chainArray(i)\oldX = tempX
    chainArray(i)\oldY = tempY

    ; Reset acceleration after moving particle
    chainArray(i)\aX = 0 : chainArray(i)\aY = 0
  Next



  ; Make each link experience a force from the surrounding ones
	For j = 1 To Iterations
	  For i = 0 To numLinks - 2
	
	    temp_dx# = chainArray(i + 1)\x - chainArray(i)\x
	    temp_dy# = chainArray(i + 1)\y - chainArray(i)\y
	    temp_Separation# = Sqr(temp_dx * temp_dx + temp_dy * temp_dy)
	    temp_Extension# = temp_Separation - restLength
	
	    ; Start 
	    temp_SpringForce# = temp_Extension / ( temp_Separation * (chainArray(i)\mass + chainArray(i + 1)\mass) )
	    chainArray(i)\x = chainArray(i)\x + chainArray(i)\mass * temp_dx * temp_SpringForce
	    chainArray(i)\y = chainArray(i)\y + chainArray(i)\mass * temp_dy * temp_SpringForce
	    chainArray(i + 1)\x = chainArray(i + 1)\x - chainArray(i + 1)\mass * temp_dx * temp_SpringForce
	    chainArray(i + 1)\y = chainArray(i + 1)\y - chainArray(i + 1)\mass * temp_dy * temp_SpringForce
	
	  Next
	
	 Next

  ; Draw Links
  For i = 0 To numLinks - 1
    Rect chainArray(i)\x, chainArray(i)\y, 1, 1, 1
  Next



Flip
Wend
End
</textarea><br><br>p.s. Use the TAB button to indent your code .. not spaces!!<br><br>Stevie <br><br></td></tr></table><br>
<a name="770879"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >PoliteProgrammer</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks, that was brilliant - just what I'm looking for. Thanks for explaining some bits too. I'm confident that I know how this is working now, so I should be able to apply it to other things. :) <br><br></td></tr></table><br>
<a name="770882"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >bytecode77</td><td align="right"><font class=tiny>(Posted 2007)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> <img src="http://www.dc.chat-blitz.de/devilengines/Screenshots/DPE_002.jpg"><br>www.devil-engines.net :P <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
