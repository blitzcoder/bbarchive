<!DOCTYPE html><html lang="en" ><head ><title >Alignment problems.</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Alignment problems.</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Alignment problems.</a><br><br>
<a name="511434"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Maximus Primal</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am trying to do a driving game where you race in a walled in track. The track has both hills and long banked corners. The track and walls are seperate 3ds models which are loaded in.<br><br>I have got the player's vehicle to follow the hills properly. But when I go round the banked curve the player comes out of it slightly misaligned (as in not looking straight ahead). The vehicle is driveable as normal but just isn't facing the correct direction.<br><br>Anyone know what might cause this? <br><br>Also I have the the collision for the walls set to 2,1 (polygon,stop) but when the player hits a wall they will either drive straight through it or move over it. Again, any ideas why?<br><br>The code as it stands is below if it helps. A couple of notes though: <br><br>1) I started with Gravity on, but had to disable it as I could not get the correct alignment when using the linepick command with it on for some reason.<br><br>2) The objects are a little large (for ease when modeling them as I can adjust them more accurately) hence the large scale factor.<br><br><br>Any suggestions are really welcomed as this is already proving to be a <b>lot</b> harder than I imagined :(<br><br>Max<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,16,2
Text 320,200,"Please Wait, Loading Data...."

SetBuffer BackBuffer()


; Setup contants and varibles as used in the driver example and modified for what I think I need.

Const GRAVITY#=-5
Const BODY=1,WALL=2,RACETRACK=3,SHIPPIVOT=4

Global dist_above_track# = 10			;	Distance Above Track That Ship Hovers.
Global dist_away_from_wall# = 50
Global bank_angle# = 0
Global max_speed# = -500				;	Maximum Speed The Ship Can Go.
Global speed# = 0						;	Set Speed To 0 At Start.
Global displayspeed# = 0

; Load required Track &amp; Walls and set them up as needed. This includes linking to any variables.

Global track=LoadMesh ("Media/TestTrack3a.3ds")
Global walls=LoadMesh ("Media/TestTrack3b.3ds")
Global scenery=LoadMesh ("Media/TestTrack3c.3ds")


PositionEntity track,0,0,0
PositionEntity walls,0,0,0
PositionEntity scenery,0,0,0
RotateEntity track,0,0,-90
RotateEntity walls,0,0,-90

EntityType track,RACETRACK
EntityType walls,WALL

EntityPickMode track,2
EntityPickMode walls,2

tex=LoadTexture( "Media/tiles.jpg" )
ScaleTexture tex,50,50
EntityTexture track,tex



; Now do the same for the ship (this replaces the car in the driver example).

Global ship=LoadMesh ("Media/fighter.3ds")
PositionEntity ship,0,20,0
EntityShininess ship,1
EntityType ship,BODY

ScaleEntity track,.05,.05,.05
ScaleEntity walls,.05,.05,.05
ScaleEntity scenery,.05,.05,.05
ScaleEntity ship,.05,.05,.05


; Set up the main light for the scene.

light=CreateLight()
PositionEntity (light,0,10,-10)


; Now get the camera up and running...

camera=CreateCamera()
CameraRange camera,1,10000
CameraFogMode camera,1
CameraFogRange camera,100,9000
PositionEntity camera,0,25,70
PointEntity camera,ship


; Setup collisions

Collisions BODY,RACETRACK,2,2
Collisions BODY,WALL,2,1


; Setup other variables

camx#=0				; Camera X Angle.
camy#=0				; Camera Y Angle.
camz#=0				; Camera Z Angle.
pitch#=0			; Camera &amp; Ship Rotation Angle.


; Okay that should be everything up and running. Now it is time to do the main program loop.

While Not KeyHit(1)

	
;move ship
	
If KeyDown(203) 

	camy=camy+3
	TurnEntity ship,0,3,0	

ElseIf KeyDown(205) 

	camy=camy-3
	TurnEntity ship,0,-3,0

EndIf

If KeyDown(200) 
If speed&gt;max_speed

	speed=speed-20
	If speed&lt;max_speed
		speed=max_speed
	EndIf
	
EndIf
	
Else If KeyDown(208)

	speed=speed+20
	If speed&gt;0 
		speed=0
	EndIf

Else
		speed=speed+5
		If speed&gt;0
			speed=0
		EndIf
	
EndIf

sx#=EntityX(ship,True)
sy#=EntityY(ship,True)
sz#=EntityZ(Ship,True)

LinePick sx,sy,sz,0,-20,0

If PickedEntity()&lt;&gt;0
		PositionEntity ship,sx,PickedY()+dist_above_track,sz
		AlignToVector ship,PickedNX(),PickedNY(),PickedNZ(),2,0.5
EndIf

	realspeed=speed/10
	displayspeed=speed
	MoveEntity ship,0,0,realspeed
;	TranslateEntity ship,0,GRAVITY,0

	; Reset camera position and view.
	PositionEntity camera,EntityX(ship),EntityY(ship),EntityZ(ship)
	RotateEntity camera,0,camy,pitch
	MoveEntity camera,0,20,70
	AlignToVector camera,PickedNX(),PickedNY(),PickedNZ(),2,0.5
	PointEntity camera,ship
	
	

	
	UpdateWorld
	RenderWorld
	
	Text 320,500,"Racer V0.01"
	Text 320,520,"Speed:"
	Text 380,520,displayspeed
	Flip

Wend
End
</textarea> <br><br></td></tr></table><br>
<a name="511437"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> It may be that your linepick isn't picking anything up .. seems quite short for such a large scale scene...  Start the pick above the point and make the range longer ...<br><br>LinePick sx, sy-20, 0, 100,0 <br><br></td></tr></table><br>
<a name="511482"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Maximus Primal</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> The linepick does pick up the track as needed. The ship hovers as it is meant to so it's that (well not that I can see anyway).<br><br>Max <br><br></td></tr></table><br>
<a name="511524"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well there isn't very much too your code and that is the only part doing the alignment so it'd be worth playing with.  <br><br>Also note that if no enitty is picked then the camera will still be trying to align to the last picked command incorrectly. <br><br></td></tr></table><br>
<a name="511526"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> I could be the align to vector command is causing the sip to be aligned wrongly. It's kind of hard to tell though. I remember writing that snippet of code for you. Hmmm. It should align the ship to the angle of the triangle it picks... <br><br></td></tr></table><br>
<a name="511532"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Maximus Primal</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Correct it does align to the angle of the track. but on a banked curve it misangles it on the exit (or poss during the curve) I don't know why it does that. I wasn't sure if I had missed something or not...<br><br>Max <br><br></td></tr></table><br>
<a name="511839"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's hard to tell exactly, without seeing it in action... Definetly a wrong value in that statement somewhere. I'll have a fiddle around with it later :o) <br><br></td></tr></table><br>
<a name="511866"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Maximus Primal</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> The media files should be <a href="http://www.primalslair.homeip.net/Media2.zip" target="_blank">HERE</a>. At least I hope they are! :) 1.6mb.<br><br>Max <br><br></td></tr></table><br>
<a name="512737"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, i've had a look and it's actually the camera thats getting knocked out of sync. :o) <br><br></td></tr></table><br>
<a name="512747"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok man. I've fixed your problems and added a few more things in. Added in hovering above the track and some better acceleration for the ship. Added in comments too, to explain what i've done. Any questions, just post em back in here :o)<br><br><pre class=code>
Graphics3D 800,600,16,2
Text 320,200,"Please Wait, Loading Data...."

SetBuffer BackBuffer()


; Setup contants and varibles as used in the driver example and modified for what I think I need.

Const GRAVITY#=-5
Const BODY=1,WALL=2,RACETRACK=3,SHIPPIVOT=4

Global dist_above_track# = 50			;	Distance Above Track That Ship Hovers.
Global hover = 0                    ; hover variable to control the hovering above the track
Global hover_speed = 1              ; speed the craft will hover
Global hover_range = 10             ; hover range of distance
Global dist_away_from_wall# = 50
Global bank_angle# = 0
Global max_speed# = 80				;	Maximum Speed The Ship Can Go.
Global speed# = 0						;	Set Speed To 0 At Start.
Global displayspeed# = 0

; Load required Track &amp; Walls and set them up as needed. This includes linking to any variables.

Global track=LoadMesh ("Media/TestTrack3a.3ds")
Global walls=LoadMesh ("Media/TestTrack3b.3ds")
Global scenery=LoadMesh ("Media/TestTrack3c.3ds")


PositionEntity track,0,0,0
PositionEntity walls,0,0,0
PositionEntity scenery,0,0,0
RotateEntity track,0,0,-90
RotateEntity walls,0,0,-90

EntityType track,RACETRACK
EntityType walls,WALL

EntityPickMode track,2
EntityPickMode walls,2

tex=LoadTexture( "Media/tiles.jpg" )
ScaleTexture tex,50,50
EntityTexture track,tex



; Now do the same for the ship (this replaces the car in the driver example).

Global ship=LoadMesh ("Media/fighter.3ds")
RotateMesh ship,0,180,0 ; ADDED &lt;&lt;&lt;&lt; Rotate the mesh so it's facing the +Z. Makes moving it more
                        ; straightforward. Rotatemesh is perminate, and doesn't reset like rotateentity.
PositionEntity ship,0,20,0
EntityShininess ship,1
EntityType ship,BODY

ScaleEntity track,.05,.05,.05
ScaleEntity walls,.05,.05,.05
ScaleEntity scenery,.05,.05,.05
ScaleEntity ship,.05,.05,.05


; Set up the main light for the scene.

light=CreateLight()
PositionEntity (light,0,10,-10)


; Now get the camera up and running...

Global camera = CreateCamera()
CameraRange camera,1,10000
CameraFogMode camera,1
CameraFogRange camera,100,9000
PositionEntity camera,0,25,70
PointEntity camera,ship

Global cam_piv = CreatePivot() ; create camera pivot
							   ; this camera pivot is where the camera will come to rest when motionless
EntityParent cam_piv, ship ; parent the pivot to the ship
PositionEntity cam_piv,0,325,-470 ; location of the pivot in relation to the ship

Global cam_look_piv = CreatePivot() ;the point the camera will point to.
EntityParent cam_look_piv, ship
PositionEntity cam_look_piv,0,150,200

Global cam_speed = 10 ;the speed the camera will align to the ship at. Lower values = faster alignment

; Setup collisions

Collisions BODY,RACETRACK,2,2
Collisions BODY,WALL,2,1


; Setup other variables

camx#=0				; Camera X Angle.
camy#=0				; Camera Y Angle.
camz#=0				; Camera Z Angle.
pitch#=0			; Camera &amp; Ship Rotation Angle.


; Okay that should be everything up and running. Now it is time to do the main program loop.

While Not KeyHit(1)

	
;move ship
	
If KeyDown(203) 

	camy=camy+1
	TurnEntity ship,0,1,0	

ElseIf KeyDown(205) 

	camy=camy-1
	TurnEntity ship,0,-1,0

EndIf

; These lines i put in below replace the old ones you had. This makes the speeding up and slowing down
; more "real" looking. It does this by using the SIN wave. /-\_/ The closer the speed variable
; gets to 90, the exponetially faster the speed gets. It simulates acceleration quite well.

If KeyDown(200)
	speed = speed + 0.4
	If speed &gt; max_speed Then
		speed = max_speed
	End If
ElseIf KeyDown(208) Then
	speed = speed - 0.4
	If speed &lt; 0 Then
		speed = 0
	End If
Else
	speed = speed - 0.2
	If speed &lt; 0 Then
		speed = 0
	End If
End If



sx#=EntityX(ship,True)
sy#=EntityY(ship,True)
sz#=EntityZ(Ship,True)

LinePick sx,sy,sz,0,-100,0

hover = hover + hover_speed


If PickedEntity()&lt;&gt;0
		AlignToVector ship,PickedNX(),PickedNY(),PickedNZ(),2,0.2
		; the same idea as the speed, using the sin wave to hover the ship. Perfect for this type
		; of thing, as all you do is increase the hover variable by a constant number (hover_range)
		; to get the ship to simulate a hovering motion. No need to reset the variable, as this is
		; not needed.
		PositionEntity ship, sx, PickedY() + dist_above_track + (Sin(hover)*hover_range), sz,True

EndIf

	; As explained above, the movement uses the SIN wave. You basically move the ship using the
	; SIN of the speed variable. Because sin values only range from -1 to 1, you need to multiply
	; the result by a number to obtain faster speeds

	MoveEntity ship,0,0,Sin(speed)*20
	displayspeed = speed


	; perform smooth camera operations
	; these code lines point the camera at the cam_pivot, then moves the camera towards that point,
	; based on the distance remaining between them. It gives a smooth movement because the distance
	; is decreasing as the camera moves, and thus, the speed it moves at decreases the closer it gets
	; to the point. The cam_look_piv, is basically just a pivot the camera points to, instead of pointing
	; at the ship. Gives a beter view of the track. Both pivots are parented to the ship, so they move
	; along with it
	PointEntity camera, cam_piv
	MoveEntity camera,0,0,EntityDistance(cam_piv,camera)/ cam_speed
	PointEntity camera, cam_look_piv

	
	UpdateWorld
	RenderWorld
	
	Text 320,500,"Racer V0.01"
	Text 320,520,"Speed:"
	Text 380,520,displayspeed
	Flip

Wend
End
</pre> <br><br></td></tr></table><br>
<a name="512832"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Maximus Primal</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> Wow! Thank you so much!!!! I didn't expect you to do that. I only expected to be told where to the problem was (and perhaps how to fix it) not to have it fixed for me!!!<br><br>That is VERY kind of you. Thank you again!!!<br><br>Max <br><br></td></tr></table><br>
<a name="512955"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> No worries ^_^ <br><br></td></tr></table><br>
<a name="512966"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Stevie G</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br>Also note that if no enitty is picked then the camera will still be trying to align to the last picked command incorrectly.<br> <br></div> <br><br></td></tr></table><br>
<a name="512990"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry Stevie, never read that part of your post :S <br><br></td></tr></table><br>
<a name="513273"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Maximus Primal</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry, got a small question sort of related to this. On the sine wave that gives the ship a more "floaty" feel. How would I speed it up so it does it twice as fast?<br><br>Max <br><br></td></tr></table><br>
<a name="513330"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Increase the "hover_speed" variable :o) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
