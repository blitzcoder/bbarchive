<!DOCTYPE html><html lang="en" ><head ><title >3d a* pathfinding</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >3d a* pathfinding</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >3d a* pathfinding</a><br><br>
<a name="561681"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skyfire1</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> how do you tackle pathfinding in 3d? should you use types or arrays? <br><br></td></tr></table><br>
<a name="561690"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dreamora</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> if it is really 3D you wouldn't use a* but given pathing graphs.<br><br>if it is a 3D representation of a 2D map then a 2D array. types become extremely slow if you need many of them <br><br></td></tr></table><br>
<a name="561691"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Techlord</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> skyfire1<br><br>You can use both. You can use banks as well. It really does depend on how your implementing it. I used a types to manage priority queues for open/closed waypoint nodes. I've seen others use a Binary Heap Array.<br><br>You may be interested in the Bot System I propose <a href="/posts.php?topic=50039" target="_blank">here</a>. <br><br></td></tr></table><br>
<a name="561700"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >skyfire1</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> dang it! pathfinding is hard! is there any a* pathfinding library that's easy to implement? <br><br></td></tr></table><br>
<a name="561701"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Dreamora</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nope but a waypoint based pathfinding library :-)<br>I think its even in the toolbox. <br><br></td></tr></table><br>
<a name="561777"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Banshee</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> 3D games usually use 2D pathfinding because at the end of the day, tanks dont fly, if you see what i'm getting at.  The point is doing a game in 3D need not complicate your path finding, you can treat it just like 2D.<br><br>You're better off writting the pathfinding yourself so that you understand it enough to make it work well for your game, but there's no harm in doing so by learning from existing code examples/libraries.<br><br>Path finding is one of the harder things to implement in a game which is why I try to solve it relatively early in the programs development cycle.  Better to grin and bear it early than to put it off and never face it.<br><br>I usually make my path finding functions in a seperate program using the games actual data but shown onscreen as a 2D map - it helps me tweak the algorythm.<br><br>This is a modified A* routine which is quite good for searching routes out of a large heightmap based terrain.  You can make extra "blocked" areas for trees &amp; houses simply by painting the colour red(255).<br><br>You'll need to change the heightmap file loaded before this sample will run - I last used this for my Paradise Island project so it's trying to load the heightmap from that...<br><br><pre class=code>
Graphics 512,512,0,2
AppTitle("A*")
Dim mapData(512,512)

Global treeMap=LoadImage("C:\BeckysGames\Paradise Island\Gfx\Terrain\map2.png")
SetBuffer ImageBuffer(treeMap)
For x=0 To 511
	For z=0 To 511
		GetColor(x,z)
		mapData(x,511-z)=ColorRed()
		If mapData(x,511-z)=255 Then mapData(x,511-z)=999
	Next
Next
SetBuffer BackBuffer()

Type openList
	Field x,z
	Field parentX,parentZ
	Field fSum
	Field gCost
	Field hGuess
	Field mapHeight
End Type

Type closedList
	Field x,z
	Field parentX,parentZ
End Type

Type AIpath
	Field x,z
End Type

sx=256
sz=256
tx=256
tz=256

SeedRnd MilliSecs()
Repeat
	strt=MilliSecs()
	unit=Rnd(1,5)
	route(sx,sz,tx,tz,unit)
	time=MilliSecs()-strt

	Cls
	DrawImage treeMap,0,0

	Color 255,255,0
	Oval sx,511-sz,3,3,0
	Color 0,255,0
	Oval tx,511-tz,5,5,0

	Color 0,0,255
	For n=1 To 5
		id=0
		For p.AIpath=Each AIpath
			id=id+1
			If id&gt;1
				o.AIpath=Before p
				Line p\x,512-p\z, o\x,512-o\z
			EndIf
		Next
	Next
	Color 255,255,0

	Text 10,10,time+"ms for "+id+" steps"
	Flip 0
	
	Repeat
		If MouseDown(1)
			sX=MouseX()
			sZ=512-MouseY()
		EndIf
		If MouseDown(2)
			tX=MouseX()
			tZ=512-MouseY()
		EndIf
	Until KeyDown(17)
	;Stop

	For clearPath.AIpath=Each AIpath
		Delete clearPath
	Next
Forever

Function route(sx,sz,tx,tz,id)

	node.openList=New openList
	node\x=sx : node\z=sz
	node\fSum=0 : node\gCost=0
	node\hGuess=0
	node\mapHeight=mapData(node\x,node\z)

	maxNodeRange=(Abs(sx-tx)+Abs(sz-tz))*1.1
	searchTime=MilliSecs()+333
	
	Repeat
		lowestScore=99999999
		nodes=0
		For checkNode.openList=Each openList
			nodes=nodes+1
			If checkNode\fSum&lt;lowestScore
				lowestScore=checkNode\fSum
				node.openList=checkNode.openList
			EndIf
		Next

		;DebugLog(node\x+"x"+node\z+" = "+node\gCost+" ("+tX+"x"+tZ+") ["+nodes+"]")
	
		;we now have the node to search
		If lowestScore=99999999; Or MilliSecs()&gt;searchTime
			buildPath=2
		Else
			If Abs(node\x-tx)&lt;=3 And Abs(node\z-tz)&lt;=3
				buildPath=1
			Else
				For bearing=0 To 359 Step 40
					rX#=node\x : rZ#=node\z
					range=0
					ht=mapData(node\x,node\z)
					While range&lt;=5
						rX=rX+Cos(bearing)
						rZ=rZ+Sin(bearing)
						If rX=&gt;0 And rX&lt;=511 And rZ=&gt;0 And rZ&lt;=511
							If Int(rX)=tX And Int(rZ)=tZ
								range=10
							Else
								If mapData(rX,rZ)-ht&lt;=4
									ht=mapdata(rX,rZ)
									range=range+1
								Else
									range=10
								EndIf
							EndIf
						Else
							range=10
						EndIf
					Wend
					
					sX=rX : sZ=rZ
					;*** CORE SYSTEM	
						If sX=&gt;0 And sX&lt;=511 And sZ=&gt;0 And sZ&lt;=511
							If sX&lt;&gt;node\X Or sZ&lt;&gt;node\Z
								onClosedList=0
								For checkCNode.closedList=Each closedList
									If Abs(checkCNode\x-sX)&lt;=1
										If Abs(checkCNode\z-sZ)&lt;=1
											onClosedList=1
											checkCNode.closedList=Last closedList
										EndIf
									EndIf
								Next

								If Not onClosedList
									onOpenList=0
									For checkONode.openList=Each openList
										If Abs(checkONode\x-sX)&lt;=1
											If Abs(checkONode\z-sZ)&lt;=1
												onOpenList=1
												If node\gCost+1&lt;checkONode\gCost
													If checkONode\mapheight-node\mapHeight&lt;=4
														If checkOnode\mapHeight&lt;8 Then cost=10 Else cost=1
														checkONode\parentX=node\X
														checkONode\parentZ=node\Z
														checkONode\gCost=node\gCost+cost
														checkONode\fSum=checkONode\gCost+checkONode\hGuess
													EndIf
												EndIf
												checkONode.openList=Last openList
											EndIf
										EndIf
									Next

									If Not onOpenList
										If mapData(sX,sZ)-node\mapHeight&lt;=4
											If node\gCost&lt;maxNodeRange
												If mapData(sX,sZ)&lt;8 Then cost=10 Else cost=1
												newNode.openList=New openList
												newNode\x=sX : newNode\z=sZ
												newNode\parentX=node\x : newNode\parentZ=node\z
												newNode\gCost=node\gCost+cost
												newNode\hGuess=moveHeuristic(sX,sZ,tX,tZ)
												newNode\fSum=newNode\gCost+newNode\hGuess
												newNode\mapHeight=mapData(sX,sZ)
											EndIf
										EndIf
									EndIf
								EndIf
							EndIf
						EndIf
					;*** END CORE SYSTEM
				Next
		
				finished.closedList=New closedList
				finished\x=node\x : finished\z=node\z
				finished\parentX=node\parentX : finished\parentZ=node\parentZ
				Delete node.openList
			EndIf
		EndIf
		
		up=up+1
		ms=MilliSecs()
		If up=&gt;lastTime-ms
			lastTime=ms
			up=0
			Color 255,255,0
			DrawImage treeMap,0,0
			For showO.openList=Each openList
				Plot showO\x,512-showO\z
			Next
			Color 0,255,255
			For showC.closedList=Each closedList
				Plot showC\x,512-showC\z
			Next
			Color 0,255,0
			Oval tx,512-tz,3,3,0
			Flip 0
			Color 0,0,255
			Oval ox,512-oz,5,5,0
		EndIf
	Until buildPath&gt;0
	
	If buildPath=2
		For checkOnode.openList=Each openList
			finished.closedList=New closedList
			finished\x=checkOnode\x
			finished\z=checkOnode\z
			finished\parentX=checkOnode\parentX
			finished\parentZ=checkOnode\parentZ
			Delete checkOnode
		Next
	
		nearest=99999999
		newTx=tx : newTz=tz
		For CheckCnode.closedList=Each closedList
			dx#=checkCnode\x-tx
			dz#=checkCnode\z-tz
			rng=Sqr((dx*dx)+(dz*dz))
			If rng&lt;nearest
				newTx=checkCnode\x
				newTz=checkCnode\z
				thisNode.closedList=checkCnode.closedList
				nearest=rng
			EndIf
		Next
		path.AIpath=New AIpath
		path\x=thisNode\x
		path\z=thisNode\z
		x=thisNode\parentX
		z=thisNode\parentZ
	EndIf

	If buildPath=1	
		path.AIpath=New AIpath
		path\x=node\x : path\z=node\z
		x=node\parentX : z=node\parentZ
		Delete node
	EndIf
	
	Repeat
		For pathnode.closedList=Each closedList
			If x=pathnode\x And z=pathnode\z
				path.AIPath=New AIPath
				path\x=x
				path\z=z
				x=pathnode\parentX
				z=pathnode\parentZ
				Delete pathNode
			EndIf
		Next
	Until x=0 And z=0

	For clearOList.openList=Each openList
		Delete clearOList
	Next
	For clearCList.closedList=Each closedList
		Delete clearCList
	Next
End Function
;Function moveHeuristic(sX,sZ,tX,tZ)
;	difX=Abs(sX-tX)
;	difZ=Abs(sZ-tZ)
;	If difX&gt;difZ
;		Return difZ+(difX*3)
;	Else
;		Return difX+(difZ*3)
;	EndIf
;End Function
;Function moveHeuristic(sX,sZ,tX,tZ)
;	dx#=sX-tX
;	dz#=sZ-tZ
;	Return Sqr((dx*dx)+(dz*dz))
;End Function
Function moveHeuristic(sX,sZ,tX,tZ)
	Return (Abs(sX-tX)+Abs(sZ-tZ))*2
End Function
</pre> <br><br></td></tr></table><br>
<a name="561823"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Techlord</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Becky,<br><br>Very nice looking A* code you got there. <br><br></td></tr></table><br>
<a name="561847"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bouncer</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> And use heaps... you can really optimize a* with it. <br><br></td></tr></table><br>
<a name="561851"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bouncer</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here's my a* function ... might give you ideas how to construct yours. It's a node graph based and it uses heap to  really speed it up.<br><br><br><pre class=code>
Function P_ASTAR(node1%,GOAL%,ply%=0)
Local CLOSED%[P_MAXNODES]
Local PARENT%[P_MAXNODES]
Local G#[P_MAXNODES]					;GENERATED COST
Local H#[P_MAXNODES]					;ESTIMATED COST (to target)
goal_found=0

If ply=0 Then P_GOAL_NODE(ply) = 0


If NODE1 &lt;&gt; GOAL
CLEAR_HEAP()
HEAP_INSERT(node1,0)


;P_ELAPSEDTIME=MilliSecs()
Repeat
;GET THE MOST LOWEST COST NODE FROM THE HEAP
current = HEAP_REMOVELOWEST()

;PUT IT TO THE CLOSED LIST
CLOSED[current] = 1

	;CHECK ALL THE NODES CONNECTED TO CURRENT NODE
	For i=1 To P_NODE_CONNECTIONS(current)
	nn = P_NODE_CON(current,i)

	;IF NOT ON THE CLOSED LIST PROCEED
	If CLOSED[nn]=0
	
	;SEARCH THE OPEN LIST -&gt; PUT THE HEAP POS IN found ELSE found = 0
	found=0
	For ii=1 To HEAPSIZE
	If HEAPITEM(ii) = nn Then found=ii:Exit
	Next

	;NOT ON OPENLIST ---&gt; CALCULATE COST AND PUT IT IN!
	If found=0
	PARENT[nn] = current
	H[nn] = P_MANHATTAN_DISTANCE(nn,GOAL)					;ESTIMATE THE DISTANCE TO GOAL
	G[nn] = P_NODE_ARCLENGTH(current,i) + G[current]		;G = distance to this node + G of the parent node
	F# = G[nn] + H[nn]																	;CALCULATE FINAL COST

	;PUT TO OPENLIST
	HEAP_INSERT(nn,F)
	End If

	;IS ON THE OPEN LIST --- &gt; CHECK IF THIS IS BETTER PATH TO  IT
	If found&gt;0
	NG# = P_NODE_ARCLENGTH(current,i) + G[current]

		;IF THIS PATH*S G SCORE = LOWER THAN OLD G --&gt; UPDATE
		If NG&lt;G[nn]
		PARENT[nn] = current
		G[nn] = NG																				;REPLACE THE OLD G WITH THE CURRENT G
		F# = G[nn] + H[nn]																	;CALCULATE FINAL COST
		UPDATE_HEAPITEM(nn,F,found)											;UPDATE THIS NODE
		End If
		
	End If

	End If
	Next

If current=GOAL Then goal_found=1
Until  HEAP_EMPTY()=1 Or goal_found=1 
End If

	P_PATHNODES(ply)=0
	P_PATHCOST(ply)=0
	
	If P_GOAL_NODE(ply)&gt;0
	P_PATHNODES(ply) = P_PATHNODES(ply) + 1
	P_PATHNODE(ply,P_PATHNODES(ply)) = P_GOAL_NODE(ply)
	End If

;TRACE THE PATH!
If goal_found=1
current=GOAL
P_PATHCOST(ply) = G[current]

	Repeat
	P_PATHNODES(ply) = P_PATHNODES(ply)+1
	P_PATHNODE(ply,P_PATHNODES(ply)) = current
	current = PARENT[current]
	Until current=node1
	P_PATHNODES(ply) = P_PATHNODES(ply)+1
	P_PATHNODE(ply,P_PATHNODES(ply)) = current

End If



;P_ELAPSEDTIME=MilliSecs()-P_ELAPSEDTIME

Return P_PATHNODES(ply)
End Function

</pre> <br><br></td></tr></table><br>
<a name="561895"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Techlord</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Bouncer,<br><br>Very nice compact code. Thanks for sharing. <br><br></td></tr></table><br>
<a name="561937"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bouncer</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> And while I'm at it... Here's some ready to use heap function.s<br><br><pre class=code>

;HEAP FUNCTIONS
Global HEAPSIZE=0
Global HMAXSIZE=P_MAXNODES
Dim HEAPITEM%(HMAXSIZE)
Dim HEAPKEY#(HMAXSIZE)



Function HEAP_INSERT(n,key#)
HEAPSIZE=HEAPSIZE+1
HEAPITEM(HEAPSIZE) = n

m = HEAPSIZE
HEAPKEY(HEAPITEM(m))=key

  While m &lt;&gt; 1 ;While item hasn't bubbled to the top (m=1)

     ;Check if child is &lt;= parent. If so, swap them.
     If HEAPKEY(HEAPITEM(m)) &lt;= HEAPKEY(HEAPITEM(m/2)) Then
        temp = HEAPITEM(m/2)
        HEAPITEM(m/2) = HEAPITEM(m)
        HEAPITEM(m) = temp
        m = m/2 
     Else

        Exit ;exit the while/wend loop
     End If
  Wend
End Function




Function HEAP_REMOVELOWEST()
;If HEAP_EMPTY()=0
L = HEAPITEM(1)

;GET THE LAST ITEM AND PUT IT IN THE FIRST POSITION
;(FIRST GET*S DELETED)
HEAPITEM(1) = HEAPITEM(HEAPSIZE)
HEAPSIZE = HEAPSIZE - 1

v = 1
;Repeat the following until the item sinks to its proper spot in the binary heap.
Repeat
  u = v
  If 2*u+1 &lt;= HEAPSIZE;if both children exist
    ;Select the lowest of the two children.
    If HEAPKEY(HEAPITEM(u)) &gt;= HEAPKEY(HEAPITEM(2*u)) Then v = 2*u 
    If HEAPKEY(HEAPITEM(v)) &gt;= HEAPKEY(HEAPITEM(2*u+1)) Then v = 2*u+1

  Else If 2*u &lt;= HEAPSIZE ;if only child #1 exists
    ;Check if the F cost is greater than the child
    If HEAPKEY(HEAPITEM(u)) &gt;= HEAPKEY(HEAPITEM(2*u))Then v = 2*u
  End If

  If u &lt;&gt; v Then ; If parent's F &gt; one or both of its children, swap them
    temp = HEAPITEM(u)
    HEAPITEM(u) = HEAPITEM(v)
    HEAPITEM(v) = temp
  Else
    Exit ;if item &lt;= both children, exit repeat/forever loop
  End If
Forever

Return L
;End If
End Function



Function UPDATE_HEAPITEM(item,newkey#,HEAPPOS=0)
If HEAPPOS=0
For i=1 To HEAPSIZE
If HEAPITEM(i) = item Then m=i:Exit
Next
Else 
m=HEAPPOS
End If

HEAPKEY(HEAPITEM(m)) = newkey

  While m &lt;&gt; 1 ;While item hasn't bubbled to the top (m=1)
     ;Check if child is &lt;= parent. If so, swap them.
     If HEAPKEY(HEAPITEM(m)) &lt;= HEAPKEY(HEAPITEM(m/2)) Then
        temp = HEAPITEM(m/2)
        HEAPITEM(m/2) = HEAPITEM(m)
        HEAPITEM(m) = temp
        m = m/2 
     Else
        Exit ;exit the while/wend loop
     End If
  Wend
End Function



Function HEAP_EMPTY()
If HEAPSIZE&gt;0 Return 0 Else Return 1
End Function
Function CLEAR_HEAP()
Dim HEAPITEM%(HMAXSIZE)
Dim HEAPKEY#(HMAXSIZE)
HEAPSIZE=0
End Function

</pre> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
