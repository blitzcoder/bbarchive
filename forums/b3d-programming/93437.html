<!DOCTYPE html><html lang="en" ><head ><title >Unusual Object Does Not Exist error</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Unusual Object Does Not Exist error</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Unusual Object Does Not Exist error</a><br><br>
<a name="1068833"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Whenever I try to run this, I get an "Object Does Not Exist" error at line 311<br><br>which is simply<br><br>Return Mesh<br><br>Within the Function Build_Invader%(INVADER_TYPE%)<br><br>The dependancies included are just data used to 'pack' images and sound, but I suspect these may be responsible for the error, due to using up memory?<br>Although my system still has  well over a gigabyte of RAM to spare...<br><br>I really don't understand what the problem is, since at the very least, oughtn't it be Entity doesn't exist, if somehow 'Mesh' was nop longer a valid handle?<br><br>If anyone can shed any light on just what's happening here, I'd be really grateful! Thanks :)<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Graphics3D 800,600,32,7
SetBuffer BackBuffer()

;	Randomise
SeedRnd MilliSecs()

; Include Media Data
Include "Starfield.bb"	;STARFIELD
Include "InvaderTexture.bb"	;TEXTURE
Include "Theme.bb"	;THEME












; Resources
Const RESOURCE_FILENAME$="~Temp"
Const RESOURCE_IMAGE$=".jpg"
Const RESOURCE_SOUND$=".ogg"

Global STARFIELD1_IMAGEHANDLE%
Global THEME_MAINTHEME_SOUNDHANDLE%

; Static Resources
Global STARFIELD_ENTITY_HANDLE%
Global INVADER_IMAGEHANDLE%
Global THEME_CHANNEL%

;	Initialisation Stats
Const INVADERS_PER_LINE%=12
Const INVADERS_LINES%=6
Const NUMBER_OF_INVADER_TYPES%=3

Const DEFAULT_START_LEVEL%=3

Const MIN_LEVEL_CHOICE%=1
Const MAX_LEVEL_CHOICE%=5

Global START_LEVEL%=DEFAULT_START_LEVEL%
Global CURRENT_LEVEL%=START_LEVEL%

Global INVADER_TYPE_PER_LINE%=INVADERS_LINES%/NUMBER_OF_INVADER_TYPES%

Global MAX_INVADERS%=INVADERS_PER_LINE%*INVADERS_LINES%

Const INVADER_START_DIRECTION%=1
Global INVADER_DIRECTION=INVADER_START_DIRECTION%

Const INVADER_START_SPEED#=0.2
Const INVADER_SPEED_INCREMENT#=0.05

Global INVADER_SPEED#=INVADER_START_SPEED

Const MAXIMUM_INVADERS_CAN_FIRE%=10

Const INVADER_TOP_LINE_Y_POS=90
Const INVADER_BASELINE_Y_POS=0
Const INVADER_X_SPACING=5
Const INVADER_Y_SPACING=9

Const PLAYER_X_BOUND_MINIMUM=-15
Const PLAYER_X_BOUND_MAXIMUM=+15
Const PLAYER_BASELINE_Y=0

Const PLAYER_BASE_X_SPACING=20
Const PLAYER__BASE_BASELINE_Y=3

Const MAX_PLAYER_BASES=3
Global PLAYER_BASES=MAX_PLAYER_BASES

Const INVADER_TYPE_1=1
Const INVADER_TYPE_2=2
Const INVADER_TYPE_3=3

Const EXPLOSION_TIMEOUT=10000

;	Collision Data
Const COLLISION_TYPE_INVALID=0
Const COLLISION_TYPE_PLAYERSHIP=1
Const COLLISION_TYPE_INVADER=2
Const COLLISION_TYPE_PLAYERBASE=3
Const COLLISION_TYPE_INVADERMOTHER=4
Const COLLISION_TYPE_PLAYERBULLET=5
Const COLLISION_TYPE_INVADERBULLET=6

Type INVADERS
	Field MeshHandle%
	Field LastFired%
End Type

Type Explosion
	Field MilliSecs
	Field Entity
End Type

Initialise
Stop








cam=CreateCamera()
M=Build_Invader((MilliSecs() Mod 3) + 1)
PositionEntity M,0,0,15

sun=CreateLight(2)
AmbientLight 160,160,186
PositionEntity sun,-20,15,-1

While Not KeyDown(1)
	If (M) 
		TurnEntity M,0,1,0
		
	End If
	UpdateWorld
	RenderWorld
	
	Flip
Wend

Function Initialise()
	PopulateImages()
	PopulateSounds()
	InitialiseInvaders()
End Function

Function InitialiseInvaders()
	Local BuildInvaders.INVADERS
	Local iterbuildX
	Local iterbuildY
	For iterbuildY=1 To INVADERS_LINES
		For iterbuildX=1 To INVADERS_PER_LINE
			BuildInvaders\MeshHandle=Build_Invader(INVADER_TYPE_PER_LINE*iterbuildY)
			BuildInvaders\LastFired=Rand(2^31,0-2^31)
			PositionEntity BuildInvaders\MeshHandle,(iterbuildX*INVADER_X_SPACING)+(PLAYER_X_BOUND_MINIMUM),(iterbuildY*INVADER_Y_SPACING)+INVADER_TOP_LINE_Y_POS,0,True
			UpdateInvaderColour(BuildInvaders\MeshHandle)
		Next
	Next
End Function

Function UpdateInvaderColour(InvaderMesh)
	Local RelativePosition=(EntityY(InvaderMesh,True)-INVADER_BASELINE_Y_POS)/(INVADER_BASELINE_Y_POS-INVADER_TOP_LINE_Y_POS)
	Local R=(RelativePosition&gt;0.5)*255
	Local B=(RelativePosition&lt;0.5)*255
	Local G=(RelativePosition&gt;0.25 And RelativePosition&lt;0.75)*255
	EntityColor InvaderMesh,R,G,B
End Function

Function GenerateStarfieldMesh()
	Local Mesh=CreateQuad()
	Local Texture=STARFIELD1_IMAGEHANDLE
	Local Buffer=TextureBuffer(Texture)
	LockBuffer Buffer
	Local Colour%
	Local IterStars
	For IterStars=1 To 1000
		Colour=RandomColour(160,240,64,240,128,255)
		WritePixelFast Rand(0,511),Rand(0,511),Colour,Buffer
	Next
	EntityTexture Mesh,Texture
	FreeTexture Texture
	EntityFX Mesh,16
	Return Mesh
End Function	

Function UpdateInvaders()
	Local iterinvaders.INVADERS
	For iterinvaders.INVADERS=Each INVADERS
		AnimateInvaderMesh(iterinvaders\MeshHandle)
		MoveInvader(iterinvaders.INVADERS)
	Next
	If (MOVEALLDOWN)
		MoveInvadersDown()
		MOVEALLDOWN=False
		
	End If
End Function

Function MoveInvader(CurrentInvader.INVADERS)
	
	If ((INVADER_DIRECTION=(0-1)) And (EntityX(CurrentInvader\MeshHandle,True)&lt;=PLAYER_X_BOUND_MINIMUM))
		MOVEALLDOWN=True
		INVADER_DIRECTION=1
	End If
	If ((INVADER_DIRECTION=1) And (EntityX(CurrentInvader\MeshHandle,True)&gt;=PLAYER_X_BOUND_MAXIMUM))
		MOVEALLDOWN=True
		INVADER_DIRECTION=(0-1)
	End If	
	
	TranslateEntity CurrentInvader\MeshHandle,INVADER_SPEED*INVADER_DIRECTION,0,0,True
End Function

Function MoveInvadersDown()
	Local IterInvaders.INVADERS
	For IterInvaders.INVADERS=Each INVADERS
		TranslateEntity IterInvaders\MeshHandle,0,-INVADER_Y_SPACING,0,True
		UpdateInvaderColour(IterInvaders\MeshHandle)
		If (EntityY(IterInvaders\MeshHandle,True)&gt;INVADER_BASELINE_Y_POS) Then GameOver()
	Next
End Function	

Function PopulateImages()
	
	; Starfield
	Restore STARFIELD
	Unpack(RESOURCE_IMAGE)
	STARFIELD1_IMAGEHANDLE%=LoadTexture(CurrentDir()+RESOURCE_FILENAME+RESOURCE_IMAGE)
	DeleteFile CurrentDir()+RESOURCE_FILENAME+RESOURCE_IMAGE
	If (Not(STARFIELD1_IMAGEHANDLE)) Then STARFIELD1_IMAGEHANDLE=CreateTexture(2,2)
	STARFIELD_ENTITY_HANDLE%=GenerateStarfieldMesh()
	
	; InvaderTexture
	Restore INVADERTEXTURE
	Unpack(RESOURCE_IMAGE)
	INVADER_IMAGEHANDLE%=LoadTexture(CurrentDir()+RESOURCE_FILENAME+RESOURCE_IMAGE)
	DeleteFile CurrentDir()+RESOURCE_FILENAME+RESOURCE_IMAGE
	If (Not(INVADER_IMAGEHANDLE)) Then INVADER_IMAGEHANDLE=CreateTexture(2,2)
	TextureBlend INVADER_IMAGEHANDLE,4
End Function	

Function PopulateSounds()
	
	; Main Theme
	Restore THEME
	Unpack(RESOURCE_SOUND)
	THEME_MAINTHEME_SOUNDHANDLE%=LoadSound(CurrentDir()+RESOURCE_FILENAME+RESOURCE_SOUND)
	DeleteFile CurrentDir()+RESOURCE_FILENAME+RESOURCE_SOUND
End Function	

Function Unpack(ResExt$)
	; Temporary file creation from Resource Data
	;Ensure Restore is called just prior to use
	Local file = WriteFile(CurrentDir()+RESOURCE_FILENAME+ResExt) ;&lt;- specify the file to recreate
	Local Byte
	While (Byte&gt;=0)
		Read Byte ;get next byte
		WriteByte(file,Byte)
	Wend
	CloseFile(file)
End Function

Function FreeEntityWithChildren(Entity)
	
	;// DEPRECATED. CHILD ENTITIES FREE'D WITH FREE ENTITY BY DEFAULT
	
;	Local Children=CountChildren(Entity)
;	Local nIterChilds
;	If (Children)
;		For nIterChilds=1 To Children
;			FreeEntityWithChildren(GetChild(Entity,nIterChilds))
;		Next
;	End If
	FreeEntity Entity
;	
End Function	

Function Build_Invader%(INVADER_TYPE%)
	Local Mesh%=CreateMesh()
	Local Surface=CreateSurface(Mesh)
	Local Cube, Cube2
	Local f,g,Dat
	Select (INVADER_TYPE)
		Case INVADER_TYPE_1:
			Restore INVADER1
		Case INVADER_TYPE_2:
			Restore INVADER2
		Case INVADER_TYPE_3:
			Restore INVADER3
		Default:
			RuntimeError "INVALID INVADER REFERENCE: "+Str(INVADER_TYPE)
	End Select
	Local Anim=False
	For f=1 To 7
		Read Dat
		For g=0 To 7
			If Dat And 2^g
				Cube=CreateQuad(Mesh)
				ScaleMesh Cube,1,1,0.01
				PositionMesh Cube,(0-g),7-f,0
				AddMeshToSurface(Cube,Mesh,Surface)
				
				Cube2=CreateQuad(Mesh)
				ScaleMesh Cube2,1,1,0.01
				PositionMesh Cube2,g,7-f,0
				AddMeshToSurface(Cube2,Mesh,Surface)
				
				FreeEntity Cube
				FreeEntity Cube2
			End If
		Next
	Next
	
	EntityShininess Mesh,0.79
	EntityAutoFade Mesh,30,50
	EntityType Mesh,COLLISION_TYPE_INVADER,True
	EntityPickMode Mesh,1,True
	EntityRadius Mesh,COLLISION_TYPE_INVADER,True
	
	EntityTexture Mesh,INVADER_IMAGEHANDLE,0,1
	
	Return Mesh
End Function	

; INVADER QUAD POSITION DATA
.INVADER1
Data 1,3,5,15,2,5,10
.INVADER2
Data 4,2,7,13,15,4,2
.INVADER3
Data 7,15,9,15,6,9,4

Function RandomColour%(R1,R2,G1,G2,B1,B2)
	Local R=Rand(R1,R2)
	Local G=Rand(G1,G2)
	Local B=Rand(B1,B2)
	
	Return (B And 255) Or ((G Shr 8)And 255) Or ((R Shr 16)And 255)
	
End Function

Function AnimateInvaderMesh(Mesh)
	Local Surface=GetSurface(Mesh,1)
	Local Verts=CountVertices(Surface)
	Local IterVerts
	For IterVerts=0 To Verts-1
		If (EntityX(Mesh,1)-((VertexY(Surface,IterVerts))&lt;1))
			VertexCoords Surface,IterVerts,VertexX(Surface,IterVerts)+Sin(MilliSecs())*0.25,VertexY(Surface,IterVerts)+Cos(MilliSecs())*0.02,VertexZ(Surface,IterVerts)
		End If
	Next
End Function

Function MatchEntityPosition(Source%,Target%,Orientation=False)
	PositionEntity Source,EntityX(Target,1),EntityY(Target,1),EntityZ(Target,1),1
	If (Orientation) Then RotateEntity Source,EntityPitch(Target,1),EntityYaw(Target,1),EntityRoll(Target,1),1
End Function

Function CreateQuad(Parent=False) 
	Local Mesh=CreateMesh(Parent)
	Local Surface
	Surface=CreateSurface(Mesh)
	Local iterMem%
	iterMem=AddVertex(Surface,0,0,0,0,1)
	AddVertex (Surface,0,0,1,0,0)
	AddVertex (Surface,1,0,1,1,0)
	AddVertex (Surface,1,0,0,1,1)
	
	;VertexColor Surface,iterMem,255,255,255,224
	;VertexColor Surface,iterMem+1,255,255,255,224
	;VertexColor Surface,iterMem+2,255,255,255,224
	;VertexColor Surface,iterMem+3,255,255,255,224
	
	AddTriangle Surface,iterMem,iterMem+2,iterMem+3
	AddTriangle Surface,iterMem,iterMem+1,iterMem+2
	
	;EntityColor Mesh,255,255,255
	
	RotateMesh Mesh,90,0,0
	
	;EntityFX Mesh,38
	
	Return Mesh
End Function

Function AddMeshToSurface(SrcMesh, DestMesh, DestSurface, Voffsetx#=0, Voffsety#=0, Voffsetz#=0, Vpitch#=0, Vyaw#=0, Vroll#=0, Vscalex#=1, Vscaley#=1, VscaleZ#=1)
	;// FUNCTION WRITTEN BY Shawn Swift
	
	Local VertLoop,Vu#,Vv#,Vw#,Vx#,Vy#,Vz#,VNx#,VNy#,VNz#
	Local vr,vg,vb
	
	; Determine if we can optimize the mesh copying.
	Local TransformVertices = True
	
	If (Vpitch#=0) And (Vyaw#=0) And (Vroll#=0) 
		TransformVertices = False
	EndIf
	
	
	; Make sure there's a surface to copy, because the mesh might be empty.
	If CountSurfaces(SrcMesh) &gt; 0
		
		Local SrcSurface = GetSurface(SrcMesh, 1)
		
		Local DestVerts  = CountVertices(DestSurface)
		Local SrcVerts   = CountVertices(SrcSurface)
		
		; If we need to transform the vertices in a complex way...
		If TransformVertices
			
			; Do slower copy method because we need to rotate the vertices.
			
			; Create a pivot to do the transformations with.
			Local ThisPivot = CreatePivot()
			PositionEntity ThisPivot, Voffsetx#, Voffsety#, Voffsetz#, True
			RotateEntity ThisPivot, Vpitch#, Vyaw#, Vroll#, True
			ScaleEntity ThisPivot, Vscalex#, Vscaley#, VscaleZ#
			
			; Copy all the vertices from the source mesh to the destination surface.
			For VertLoop = 0 To SrcVerts-1
				
				Vu#  = VertexU#(SrcSurface, VertLoop)
				Vv#  = VertexV#(SrcSurface, VertLoop)		
				Vw#  = VertexW#(SrcSurface, VertLoop)
				vr   = VertexRed(SrcSurface, VertLoop)
				vg   = VertexGreen(SrcSurface, VertLoop)
				vb   = VertexBlue(SrcSurface, VertLoop)
				
				TFormPoint VertexX#(SrcSurface, VertLoop), VertexY#(SrcSurface, VertLoop), VertexZ#(SrcSurface, VertLoop), ThisPivot, 0
				Vx# = TFormedX()
				Vy# = TFormedY()
				Vz# = TFormedZ()
				
				TFormNormal VertexNX#(SrcSurface, VertLoop), VertexNY#(SrcSurface, VertLoop), VertexNZ#(SrcSurface, VertLoop), ThisPivot, 0
				VNx# = TFormedX()
				VNy# = TFormedY()
				VNz# = TFormedZ()
				
				AddVertex(DestSurface, Vx#, Vy#, Vz#, Vu#, Vv#, Vw#)
				VertexNormal(DestSurface, VertLoop+DestVerts, VNx#, VNy#, VNz#)
				VertexColor(DestSurface, VertLoop+DestVerts, vr, vg, vb) 
				
			Next
			
			FreeEntity ThisPivot
			
		Else
			
			; Do the fast copy.
			; Fast copy can do offset and scaling, but not rotation.
			
			; Copy all the vertices from the source mesh to the destination surface.
			For VertLoop = 0 To SrcVerts-1
				
				
				Vx#  = VertexX#(SrcSurface, VertLoop)
				Vy#  = VertexY#(SrcSurface, VertLoop)
				Vz#  = VertexZ#(SrcSurface, VertLoop)
				Vu#  = VertexU#(SrcSurface, VertLoop)
				Vv#  = VertexV#(SrcSurface, VertLoop)		
				Vw#  = VertexW#(SrcSurface, VertLoop)
				VNx# = VertexNX#(SrcSurface, VertLoop)
				VNy# = VertexNY#(SrcSurface, VertLoop)
				VNz# = VertexNZ#(SrcSurface, VertLoop)						
				vr   = VertexRed(SrcSurface, VertLoop)
				vg   = VertexGreen(SrcSurface, VertLoop)
				vb   = VertexBlue(SrcSurface, VertLoop)
				
				AddVertex(DestSurface, (Vx#*Vscalex#)+Voffsetx#, (Vy#*Vscaley#)+Voffsety#, (Vz#*VscaleZ#)+Voffsetz#, Vu#, Vv#, Vw#)
				VertexNormal(DestSurface, VertLoop+DestVerts, VNx#, VNy#, VNz#)
				VertexColor(DestSurface, VertLoop+DestVerts, vr, vg, vb) 
				
			Next
			
		EndIf
		
		; Copy all triangles from the source surface to the destination surface.	
		Local SrcTris  = CountTriangles(SrcSurface)
		Local TriLoop, v0,v1,v2
		For TriLoop = 0 To SrcTris-1
			
			v0 = TriangleVertex(SrcSurface, TriLoop, 0)
			v1 = TriangleVertex(SrcSurface, TriLoop, 1)
			v2 = TriangleVertex(SrcSurface, TriLoop, 2)
			
			AddTriangle(DestSurface, v0+DestVerts, v1+DestVerts, v2+DestVerts)
			
		Next
		
	EndIf
End Function

Function Shrapnel(X=0,Y=0,Z=0,PH=0,YW=0,RL=0)
	Local Mesh=CreateMesh()
	Local Surface
	Surface=CreateSurface(Mesh)
	Local iterMem%
	iterMem=AddVertex(Surface,0,0,0,0,1)
	AddVertex (Surface,0,0,1,0,0)
	AddVertex (Surface,1,0,1,1,0)
	
	;VertexColor Surface,iterMem,255,255,255,224
	;VertexColor Surface,iterMem+1,255,255,255,224
	;VertexColor Surface,iterMem+2,255,255,255,224
	
	AddTriangle Surface,iterMem,iterMem+1,iterMem+2
	
	;EntityColor Mesh,255,255,255
	
	PositionMesh Mesh,0.5,0.5,0.5
	RotateMesh Mesh,90+PH,0+RL,0+YW
	
	PositionEntity Mesh,0+X,0+Y,0+Z,True
	
	;EntityFX Mesh,38
	
	Return Mesh
End Function

Function GameOver()
	RuntimeError "Game Over placeholder"
End Function
;~IDEal Editor Parameters:
;~C#Blitz3D</textarea> <br><br></td></tr></table><br>
<a name="1068853"></a>

<a name="1068854"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Warner</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I think it is not so much the function, but the point where the function is called. Ie this line:<br><pre class=code>
BuildInvaders\MeshHandle=Build_Invader(INVADER_TYPE_PER_LINE*iterbuildY)
</pre><br>When "build_invader" returns, it tries to write the mesh data into the "meshhandle" field of "buildinvaders". If Buildinvaders does not exist, you get that error.<br>Edit: looking closer at that function:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Function InitialiseInvaders()
	Local BuildInvaders.INVADERS
	Local iterbuildX
	Local iterbuildY
	For iterbuildY=1 To INVADERS_LINES
		For iterbuildX=1 To INVADERS_PER_LINE
			BuildInvaders\MeshHandle=Build_Invader(INVADER_TYPE_PER_LINE*iterbuildY)
			BuildInvaders\LastFired=Rand(2^31,0-2^31)
			PositionEntity BuildInvaders\MeshHandle,(iterbuildX*INVADER_X_SPACING)+(PLAYER_X_BOUND_MINIMUM),(iterbuildY*INVADER_Y_SPACING)+INVADER_TOP_LINE_Y_POS,0,True
			UpdateInvaderColour(BuildInvaders\MeshHandle)
		Next
	Next
End Function
</textarea><br>It seems it is missing the line:<br>"BuildInvaders = new INVADERS" right after the second "For" line.<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1069102"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> DOH!!!<br><br><br>Thanks a million Warner - so obvious in retrospect! :D<br><br>Being "Object does not exist" should have been a clue it referred to the type, but I just completely missed that :)<br><br>Really thanks very much! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
