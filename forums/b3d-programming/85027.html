<!DOCTYPE html><html lang="en" ><head ><title >Tweaking an example</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Tweaking an example</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Tweaking an example</a><br><br>
<a name="961054"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Wigwam</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK, so I was tweaking with nawi's Shadow example <a href="/codearcs.php?code=2173" target="_blank">here</a>, and was trying to get the shadows to be clones of the original mesh, in order to preserve textures (so masked textures would still work).<br> but some of the vertices seem to get left behind and stuck on the mesh. I'll admit I'm not very good with vertexes and polygons at a low level.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">Graphics3D 800,600,0,2
SetBuffer BackBuffer()


Global Cam = CreateCamera()
MoveEntity Cam,0,5,-2
RotateEntity Cam,45,0,0



    NewObject = GetObject()



;Position our entity
MoveEntity NewObject,0,2.5,0

;White plane
Plane = CreatePlane()
EntityColor Plane,255,255,255
MoveEntity Plane,0,-0.01,0

;Create light
Light = CreateSphere(5)
Spot = CreateLight(2,Light)
ScaleEntity Light,0.1,0.1,0.1
EntityColor Light,255,255,0

Global wire=0



Repeat
	;Camera movement
	If KeyDown(200) Then MoveEntity Cam,0,0,1
	If KeyDown(208) Then MoveEntity Cam,0,0,-1
	If KeyDown(203) Then MoveEntity Cam,-1,0,0
	If KeyDown(205) Then MoveEntity Cam,1,0,0
    TurnEntity Cam,MouseYSpeed(),-MouseXSpeed(),0

    Shadow = CopyMesh(NewObject)
	MoveEntity Shadow,0,2.5,0
	
	;There is sadly a bug with alpha shadows.. Didn't figure out a proper solution to this one
	;You could use a camera, but would be a major slowdown
	
;	EntityColor Shadow,0,0,0
	

	;Light movement
	GY# = 7+Sin(MilliSecs()/10)*0.5
	GX# = Cos(MilliSecs()/10)*3
	
	;Draw shadow
	DrawShadow(NewObject,GX#,GY#,GZ#,Shadow)
	PositionEntity Light,GX#,GY#,GZ#
	
	
	;FPS
	FPSC=FPSC+1
	If MilliSecs()&gt;FPSTimer+999 Then
		FPSTimer = MilliSecs()
		FPS=FPSC
		FPSC=0
	EndIf
	
	


	RenderWorld

	Text 0,0,"FPS: " + FPS
	;Text 0,20,"Trisrendered: " + TrisRendered()
	Flip 0

	;Destroy shadow
	FreeEntity Shadow
	
	If KeyHit(19)
	If wire=1
	 wire=0
	 WireFrame False
	ElseIf wire=0
	 wire=1
	 WireFrame True
	EndIf
	EndIf


Until KeyHit(1)

Function DrawShadow(Entity,SX#,SY#,SZ#,Shadow)
	;This works by looping through all triangles, then calculating a line for each vertex to ground (y=0)
	EX#=EntityX#(Entity,1)
	EY#=EntityY#(Entity,1)
	EZ#=EntityZ#(Entity,1)
	Surfaces=CountSurfaces(Entity)
	For s=1 To Surfaces
		Surface = GetSurface(Entity,s)
	    ShadowSurface = GetSurface(Shadow,s)
		

		Triangles=CountTriangles(Surface)-1
		For t=0 To Triangles
			v1 = TriangleVertex(Surface,t,0)
			v2 = TriangleVertex(Surface,t,1)
			v3 = TriangleVertex(Surface,t,2)
			
			v1X# = VertexX#(Surface,v1)+EX#
			v1Y# = VertexY#(Surface,v1)+EY#
			v1Z# = VertexZ#(Surface,v1)+EZ#
			K#=-SY#/(SY#-v1Y#)
			v1XR# = SX# + (SX#-v1X#)*K#
			v1ZR# = SZ# + (SZ#-v1Z#)*K#

			v2X# = VertexX#(Surface,v2)+EX#
			v2Y# = VertexY#(Surface,v2)+EY#
			v2Z# = VertexZ#(Surface,v2)+EZ#
			K#=-SY#/(SY#-v2Y#)
			v2XR# = SX# + (SX#-v2X#)*K#
			v2ZR# = SZ# + (SZ#-v2Z#)*K#
			
			v3X# = VertexX#(Surface,v3)+EX#
			v3Y# = VertexY#(Surface,v3)+EY#
			v3Z# = VertexZ#(Surface,v3)+EZ#
			K#=-SY#/(SY#-v3Y#)
			v3XR# = SX# + (SX#-v3X#)*K#
			v3ZR# = SZ# + (SZ#-v3Z#)*K#
			
			;Backface culling works by calculating a camera vector and a surface normal
			;If the dot product is &gt; 0 then draw
			
			;Camera vector
			CX#=SX#-(v1X#+v2X#+v3X#)*0.33333
			CY#=SY#-(v1Y#+v2Y#+v3Y#)*0.33333
			CZ#=SZ#-(v1Z#+v2Z#+v3Z#)*0.33333
			
			;Surface normal, create 2 vectors and then create vector W from their cross product
			UX# = V2X#-V1X#
			UY# = V2Y#-V1Y#
			UZ# = V2Z#-V1Z#
			VX# = V3X#-V1X#
			VY# = V3Y#-V1Y#
			VZ# = V3Z#-V1Z#
			WX# = UY#*VZ#-UZ#*VY#
			WY# = -(UX#*VZ#-UZ#*VX#)
			WZ# = UX#*VY#-UY#*VX#
			
			;Move triangle
			If (SY#&gt;v1Y#) And (SY#&gt;v2Y#) And (SY#&gt;v3Y#) And (CX#*WX#+CY#*WY#+CZ#*WZ#)&gt;0.0 Then
				VertexCoords(ShadowSurface,v1,v1XR#,0,v1ZR#)
				VertexCoords(ShadowSurface,v2,v2XR#,0,v2ZR#)
				VertexCoords(ShadowSurface,v3,v3XR#,0,v3ZR#)
			EndIf

		Next
	Next
End Function

;Create a test object
Function GetObject()
	NewObject = CreateMesh()
	Cube = CreateCube()
	ScaleMesh Cube,0.6,2.5,0.6
	
	PositionMesh Cube,-3,0,3
	AddMesh Cube,NewObject
	
	PositionMesh Cube,6,0,0
	AddMesh Cube,NewObject
	
	PositionMesh Cube,-6,0,-6
	AddMesh Cube,NewObject
	
	PositionMesh Cube,6,0,0
	AddMesh Cube,NewObject
	
	Sphere = CreateSphere(16)

	
	PositionMesh Sphere,-3,2.5,3
	AddMesh Sphere,NewObject

	PositionMesh Sphere,6,0,0
	AddMesh Sphere,NewObject
	
	PositionMesh Sphere,-6,0,-6
	AddMesh Sphere,NewObject
	
	PositionMesh Sphere,6,0,0
	AddMesh Sphere,NewObject
	
	
	
	FreeEntity Cube
	FreeEntity Sphere
	
	EntityColor NewObject,0,200,0
	Return NewObject
End Function

</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
