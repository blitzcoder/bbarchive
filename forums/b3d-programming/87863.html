<!DOCTYPE html><html lang="en" ><head ><title >Outlined 3D object - how to do it ?</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Outlined 3D object - how to do it ?</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Outlined 3D object - how to do it ?</a><br><br>
<a name="996761"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >semar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> All,<br>how to make a 3D object outlined ?<br><br>From what I've tryed, a way would be to modify the texture and add the desired outline to it, before applying it to the 3D model.<br><br>But that's means to have to edit all the models textures.<br><br>Is there any other way to accomplish this ?<br><br>Regards,<br>Sergio. <br><br></td></tr></table><br>
<a name="996767"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You mean a black line around the outside of an object, as though it were outlined with a pen on paper? <br><br></td></tr></table><br>
<a name="996769"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >semar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> No, an outline of any other color; like the players in this video:<br><a href="http://www.youtube.com/watch?v=VVIdHPG0wYI" target="_blank">http://www.youtube.com/watch?v=VVIdHPG0wYI</a><br>the players and the pickable objects are outlined in blue. <br><br></td></tr></table><br>
<a name="996776"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Yasha</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmmm...<br><br>I think there's no way to do something like that without at least three render passes; more if you haven't got FastExtension or some other library that'd let you do blurring in hardware. But one possible way:<br><br>1) Colour your objects/players the shade of the outline. Render these objects, and not the rest of the world, and do so without textures.<br><br>2) Apply a fairly heavy blur filter to the result. Disable colour clearing and render the objects again, but this time coloured black (so they chop holes out of the blurs).<br><br>3) Store the result somehow.<br><br>4) Render the scene as normal, with the previous render overlaid by some means.<br><br>There's more than one valid way of going about these steps, hence the "somehow". There's probably also a much better way of doing this altogether, but this is what comes to mind immediately. I'm not sure how you could do this if you don't have a DLL to let you use postprocess effects.<br><br><br>EDIT: Here's an example (dump it in the FastExt examples folder):<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Include "include\FastExt.bb"	; &lt;&lt;&lt;&lt;    Include FastExt.bb file

Graphics3D 800,600,0,2

InitExt					; &lt;&lt;&lt;&lt; 	Обязательно инициализуем после Graphics3D
						;               Initialize library after Graphics3D function


; Создадим 3Д мир для примера
; Create world objects
Global Camera, Light, Sphere, Cylinder, Help : CreateScene


Local rtime=Floor(1000.0/60),ctime=MilliSecs(),FLimit=True

Local bluetexture = CreateTexture(256,256)
SetBuffer TextureBuffer(bluetexture)	;Colour it
Color 32,32,192
Rect 0,0,256,256
SetBuffer BackBuffer()

Local player = LoadAnimMesh("..\media\terror.b3d")
PositionEntity player,-30,30,20
Animate player,1,0.5,0
EntityTexture player,bluetexture,0,1	;Put it on a higher level
TextureBlend bluetexture,0				;...and hide it for now

Local FSquad=CreateQuad(GraphicsWidth()/2,GraphicsHeight()/2,Camera)
EntityFX FSquad,1:EntityOrder FSquad,-1000:EntityBlend FSquad,3
SetQuadUVs(FSquad,0,0,GraphicsWidth()/2^Ceil(Log(GraphicsWidth())/Log(2)),GraphicsHeight()/2^Ceil(Log(GraphicsHeight())/Log(2)))
Local FStex=CreateTexture(GraphicsWidth(),GraphicsHeight(),1+2+48+256+FE_RENDER+FE_ZRENDER)
EntityTexture FSquad,FStex
PositionEntity FSquad,0,0,GraphicsWidth()/2

; Главный цикл
; Main loop
While Not KeyDown(1)
	ctime=MilliSecs()
	
	MouseLook Camera
	AnimateScene
	
	UpdateWorld
	
	Color 0,0,0:SetBuffer TextureBuffer(FStex):Rect 0,0,TextureWidth(FStex),TextureHeight(FStex)
	
	SetBuffer TextureBuffer(FStex)
	EntityFX player,1
	TextureBlend bluetexture,1
	RenderEntity player,Camera,1
	EntityFX player,0
	TextureBlend bluetexture,0
	CustomPostprocessBlur 1, 4, 0.25, 1, 255, 255, 255
	RenderPostprocess FE_BLUR
	EntityColor player,0,0,0
	RenderEntity player,Camera,0
	EntityColor player,255,255,255
	
	
	SetBuffer BackBuffer()
	RenderWorld
	
	
	Delay (rtime-(MilliSecs()-ctime))-(FLimit+1)
	Flip FLimit
Wend



Function CreateQuad(xsize#=1,ysize#=1,parent=0)
	Local mesh,surf,v0,v1,v2,v3,maxu#,maxv#
	
	If xsize&gt;ysize
		maxu=1
		maxv=ysize/Float(xsize)
	Else
		maxv=1
		maxu=xsize/Float(ysize)
	EndIf
	
	mesh=CreateMesh(parent)
	surf=CreateSurface(mesh)
	v0=AddVertex(surf,-xsize, ysize,0,   0,   0)
	v1=AddVertex(surf, xsize, ysize,0,maxu,   0)
	v2=AddVertex(surf, xsize,-ysize,0,maxu,maxv)
	v3=AddVertex(surf,-xsize,-ysize,0,   0,maxv)
	AddTriangle(surf,v0,v1,v2)
	AddTriangle(surf,v2,v3,v0)
	Return mesh
End Function

Function SetQuadUVs(quad,minx#,miny#,maxx#,maxy#)	;Mainly for the FSQuad
	Local s=GetSurface(quad,1)
	
	VertexTexCoords s,0,minx,miny
	VertexTexCoords s,1,maxx,miny
	VertexTexCoords s,2,maxx,maxy
	VertexTexCoords s,3,minx,maxy
End Function








; разные вспомогательные функции
; auxiliary functions

Function MouseLook ( ent, mov# = 0.3 )
	mxspd#=MouseXSpeed()*0.25
	myspd#=MouseYSpeed()*0.25	
	campitch#=EntityPitch(ent)+myspd#
	If campitch#&lt;-65 Then campitch#=-65
	If campitch#&gt;65 Then campitch#=65
	RotateEntity ent,campitch#,EntityYaw(ent)-mxspd#,EntityRoll(ent)
	If KeyDown(17) MoveEntity ent,0,0,mov
		If KeyDown(31) MoveEntity ent,0,0,-mov
			If KeyDown(32) MoveEntity ent,mov,0,0
				If KeyDown(30) MoveEntity ent,-mov,0,0
					MoveMouse GraphicsWidth()/2,GraphicsHeight()/2
End Function

Function Musor()
	For i=1 To 50
		cub=CreateCube()
		EntityColor cub,Rand(128,255),Rand(128,255),Rand(128,255)
		;EntityAlpha cub,Rnd(0.3,1.0)
		PositionEntity cub,Rnd(-10,10),Rnd(-10,10),Rnd(5,15)
		ScaleEntity cub,Rnd(0.3,0.5),Rnd(0.3,0.5),Rnd(0.3,0.5)
		;TurnEntity cub,Rnd(0,90),Rnd(0,90),Rnd(0,90)
		;EntityFX cub,32
	Next
End Function


Global FPSCount = 0
Global FPSCountTemp = 0
Global FPSTime = 0
Function FPS()
	If (MilliSecs()-FPSTime)&gt;=1000 Then
		FPSTime = MilliSecs()
		FPSCount = FPSCountTemp
		FPSCountTemp = 0
	EndIf
	FPSCountTemp = FPSCountTemp + 1
	Return FPSCount
End Function


Function LoadSkyBox( file$ )
	m=CreateMesh()
	;front face
	b=LoadBrush( file$+"_FR.jpg",49 )
	s=CreateSurface( m,b )
	AddVertex s,-1,+1,-1,0,0:AddVertex s,+1,+1,-1,1,0
	AddVertex s,+1,-1,-1,1,1:AddVertex s,-1,-1,-1,0,1
	AddTriangle s,0,1,2:AddTriangle s,0,2,3:
	FreeBrush b
	;right face
	b=LoadBrush( file$+"_LF.jpg",49 )
	s=CreateSurface( m,b )
	AddVertex s,+1,+1,-1,0,0:AddVertex s,+1,+1,+1,1,0
	AddVertex s,+1,-1,+1,1,1:AddVertex s,+1,-1,-1,0,1
	AddTriangle s,0,1,2:AddTriangle s,0,2,3
	FreeBrush b
	;back face
	b=LoadBrush( file$+"_BK.jpg",49 )
	s=CreateSurface( m,b )
	AddVertex s,+1,+1,+1,0,0:AddVertex s,-1,+1,+1,1,0
	AddVertex s,-1,-1,+1,1,1:AddVertex s,+1,-1,+1,0,1
	AddTriangle s,0,1,2:AddTriangle s,0,2,3
	FreeBrush b
	;left face
	b=LoadBrush( file$+"_RT.jpg",49 )
	s=CreateSurface( m,b )
	AddVertex s,-1,+1,+1,0,0:AddVertex s,-1,+1,-1,1,0
	AddVertex s,-1,-1,-1,1,1:AddVertex s,-1,-1,+1,0,1
	AddTriangle s,0,1,2:AddTriangle s,0,2,3
	FreeBrush b
	;top face
	b=LoadBrush( file$+"_UP.jpg",49 )
	s=CreateSurface( m,b )
	AddVertex s,-1,+1,+1,0,1:AddVertex s,+1,+1,+1,0,0
	AddVertex s,+1,+1,-1,1,0:AddVertex s,-1,+1,-1,1,1
	AddTriangle s,0,1,2:AddTriangle s,0,2,3
	FreeBrush b
	;bottom face	
	b=LoadBrush( file$+"_DN.jpg",49 )
	s=CreateSurface( m,b )
	AddVertex s,-1,-1,-1,1,0:AddVertex s,+1,-1,-1,1,1
	AddVertex s,+1,-1,+1,0,1:AddVertex s,-1,-1,+1,0,0
	AddTriangle s,0,1,2:AddTriangle s,0,2,3
	FreeBrush b
	ScaleMesh m,100,100,100
	FlipMesh m
	EntityFX m,1
	Return m
End Function

Function Reflect(Entity,Camera,RTexture)
	Local fxCamera = CreateCamera()
	HideEntity Entity
	WaterMapSize=TextureWidth(RTexture)
	CameraProjMode fxCamera,1
	CameraProjMode Camera,0
	CameraViewport fxCamera,0,0,WaterMapSize,WaterMapSize
	PositionEntity fxCamera,EntityX(Entity,1), EntityY(Entity,1) ,EntityZ(Entity,1),1; EntityY(Entity)
	; do left view
	SetCubeFace RTexture,0
	RotateEntity fxCamera,0,90,0
	RenderWorld
	CopyRect 0,0,WaterMapSize,WaterMapSize,0,0,BackBuffer(),TextureBuffer(RTexture)
	; do forward view
	SetCubeFace RTexture,1
	RotateEntity fxCamera,0,0,0
	RenderWorld
	CopyRect 0,0,WaterMapSize,WaterMapSize,0,0,BackBuffer(),TextureBuffer(RTexture)
	; do right view	
	SetCubeFace RTexture,2
	RotateEntity fxCamera,0,-90,0
	RenderWorld
	CopyRect 0,0,WaterMapSize,WaterMapSize,0,0,BackBuffer(),TextureBuffer(RTexture)
	; do backward view
	SetCubeFace RTexture,3
	RotateEntity fxCamera,0,180,0
	RenderWorld
	CopyRect 0,0,WaterMapSize,WaterMapSize,0,0,BackBuffer(),TextureBuffer(RTexture)
	; do up view
	SetCubeFace RTexture,4
	RotateEntity fxCamera,-90,0,0
	RenderWorld
	CopyRect 0,0,WaterMapSize,WaterMapSize,0,0,BackBuffer(),TextureBuffer(RTexture)		
	SetCubeFace RTexture,5
	RotateEntity fxCamera,90,0,0
	RenderWorld
	CopyRect 0,0,WaterMapSize,WaterMapSize,0,0,BackBuffer(),TextureBuffer(RTexture)	
	db=db+1
	CameraProjMode fxCamera,0
	CameraProjMode Camera,1
	ShowEntity Entity
End Function




Function CreateScene()
	SetFont LoadFont("Tahoma",13)
	AmbientLight 50,50,70
	
	Camera=CreateCamera()
	ScaleEntity Camera,1,1,1
	CameraRange Camera,1,10000
	
	
	PositionEntity Camera,0,40,95
	RotateEntity Camera,1,180,0
	
	Sky = LoadSkyBox ( "..\media\sky" )
	ScaleEntity Sky, 10, 10, 10
	EntityOrder sky,1
	
	Light=CreateLight(2)
	
	Center=CreatePivot()
	
	PositionEntity Light,50,80,0
	
	ReflectTex = CreateTexture(128,128,  128+256)
	
	tex1=LoadTexture("..\media\wall.jpg")
	ScaleTexture tex1,.3,.3
	tex2=LoadTexture("..\media\wood.jpg")
	
	modelR=LoadMesh( "..\media\teapot.x" )
	PositionEntity modelR,0,35,0
	ScaleEntity modelR, 15, 15, 15
	EntityTexture modelR, ReflectTex
	EntityShininess modelR,1
	
	Sphere=CreateSphere(16)
	PositionEntity Sphere,40,40, 0
	ScaleEntity Sphere,10,10,10
	EntityTexture Sphere, ReflectTex
	EntityShininess Sphere,1
	
	Cylinder=CreateCylinder(12,1)
	PositionEntity Cylinder,30,10,20
	ScaleEntity Cylinder,5,15,5
	RotateEntity Cylinder,91,0,0
	EntityTexture Cylinder,tex2
	
	
	
	cube2=LoadMesh("..\media\cube.3DS"); create receiver
	ScaleEntity cube2,180,2,180
	EntityTexture cube2,tex1
	
	cube3=CreateCylinder(16)
	ScaleMesh cube3,3,15,3
	sp = CreateSphere(12)
	ScaleMesh sp ,5,5,5
	PositionMesh sp,0,20,0
	AddMesh sp, cube3
	FreeEntity sp
	
	
	PositionEntity cube3,60,10,60
	EntityTexture cube3,tex2
	For i=1 To 3
		cuben = CopyEntity( cube3 )
		PositionEntity cuben,60,10,60 - (30*i)
	Next
	For i=1 To 3
		cuben = CopyEntity( cube3 )
		PositionEntity cuben,-60,10,60 - (30*i)
	Next
	For i=1 To 3
		cuben = CopyEntity( cube3 )
		PositionEntity cuben,60 - (30*i),10,60 
	Next
	For i=1 To 3
		cuben = CopyEntity( cube3 )
		PositionEntity cuben,60 - (30*i),10,-60
	Next
	
	cube4=CopyEntity( cube3 )
	PositionEntity cube4,-60,10,60
	
	cube5=CopyEntity( cube3 )
	PositionEntity cube5,60,10,-60
	
	cube6=CopyEntity( cube3 )
	PositionEntity cube6,-60,10,-60
	
	Reflect( ModelR,Camera,ReflectTex)
	MoveMouse GraphicsWidth()/2,GraphicsHeight()/2
End Function

Function AnimateScene()
	TurnEntity Light,0,.5,0
 	MoveEntity Light,.0,.0,.5
 	TurnEntity Sphere,0,.3,0
 	MoveEntity Sphere,.0,.0,.2
 	TurnEntity Cylinder,0,0,1
End Function

Function DrawHelper()
	Color 0,0,128
	If Help=0
		Text 10,10,FPS()+" fps"
		Text 10,30,"F1 - help"
	Else
		Text 10,10,FPS()+" fps"
		Text 10,30,"F1 - hide help"
		Text 10,50,"W,S,A,D keys for move"
		Text 10,70,"keys 1/2 - increase/decrease Blur alpha value: "+Str(BlurAlpha)
		Text 10,90,"keys 3/4 - increase/decrease Blur passes count: "+Str(BlurPasses)
		Text 10,110,"keys 5/6 - increase/decrease Blur radius: "+Str(BlurRadius)
		Text 10,130,"key 7 - change Blur quality: "+Str(BlurQuality)
		Text 10,150,"key 8 - change Blur color (random)"
		Text 10,170,"key 9 - enable/disable Blur alpha-texture: $"+Hex(BlurAlphaTexture)
	EndIf
End Function
</textarea> <br><br></td></tr></table><br>
<a name="996779"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RifRaf</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> perhaps you could do somthing like this ro remove multiple renders and the need for fastlibs.<br><br>1.remove texture / color outline color / blend multiply x2 / entityorder to draw last<br>2.copy the entity color it black. move it away from the camera a few units. normal blend, entityorder to draw last<br>3. renderworld<br>4.hide/remove black copy , reset entities normal texture,entityorder, and blendmode <br><br></td></tr></table><br>
<a name="996831"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Rroff</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> It might not be perfect but...<br><br>Make a copy of the model entity, scale it up slightly, use flipmesh to flip the normals, and use entityfx and colors, etc. to make the copy a colored outline. <br><br></td></tr></table><br>
<a name="996853"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >RifRaf</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> you dont have to flip it, since orders will have to be changed on the fly anyway just put the dark one in front of the main mesh and order it last.or scale it up and keep it in the same place.. (but i would think that movement would be less costly than scaling every frame on several entities but i could be wrong there) <br><br></td></tr></table><br>
<a name="996866"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> what if you do a normal render of your scene then hide everything but the objects you want the glow for... then you color them black, copy them flip the copies and scale/color blue/alpha them appropriatelythen render that with a different camera mode set to not overwrite the previous render... <br><br></td></tr></table><br>
<a name="996867"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Nate the Great</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> what if you do a normal render of your scene then hide everything but the objects you want the glow for... then you color them black, copy them flip the copies and scale/color blue/alpha them appropriatelythen render that with a different camera mode set to not overwrite the previous render... <br><br></td></tr></table><br>
<a name="996869"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >semar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks all, your suggestions were very inspiring; EntityOrder does the trick, even with animated meshes:<br><br><pre class=code>
Graphics3D 1024,768
cam = CreateCamera()

Global zombie = LoadAnimMesh("C:\Projects\BLITZ\models\zombie\zombie.b3d")

ShowEntity zombie
ScaleEntity zombie,.5,.5,.5

Global pivot = CreatePivot()
EntityParent zombie,pivot
PositionEntity pivot,0,-3,12


zo = CopyEntity(zombie,pivot)
ScaleEntity zo,.54,.5,.54

EntityColor zo,0,0,255
EntityOrder zombie,-1
EntityParent zo,pivot

tex = CreateTexture(2,2)
EntityTexture zombie,tex

sp# = .5
Animate zombie,1,sp
Animate zo,1,sp

light = CreateLight()
TurnEntity light,0,45,0

While Not KeyDown(1)


TurnEntity pivot,0,.5,0

UpdateWorld
RenderWorld
Flip

Wend
End
</pre><br><br>It works ! try also to comment the entitytexture thing.<br>The black inner model will cover any background though. Perhaps I should investigate about the command CaptureWorld.<br><br>Regards,<br>Sergio. <br><br></td></tr></table><br>
<a name="996894"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >D4NM4N</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> What you could do is:<br>- copy the object 3 times.<br>- color one total black, one light blue with 50%alpha and one darker blue with 25%alpha, use fullbright entityFX on all (may need a recursive set if mesh has children).<br>- scale/zposition the blue ones so they appear slightly larger respectively and <br>- then Zorder the lot so all appear in front of everything but the black one in the front of the other two.<br><br>I reckon that will be very close to what you want. With some adaption you could even make it trail slightly :) <br><br></td></tr></table><br>
<a name="996954"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> As the entity will be animated, you 'll have to animate the outline too.<br><br>An other way ( for non animated - simple objects ) could be to calculate the outline :<br>1/ find edge of the outline<br> +&gt; for each edges find if one vertex is z+ or z- from the camera and the other is z- or z+ ( or two are "0" or almost )<br>2/ push the edges<br> +&gt; using the camera projection, you can find the normal of the edge that is the cross vector from egde and camera vector<br> +&gt; build a second edge that will be the projection of the edge found along the vector with the size you setted up.<br>3/ build a single surface with all outline and texture it with a simple alpha texture <br><br></td></tr></table><br>
<a name="997024"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >D4NM4N</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> Not if you use my method above because the copies will animate. Plus, if you did want static trailing effects you could keep the black one and one blue transparent one animated and freeze the fading blue trail copies at their current frame when drawn. I reckon that would look wicked. <br><br></td></tr></table><br>
<a name="997209"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Mike0101</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Yasha sample is the perfect solution. With some tweek (entity visible or something) this gives the left4dead effect. You need the fastext library. <br><br></td></tr></table><br>
<a name="997393"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >semar</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Yasha,<br>hey what a cool piece of code - thank you !<br><br>I've tried it just now, and resembles exactly what I had in my mind.<br><br>It seems that this FastExtension library is quite good indeed. A nice add-on to the Blitz3D arsenal.<br><br>Cheers,<br>Sergio. <br><br></td></tr></table><br>
<a name="999114"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >neoshaman</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> i would the simpler one of having:<br>1) copy model<br>2) flip mesh and set it to additive blend<br>3) aply an en environment texture with black outline and whatever color gradient toward the center<br><br>No Z ordering to do<br>No library nor dll<br><br>and plus entity order work on an empty scene but not featured one (the draw would be behind the z sorted geometry or in front) <br><br></td></tr></table><br>
<a name="999178"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> The enviroment map fails on very "sharp" edges though. Saying that, you'll never get the perfect solution in blitz, as scaling an entity doesn't each vertex along it's normal, but rather scales from the middle, which isn't ideal. <br><br></td></tr></table><br>
<a name="999283"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ZJP</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> @Yasha,Thx.<br><br>JP <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
