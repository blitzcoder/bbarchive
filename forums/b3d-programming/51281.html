<!DOCTYPE html><html lang="en" ><head ><title >Mouse location on mesh</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Mouse location on mesh</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Mouse location on mesh</a><br><br>
<a name="572036"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grovesy</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Me again,<br><br>How would you go about finding the x,y,z locations of the point on a mesh where the mouse is pointer to?<br><br>Is it a simple case of collion detection between the mouse and all objects in your enviroment?<br><br>Just to help make this clear, I current have a flat plane (will eventually be a terrain style mesh) and want to get the mouse location when the user clicks the mouse. An object will then move to this location on the planes surface.<br><br>Thanks again <br><br></td></tr></table><br>
<a name="572041"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I had this old code on my HD that may help:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
	Graphics3D 800,600,32
	SetBuffer BackBuffer()

	WireFrame 0
	AntiAlias 0

	SeedRnd MilliSecs()

	Global frame_count%
	Global fps%
	Global slowest_fps%
	Global fps_timeout%
	Global frame_time%
	Global slowest_frame%
	Global frame_start%
	fps_timer = CreateTimer(60)
	slowmo% = False
	wiref% = False
	
	cam = CreateCamera()
	PositionEntity cam,0,0,-15
	
	light = CreateLight()
	
	plane = CreatePlane()
	RotateEntity plane,-90,0,0
	EntityColor plane,0,0,100
	EntityPickMode plane,2
	
	; Main loop
	While Not KeyHit(1)
		frame_start = MilliSecs()
	
		If KeyHit(28) Then slowmo = Not slowmo
		If KeyHit(14)
			wiref = Not wiref
			WireFrame wiref
		EndIf

		If MouseHit(1)
			pent = CameraPick(cam,MouseX(),MouseY())
			px# = PickedX()
			py# = PickedY()
			pz# = PickedZ()
		EndIf
	

		UpdateWorld
		RenderWorld
		
		Rect MouseX(),MouseY(),2,2,1
		Text 10,100,"Entity: "+pent
		Text 10,120,"x: "+px
		Text 10,130,"y: "+py
		Text 10,140,"z: "+pz
		frame_time = MilliSecs() - frame_start	
		show_info()

		WaitTimer(fps_timer)
		Flip(1)

		If slowmo Then Delay 200
	Wend

	ClearWorld	

	End


;
; Display debug info
;
Function show_info()
	
	If fps_timeout
		frame_count = frame_count + 1

		If MilliSecs() &gt; fps_timeout Then
			fps_timeout = MilliSecs() + 1000 
			fps = frame_count 
			frame_count = 0 
		
			If fps &lt; slowest_fps Or slowest_fps = 0 Then slowest_fps = fps
		EndIf 
		
		If frame_time &gt; slowest_frame Then slowest_frame = frame_time
		
		Color 0,255,0
		Text 10,10," Triangles: " + TrisRendered()
		Color 255,255,0
		Text 10,25," Millisecs: " + frame_time
		Text 10,40,"   Slowest: " + slowest_frame
		Color 0,255,255
		Text 10,55,"       FPS: " + fps
		Text 10,70,"     Worst: " + slowest_fps
		Color 255,255,255
	Else
		; First call initialization.
		fps_timeout = MilliSecs() + 1000 
	EndIf
	
End Function
</textarea> <br><br></td></tr></table><br>
<a name="572332"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >gpete</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> heres a little better version:<br><br><pre class=code>
Graphics3D 800,600,32
	SetBuffer BackBuffer()

	

	SeedRnd MilliSecs()

	Global frame_count%
	Global fps%
	Global slowest_fps%
	Global fps_timeout%
	Global frame_time%
	Global slowest_frame%
	Global frame_start%
	fps_timer = CreateTimer(60)
	slowmo% = False
	wiref% = False
	
	cam = CreateCamera()
	PositionEntity cam,0,50,-35
	
	
	light = CreateLight()
	
	plane = CreatePlane()
	;RotateEntity plane,-90,0,0
	EntityColor plane,80,55,100
	NameEntity plane,"The Plane"
	EntityPickMode plane,2
	PositionEntity plane,0,0,0
	
	; Main loop
	While Not KeyHit(1)
		frame_start = MilliSecs()
	
		If KeyHit(28) Then slowmo = Not slowmo
		;If KeyHit(14)
		;	wiref = Not wiref
		;	WireFrame wiref
		;EndIf

		If MouseHit(1)
			pent =CameraPick(cam,MouseX(),MouseY())
			pentt$=EntityName$(pent)
			px# = PickedX()
			py# = PickedY()
			pz# = PickedZ()
		EndIf
	


		UpdateWorld
		RenderWorld
		
		Rect MouseX(),MouseY(),2,2,1
		Text 10,100,"Entity: "+pentt$
		Text 10,120,"x: "+px
		Text 10,130,"y: "+py
		Text 10,140,"z: "+pz
		frame_time = MilliSecs() - frame_start	
		show_info()

		WaitTimer(fps_timer)
		Flip(1)

		If slowmo Then Delay 200
	Wend

	ClearWorld	

	End


;
; Display debug info
;
Function show_info()
	
	If fps_timeout
		frame_count = frame_count + 1

		If MilliSecs() &gt; fps_timeout Then
			fps_timeout = MilliSecs() + 1000 
			fps = frame_count 
			frame_count = 0 
		
			If fps &lt; slowest_fps Or slowest_fps = 0 Then slowest_fps = fps
		EndIf 
		
		If frame_time &gt; slowest_frame Then slowest_frame = frame_time
		
		Color 0,255,0
		Text 10,10," Triangles: " + TrisRendered()
		Color 255,255,0
		Text 10,25," Millisecs: " + frame_time
		Text 10,40,"   Slowest: " + slowest_frame
		Color 0,255,255
		Text 10,55,"       FPS: " + fps
		Text 10,70,"     Worst: " + slowest_fps
		Color 255,255,255
	Else
		; First call initialization.
		fps_timeout = MilliSecs() + 1000 
	EndIf
	
End Function
</pre><br><br>For more fun, make a height map and replace the plane with a textured terrain,  then the pickY() will give you numbers generally greater than 0.0 <br><br></td></tr></table><br>
<a name="572427"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >big10p</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> heres a little better version: <br></div><br>How dare you, sir! :P <br><br></td></tr></table><br>
<a name="572991"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grovesy</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the help guys, unfortunatly my code isn't working quite how I was expecting.<br><br>I was hoping that were i click on the plane my object will be relocated to that position, but its appears to jump around!<br><br><pre class=code>Graphics3D 640,480
SetBuffer BackBuffer()

Global PlayerX# = 0
Global PlayerY# = 0
Global PlayerZ# = 0

Global cube=CreateCube()
PositionEntity cube,PlayerX,PlayerZ,PlayerY
ScaleEntity cube,0.2,0.2,0.2
EntityColor cube,100,100,100

Global camera=CreateCamera()
PositionEntity camera,PlayerX,5,PlayerY-5
PointEntity camera,cube

look=CreatePivot()
PositionEntity look,PlayerX,0,PlayerY
PointEntity camera,look
EntityParent camera,look

light=CreateLight()
RotateEntity light,90,0,00

;Create plane
plane=CreatePlane()
EntityColor plane,100,120,100

NameEntity plane,"The Plane"
EntityPickMode plane,2

While Not KeyDown( 1 )
If KeyDown( 205 )=True Then TurnEntity camera,0,-1,0
If KeyDown( 203 )=True Then TurnEntity camera,0,1,0
If KeyDown( 208 )=True Then MoveEntity camera,0,0,-0.05
If KeyDown( 200 )=True Then MoveEntity camera,0,0,0.05
	
	check_mouse()
	update_player()
	
	RenderWorld
	Text 0,40,"PickedX: "+PickedX#()
	Text 0,60,"PickedY: "+PickedY#()
	Text 0,80,"PickedZ: "+PickedZ#()
	Text 0,0,"Use cursor keys to move about the infinite plane "
	Flip
Wend
End 

Function check_mouse()
	If MouseHit(1)=True
		CameraPick(camera,MouseX(),MouseY())
	
		PlayerX = PickedX#()
		PlayerY = PickedY#()
		PlayerZ = PickedZ#()
	EndIf
	
End Function

Function update_player()
	PositionEntity cube,PlayerX,PlayerZ,PlayerY
End Function</pre><br><br>Any ideas?<br>Cheers <br><br></td></tr></table><br>
<a name="573001"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >ratking</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> The error is on this line:<br><pre class=code>PositionEntity cube,PlayerX,PlayerZ,PlayerY</pre> <br><br></td></tr></table><br>
<a name="573337"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Grovesy</td><td align="right"><font class=tiny>(Posted 2005)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Doh! How silly do i feel.<br><br>Thanks for your help all. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
