<!DOCTYPE html><html lang="en" ><head ><title >Simple Code Searcher</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Simple Code Searcher</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >Simple Code Searcher</a><br><br>
<a name="1177762"></a>

<a name="1177763"></a>

<a name="1177764"></a>

<a name="1177766"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >virtlands</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi, most of us have lots of *.BB or *.BMX files lying about, and probably 1000's of them in a directory, ...<br><br> In my case I have over 5000 BB files (in various dirs).<br><br> Sometimes I forget where certain code is (whether it's made by me or someone else). <img src="http://i411.photobucket.com/albums/pp194/aeDream/CodeProjEmoticons/smiley_confused_zpsc9edebf5.gif"><br><br> So, I thought of making this program here, called "<b>Simple Code Searcher</b>".<br><br> What it does is scan any list of directories that you configure it to scan; It shall scan the AppDir (where the program is) by default.<br><br>How it works:<br>You input a keyword to search for, and it displays all lines in the <br>BB or BMX files that contain that keyword.<br><br>One very popular keyword to search for = "<u>FUNCTION</u>", <br>because we forget where certain code is that we made sometimes.<br><br>I made this in a hurry, took about 3 hours to make it, so it's not something fancy. <br>It won't recurse to deeper sub-directories, so if necessary just make a copy all your BBs into 1 place, so that it can have <i>access</i>.<br><br>You can download it from this link if you prefer:<br><a href="http://uploadingit.com/file/g9tuwkpcoiv38p2q/SimpleCodeSearcher.zip" target="_blank">http://uploadingit.com/file/g9tuwkpcoiv38p2q/SimpleCodeSearcher.zip</a><br><br>Here's the B3D program:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
 ;;; Simple Code Searcher 
 ;;; created by VirtLands
 ;;; Feb 20, 2013

 ; The purpose of this program is to look through *.BB files and *.BMX files
 ; to search for special search strings, and then display what's found.
 ;     
 ;
 ; --- For example, search for "FUNCTION" to display all Function names, 
 ; --- or search for "DIM" or "Global"
 ;
 Global Appdir$   = SystemProperty$("AppDir")
 Global pathsMax = 10

 Global resultsMax =1000
 Global resultsPtr = 0

 Dim result$(resultsMax)
 Dim pathfound(resultsMax)

 Global IGNORE_COMMENTS=True  ;;  Set this to True to avoid displaying commented stuff.

 Dim path$(pathsMax)    ;; As your option, give search path names to any members of this array....

 ;; The *.bb or *.bmx files are expected to be within any of the following user-supplied directories
 ;; No depth recursion is available yet in this version (1).

 path(0) = AppDir$                       ;; The default is to search the same directory that this program is in 
 path(1) = "C:\Blitz3d Projects\"    ;; Replace these paths with your own Directories that YOU have.
 path(2) = "C:\wonderlands\rtw\"
 path(3) = "C:\Blitz3D\"

 ;path(4) = "."
 ;path(5) = "."      ;; etc...
 ;path(10) = "."


 Graphics 1024,700,32,2   
 ClsColor 22,121,170   
 
 F_height =15 : F_bold = False : F_italic = False : F_under = False 
 font = LoadFont("arial", F_height, F_bold, F_italic, F_under) 
 SetFont font 

 title$ = " Simple BB Code Searcher v1.0, [ by VirtLands ] "  

 s$ = "" : search$ = "" : file$ = "" : filesearch$ = ""
 Local LinePtr
 
 AppTitle Title 
      .Start
      Cls              
      Print
      Print Title + " { This program searches through your *.BB files } "
      Print
      Print" Please enter a search string :  ( any piece of code to search for,  such as "+Q("FUNCTION")+" .)  "
      Print 
      search = Trim$(Lower$(Input$(" &gt; ")))
      If Len(search)=0 Then search = "function"   ;; search for functions by default
      Cls : Locate 1,1 

 For z=0 To pathsMax
          If Len(path(z))
                  ft = FileType( lastslash( path(z) ))
                  If ft=2       
                            Readpath = ReadDir( path(z) )
                            Repeat 
                                        Repeat
                                                file = Lower$(NextFile(Readpath))
                                                L = Len(file)
                                                If L=0 
                                                         CloseDir Readpath
                                                         Exit 
                                                End If 
                                                If FileType(file)=1                                                          
                                                           If L&gt;2 Then If Mid$(file, L-2, 3) = ".bb" Then Exit   
                                                           If L&gt;3 Then If Mid$(file, L-3, 4) = ".bmx" Then Exit                                                                                              
                                                End If 
                                        Forever
                                        If L=0 Then Exit      
                                  
                                        If True                                                                                                                                        
                                                             resultsCount = 0 
                                                             filesearch = " ______Searchig through file "+Q(file)+" _______"                                                                
                                                                                                                    
                                                             R = ReadFile(file)
                                                             Repeat                                                              
                                                                            s = Trim(Lower$(ReadLine(R)))
                                                                            If IGNORE_COMMENTS=True 
                                                                                     i = Instr(s,";")
                                                                                     If i Then s = Left$(s,i-1)
                                                                            End If 

                                                                            i = Instr(s,search)
                                                                            If i
                                                                                 If search="function"                                                                                       
                                                                                       If i=1    
                                                                                       resultsCount = resultsCount +1                                                                                   
                                                                                       result(resultsCount) = S                                                                                      
                                                                                       If resultsMax - resultsCount &lt;3
                                                                                                 resultsMax = resultsMax +400
                                                                                                 Dim result(resultsMax)
                                                                                       End If  
                                                                                       End If 
                                                                                 Else 
                                                                                       resultsCount = resultsCount +1                                                                                   
                                                                                       result(resultsCount) = S     
                                                                                       If resultsMax - resultsCount &lt;3
                                                                                                 resultsMax = resultsMax +400
                                                                                                 Dim result(resultsMax)
                                                                                       End If
                                                                                 End If
                                                                            End If
                                                             Until Eof(R)
                                                             CloseFile R
                                                             result(resultsCount+1) = ""   ;; End the list with a blank   
                                                   End If  

                                                   If resultsCount&gt;0
                                                   Print 
                                                   Print filesearch 
                                                   LinePtr = LinePtr +2

                                                   For x = 1 To resultsCount
                                                              Print "    "+result(x)
                                                              LinePtr = LinePtr +1
                                                              If LinePtr&gt;=45
                                                                         key=WaitKey()                                                                         
                                                                         Cls:Locate 1,1
                                                                         If key=27 Then  
                                                                                   z=  pathsMax +1
                                                                                   Goto SkipData
                                                                         End If 

                                                                         If x&lt;resultsCount
                                                                                  Print 
                                                                                  Print filesearch+" .. "
                                                                                  LinePtr = 2
                                                                         Else 
                                                                                  LinePtr =0
                                                                         End If 
                                                              End If 
                                                   Next    
                                                   .SkipData  
                                                   If key=27 Then Exit                                                                                                                                
                                        End If 
                            Forever     
                  End If 
         End If 
 Next 
 
 WaitKey()
 key=0 : LinePtr=0 : resultsCount=0
 Goto Start

 WaitKey():End 
 
Function LastSlash$(s$)    ; Gives a last slash to any string that is missing one
     L = Len(s$)
     If L=0 Then Return "\"

     If Mid$(s$,L,1) = "\" 
              Return s$
     Else  Return s$+"\"

     End If
End Function

Function Q$(s$)
     Local chr34$ = Chr$(34)
     Return Chr34 + s + Chr34
End Function  </textarea><br><br>; The program is coded to ignore comments, but if you want<br>; it to search through comments then just change the Global Variable as you wish. <br>Global IGNORE_COMMENTS=True  <br><br>; There is a portion of the code that has this path(..) array:<br>; Just change these directories to your own directories that you <br>; wish to search; <br><br> path(0) = AppDir$                  ;; The default <br> path(1) = "C:\Blitz3d Projects\"   ;; Replace these <br> path(2) = "C:\wonderlands\rtw\"<br> path(3) = "C:\Blitz3D\"<br><br> ;path(4) = "."<br> ;path(5) = "."      ;; etc...<br> ;path(10) = "."<br>;---------------------------------------------------------------<br><br>Here's a sample screenshot where it searches for the keyword: <b>FUNCTION</b><br><img src="http://uploadingit.com/file/x1pubapbtmkacf1v/Function_search.jpg"><br><br>You can search for anything you want, like "DIM", or "bit" or whatever.<br><br>If you think this is useful or not, let me know. <img src="http://i411.photobucket.com/albums/pp194/aeDream/CodeProjEmoticons/smiley_biggrin_002_zpsaf2350ef.gif"> <br><br></td></tr></table><br>
<a name="1177888"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Axel Wheeler</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Nice job!<br><br>FYI, The AppDir location doesn't actually scan the directory the bb file (your program) is in, it has to be compiled (exe).  But then it works.<br><br>When I put an absolute path into the path$ array, it works fine also.<br><br>Anyway, thanks! <br><br></td></tr></table><br>
<a name="1177897"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >virtlands</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Glad you tried it.  <br><br>There is this free program called <b>UltraFileSearch</b> that does similar. <img src="http://i411.photobucket.com/albums/pp194/aeDream/MSN%20emoticons/coffee.gif"><br><br><a href="http://www.ultrafilesearch.com/download.html" target="_blank">http://www.ultrafilesearch.com/download.html</a><br><br>{The basic difference is that my prog displays the actual lines of info.) <br><br></td></tr></table><br>
<a name="1179384"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> you could try with a recursive function<br>it's way easier to search for keywords.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
Const IGNORE_COMMENTS=True  ;;  Set this to True to avoid displaying commented stuff.
Graphics 1800,900,0,2
Local dir$ = CurrentDir() ; Your Path here

; write results in a file
	Local out=WriteFile(dir+"\SearchResults.txt")
	
; start search
	Global totalfiles%=0, foundfiles%=0
	Local t0%=MilliSecs()
	SearchKeywordInDir("function", dir,out,-1,False)
	Local t1%=MilliSecs()

; close the result file
	CloseFile out

Print "Computed "+foundfiles+" files (total : "+totalfiles+") in : "+(t1-t0)+".ms"
WaitKey
End

; @RecurseDepth &lt;0 : full recursive, else stop recursive search after @RecurseDepth loop
Function SearchKeywordInDir(keyword$,dir$,streamout,RecurseDepth=-1,casesensitive%=False)
	If RecurseDepth=0 Then Return
	dir=Replace(dir,"/","\") : If Right(dir,1)&lt;&gt;"\" Then dir=dir+"\"
	Local d=ReadDir(dir) : If Not(d) Then Return
	If Not(casesensitive) Then keyword=Lower(keyword)
	Repeat
		Local f$=NextFile(d)
		If f="" Then Exit
		If f&lt;&gt;"." And f&lt;&gt;".."
			; recursive search in directories
			If FileType(dir+f)=2 : SearchKeywordInDir(keyword,dir+f,streamout,RecurseDepth-1,casesensitive)
			Else
				; search In file
				If Lower(FileExt(f))="bb" Or Lower(FileExt(f))="bmx"
					totalfiles=totalfiles+1
					Local com$ = "'" : If Lower(FileExt(f))="bb" Then com = ";"
					Local stream=ReadFile(dir+f), firstfound%=True
					If stream
						While Not(Eof(stream))
							Local l$=Trim(ReadLine(stream))
							If Len(l)
								If (Left(l,1)&lt;&gt;com) Or (IGNORE_COMMENTS=False)
									Local found%=False
									If casesensitive:found=Instr(l,keyword)
									Else:found=Instr(Lower(l),keyword)
									EndIf
									If found
										If firstfound Then WriteLine streamout, "In File : '"+dir+f+"'":firstfound=False:foundfiles=foundfiles+1
										WriteLine streamout, Chr(9)+"&gt; "+l
									EndIf
								EndIf
							EndIf
						Wend
						CloseFile stream
					EndIf
				EndIf
			EndIf
		EndIf
	Forever
	CloseDir(d)
End Function
Function FileExt$(file$)
	Local i%:For i=Len(file) To 1 Step -1:If Mid(file,i,1)=".":Return Right(file,Len(file)-i):EndIf:Next
End Function

</textarea><br><br>this code will export the results in the "out" file. <br><br></td></tr></table><br>
<a name="1179686"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Addi</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Am I allowed to modify your code?<br>I would like to include a GUI and a menu for options. <br><br></td></tr></table><br>
<a name="1179692"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Bobysait</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Codes deposed on the forum without copyright or explicit license are generally "open" and free to use.<br><br>(at least, I assume you can use mine) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
