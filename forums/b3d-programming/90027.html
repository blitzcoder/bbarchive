<!DOCTYPE html><html lang="en" ><head ><title >how do you get a child entities location</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >how do you get a child entities location</h1><a href="forums.php" >Blitz3D Forums</a>/<a href="topics.php?forum=82" >Blitz3D Programming</a>/<a href="#bottom" >how do you get a child entities location</a><br><br>
<a name="1023093"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> So i have a parent pivot at 0,0,0 and have many children scattered around the parent in every direction, but when I call getchild() then ask for the distance between the child and camera i get the distance between the camera and parent.  if i use getchild() and ask for entityX , y , z with the global position relative to 0,0,0 on, i still get the parent to camera distance.  <br><br>I am working on a tiled geosphere, it works, but i want to have only tiles that are in range to be rendered.  So if a multi-million poly geosphere is created only a few thousand poly's need to be shown.<br><br><pre class=code>
Function HideShowTiles(Source,Parent,Distance#)
	For i = 1 To CountChildren(Parent)
		ent = GetChild(Parent,i)
		eX# = EntityX#(ent,1):eY# = EntityY#(ent,1):eZ# = EntityZ#(ent,1)
		sX# = EntityX#(Source,1):sY# = EntityY#(Source,1):sZ# = EntityZ#(Source,1)
		Dist# = Sqr((eX#-sX#)*(eX#-sX#) + (eY#-sY#)*(eY#-sY#) + (eZ#-sZ#)*(eZ#-sZ#))
		If dist# &lt; Distance# Then 
			ShowEntity (ent)
		Else
			HideEntity (ent)
		EndIf
	Next
End Function

</pre> <br><br></td></tr></table><br>
<a name="1023096"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Ross C</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Are you parenting these entities in blitz, or are you loading a mesh containing child entities? <br><br></td></tr></table><br>
<a name="1023097"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I am generating the entities in Blitz.  So yes parenting in Blitz. <br><br></td></tr></table><br>
<a name="1023099"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> TheWhjat sort of scales are you using? How far (should) the children be from the parent (roughly) in blitz units?<br><br>I think this line may be a root of the problem:<br><pre class=code>
Sqr((eX#-sX#)*(eX#-sX#) + (eY#-sY#)*(eY#-sY#) + (eZ#-sZ#)*(eZ#-sZ#))
</pre> <br><br></td></tr></table><br>
<a name="1023107"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Well right now the geosphere is scaled by 100, from a normal createsphere() size.  the camera is  150 units from the parent<br>the parent is located at 0,0,0.<br>range to be rendered is 50.<br>but the moment a single tile is rendered all tiles are rendered.<br>plus the camera is inside the geosphere.<br><br>I'm starting to think that because all tiles are created relative to the global 0,0,0 that when the geosphere is scaled, Blitz uses the 0,0,0 location of the tiles created point, and because all are tiles created within the size of -.5 to .5 then when the camera is within the render range distance, then all the tiles are then rendered.  I hope that explains it.   <br><br>erm, is there a mesh distance, IE take the average of all the vertexes in a tile and use that scaled value to determine the distance to camera. that should work.  or maybe a single vertex would work. <br><br></td></tr></table><br>
<a name="1023109"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >_PJ_</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Oh right :)<br><br>I just realised... yoou're not Showing the parent after this function is called, are you?<br>Obviously when Show or Hide Entity is used on a parent, then it will affect the children too. <br><br></td></tr></table><br>
<a name="1023112"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Malice, Didn't work.  I'm not hiding or showing the parent, just the children. <br><br></td></tr></table><br>
<a name="1023115"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> oh yep, if I use a scaled vertex  then it will show when within the distance range.  but.... as the camera moves so does the vertex in opposite direction making visible tiles move opposing to the camera.  not good, I'm guessing part of the Blitz3d engine. hmm... <br><br></td></tr></table><br>
<a name="1023122"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MusicianKool</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> Here is the source.  Fixed and works.  sorry about the poor commenting.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
ms = MilliSecs()
Graphics3D 800,600,32,2
SetBuffer BackBuffer()
SeedRnd MilliSecs()
Tiles = 16
Segments = 16
Dim HeightMap(6):Dim Planet_Texture (6):Dim HeightMaps(0,0)
Dim planetTiled#(1,1)
Dim GradientR%(0),GradientG%(0),GradientB%(0),Percent#(0),Red%(0),Green%(0),Blue%(0)
Restore Planet
Global DEPTH = 256
CreateGradient(10,DEPTH)

MeshBank = CreateBank()

;#Region GeoSphere &amp; segmented cube

; transform a cube to sphere Using Heightmap Data
Function GeodesicHeightmapedSphere(SphereHeightMap,SegMent,Scale#=.25,Parent=0)
	IW = ImageWidth(SphereHeightMap)
	QS = IW / 4
	Local s%,surf%,vc%
	Local vx#,vy#,vz#,v#,u#
	LockBuffer (ImageBuffer (SphereHeightMap))
	child = GetChild(parent,SegMent)
	For s=1 To CountSurfaces(child)
		surf=GetSurface(child,s)
		For vc=0 To CountVertices(surf)-1	
			vx#=VertexX(surf,vc)
			vy#=VertexY(surf,vc)
			vz#=VertexZ(surf,vc)
			u# = VertexU(surf,vc)*(ImageWidth(SphereHeightMap)-1)
			v# = VertexV(surf,vc)*(ImageHeight(SphereHeightMap)-1)
			
			;DebugLog (u) + "   " +  (v) + "      " + "Segment: "+SegMent
			
			argb = ReadPixelFast( Int(u),Int(v),ImageBuffer(SphereHeightMap))
			cc = Get_RED_From(argb)
			
			height# = (Float cc/ Float DEPTH)*Scale
			ra# = Float 1+height#;.985 + (height#)
			Spherical(vx#,vy#,vz#,ra#)
			sx# = Spherical_X#()
			sy# = Spherical_Y#()
			sz# = Spherical_Z#()
			VertexCoords surf,vc,sx#,sy#,sz#
		Next			
	Next
	UnlockBuffer (ImageBuffer (SphereHeightMap))
	Return ParentPiv
End Function

; creates a segmented cube
Function CreateSegCube(tiles = 1,segs=1,parent=0)
	Local side%,surf%
	Local a%,v#,b%,u#
	Local v0%,v1%,v2%,v3%

	; sides
	For side=0 To 3
		For t = 0 To tiles-1
			For q = 0 To tiles-1
				mesh%=CreateMesh( parent )
				surf=CreateSurface( mesh )
				
				For a=0 To segs
					v=((a+(t*segs))/Float(segs))/tiles;y axis
					For b=0 To segs
						u=((b+(q*segs))/Float(segs))/tiles ;x axis
						AddVertex(surf,u-.5,v-.5,0.5,u,v)	
					Next		
				Next
				For a=0 To segs-1
					For b=0 To segs-1	
						v0=a*(segs+1)+b:v1=v0+1
						v2=(a+1)*(segs+1)+b+1:v3=v2-1
						AddTriangle( surf,v0,v1,v2 )
						AddTriangle( surf,v0,v2,v3 )	
					Next
				Next
				Select True
					Case side = 0 
						RotateMesh mesh,0,-90,0
					Case side = 1
						RotateMesh mesh,0,180,0
					Case side = 2
						RotateMesh mesh,0,90,0
					Case side = 3
						RotateMesh mesh,0,0,0
				End Select
			Next
		Next
	Next
	;top bottom
	For side=0 To 1
		For t = 0 To tiles-1
			For q = 0 To tiles-1
				mesh%=CreateMesh( parent )
				surf=CreateSurface( mesh )	
				For a=0 To segs
					v=(Float (a+(t*segs))/Float(segs))/tiles;y axis
					For b=0 To segs
						u=(Float (b+(q*segs))/Float(segs))/tiles;x axis
						AddVertex(surf,u-.5,v-.5,0.5,u,v)
					Next
				Next
				
				For a=0 To segs-1
					For b=0 To segs-1
						v0=a*(segs+1)+b:v1=v0+1
						v2=(a+1)*(segs+1)+b+1:v3=v2-1
						AddTriangle( surf,v0,v1,v2 )
						AddTriangle( surf,v0,v2,v3 )
					Next
				Next
				Select True
					Case side = 0
						RotateMesh mesh,90,-90,-90
					Case side = 1
						RotateMesh mesh,-90,-90,90
				End Select
			Next
		Next
	Next
	
	RotateEntity parent,0,0,180
End Function

; creates a segmented cube
Function CreateSegCube2(Tiles = 1,segs=20,parent=0)
	;Tiles = Tiles * 4
	;ResizeBank (MeshBank,Tiles*6)
	Local side%,surf%
	Local stx#,sty#,stp#,y#
	Local a%,x#,v#,b%,u#
	Local v0%,v1%,v2%,v3%
	Local mesh% ;= CreateMesh(parent)
	;PokeInt MeshBank,0,CreateMesh(parent)
	
	; sides
	For side=0 To 3
		;For MeshTile = 0 To Tiles
			mesh=CreateMesh(parent);PeekInt MeshBank,MeshTile*4
			surf=CreateSurface( mesh )
			stx=-.5
			sty=stx
			stp=Float(1)/Float(segs)
			y=sty
			For a=0 To segs
				x=stx
				v=(Float .25)+(a/Float(segs))*(Float .25);y axis
				For b=0 To segs
					Select True
						Case side = 0
							u=(Float 0.0)+(b/Float(segs))*(Float .25) ;x axis
						Case side = 1
							u=(Float .25)+(b/Float(segs))*(Float .25) ;x axis
						Case side = 2
							u=(Float 0.5)+(b/Float(segs))*(Float .25) ;x axis
						Case side = 3
							u=(Float .75)+(b/Float(segs))*(Float .25) ;x axis
					End Select
					AddVertex(surf,x,y,0.5,u,v)
					x=x+stp	
				Next	
				y=y+stp	
			Next
			For a=0 To segs-1
				For b=0 To segs-1
					v0=a*(segs+1)+b:v1=v0+1
					v2=(a+1)*(segs+1)+b+1:v3=v2-1
					AddTriangle( surf,v0,v1,v2 )
					AddTriangle( surf,v0,v2,v3 )	
				Next
			Next
			Select True
				Case side = 0 
					RotateMesh mesh,0,-90,0
				Case side = 1
					RotateMesh mesh,0,180,0
				Case side = 2
					RotateMesh mesh,0,90,0
				Case side = 3
					RotateMesh mesh,0,0,0
			End Select
		;Next
	Next
	;top bottom
	For side=0 To 1
		mesh%=CreateMesh( parent )
		surf=CreateSurface( mesh )
		stx#=-.5
		sty#=stx
		stp#=Float(1)/Float(segs)
		y#=sty
		
		For a=0 To segs
			x#=stx
			If side = 0 Then
				v=(Float 0.0)+(a/Float(segs))*(Float .25);y axis
			Else
				v=(Float .50)+(a/Float(segs))*(Float .25);y axis
			EndIf
			For b=0 To segs
				u=(Float .25)+(b/Float(segs))*(Float .25) ;x axis
				AddVertex(surf,x,y,0.5,u,v)
				x=x+stp
			Next
			y=y+stp
		Next
		
		For a=0 To segs-1
			For b=0 To segs-1
				v0=a*(segs+1)+b:v1=v0+1
				v2=(a+1)*(segs+1)+b+1:v3=v2-1
				AddTriangle( surf,v0,v1,v2 )
				AddTriangle( surf,v0,v2,v3 )
			Next
		Next
		Select True
			Case side = 0
				RotateMesh mesh,90,-90,-90
			Case side = 1
				RotateMesh mesh,-90,-90,90
		End Select
	Next
End Function

;#End Region 

;#Region Heightmap Creator

Function landtex(img)
	IW = ImageWidth(img)-1
	LockBuffer ImageBuffer(img)
	For x = 0 To IW
		For y = 0 To IW
			Select True
				Case planetTiled(x,y) &lt;= 255
					r = planetTiled(x,y):g=0:b=0
				Case planetTiled(x,y) &gt; 256 And planetTiled(x,y) &lt;= 511
					r=255:g = planetTiled(x,y)-256:b=0
				Case planetTiled(x,y) &gt; 511
					r=255:g=255:b = planetTiled(x,y)-511
			End Select
			WritePixelFast x,y,Get_ARGB_From(Int(r),Int(r),Int(r)),ImageBuffer(img)
		Next
	Next
	UnlockBuffer ImageBuffer(img)
End Function

Function FLine(x1,y1,x2,y2,gray,px,py)
	xd=(x2-x1)
	yd=(y2-y1)
	If Abs(xd)&gt;Abs(yd)
		If x1&gt;x2
			temp=x1
			x1=x2
			x2=temp
			temp=y1
			y1=y2
			y2=temp
			xd=(x2-x1)
			yd=(y2-y1)
		EndIf
		grad#=Float yd/Float xd
		If grad#=0.0 Then grad#=0.0001
		yf#=Float y1+grad#
		For x=x1 To x2 Step 1
			tx = Abs(Int(x Mod px))
			yf# = Abs(yf# Mod py)
			planetTiled (tx,Int(yf#))=gray
			yf#=yf#+(grad#*1.0)
			
		Next
	Else
		If y1&gt;y2
			temp=x1
			x1=x2
			x2=temp
			temp=y1
			y1=y2
			y2=temp
			xd=(x2-x1)
			yd=(y2-y1)
		EndIf
		grad#=Float xd/Float yd
		If grad#=0.0 Then grad#=0.0001
		xf#=Float x1+grad#
		For y=y1 To y2 Step 1
			ty = Abs(Int(y Mod py))
			xf# = Abs(xf# Mod px)
			planetTiled (Int(xf#),ty)=gray
			xf#=xf#+(grad#*1.0)
			
		Next
	EndIf
End Function

Function CreateHeightMap(Image)
	px = ImageWidth(Image)-1
	py = ImageHeight(Image)-1
	Dim planetTiled#(px,py)
	
	For x = 0 To px
		For y = 0 To py
			planetTiled#(x,y) = Rand(DEPTH/2)
		Next
	Next
	landtex(Image)
	Return Image
End Function 

Function SmoothenHeightMaps(Passes=1,Roughness=0)
	IW = ImageWidth (HeightMap(1))-1
	For pass = 1 To Passes
		For i = 1 To 6
			LockBuffer ImageBuffer(HeightMap(i))
			For x = 0 To IW
				For y = 0 To IW
					tx = x:ty = y
					x = x - 1
					If x &gt; 0 Then x = IW
					argb = ReadPixelFast (x,y,ImageBuffer(HeightMap(i)))
					avg# = avg# + Get_RED_From(argb)
					x = tx:y = ty
					y = y - 1
					If y&gt;0 Then y = iw
					argb = ReadPixelFast (x,y,ImageBuffer(HeightMap(i)))
					avg# = avg#+Get_RED_From(argb)
					x = tx:y = ty
					x = x + 1
					If x &gt; iw Then x = 0
					argb = ReadPixelFast (x,y,ImageBuffer(HeightMap(i)))
					avg# = avg#+Get_RED_From(argb)
					x = tx:y = ty
					y = y + 1
					If y&gt;iw Then y = 0
					argb = ReadPixelFast (x,y,ImageBuffer(HeightMap(i)))
					avg# = avg#+Get_RED_From(argb)
					x = tx:y = ty
					argb = ReadPixelFast (x,y,ImageBuffer(HeightMap(i)))
					avg# = avg#+Get_RED_From(argb)
					avg# = avg#/5+Rand(-Roughness,Roughness)
					WritePixelFast x,y,Get_ARGB_From(avg,avg,avg),ImageBuffer(HeightMap(i))
				Next
			Next
			UnlockBuffer ImageBuffer(HeightMap(i))
		Next
		EdgeConnector()
	Next
	EdgeConnector()
End Function

Function EdgeConnector()
	IW = ImageWidth(HeightMap(1))-1
	;#Region (Image 1)
	;left (1)
	LockBuffer ImageBuffer(HeightMap(1))
	LockBuffer ImageBuffer(HeightMap(4))
	For y = 0 To IW
		argb = ReadPixelFast (0,y,ImageBuffer(HeightMap(1)))
		WritePixelFast iw,y,argb,ImageBuffer(HeightMap(4))
	Next
	UnlockBuffer ImageBuffer(HeightMap(1))
	UnlockBuffer ImageBuffer(HeightMap(4))
	;top (1)
	LockBuffer ImageBuffer(HeightMap(1))
	LockBuffer ImageBuffer(HeightMap(5))
	For x = 0 To IW
		argb = ReadPixelFast (x,0,ImageBuffer(HeightMap(1)))
		WritePixelFast 0,x,argb,ImageBuffer(HeightMap(5))
	Next
	UnlockBuffer ImageBuffer(HeightMap(1))
	UnlockBuffer ImageBuffer(HeightMap(5))
	;right (1)
	LockBuffer ImageBuffer(HeightMap(1))
	LockBuffer ImageBuffer(HeightMap(2))
	For y = 0 To IW
		argb = ReadPixelFast (iw,y,ImageBuffer(HeightMap(1)))
		WritePixelFast 0,y,argb,ImageBuffer(HeightMap(2))
	Next
	UnlockBuffer ImageBuffer(HeightMap(1))
	UnlockBuffer ImageBuffer(HeightMap(2))
	;bottom (1)
	LockBuffer ImageBuffer(HeightMap(1))
	LockBuffer ImageBuffer(HeightMap(6))
	For x = 0 To IW
		argb = ReadPixelFast (x,iw,ImageBuffer(HeightMap(1)))
		WritePixelFast 0,iw-x,argb,ImageBuffer(HeightMap(6))
	Next
	UnlockBuffer ImageBuffer(HeightMap(1))
	UnlockBuffer ImageBuffer(HeightMap(6))
	;#End Region
	
	;#Region (Image 2)
	;Top (2)
	LockBuffer ImageBuffer(HeightMap(2))
	LockBuffer ImageBuffer(HeightMap(5))
	For x = 0 To IW
		argb = ReadPixelFast (x,0,ImageBuffer(HeightMap(2)))
		WritePixelFast x,iw,argb,ImageBuffer(HeightMap(5))
	Next
	UnlockBuffer ImageBuffer(HeightMap(2))
	UnlockBuffer ImageBuffer(HeightMap(5))
	;Right (2)
	LockBuffer ImageBuffer(HeightMap(2))
	LockBuffer ImageBuffer(HeightMap(3))
	For y = 0 To IW
		argb = ReadPixelFast (iw,y,ImageBuffer(HeightMap(2)))
		WritePixelFast 0,y,argb,ImageBuffer(HeightMap(3))
	Next
	UnlockBuffer ImageBuffer(HeightMap(2))
	UnlockBuffer ImageBuffer(HeightMap(3))
	;Bottom (2)
	LockBuffer ImageBuffer(HeightMap(2))
	LockBuffer ImageBuffer(HeightMap(6))
	For x = 0 To IW
		argb = ReadPixelFast (x,iw,ImageBuffer(HeightMap(2)))
		WritePixelFast x,0,argb,ImageBuffer(HeightMap(6))
	Next
	UnlockBuffer ImageBuffer(HeightMap(2))
	UnlockBuffer ImageBuffer(HeightMap(6))
	;#End Region
	
	;#Region (Image 3)
	
	;Top (3)
	LockBuffer ImageBuffer(HeightMap(5))
	LockBuffer ImageBuffer(HeightMap(3))
	For x = 0 To IW
		argb = ReadPixelFast (iw,iw-x,ImageBuffer(HeightMap(5)))
		WritePixelFast x,0,argb,ImageBuffer(HeightMap(3))
	Next
	UnlockBuffer ImageBuffer(HeightMap(5))
	UnlockBuffer ImageBuffer(HeightMap(3))
	;Right (3)
	LockBuffer ImageBuffer(HeightMap(3))
	LockBuffer ImageBuffer(HeightMap(4))
	For y = 0 To IW
		argb = ReadPixelFast (iw,y,ImageBuffer(HeightMap(3)))
		WritePixelFast 0,y,argb,ImageBuffer(HeightMap(4))
	Next
	UnlockBuffer ImageBuffer(HeightMap(3))
	UnlockBuffer ImageBuffer(HeightMap(4))
	;Bottom (3)
	LockBuffer ImageBuffer(HeightMap(6))
	LockBuffer ImageBuffer(HeightMap(3))
	For x = 0 To IW
		argb = ReadPixelFast (iw,x,ImageBuffer(HeightMap(6)))
		WritePixelFast x,iw,argb,ImageBuffer(HeightMap(3))
	Next
	UnlockBuffer ImageBuffer(HeightMap(6))
	UnlockBuffer ImageBuffer(HeightMap(3))
	;#End Region
	
	;#Region (Image 4)
	;Top (4)
	LockBuffer ImageBuffer(HeightMap(4))
	LockBuffer ImageBuffer(HeightMap(5))
	For x = 0 To IW
		argb = ReadPixelFast (x,0,ImageBuffer(HeightMap(4)))
		WritePixelFast iw-x,0,argb,ImageBuffer(HeightMap(5))
	Next
	UnlockBuffer ImageBuffer(HeightMap(4))
	UnlockBuffer ImageBuffer(HeightMap(5))
	;Bottom (4)
	LockBuffer ImageBuffer(HeightMap(6))
	LockBuffer ImageBuffer(HeightMap(4))
	For x = 0 To IW
		argb = ReadPixelFast (iw-x,iw,ImageBuffer(HeightMap(6)))
		WritePixelFast x,iw,argb,ImageBuffer(HeightMap(4))
	Next
	UnlockBuffer ImageBuffer(HeightMap(6))
	UnlockBuffer ImageBuffer(HeightMap(4))
	;#End Region
	
End Function

;#End Region

;#Region Cube Heightmap Grayscale to color

Function Create_Load_HeightMappedTexture(HeightMap,i)
	Local Texture%
	Texture = CreateImage(ImageWidth(HeightMap),ImageHeight(HeightMap))
	Convert_Gray_Color(Texture,HeightMap)
	CopyRect 0,0,ImageWidth(HeightMap),ImageHeight(HeightMap),0,0,ImageBuffer(HeightMap),ImageBuffer(Texture)
	SetBuffer ImageBuffer(Texture)
	Color 255,0,0
	Text ImageWidth(HeightMap)/2,ImageHeight(HeightMap)/2,i,True,True
	SetBuffer BackBuffer()
	SaveBuffer ImageBuffer(Texture),"tx"+(i)+".bmp"
	FreeImage Texture
	Texture = LoadTexture ("tx"+i+".bmp",1+16+32)
	Return Texture
End Function

Function Convert_Gray_Color(t,H)
	tw = ImageWidth( t)
	th = ImageHeight( t)
	LockBuffer ImageBuffer(t)
	LockBuffer ImageBuffer (H)
	For y = 0 To tH
		For x = 0 To tw
			cc = ReadPixelFast (x,y,ImageBuffer(H))
			i = DEPTH-(Get_RED_From(cc))
			r = GradientR(i)
			g = GradientG(i)
			b = GradientB(i)
			WritePixelFast x,y,Get_ARGB_From(r,g,b),ImageBuffer(t)
		Next
	Next
	UnlockBuffer ImageBuffer(t)
	UnlockBuffer ImageBuffer(H)
End Function

;#End Region


;Color Gradient
Function CreateGradient(colors%,steps%)
	
	Dim GradientR(steps),GradientG(steps),GradientB(steps),Percent(colors),Red(colors),Green(colors),Blue(colors)
	
	Local i%,pos1%,pos2%,pdiff%
	Local rdiff%,gdiff%,bdiff%
	Local rstep#,gstep#,bstep#
	Local counter%=1
	
    ; read color codes
	For i=1 To colors : Read Percent(i),Red(i),Green(i),Blue(i) : Next
	
    ; calculate gradient
	While counter&lt;colors
		
        ; transform percent value into step position
		pos1%=Percent(counter)*steps/100
		pos2%=Percent(counter+1)*steps/100
		
        ; calculate position difference
		pdiff%=pos2-pos1
		
        ; calculate color difference
		rdiff%=Red(counter)-Red(counter+1)
		gdiff%=Green(counter)-Green(counter+1)
		bdiff%=Blue(counter)-Blue(counter+1)
		
        ; calculate color steps
		rstep#=rdiff*1.0/pdiff
		gstep#=gdiff*1.0/pdiff
		bstep#=bdiff*1.0/pdiff
		
        ; calculate "in-between" color codes
		For i=0 To pdiff
			
			GradientR(pos1+i)=Int(Red(counter)-(rstep*i))
			GradientG(pos1+i)=Int(Green(counter)-(gstep*i))
			GradientB(pos1+i)=Int(Blue(counter)-(bstep*i))
			
		Next
		
        ; increment counter
		counter=counter+1
		
	Wend
	
End Function

.Planet
Data   0.0,255,255,255   ; white: snow
Data   20.0,179,179,179   ; grey: rocks
Data  30.0,153,143, 92   ; brown: tundra
Data  50.0,115,128, 77   ; light green: veld
Data  80.0, 42,102, 41   ; green: grass
Data  87.0,255,246,143   ; gold:Beach
Data  93.0, 69,108,118   ; light blue: shore
Data  96.0, 17, 82,112   ; blue: shallow water
Data  98.0,  9, 62, 92   ; dark blue: water
Data 100.0,  2, 43, 68   ; very dark blue: deep water



;========================================
;		     Color Functions
;#Region  ;========================================

Function Get_ARGB_From(r,g,b):		Return b Or(g Shl 8)Or(r Shl 16):	End Function

Function Get_ALPHA_From(ARGB):		Return ARGB Shr 32 And %11111111 :	End Function

Function Get_RED_From(ARGB):	    Return ARGB Shr 16 And %11111111 :	End Function

Function Get_GREEN_From(ARGB):		Return ARGB Shr 8 And %11111111 :	End Function

Function Get_BLUE_From(ARGB):		Return ARGB And %11111111 :			End Function

;#End Region

;#Region    ;Spherical Vector
Global SphericalBank = CreateBank(4*5)
Function Spherical(x#,y#,z#,Vector#)
	RD# = Vector#
	zen# = ATan2(Sqr(x#*x#+y#*y#),z#)
	azi# = ATan2(y#,x#)
	PokeFloat (SphericalBank,0,Sin(Zen#))
	PokeFloat (SphericalBank,4,Cos(Zen#))
	PokeFloat (SphericalBank,8,Sin(Azi#))
	PokeFloat (SphericalBank,12,Cos(Azi#))
	PokeFloat (SphericalBank,16,RD#)
End Function
Function Spherical_X#(Vector#=0)
	If Not Vector# Then
		Return (PeekFloat(SphericalBank,16)*PeekFloat(SphericalBank,0)*PeekFloat(SphericalBank,12))
	Else
		Return (Vector#*PeekFloat(SphericalBank,0)*PeekFloat(SphericalBank,12))
	EndIf
End Function
Function Spherical_Y#(Vector#=0)
	If Not Vector# Then
		Return (PeekFloat(SphericalBank,16)*PeekFloat(SphericalBank,0)*PeekFloat(SphericalBank,8))
	Else
		Return (Vector#*PeekFloat(SphericalBank,0)*PeekFloat(SphericalBank,8))
	EndIf
End Function
Function Spherical_Z#(Vector#=0)
	If Not Vector# Then
		Return (PeekFloat(SphericalBank,16)*PeekFloat(SphericalBank,4))
	Else
		Return (Vector#*PeekFloat(SphericalBank,4))
	EndIf
End Function
;#End Region


Function HideShowTiles(Source,Parent,Distance#,scale#)
	For i = 1 To CountChildren(Parent)
		ent = GetChild(Parent,i)
		surf = GetSurface(ent,1)
		ex# = VertexX#(surf,1)*scale#:ey# = VertexY#(surf,1)*scale#:ez# = VertexZ#(surf,1)*scale#
		
		sX# = -EntityX#(Source,1):sY# = -EntityY#(Source,1):sZ# = EntityZ#(Source,1)
		Dist# = Sqr((sX#-eX#)*(sX#-eX#) + (sY#-eY#)*(sY#-eY#) + (sZ#-eZ#)*(sZ#-eZ#))
		If dist# &lt; Distance# Then 
			ShowEntity (ent)
		Else
			HideEntity (ent)
		EndIf
	Next
End Function

PlanetSize = 100 ;Scale the planet n times bigger

Camera = CreateCamera()
Light = CreateLight()

PositionEntity Camera,0,0,-(PlanetSize+(planetsize/2)),1
PositionEntity Light,0,0,-(PlanetSize*2)
;AmbientLight (0,0,0)
Print "Creating HeightMap":Flip


For i = 1 To 6
	HeightMap(i) = CreateImage(128,128) ;Must be a power of 2
	CreateHeightMap(HeightMap(i))
Next
SmoothenHeightMaps(3,5)
Planet = CreatePivot()


CreateSegCube(tiles,segments,Planet);Create a Segmented Cube
For i = 0 To 6-1
	;Apply heightmaps to Segmented Cube
	For j = 1 To tiles^2
		GeodesicHeightmapedSphere(HeightMap(i+1),j+i*(Tiles^2),.25,Planet)
	Next
	;Convert Cubic HeightMap to Texture and loads it
	Planet_Texture(i+1) = Create_Load_HeightMappedTexture(HeightMap(i+1),i+1)
Next

PointEntity light,Planet ;point the light at the Mesh

For i = 0 To 6-1
	For j = 1 To tiles*tiles
		ent = GetChild(Planet,j+i*(Tiles^2))
		EntityFX ent,16
		EntityPickMode ent,2
		EntityTexture ent ,Planet_Texture(i+1)
		UpdateNormals ent
	Next
Next
final = MilliSecs()-ms
ScaleEntity Planet,PlanetSize,PlanetSize,PlanetSize
MoveMouse GraphicsWidth()/2,GraphicsHeight()/2

While Not KeyHit(1)
	Cls
	HideShowTiles(Camera,planet,50,planetsize)
	;ShowEntity planet
	If KeyHit(57) Then wf=1-wf : WireFrame wf
	If KeyDown (17) Then MoveEntity camera,0,0,1 	;W
	If KeyDown (31) Then MoveEntity camera,0,0,-1	;S
	If KeyDown (30) Then MoveEntity camera,.3,0,0	;A
	If KeyDown (32) Then MoveEntity camera,-.3,0,0	;D
	If KeyDown (16) Then TurnEntity camera,0,0,2	;Q
	If KeyDown (18) Then TurnEntity camera,0,0,-2	;E
	
	If KeyDown (203) TurnEntity planet,0,1,0
	If KeyDown (205) TurnEntity planet,0,-1,0
	If KeyDown (200) TurnEntity planet,1,0,0
	If KeyDown (208) TurnEntity planet,-1,0,0
	If KeyDown(44) TurnEntity planet,0,0,1
	If KeyDown(45) TurnEntity planet,0,0,-1
	
	TurnEntity camera,MouseYSpeed(),-MouseXSpeed(),0
	MoveMouse GraphicsWidth()/2,GraphicsHeight()/2
	UpdateWorld
	RenderWorld
	Locate 0,0
	Print "Triangles Rendered: " + TrisRendered()
	
	Print "Inital render time: " + final
	Print "Distance to Planet Core: " + EntityDistance#(camera,planet)
	Flip
Wend
</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
