<!DOCTYPE html><html lang="en" ><head ><title >wxGenericDirCtrl with cut,copy,past...</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >wxGenericDirCtrl with cut,copy,past...</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=124" >Brucey's Modules</a>/<a href="#bottom" >wxGenericDirCtrl with cut,copy,past...</a><br><br>
<a name="1206095"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >UNZ</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi,<br>because I needed such a thing I added some functionality to wxGenericDirCtrl:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import wx.wxGenericDirCtrl
Import wx.wxMouseEvent
Import wx.wxLocale
Import wx.wxTextEntryDialog
Import wx.wxtextdataobject
Import wx.wxClipboard

'===================================================================
Rem
	bbdoc: wxGenericDirCtrl with context menu to create (file / dir), cut, copy, past, rename, delete, refresh
End Rem
Type myGenericDirCtrl Extends wxGenericDirCtrl
	Field ContextMenuItem:wxTreeItemId
	Field ContextMenuPath$
	
	Const ID_CUT:Int = wxID_HIGHEST + 1
	Const ID_COPY:Int = wxID_HIGHEST + 2
	Const ID_PASTE:Int = wxID_HIGHEST + 3
	Const ID_RENAME:Int = wxID_HIGHEST + 4
	Const ID_NEW_FILE:Int = wxID_HIGHEST + 5
	Const ID_NEW_DIR:Int = wxID_HIGHEST + 6
	Const ID_DELETE:Int = wxID_HIGHEST + 7
	Const ID_REFRESH:Int = wxID_HIGHEST + 8
	'-----------------------------------------------------
	Method OnInit()
		Super.OnInit()
		ConnectAny(wxEVT_CONTEXT_MENU, OnContextMenu, Self)
		Connect(ID_CUT, wxEVT_COMMAND_MENU_SELECTED, OnCut, Self)
		Connect(ID_COPY, wxEVT_COMMAND_MENU_SELECTED, OnCopy, Self)
		Connect(ID_PASTE, wxEVT_COMMAND_MENU_SELECTED, OnPaste, Self)
		Connect(ID_RENAME, wxEVT_COMMAND_MENU_SELECTED, OnRename, Self)
		Connect(ID_NEW_DIR, wxEVT_COMMAND_MENU_SELECTED, OnNewDir, Self)
		Connect(ID_NEW_FILE, wxEVT_COMMAND_MENU_SELECTED, OnNewFile, Self)
		Connect(ID_DELETE, wxEVT_COMMAND_MENU_SELECTED, OnDelete, Self)
		Connect(ID_REFRESH, wxEVT_COMMAND_MENU_SELECTED, OnRefresh, Self)
	End Method
	'------------------------------------------
	Rem
		bbdoc: Returns the path, or the dir of the file if the path is a file
	End Rem
	Function GetDir$(path$)
		If FileType(path) = FILETYPE_DIR
			Return path
		Else
			Return ExtractDir(path)
		EndIf
	End Function
	
	'------------------------------------------
	Function OnCut(event:wxEvent)
		Local win:myGenericDirCtrl = myGenericDirCtrl(event.userData)
		'write path to clipboard
		If FileType(win.ContextMenuPath) &lt;&gt; FILETYPE_NONE
			WirteToClipboard(win.ContextMenuPath + "|cut")
		EndIf
	End Function
	'------------------------------------------
	Function OnCopy(event:wxEvent)
		Local win:myGenericDirCtrl = myGenericDirCtrl(event.userData)
		'write path to clipboard with cut information
		If FileType(win.ContextMenuPath) &lt;&gt; FILETYPE_NONE
			WirteToClipboard(win.ContextMenuPath)
		EndIf
	End Function
	'------------------------------------------
	Function WirteToClipboard(txt$)
		wxClipboard.Get().Open()
		wxClipboard.Get().SetData(New wxTextDataObject.Create(txt) )
		wxClipboard.Get().Close()
	End Function
	'------------------------------------------
	Function OnPaste(event:wxEvent)
		Local win:myGenericDirCtrl = myGenericDirCtrl(event.userData)
		'read the source from clipboard
		wxClipboard.Get().Open()
		Local textData:wxTextDataObject = New wxTextDataObject.Create("")
		If wxClipboard.Get().GetData(textData)
			Local txt$[] = textData.GetText().Split("|")
			Local path$ = txt[0]
			Local action$ 'this is 'cut' if the path should not be copied
			If txt.length &gt; 1 Then action = txt[1]
			
			Local targetDir$ = GetDir(win.ContextMenuPath)
			Local target$ = targetDir + "/" + StripDir(path)
			'check if target is unique
			While FileType(target) &lt;&gt; FILETYPE_NONE
				Local dialog:wxTextEntryDialog = New wxTextEntryDialog.Create(win, _("The path already exists.~nNew name:"), _("Rename"), StripDir(target) )
				If dialog.ShowModal() = wxID_OK
					target = targetDir + "/" + dialog.GetValue()
				Else
					Return
				EndIf
			Wend
			'do action
			Select action
				'cut
				Case "cut"
					RenameFile(path, target)
					win.ReCreateTree()
					'copy
				Default
					If FileType(path) = FILETYPE_FILE
						CopyFile(path, target)
					ElseIf FileType(path) = FILETYPE_DIR
						CopyDir(path, target)
					EndIf
					win.ReCreateTree()
			End Select
		EndIf
		wxClipboard.Get().Close()
	End Function
	'------------------------------------------
	Function OnRename(event:wxEvent)
		Local win:myGenericDirCtrl = myGenericDirCtrl(event.userData)
		Local dialog:wxTextEntryDialog = New wxTextEntryDialog.Create(win, _("New name:"), _("Rename"), StripDir(win.ContextMenuPath) )
		If dialog.ShowModal() = wxID_OK
			If FileType(win.ContextMenuPath) &lt;&gt; FILETYPE_NONE
				Local dir$ = ExtractDir(win.ContextMenuPath)
				Local path$ = dir + "/" + dialog.GetValue()
				If Not RenameFile(win.ContextMenuPath, path)
					wxMessageBox(_("Can't rename path:~n"+win.ContextMenuPath))
				Else
					win.ReCreateTree()
				EndIf
			EndIf
		EndIf
	End Function
	'------------------------------------------
	Function OnNewFile(event:wxEvent)
		Local win:myGenericDirCtrl = myGenericDirCtrl(event.userData)
		Local dialog:wxTextEntryDialog = New wxTextEntryDialog.Create(win, _("Name:"), _("New file") )
		If dialog.ShowModal() = wxID_OK
			Local dir$
			If FileType(win.ContextMenuPath) = FILETYPE_DIR
				dir = win.ContextMenuPath
			Else
				dir = ExtractDir(win.ContextMenuPath)
			EndIf
			Local path$ = dir + "/" + dialog.GetValue()
			Local ok:Int
			'check if file exists
			If Not FileType(path) = FILETYPE_FILE
				ok = True
			Else
				'ask to overwrite
				If wxMessageBox(_("The file already exists. Overwrite it?:~n" + path),, wxYES_NO) = wxYES
					ok = True
				EndIf
			EndIf
			'create the file
			If ok
				If Not CreateFile(path)
					wxMessageBox(_("Can't create file:~n"+path))
				Else
					win.ReCreateTree()
				EndIf
			EndIf
		EndIf
	End Function
	'------------------------------------------
	Function OnNewDir(event:wxEvent)
		Local win:myGenericDirCtrl = myGenericDirCtrl(event.userData)
		Local dialog:wxTextEntryDialog = New wxTextEntryDialog.Create(win, _("Name:"), _("New directory") )
		If dialog.ShowModal() = wxID_OK
			Local dir$
			If FileType(win.ContextMenuPath) = FILETYPE_DIR
				dir = win.ContextMenuPath
			Else
				dir = ExtractDir(win.ContextMenuPath)
			EndIf
			Local path$ = dir + "/" + dialog.GetValue()
			Local ok:Int
			'check if dir exists
			If FileType(path) = FILETYPE_DIR
				wxMessageBox(_("The directory already exists." + path) )
				Return
			Else
				If Not CreateDir(path)
					wxMessageBox(_("Can't create directory:~n"+path))
				Else
					win.ReCreateTree()
				EndIf
			EndIf
		EndIf
	End Function
	'------------------------------------------
	Function OnDelete(event:wxEvent)
		Local win:myGenericDirCtrl = myGenericDirCtrl(event.userData)
		Local path$ = win.GetPath()
		If wxMessageBox(_("Are you sure that you want to delete this from disk(!) ?~n" + path),, wxYES_NO) = wxYES
			If FileType(path) = FILETYPE_FILE
				DeleteFile(path)
			ElseIf FileType(path) = FILETYPE_DIR
				DeleteDir(path, True)
			EndIf
			win.ReCreateTree()
		EndIf
	End Function
	'------------------------------------------
	Function OnRefresh(event:wxEvent)
		Local win:myGenericDirCtrl = myGenericDirCtrl(event.userData)
		win.ReCreateTree()
	End Function
	'------------------------------------------
	Function OnContextMenu(event:wxEvent)
		Local win:myGenericDirCtrl = myGenericDirCtrl(event.userData)
		Local x:Int, y:Int
		wxContextMenuEvent(event).GetPosition(X, y)
		Local flags:Int
		win.ScreenToClient(X, y)
		win.ContextMenuItem = win.GetTreeCtrl().HitTest(X, y, flags)
		If win.ContextMenuItem.IsOk()
			win.GetTreeCtrl().SelectItem(win.ContextMenuItem)
			win.ContextMenuPath = win.GetPath()
			win.ShowContextMenu(X, y)
		EndIf
	End Function
	'------------------------------------------
	Method ShowContextMenu(x:Int, y:Int)
		Local flags:Int
		ContextMenuItem = GetTreeCtrl().HitTest(X, y, flags)
		
		Local menu:wxMenu = New wxMenu.Create()
			Local sub:wxMenu = New wxMenu.Create()
			menu.AppendSubMenu(sub, "New")
			sub.Append(ID_NEW_FILE, "File")
			sub.Append(ID_NEW_DIR, "Directory")
		menu.Append(ID_DELETE, _("Delete") )
		menu.Append(ID_RENAME, _("Rename") )
		menu.AppendSeparator()
		menu.Append(ID_CUT, _("Cut") )
		menu.Append(ID_COPY, _("Copy") )
		menu.Append(ID_PASTE, _("Paste") )
		menu.AppendSeparator()
		menu.Append(ID_REFRESH, _("Refresh") )
		
		PopupMenu(menu, X, y)
		menu.Free()
	End Method
End Type
</textarea><br><br>example:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework wx.wxApp
Import wx.wxFrame
Import "myGenericDirCtrl.bmx"

New MyApp.Run()

Type MyApp Extends wxApp

	Field frame:MyFrame
	Method OnInit:Int()
		frame = MyFrame(New MyFrame.Create(Null, wxID_ANY, "wxMax") )
		frame.Show()
		Return True
	End Method
End Type

Type MyFrame Extends wxFrame
	Field dirCtrl:myGenericDirCtrl
	Method OnInit()
		dirCtrl = myGenericDirCtrl(New myGenericDirCtrl.Create(Self, wxID_ANY, AppDir) )
		dirCtrl.OnInit()
	End Method
End Type
</textarea><br><br>(I call dirCtrl.OnInit() because in the current wxMax revision this is missing in the Create() Method of wxGenericDirCtrl.)<br><br>Have fun. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
