<!DOCTYPE html><html lang="en" ><head ><title >wxMultiChoiceDialog (Select All/Select None)</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >wxMultiChoiceDialog (Select All/Select None)</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=124" >Brucey's Modules</a>/<a href="#bottom" >wxMultiChoiceDialog (Select All/Select None)</a><br><br>
<a name="953270"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kenshin</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm using the wxMultiChoiceDialog for some reasonable size lists.  Is there any way to get "ALL / NONE" buttons (or something functionally equivalent) on it in addition to the "OK / CANCEL" ones?<br><br>I can't find any evidence of this option, although it seems like such an obvious thing to have when multiple-selection from a list is being done.<br><br>btw v2.8.9 is working great here. <br><br></td></tr></table><br>
<a name="954686"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kenshin</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> I've knocked up a type that can be used as an alternative to the modal wxMultiChoiceDialog requester, but also implements the 'Select All' and 'Select None' buttons.  It's pretty much a drop in replacement for the original requester except for minor differences.  I've also made it to look and act as close as possible to the original requester as I could.<br><br>Here it is with a demo showing how to use it.  Just copy, paste, and run.  Hope this helps anyone else looking for the extra All/None function.<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Import wx.wxApp
Import wx.wxDialog
Import wx.wxCheckListBox
Import wx.wxButton
Import wx.wxPanel
Import wx.wxStaticLine
Import wx.wxStaticText
Import brl.standardio

New MultiChoiceDialogDemoApp.Run()
End

Type MultiChoiceDialogDemoApp Extends wxApp

	Method OnInit:Int()
		
		Local Choices:String[] = [ "Zero", "One", "Two", "Three", "Four", "Five", "Six", "Seven", "Eight", "Nine" ]
		Local PreselectedChoices:Int[] = [ 1, 5 ]
		Local MultiChoiceDialogDemo:TwxMultiChoiceDialog = TwxMultiChoiceDialog( New TwxMultiChoiceDialog.Create_( Null, wxID_ANY, "Multi Choice Dialog", , , , , wxDEFAULT_DIALOG_STYLE | wxRESIZE_BORDER ) )
		MultiChoiceDialogDemo.Initialize( "Select the Items.", 400, 400, True, Choices )
		MultiChoiceDialogDemo.SetSelections( PreselectedChoices )
		Local Modal:Int = MultiChoiceDialogDemo.ShowModal()
		MultiChoiceDialogDemo.EndModal( Modal )
		MultiChoiceDialogDemo.Destroy()

		If MultiChoiceDialogDemo.OK = True
		
			Print "The items you have selected are:"
		
			Local SelectedChoices:Int[] = MultiChoiceDialogDemo.GetSelections()
			
			For Local i:Int = EachIn SelectedChoices
			
				Print "Item " + i
			
			Next
		
		EndIf

		Return True
	
	End Method

End Type

Type TwxMultiChoiceDialog Extends wxDialog

	Field Panel:wxPanel
	Field CheckListBox:wxCheckListBox
	Field choices_cnt:Int = 0
	Field OK:Int = False
	Const CheckListBox_ID:Int 	= wxID_HIGHEST + 1
	Const Btn_OK_ID:Int 		= wxID_HIGHEST + 2
	Const Btn_CANCEL_ID:Int 	= wxID_HIGHEST + 3
	Const Btn_ALL_ID:Int 		= wxID_HIGHEST + 4
	Const Btn_NONE_ID:Int 		= wxID_HIGHEST + 5
	Const StaticLine_ID:Int 	= wxID_HIGHEST + 6
	Field MessageText_GDGT:wxStaticText
	Const MessageText_ID:Int 	= wxID_HIGHEST + 7

	Function Create:TwxMultiChoiceDialog( parent_wndw:wxWindow, x_pos:Int = -1, y_pos:Int = -1, x_size:Int = 380, y_size:Int = 380 )

	End Function

	Method Initialize( message:String = "", x_size:Int = 400, y_size:Int = 400, center:Int = True, choices:String[] )
	
		Panel = wxPanel.CreatePanel( Self )
		Local panel_colour:wxColour = Panel.GetBackgroundColour()
		CheckListBox = wxCheckListBox.CreateCheckListBox( Panel, CheckListBox_ID, choices, 10, 10,,, 0 )
		choices_cnt = choices.Length
		Local btn_ok:wxButton = wxButton.CreateButton( Panel, Btn_OK_ID, "OK" )
		Local btn_cancel:wxButton = wxButton.CreateButton( Panel, Btn_CANCEL_ID, "Cancel" )
		Local btn_all:wxButton = wxButton.CreateButton( Panel, Btn_ALL_ID, "Select All" )
		Local btn_none:wxButton = wxButton.CreateButton( Panel, Btn_NONE_ID, "Select None" )
		Local static_line:wxStaticLine = wxStaticLine.CreateStaticLine( Panel, StaticLine_ID )
		MessageText_GDGT = wxStaticText.CreateStaticText( Panel, MessageText_ID, "", , , , , wxALIGN_LEFT )
		MessageText_GDGT.SetBackgroundColour( panel_colour )
		Local main_sizer:wxBoxSizer = wxBoxSizer.CreateBoxSizer( wxVERTICAL )
		main_sizer.Add( MessageText_GDGT, 0, wxEXPAND | wxTOP | wxLEFT | wxRIGHT | wxBOTTOM, 15 )
		main_sizer.Add( CheckListBox, 1, wxGROW | wxLEFT | wxRIGHT, 15 )
		Local bottom_sizer:wxBoxSizer = wxBoxSizer.CreateBoxSizer( wxHORIZONTAL )
		bottom_sizer.Add( btn_all, 0, wxLEFT | wxBOTTOM, 10 )
		bottom_sizer.Add( btn_none, 0, wxLEFT | wxBOTTOM, 10 )
		bottom_sizer.AddStretchSpacer( 20 )
		bottom_sizer.Add( btn_ok, 0, wxRIGHT | wxBOTTOM, 10 )
		bottom_sizer.Add( btn_cancel, 0, wxRIGHT | wxBOTTOM, 10 )
		main_sizer.Add( static_line, 0, wxEXPAND | wxTOP | wxBOTTOM | wxLEFT | wxRIGHT, 10 )
		main_sizer.AddSizer( bottom_sizer, 0, wxEXPAND | wxALIGN_RIGHT )
		Panel.SetAutoLayout( True )
		Panel.SetSizer( main_sizer )
		Self.SetSize( x_size, y_size )
		Self.SetMinSize( x_size, y_size )
		Self.OK = False
		
		If center = True
		
			Self.Center()
		
		End If

		MessageText_GDGT.SetLabel( message )		
		Connect( Btn_OK_ID, wxEVT_COMMAND_BUTTON_CLICKED, OnOK )
		Connect( Btn_CANCEL_ID, wxEVT_COMMAND_BUTTON_CLICKED, OnCancel )
		Connect( Btn_ALL_ID, wxEVT_COMMAND_BUTTON_CLICKED, OnAll )
		Connect( Btn_NONE_ID, wxEVT_COMMAND_BUTTON_CLICKED, OnNone )

	End Method
	
	Method SetSelections( selections:Int[] )
	
		For Local i:Int = EachIn selections
		
			CheckListBox.Check( i, True )

		Next
	
	EndMethod
	
	Method GetSelections:Int[]()
		
		Local selections_cnt:Int = 0
		
		For Local i:Int = 0 To Self.choices_cnt - 1
		
			If CheckListBox.IsChecked( i ) = True
		
				selections_cnt :+ 1
				
			EndIf
			
		Next
		
		Local selections:Int[ selections_cnt ]
		Local selections_ptr:Int = 0
		
		For Local i:Int = 0 To Self.choices_cnt - 1
		
			If CheckListBox.IsChecked( i ) = True
		
				selections[ selections_ptr ] = i
				selections_ptr :+ 1
				
			EndIf
			
		Next
		
		Return selections
	
	End Method
		
	Function OnOK( event:wxEvent )
	
		TwxMultiChoiceDialog( event.parent ).OK = True
		TwxMultiChoiceDialog( event.parent ).Close()
	
	End Function
	
	Function OnCancel( event:wxEvent )
	
		TwxMultiChoiceDialog( event.parent ).OK = False
		TwxMultiChoiceDialog( event.parent ).Close()
	
	End Function
	
	Function OnAll( event:wxEvent )
	
		For Local i:Int = 0 To TwxMultiChoiceDialog( event.parent ).choices_cnt - 1
		
			TwxMultiChoiceDialog( event.parent ).CheckListBox.Check( i, True )
		
		Next
	
	End Function
	
	Function OnNone( event:wxEvent )
	
		For Local i:Int = 0 To TwxMultiChoiceDialog( event.parent ).choices_cnt - 1
		
			TwxMultiChoiceDialog( event.parent ).CheckListBox.Check( i, False )
		
		Next
	
	End Function
		
End Type
</textarea> <br><br></td></tr></table><br>
<a name="955025"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >DavidDC</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> If you create the OK and CANCEL buttons with the stock id's wxID_OK and wxID_CANCEL respectively then you shouldn't need to: connect them to anything, call close() or end modal().<br><br>Thanks for sharing. <br><br></td></tr></table><br>
<a name="955104"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >kenshin</td><td align="right"><font class=tiny>(Posted 2009)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thanks for the tip:)<br>I'll look at doing it tonight.  About to go to work now. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
