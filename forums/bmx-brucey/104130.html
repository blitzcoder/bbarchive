<!DOCTYPE html><html lang="en" ><head ><title >FindText Scintilla flag</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >FindText Scintilla flag</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=124" >Brucey's Modules</a>/<a href="#bottom" >FindText Scintilla flag</a><br><br>
<a name="1259431"></a>

<a name="1259432"></a>

<a name="1259433"></a>

<a name="1259434"></a>

<a name="1259435"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MOBii</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> <pre class=code>	Rem
	bbdoc: Find some text in the document.
	End Rem
	Method FindText:Int(minPos:Int, maxPos:Int, text:String, flags:Int)
		Return bmx_wxscintilla_findtext(wxObjectPtr, minPos, maxPos, text, flags)
	End Method</pre>Is there any flag for: WHOLEWORD and MATCHCASE?<pre class=code>Const wxSCI_WHOLEWORD:Int = 2
Const wxSCI_MATCHCASE:Int = 4</pre> <br><br></td></tr></table><br>
<a name="1259441"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Henri</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> You can try to make a constant or variable with a content of:<pre class=code>Const wxSCI_BOTH:Int = wxSCI_WHOLEWORD | wxSCI_MATCHCASE</pre><br>...and pass that to Find function.<br><br>-Henri <br><br></td></tr></table><br>
<a name="1259443"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Of course this means you could call it that way:<br><br>FindText(minPos, maxPos, wxSCI_WHOLEWORD | wxSCI_MATCHCASE)<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1259543"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MOBii</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> sorry I can't find any flag that sort out wholeword or matchcase<br>I test binary number: 1, 2, 4, 8, 16, 32, 64, 128, 256 it find all<br>In my mind I need to let the function FindText find the text and I need to sort it out manually, but I am confident that Scintilla have a function already for this (or BlitzMax)!<br>Maybe I am wrong that the flag has anything to do with finding wholeword or matchcase?<br><br><br>Const wxSCI_WHOLEWORD:Int = 2<br>Const wxSCI_MATCHCASE:Int = 4<br>It look like these 2 constant is not for the function FindText <br><br></td></tr></table><br>
<a name="1259566"></a>

<a name="1259569"></a>

<a name="1259570"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Henri</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> It does indeed work as I just tested it and the results were exactly what should be. If you want to test it yourself then here's something to try out:<br><br>EDIT: You might want to back the file first if you ever want to revert back to original.<br><br><br>1. Open up the scintilla.bmx in the samples folder<br><br>2. Add these imports on top of file under import wx.wxFrame<pre class=code>Import wx.wxDialog
Import wx.wxPanel
Import wx.wxTextCtrl
Import wx.wxStaticLine
Import wx.wxStaticText
Import wx.wxStdDialogButtonSizer
Import wx.wxButton
Import brl.standardio</pre><br>2. Modify the Edit-type by adding line<pre class=code>ConnectAny(wxEVT_KEY_DOWN, OnKeyDown)</pre>...somewhere in the bottom of OnInit() method. Now add a new function somewhere inside Edit type called<pre class=code>Function OnKeyDown(ev:wxEvent)

	Local key_ev:wxKeyEvent = wxKeyEvent(ev)
	Local keyCode:Int = key_ev.GetKeyCode()
	Local lv:Edit = Edit(ev.sink)
	If Not lv Then Notify "LV not found!!";Return
	Local key:String = Chr(keyCode)
	
	If key = "F" Then
		If key_ev.ControlDown() Then
			
			Local txt:String = EntryDlg("Find","Find")
			If txt Then
				Print lv.FindText(0, 100, txt, wxSCI_WHOLEWORD | wxSCI_MATCHCASE)
			EndIf
		EndIf
	EndIf
	
	ev.skip()
EndFunction
</pre> This gets executed when you press any key.<br><br><br>3. Finally add this function and type in the bottom of whole file<pre class=code>Function EntryDlg:String(text:String, title:String)

	Local dial:TEntryDialog = New TEntryDialog.Create(text, title)
	Local value:String = dial.GetString()

	dial.Free()
	Return value
EndFunction

Type TEntryDialog Extends wxDialog

	Field m_panel:wxPanel
	Field m_label1:wxStaticText
	Field m_field1:wxTextCtrl
	Field m_staticline:wxStaticLine
	Field m_sdbSizer:wxStdDialogButtonSizer
	Field m_sdbSizerCancel:wxButton
	Field m_sdbSizerOK:wxButton
	Field text:String
	Field value:String
	Field ret:Int
	
	Method Create:TEntryDialog(txt:String, title:String)
		text = txt
		Return TEntryDialog(Super.Create_(Null, wxID_ANY, title, -1, -1, -1, -1, wxDEFAULT_DIALOG_STYLE))
	End Method

	Method OnInit()

		Local bSizer1:wxBoxSizer = New wxBoxSizer.Create(wxVERTICAL)
		Local bSizer2:wxBoxSizer = New wxBoxSizer.Create(wxVERTICAL)
		Local bSizer3:wxBoxSizer = New wxBoxSizer.Create(wxHORIZONTAL)
		
		m_sdbSizer = New wxStdDialogButtonSizer.CreateSizer()
		m_sdbSizerOK = New wxButton.Create(Self, wxID_OK)
		m_sdbSizerCancel = New wxButton.Create(Self, wxID_CANCEL)
		
		m_panel = New wxPanel.Create(Self, wxID_ANY,,,,, wxTAB_TRAVERSAL)
		m_panel.SetForegroundColour(New wxColour.Create(0,0,0))
		m_panel.SetBackgroundColour(New wxColour.Create(255,255,255))

		m_staticline = New wxStaticLine.Create(Self, wxID_ANY,,,,, wxLI_HORIZONTAL)
		
		m_label1 = New wxStaticText.Create(m_panel, wxID_ANY, text)
		m_label1.Wrap(-1)
		
		m_field1 = New wxTextCtrl.Create(m_panel, wxID_ANY, "",,,200,,0)

		bSizer2.AddCustomSpacer(0, 15, 1, wxEXPAND, 5)
		bSizer3.Add(m_label1, 0, wxBOTTOM|wxRIGHT|wxLEFT, 5)
		bSizer3.Add(m_field1, 0, wxRIGHT|wxLEFT, 5)
		bSizer2.AddSizer(bSizer3, 1, wxEXPAND, 5)
		bSizer2.AddCustomSpacer(0, 15, 1, wxEXPAND, 5)
		bSizer1.Add(m_panel, 1, wxEXPAND, 5)
		bSizer1.Add(m_staticline, 0, wxEXPAND, 5)
		m_sdbSizer.AddButton( m_sdbSizerOK)
		m_sdbSizer.AddButton( m_sdbSizerCancel)
		bSizer1.AddSizer(m_sdbSizer, 0, wxEXPAND|wxTOP|wxBOTTOM, 10)
				
		m_panel.SetSizer(bSizer2)
		m_panel.Layout()
		bSizer2.Fit(m_panel)

		m_sdbSizer.Realize()
		
		SetSizer(bSizer1)
		Layout()
		bSizer1.Fit(Self)
		Center(wxBOTH)
		
		m_sdbSizerCancel.ConnectAny(wxEVT_COMMAND_BUTTON_CLICKED, OnCancel, Null, Self)
		m_sdbSizerOK.ConnectAny(wxEVT_COMMAND_BUTTON_CLICKED, OnOk, Null, Self)
		
		m_sdbSizerOK.SetDefault()
		m_field1.SetFocus()
		
		Centre()
		ret = ShowModal()
		
		If ret Then
			value = m_field1.GetValue()
		Else
			value = ""
		EndIf
		
	End Method

	Method GetString:String()
		Return value
	EndMethod
	
	Function OnCancel(ev:wxEvent)
		Local d:TEntryDialog = TEntryDialog(ev.sink)
		d.EndModal(False)
	End Function

	Function OnOk(ev:wxEvent)
		Local d:TEntryDialog = TEntryDialog(ev.sink)
		d.EndModal(True)
	End Function

End Type
</pre><br>Now you have find functionality which can be activated by pressing CTRL + F . Try changing the constant in OnKeyDown()-function to see the effects. If text is found then starting position is printed in MaxIDE console and if no matching text is found then -1 is printed.<br><br>-Henri <br><br></td></tr></table><br>
<a name="1259568"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> The problem might be:<br><br><div class="quote"> I test binary number: 1, 2, 4, 8, 16, 32, 64, 128, 256 it find all <br></div><br><br>1 | 2 does not mean: "if res =1 or res = 2".<br><br>1 | 2 = 3<br>1 | 4 = 5<br>1 | 2 | 4 = 7<br>2 | 4 = 6<br><br>So you would have needed to check for 1-256 to check for all possibilities.<br><br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1259977"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MOBii</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> I use this extra check if search text = selected find text<br>_MatchCase = 0 no check, 1 check matchcase<pre class=code>	Method checkStr:Int(_Txt:String, _MatchCase:Int)
		If Not _MatchCase Then Return 1
		If GetSelectedText() = _Txt Then Return 1
	End Method</pre>Still find wholeword for another day<br><br>Derron: I see my laziness so I test up to:<pre class=code>0
1
2
4
8
16
32
64
128
256
512
1024
2048
4096
8192
16384
32768
65536
131072
262144
524288
1048576
2097152
4194304
8388608
16777216</pre>The best would be if FindText could check for the matchcase and wholeword!<br>flags in: Method FindText:Int(minPos:Int, maxPos:Int, text:String, flags:Int)<br>Don't work as I understand it anyhow!<br>By the way FindText is a scintilla Method (maybe there is many FindText that look the same)<br><br>I was eager to test Henri example, specially when I never use wxBoxSizer<br>I gone far in my project and never understand the power of wxBoxSizer yet<br>I guess it's why I could not convert my project to the aui.bmx example some month ago!<br>I still don't have a toolbar yet ^^<br>unfortunately I could not make Henri's example to run<pre class=code>Local lv:Edit = Edit(ev.sink)</pre>I search all file for Edit but it look like I don't have Edit installed in my BlitzMax <br><br></td></tr></table><br>
<a name="1259983"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> <br><pre class=code>
Method checkStr:Int(_Txt:String, _MatchCase:Int)
	If Not _MatchCase Then Return 1
	If GetSelectedText() = _Txt Then Return 1
End Method
</pre><br> <br></div><br><br>Why not<br><pre class=code>
Method checkStr:Int(_Txt:String, _MatchCase:Int)
	If not _MatchCase Then Return GetSelectedText() = _Txt

	Return GetSelectedText().ToLower() = _Txt.ToLower()
End Method
</pre><br>-- which does check ignoring the case or not ignoring it.<br><br><br><div class="quote"> Derron: I see my laziness so I test up to: [...] <br></div><br><br>Like said, if you set multiple flags, the resulting bitmask will not be power of 2 ... <br>Flags = Flag1 | Flag2 = 3<br>Flags = 1 | 2 = 3<br><br>See the example I posted in my reply above.<br><br>More exactly: you check 0, 1, 2, 4, 8 ... but you never checked "3" (flag 1 and flag 2 enabled)<br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1259992"></a>

<a name="1259993"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> here's a working example - a mix of the scintilla sample, and Henri's find dialog, with an additional set of check boxes so that you may experiment by toggling the flags :<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework wx.wxApp
Import wx.wxScintilla
Import wx.wxFrame
Import wx.wxDialog
Import wx.wxPanel
Import wx.wxTextCtrl
Import wx.wxStaticLine
Import wx.wxStaticText
Import wx.wxStdDialogButtonSizer
Import wx.wxButton
Import brl.standardio
Import wx.wxCheckBox

Global commonPrefs:CommonInfo = New CommonInfo


New MyApp.Run()

Type MyApp Extends wxApp

	Field frame:AppFrame

	Method OnInit:Int()
	
		wxInitAllImageHandlers()

		' Create application frame
		frame = AppFrame(New AppFrame.Create(,,"wxScintilla Sample"))
		
		' open application frame
		frame.Layout()
		frame.Show(True)
		SetTopWindow(frame)
	
		Return True
	
	End Method

End Type


Type AppFrame Extends wxFrame


	' edit object
	Field mEdit:Edit
	Field menuBar:wxMenuBar
	
	Method OnInit()

		' Create menu
		menuBar = New wxMenuBar.Create()
		CreateMenu()
		
		' open First page
		mEdit = Edit(New Edit.Create(Self, -1))
		mEdit.SetFocus()

	End Method
	
	Method CreateMenu()
	End Method

	Function OnClose (event:wxEvent)
	End Function
	
	Function OnAbout (event:wxEvent)
	End Function
	
	Function OnExit (event:wxEvent)
	End Function
	
	Function OnTimerEvent (event:wxEvent)
	End Function
	
	' file
	Function OnFileNew (event:wxEvent)
	End Function
	
	Function OnFileNewFrame (event:wxEvent)
	End Function
	
	Function OnFileOpen (event:wxEvent)
	End Function
	
	Function OnFileOpenFrame (event:wxEvent)
	End Function
	
	Function OnFileSave (event:wxEvent)
	End Function
	
	Function OnFileSaveAs (event:wxEvent)
	End Function
	
	Function OnFileClose (event:wxEvent)
	End Function
	
	' properties
	Function OnProperties (event:wxEvent)
	End Function
	
	' Print
	Function OnPrintSetup (event:wxEvent)
	End Function
	
	Function OnPrintPreview (event:wxEvent)
	End Function
	
	Function OnPrint (event:wxEvent)
	End Function
	
	' edit events
	Function OnEdit (event:wxEvent)
	End Function



End Type

Type Edit Extends wxScintilla

	' file
	Field filename:String
	
	' lanugage properties
	'LanguageInfo Const* m_language;
	
	' margin variables
	Field lineNrID:Int
	Field lineNrMargin:Int
	Field foldingID:Int
	Field foldingMargin:Int
	Field dividerID:Int
	
	Field findOptions:TFindOptions = New TFindOptions


	Method OnInit()
	
		filename = ""
		'm_language = Null;
		
		lineNrID = 0
		lineNrMargin = TextWidth(wxSCI_STYLE_LINENUMBER, "_999999")
		foldingID = 1
		foldingMargin = 16
		dividerID = 2

		' Default font For all styles
		SetViewEOL(commonPrefs.displayEOLEnable)
		SetIndentationGuides(commonPrefs.indentGuideEnable)
		
		If commonPrefs.lineNumberEnable Then
			SetMarginWidth(lineNrID, lineNrMargin)
		Else
			SetMarginWidth(lineNrID, 0)
		End If
		
		If commonPrefs.longLineOnEnable Then
			SetEdgeMode (wxSCI_EDGE_LINE)
		Else
			SetEdgeMode(wxSCI_EDGE_NONE)
		End If
		
		If commonPrefs.whiteSpaceEnable Then
			SetViewWhiteSpace(wxSCI_WS_VISIBLEALWAYS)
		Else
			SetViewWhiteSpace(wxSCI_WS_INVISIBLE)
		End If
		SetOvertype(commonPrefs.overTypeInitial)
		SetReadOnly(commonPrefs.readOnlyInitial)
		If commonPrefs.wrapModeInitial Then
			SetWrapMode(wxSCI_WRAP_WORD)
		Else
			SetWrapMode(wxSCI_WRAP_NONE)
		End If
		
		Local font:wxFont = New wxFont.CreateWithAttribs(10, wxTELETYPE, wxNORMAL, wxNORMAL)
		
		StyleSetFontFont(wxSCI_STYLE_DEFAULT, font)
		StyleSetForeground(wxSCI_STYLE_DEFAULT, New wxColour.CreateNamedColour("BLACK"))
		StyleSetBackground(wxSCI_STYLE_DEFAULT, New wxColour.CreateNamedColour("WHITE"))
		StyleSetForeground(wxSCI_STYLE_LINENUMBER, New wxColour.CreateNamedColour("DARK GREY"))
		StyleSetBackground(wxSCI_STYLE_LINENUMBER, New wxColour.CreateNamedColour("WHITE"))
		StyleSetForeground(wxSCI_STYLE_INDENTGUIDE, New wxColour.CreateNamedColour("DARK GREY"))
		'InitializePrefs(DEFAULT_LANGUAGE);

		' set visibility
		SetVisiblePolicy(wxSCI_VISIBLE_STRICT | wxSCI_VISIBLE_SLOP, 1)
		SetXCaretPolicy(wxSCI_CARET_EVEN | wxSCI_VISIBLE_STRICT | wxSCI_CARET_SLOP, 1)
		SetYCaretPolicy(wxSCI_CARET_EVEN | wxSCI_VISIBLE_STRICT | wxSCI_CARET_SLOP, 1)
		
		' markers
		MarkerDefine(wxSCI_MARKNUM_FOLDER, wxSCI_MARK_BOXPLUS)
		MarkerSetBackground(wxSCI_MARKNUM_FOLDER, New wxColour.CreateNamedColour("BLACK"))
		MarkerSetForeground(wxSCI_MARKNUM_FOLDER, New wxColour.CreateNamedColour("WHITE"))
		MarkerDefine(wxSCI_MARKNUM_FOLDEROPEN, wxSCI_MARK_BOXMINUS)
		MarkerSetBackground(wxSCI_MARKNUM_FOLDEROPEN, New wxColour.CreateNamedColour("BLACK"))
		MarkerSetForeground(wxSCI_MARKNUM_FOLDEROPEN, New wxColour.CreateNamedColour("WHITE"))
		MarkerDefine(wxSCI_MARKNUM_FOLDERSUB, wxSCI_MARK_EMPTY)
		MarkerDefine(wxSCI_MARKNUM_FOLDEREND, wxSCI_MARK_SHORTARROW)
		MarkerDefine(wxSCI_MARKNUM_FOLDEROPENMID, wxSCI_MARK_ARROWDOWN)
		MarkerDefine(wxSCI_MARKNUM_FOLDERMIDTAIL, wxSCI_MARK_EMPTY)
		MarkerDefine(wxSCI_MARKNUM_FOLDERTAIL, wxSCI_MARK_EMPTY)

		' miscelaneous
		UsePopUp(0)
		SetLayoutCache(wxSCI_CACHE_PAGE)
		SetBufferedDraw(1)

		ConnectAny(wxEVT_KEY_DOWN, OnKeyDown)
		
		SetText("one One ONE two Two TWO three Three THREE")
	End Method

	Function OnKeyDown(ev:wxEvent)
	
		Local key_ev:wxKeyEvent = wxKeyEvent(ev)
		Local keyCode:Int = key_ev.GetKeyCode()
		Local lv:Edit = Edit(ev.sink)
		If Not lv Then Notify "LV not found!!";Return
		Local key:String = Chr(keyCode)
		
		If key = "F" Then
			If key_ev.ControlDown() Then
				
				Local txt:String = EntryDlg("Find","Find", lv.findOptions)
				If txt Then
					' FindText returns the starting position of the match
					Local pos:Int = lv.FindText(0, 100, txt, lv.findOptions.toFlags())
					If pos &gt;= 1 Then
						lv.SetSelection(pos, pos + txt.length)
					End If
				EndIf
			EndIf
		EndIf
		
		ev.skip()
	End Function
End Type


Type CommonInfo
    ' editor functionality prefs
    Field syntaxEnable:Int = True
    Field foldEnable:Int = True
    Field indentEnable:Int = True
    ' display defaults prefs
    Field readOnlyInitial:Int = False
    Field overTypeInitial:Int = False
    Field wrapModeInitial:Int = False
    Field displayEOLEnable:Int = False
    Field indentGuideEnable:Int = False
    Field lineNumberEnable:Int = True
    Field longLineOnEnable:Int = False
    Field whiteSpaceEnable:Int = False
End Type

Type LanguageInfo
    Field name:String
    Field filepattern:String
    Field lexer:Int
    Field styles:TStyles[]
    Field folds:Int
End Type

Type TStyles
	Field kind:Int
	Field words:String
End Type

Type TFindOptions
	Field wholeWord:Int = True
	Field matchCase:Int = True
	
	Method toFlags:Int()
		Local flags:Int
		
		If wholeWord Then
			flags :| wxSCI_WHOLEWORD
		End If

		If matchCase Then
			flags :| wxSCI_MATCHCASE
		End If

		Return flags
	End Method
End Type

Function EntryDlg:String(text:String, title:String, findOptions:TFindOptions)

	Local dial:TEntryDialog = New TEntryDialog.Create(text, title, findOptions)
	Local value:String = dial.GetString()

	dial.Free()
	Return value
EndFunction

Type TEntryDialog Extends wxDialog

	Field m_panel:wxPanel
	Field m_label1:wxStaticText
	Field m_field1:wxTextCtrl
	Field m_staticline:wxStaticLine
	Field m_sdbSizer:wxStdDialogButtonSizer
	Field m_sdbSizerCancel:wxButton
	Field m_sdbSizerOK:wxButton
	Field text:String
	Field value:String
	Field ret:Int
	Field m_chkbxWholeWord:wxCheckBox
	Field m_chkbxMatchCase:wxCheckBox
	Field findOptions:TFindOptions
	
	Method Create:TEntryDialog(txt:String, title:String, findOptions:TFindOptions)
		text = txt
		Self.findOptions = findOptions
		Return TEntryDialog(Super.Create_(Null, wxID_ANY, title, -1, -1, -1, -1, wxDEFAULT_DIALOG_STYLE))
	End Method

	Method OnInit()

		Local bSizer1:wxBoxSizer = New wxBoxSizer.Create(wxVERTICAL)
		Local bSizer2:wxBoxSizer = New wxBoxSizer.Create(wxVERTICAL)
		Local bSizer3:wxBoxSizer = New wxBoxSizer.Create(wxHORIZONTAL)
		Local optionsSizer:wxBoxSizer = New wxBoxSizer.Create(wxHORIZONTAL)
		
		m_sdbSizer = New wxStdDialogButtonSizer.CreateSizer()
		m_sdbSizerOK = New wxButton.Create(Self, wxID_OK)
		m_sdbSizerCancel = New wxButton.Create(Self, wxID_CANCEL)
		
		m_panel = New wxPanel.Create(Self, wxID_ANY,,,,, wxTAB_TRAVERSAL)
		m_panel.SetForegroundColour(New wxColour.Create(0,0,0))
		m_panel.SetBackgroundColour(New wxColour.Create(255,255,255))

		m_staticline = New wxStaticLine.Create(Self, wxID_ANY,,,,, wxLI_HORIZONTAL)
		
		m_label1 = New wxStaticText.Create(m_panel, wxID_ANY, text)
		m_label1.Wrap(-1)
		
		m_field1 = New wxTextCtrl.Create(m_panel, wxID_ANY, "",,,200,,0)

		bSizer2.AddCustomSpacer(0, 15, 1, wxEXPAND, 5)
		bSizer3.Add(m_label1, 0, wxBOTTOM|wxRIGHT|wxLEFT, 5)
		bSizer3.Add(m_field1, 0, wxRIGHT|wxLEFT, 5)
		bSizer2.AddSizer(bSizer3, 1, wxEXPAND, 5)
		bSizer2.AddCustomSpacer(0, 15, 1, wxEXPAND, 5)
		bSizer1.Add(m_panel, 1, wxEXPAND, 5)
		bSizer1.Add(m_staticline, 0, wxEXPAND, 5)
		m_sdbSizer.AddButton( m_sdbSizerOK)
		m_sdbSizer.AddButton( m_sdbSizerCancel)
		
		Local panel:wxPanel = New wxPanel.Create(Self, wxID_ANY,,,,, wxTAB_TRAVERSAL)
		m_chkbxWholeWord = New wxCheckBox.Create(panel, wxID_ANY, "Whole Word")
		optionsSizer.Add(m_chkbxWholeWord, 0, wxBOTTOM|wxRIGHT|wxLEFT, 5)
		m_chkbxMatchCase = New wxCheckBox.Create(panel, wxID_ANY, "Match Case")
		optionsSizer.Add(m_chkbxMatchCase, 0, wxRIGHT|wxLEFT, 5)

		panel.SetSizer(optionsSizer)
		panel.Layout()
		optionsSizer.Fit(panel)
		bsizer1.Add(panel, 0, wxEXPAND, 10)
		
		bSizer1.AddSizer(m_sdbSizer, 0, wxEXPAND|wxTOP|wxBOTTOM, 10)
				
		m_panel.SetSizer(bSizer2)
		m_panel.Layout()
		bSizer2.Fit(m_panel)

		m_sdbSizer.Realize()
		
		SetSizer(bSizer1)
		Layout()
		bSizer1.Fit(Self)
		Center(wxBOTH)
		
		m_sdbSizerCancel.ConnectAny(wxEVT_COMMAND_BUTTON_CLICKED, OnCancel, Null, Self)
		m_sdbSizerOK.ConnectAny(wxEVT_COMMAND_BUTTON_CLICKED, OnOk, Null, Self)
		
		m_sdbSizerOK.SetDefault()
		m_field1.SetFocus()
		
		Centre()

		m_chkbxWholeWord.SetValue(findOptions.wholeWord)
		m_chkbxMatchCase.SetValue(findOptions.matchCase)

		ret = ShowModal()
		
		If ret Then
			value = m_field1.GetValue()
		Else
			value = ""
		EndIf
		
	End Method

	Method GetString:String()
		Return value
	EndMethod
	
	Function OnCancel(ev:wxEvent)
		Local d:TEntryDialog = TEntryDialog(ev.sink)
		d.EndModal(False)
	End Function

	Function OnOk(ev:wxEvent)
		Local d:TEntryDialog = TEntryDialog(ev.sink)
		d.findOptions.wholeWord = d.m_chkbxWholeWord.GetValue()
		d.findOptions.matchCase = d.m_chkbxMatchCase.GetValue()
		d.EndModal(True)
	End Function

End Type
</textarea><br><br>Try experimenting with the word "Thre", and adjust the flags to see how the find result reflects the changes.<br><br>The flags themselves are defined in consts.bmx, in the wscintilla.mod folder. <br><br></td></tr></table><br>
<a name="1260006"></a>

<a name="1260007"></a>

<a name="1260008"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MOBii</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Thank thee very much, that example was a gold mine for me <br><br></td></tr></table><br>
<a name="1260009"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I search all file for Edit but it look like I don't have Edit installed in my BlitzMax <br></div><br>"Edit" is a Type defined in the scintilla sample. If you had followed Henri's instructions, you'd have found it there :-) <br><br></td></tr></table><br>
<a name="1260029"></a>

<a name="1260030"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >MOBii</td><td align="right"><font class=tiny>(Posted 2015)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> When I run the Henri/Brucey example it didn't find anything, then I start expect it's my computer that is mocking with me!<br>So I god forbid restart my computer, that is against my religion<br>and then the example code start finding text miraculous.<br>I revert my own find and now it work, maybe I need restart my computer at least every month or so?<br>The funny part in this story is that:<pre class=code>Const wxSCI_WHOLEWORD:Int = 2
Const wxSCI_MATCHCASE:Int = 4</pre>was working the whole time ^^<br><br>Sometime laziness has it's drawbacks! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
