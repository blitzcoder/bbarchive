<!DOCTYPE html><html lang="en" ><head ><title >Wx Issues</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Wx Issues</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=124" >Brucey's Modules</a>/<a href="#bottom" >Wx Issues</a><br><br>
<a name="1183867"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Rigz</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Sorry, me again :) <br><br>So TimelineFX is compiling ok however I'm getting one or two errors occurring. The first one in dialog boxes dont seem to be returning string in a friendly way. Here's a sample program that replicates the issue:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
'
' wxAboutBox sample
'
'
'
SuperStrict

Framework wx.wxApp
Import wx.wxFrame
Import wx.wxAboutBox
Import wx.wxFileDialog
Import wx.wxBusyInfo


New MyApp.run()


Type MyApp Extends wxApp

	Field frame:MyFrame
	Field file_menu_importshape:wxMenuItem

	Method OnInit:Int()

		frame = MyFrame(New MyFrame.Create(, -1, "AboutBox example"))
		
		frame.Center()
		frame.Show()
		
	
		Return True
	
	End Method

End Type

Type MyFrame Extends wxFrame

	Method OnInit()
	
		' create a menu bar
		
		Local fileMenu:wxMenu = wxMenu.CreateMenu()
		
		' the "About" item should be in the help menu
		Local helpMenu:wxMenu = wxMenu.CreateMenu()
		Local file_menu_importshape:wxMenuItem
		
		helpMenu.Append(wxID_ABOUT, "&amp;About...~tF1", "Show about dialog")
		
		fileMenu.Append(wxID_EXIT, "E&amp;xit~tAlt-X", "Quit this program")
		file_menu_importshape = New wxMenuItem.CreateMenuItem(fileMenu, 9999, "Import &amp;Shape~tCTRL+SHIFT+I", "Load a particle shape")
		fileMenu.AppendItem(file_menu_importshape)
		
		' now append the freshly created menu to the menu bar...
		Local menuBar:wxMenuBar = wxMenuBar.CreateMenuBar()
		menuBar.Append(fileMenu, "&amp;File")
		menuBar.Append(helpMenu, "&amp;Help")
		

		SetMenuBar(menuBar)
		
		' create a status bar just for fun
		CreateStatusBar(2)
		SetStatusText("Welcome to wxWidgets!")
		
		Connect(wxID_EXIT, wxEVT_COMMAND_MENU_SELECTED, OnQuit)
		Connect(wxID_ABOUT, wxEVT_COMMAND_MENU_SELECTED, OnAbout)
		Connect(9999, wxEVT_COMMAND_MENU_SELECTED, OnImportShape)
		
	End Method

	Function OnQuit(event:wxEvent)
		' true is to force the frame to close
		wxWindow(event.parent).Close(True)
	End Function
	
	Function OnAbout(event:wxEvent)
		
		Local info:wxAboutDialogInfo = New wxAboutDialogInfo.Create()
		
		info.SetName("My Program")
		info.SetVersion("1.2.3 Beta")
		info.SetDescription("This program does something great.")
		info.SetCopyright("(C) 2007 Me &lt;my@...;")
		
		' uncomment to see "generic" about box
		'info.SetWebSite("http://www.blitzmax.com", "Visit BlitzMax for cool stuff!")
		
		wxAboutBox(info)
		
	End Function
	
	Function OnImportShape(event:wxEvent)
		Local OpenLib:wxFileDialog = New wxFileDialog.Create(wxWindow(event.parent), "Load a Particle Shape",, ,  ..
			, wxFD_OPEN | wxFD_MULTIPLE)
		If openlib.Showmodal() = wxID_OK
			Local busyinfo:wxBusyInfo = New wxBusyInfo.CreateBusyInfo("Please wait, Loading shape...")
			For Local filename:String = EachIn OpenLib.GetPaths()
				DebugLog filename

			Next
			busyinfo.Free()
		End If
		openlib.free()
	End Function
	
End Type

</textarea><br><br>Just select any file and see what debuglog throws up, I'm getting:<br><br>DebugLog:E\ieieXPrcehpsEpoin\asi.n<br><br>Any ideas? Thanks! <br><br></td></tr></table><br>
<a name="1183870"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Apologies. I seem to have introduced a string problem in a recent commit.<br><br>It should be working again now. <br><br></td></tr></table><br>
<a name="1183873"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Rigz</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Great thanks Brucey :) <br><br></td></tr></table><br>
<a name="1184488"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Rigz</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> I also have an issue with capturing the mouse. If I double click on the canvas window or If I click on another window and the mouse happens to be also over the canvas window it throws an error: <br><br>Assert "GetCapture() == this" failed in ReleaseMouse(): attempt to release mouse but this window hasn't captured it.<br><br>I recreated int eh glCanvas sample, just try double clicking on the canvas:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework wx.wxApp
Import wx.wxFrame
Import wx.wxglmax2D
Import wx.wxTimer
Import wx.wxMouseEvent

SetGraphicsDriver GLMax2DDriver()

New MyApp.run()


Type MyApp Extends wxApp

	Field frame:MyFrame

	Method OnInit:Int()

		' Create the main application windowType MyFrame Extends wxFrame

		frame = MyFrame(New MyFrame.Create(,,"", , , 640, 480))
		
		' Show it and tell the application that it's our main window
		frame.show(True)
		SetTopWindow(frame)

		Return True
	End Method

End Type

Type MyFrame Extends wxFrame

	Field canvas:MyCanvas

	Method OnInit()

		canvas = MyCanvas(New MyCanvas.Create(Self, -1, GRAPHICS_BACKBUFFER|GRAPHICS_DEPTHBUFFER))
	
		ConnectAny(wxEVT_CLOSE, OnClose)
	End Method
	
	Function OnClose(event:wxEvent)
		MyFrame(event.parent).canvas.timer.Stop() ' we really need to stop the timer on Mac...
		event.Skip()
	End Function

End Type


Type MyCanvas Extends wxGLCanvas

	Field timer:wxTimer
	Field left_mouse_state:Int

	Method OnInit()
		SetBackgroundStyle(wxBG_STYLE_CUSTOM)
	
		timer = New wxTimer.Create(Self)


		ConnectAny(wxEVT_TIMER, OnTick)
		ConnectAny(wxEVT_MOUSE_EVENTS, onMouse)

		timer.Start(100)
	End Method

	Method OnPaint(event:wxPaintEvent)
		Render()
	End Method

	Method Render()

		SetGraphics CanvasGraphics2D( Self )

		SetClsColor(255, 255, 255)
		SetColor(0, 0, 0)
		
		Cls

		drawClock()
		
		Flip

	End Method

	Function OnTick(event:wxEvent)
		wxWindow(event.parent).Refresh()
	End Function
	
	Function onMouse(event:wxEvent)
		Local evt:wxMouseEvent = wxMouseEvent(event)
		Local canvas:MyCanvas = MyCanvas(event.parent)
		Select evt.GetEventType()
			Case wxEVT_MOTION
			Case wxEVT_LEFT_DOWN
				canvas.left_mouse_state = True
				canvas.CaptureMouse()
			Case wxEVT_LEFT_UP
				canvas.left_mouse_state = False
				canvas.ReleaseMouse()
		End Select
	End Function	
	
	
	Method drawClock()
	
		SetLineWidth(2)

		Local x:Int = 200, y:Int = 200
		Local radius:Int = 100
		' clock ticks
		For Local i:Int = 0 Until 12
			Local inset:Double = 0
		
			If i Mod 3 = 0 Then
				SetLineWidth(2)
				inset = 0.2 * radius
			Else
				inset = 0.1 * radius
				SetLineWidth(1)
			End If
			
			DrawLine x + (radius - inset) * Cos(i * 30), y + (radius - inset) * Sin(i * 30), ..
				x + radius * Cos(i * 30), y + radius * Sin (i * 30)
			
			' draw numbers
			inset = 0.3 * radius
			
			Local t:Int = (i + 2) Mod 12 + 1
			DrawText romanNumerals(t), x + (radius - inset) * Cos(i * 30) - 7, y + (radius - inset) * Sin(i * 30) - 6
		Next


		' clock hands
		Local hours:Int = CurrentTime()[0..2].toInt() Mod 12
		Local minutes:Int = CurrentTime()[3..5].toInt()
		Local seconds:Int = CurrentTime()[6..8].toInt()
		
		' hour hand:
		' the hour hand is rotated 30 degrees  per hour + 1/2 a degree per minute
		'
		SetLineWidth(4)

		DrawLine x, y, x + radius / 2 * Sin(30 * hours + minutes / 2.0), ..
				   y + radius / 2 * -Cos(30 * hours + minutes / 2.0)
		
		
		' minute hand:
		' the minute hand is rotated 6 degrees per minute + 1/10 a degree per second
		'
		SetLineWidth(2)
		DrawLine x, y, x + radius * 0.75 * Sin(6 * minutes + seconds / 10.0), ..
				   y + radius * 0.75 * -Cos(6 * minutes + seconds / 10.0)
		
		' seconds hand:
		' the second hand is rotated 6 degrees per second
		'
		SetColor(255, 0, 0)

		DrawLine x, y, x + radius * 0.7 * Sin(6 * seconds), ..
				   y + radius * 0.7 * -Cos(6 * seconds)


	End Method

	Method romanNumerals:String(value:Int)
		Local rn:String = ""
		
		While value &gt; 9
			rn:+ "X"
			value:- 10
		Wend
	
		If value &gt; 8 Then
			rn:+ "IX"
			value:- 9
		End If
		
		If value &gt; 4 Then
			rn:+ "V"
			value:- 5
		End If
		
		If value &gt; 3 Then
			rn:+ "IV"
			value:- 4
		End If
		
		While value &gt; 0
			rn:+ "I"
			value:- 1
		Wend
		
		Return rn
	End Method

End Type
</textarea><br><br>Thanks! <br><br></td></tr></table><br>
<a name="1184500"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> It's possible that a double click handles differently than a normal click because it is so fast.<br>To stop the warning, you can use :<br><pre class=code>
If canvas.HasCapture()
   canvas.ReleaseMouse()
End If
</pre><br>or perhaps use HasCapture() elsewhere when you need to be sure it is captured.<br><br>I also believe that when you have the mouse down, it is automatically captured for the duration of the time you press the mouse button until you release it. (Can't remember where I read that tho…)<br><br><br>Also, if you are running a timer, you should stop it before you destruct something which it is connected to - or there is a chance the timer will tick, and call against an object (window/etc) which no longer exists. This happens with your above example on OS X ;-)<br>If it is still live when your window closes, you can always call Stop() on it, just to be on the safe side before your window Destroy() call. <br><br></td></tr></table><br>
<a name="1184591"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Rigz</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> Cheers, that seems to have worked :)<br><br>Just another quick question about wxListCtrl. I was inserting an item like this:<br><br><pre class=code>
		item.SetText(name)
		item.SetImage(image)
		item.SetData(shape)
		Self.InsertItem(item)
</pre><br><br>However that doesn't seem to insert anything anymore. I've had to change it to use InsertImageStringItem, and then use SetItemData separately. I don't think it liked the fact that an index wasn't being set. I tried SetID on the item before inserting it but still nothing happened. InsertImageStringItem seems to be working OK as an alternative so that's fine, just wondering if perhaps there's a bug in there? <br><br></td></tr></table><br>
<a name="1184592"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Rigz</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> Actually I just tested again on a different list and using SetId on the item did work so, it does seem ok.. I must have missed something first time round :) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
