<!DOCTYPE html><html lang="en" ><head ><title >Linux off-screen rendering with OSMesa</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Linux off-screen rendering with OSMesa</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=124" >Brucey's Modules</a>/<a href="#bottom" >Linux off-screen rendering with OSMesa</a><br><br>
<a name="1096714"></a>

<a name="1096715"></a>

<a name="1096756"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Since off-screen rendering has come up occasionally, I thought it would be interesting to see if we could do it in BlitzMax.<br>OSMesa is a library which can render directly into memory, and is available on Linux.<br><br>In order to use it instead of the default glgraphics and glmax2d modules, it required some new ones which implement the OSMesa functionality while mirroring the current framework.<br><br>So, I've added 3 new modules to SVN :<br>* BaH.OSMesa<br>* BaH.OSMesaGraphics<br>* BaH.OSMesaMax2D<br><br>Also required is libOSMesa and it's dev package (for compiling).<br><br>Here's a small example included with the max2d module :<br><pre class=code>

'
' OSMesa drawing example
'
'
SuperStrict

Framework BaH.OSMesaMax2D
Import BRL.PNGLoader

' create a pixmap
Local pixmap:TPixmap = CreatePixmap(400, 400, PF_RGBA8888)


'
' Note: To remain compatible with the Graphics framework, we must pass the pixel buffer
'       as an address (integer).
'
Local g:TGraphics = CreateGraphics(400, 400, 32, Int Ptr(Varptr pixmap.pixels)[0], 0)

' Set the current graphics context, so that we can do stuff.
SetGraphics(g)


' do some drawing
Cls


DrawText "OSMesa Drawing!!", 100, 200

SetColor 255, 0, 0

DrawLine 50, 50, 100, 100

SetColor 0, 255, 0
DrawLine 50, 60, 100, 110

SetColor 0, 0, 255
DrawLine 50, 70, 100, 120

' forces any buffered drawing commands to finish
Flip


' Save our pixmap!
' Graphics are drawn on an inverted Y axis, so perform a y-flip when you want to see it properly.
'
SavePixmapPNG(YFlipPixmap(pixmap), "test.png")
</pre><br><br>As you can see, everything is fairly standard, apart from the CreateGraphics call, where we also pass in the address to our image buffer. To keep it the same API, we use an address, rather than a Byte Ptr to the data itself...<br><br>Here's the resulting png from the example :<br><img src="http://brucey.net/programming/blitz/misc/screenshots/osmesamax2d_example.png"><br><br>Of course, your mileage may vary (considerably).<br>It appears that using BRL.Graphics requires some X11 libraries because BRL.System needs them for some reason. (of all modules you'd not want to tie yourself into X11....).<br>But the 3 modules don't specifically do anything with X11... so you may get away with only requiring the X11 libraries to be present, and not actually using them. I haven't tried it though..<br><br>But, an interesting test anyway.<br>Perhaps useful for a server to render 3D to images or something :-)<br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1096938"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ooh. Gonna try this later, sounds much simpler than the xvfb option, thanks Brucey! <br><br></td></tr></table><br>
<a name="1096966"></a>

<a name="1096970"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> It doesn't work... :-)<br><br>I just created a new Ubuntu 11.04 server instance, installed the required libraries, and the pre-compiled example seg-faulted...  haw haw.<br><br>However, it does that because of the calls to initialising X11 stuff...  which is a no-no on a server without X11 running.<br><br>So, I went back to the drawing board.<br>The culprit is BRL.System... so we can't have that at all...  BRL.Graphics imports it... so we need a new Graphics base... and therefore we need a new Max2D base...  so I've created one each of those, which remove the requirement of System, Hooks, and Polling...<br><br>I rebuilt the example, and ran it on the server. It works :-)<br><br>I'll commit the new stuff and changes now...<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1096968"></a>

<a name="1096969"></a>

<a name="1096974"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Right... everything is in SVN now...<br><br>The modules are :<br>* BaH.OSMesa<br>* BaH.OSMesaGraphics<br>* BaH.OSMesaGraphicsBase<br>* BaH.OSMesaMax2D<br>* BaH.OSMesaMax2DBase<br><br>The "base" modules are implementations of BRL.Graphics and BRL.Max2D with support for BRL.System (mouse/keyboard) and Polling/Hooks removed.<br><br>Of course the big caveat is, that if you need BRL.System support, you cannot use these modules. If you need BRL.Graphics, or something which imports BRL.Graphics, you cannot use these modules.. or, you change your dependencies to use BaH.OSMesaGraphicsBase instead of BRL.Graphics... etc.<br><br>But, it works... so perhaps it's worth giving up a little functionality/support for greater things...<br><br><br>This is the result of running the app on the new Ubuntu 11.04 server instance, which doesn't have X11 running (as displayed on my mac) :<br><img src="http://brucey.net/programming/blitz/misc/screenshots/osmesamax2d_example2.png"><br><br><br>On a base Ubuntu server install, I had to run the following commands to have all the required libraries :<br><pre class=code>
sudo apt-get install libgl1-mesa-dev
sudo apt-get install libglu1-mesa-dev
</pre><br>which also installed all the other required libraries. osmesa is part of the mesa distro.<br>You may not need to install the "dev" variants for your runtime binaries...<br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1097043"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Russell</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#5">[#5]</a></td></tr></table></td></tr><tr ><td class="posttext"> Are you a cybernetic programming machine from the future, Brucey? :)<br><br>Seriously, I can't imagine how you can find the time to do all of the programming you do and still find time in the day to eat, sleep, work (you're a programmer, right?), etc.<br><br>The BMax community would be a lesser entity if were not for your superhuman programming capabilities. Thank goodness you took an interest in BlitzMax and not Dark Basic or Free Basic or some other less-cool programming language...<br><br>Anyway...&lt;Steps down from soapbox&gt;, I guess the main advantage to offscreen (direct-to-memory) drawing is speed? Or are there other, less obvious advantages?<br><br>Russell <br><br></td></tr></table><br>
<a name="1097063"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#6">[#6]</a></td></tr></table></td></tr><tr ><td class="posttext"> 'lo Brucey, many thanks for this. Got it in place on the server now, but appear to have forgotten root password, so awaiting update from server-sharing accomplice to be able to install the dependencies! (Easy enough to reinstall from scratch if necessary, since it's not doing anything important and <a href="http://www.linode.com/" target="_blank">Linode</a> makes it dead easy.)<br><br>This will be really cool if it works, anyway, opening up all sorts of possibilities, and I imagine it'll be better than the xvfb option since I assume that would be tied to one app rendering at a time, whereas this probably wouldn't. Anyway, thanks again! You are truly a god among men.<br><br>I'll post if/when I get it working! <br><br></td></tr></table><br>
<a name="1097067"></a>

<a name="1097068"></a>

<a name="1097069"></a>

<a name="1178480"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#7">[#7]</a></td></tr></table></td></tr><tr ><td class="posttext"> After figuring out root pass... OMG OMG OMG... and... <a href="http://www.hi-toro.com/test.png" target="_blank">woo!</a><br><br>Only hiccup was that, contrary to what you said here...<br><br><div class="quote"> <br>which also installed all the other required libraries. osmesa is part of the mesa distro.<br>You may not need to install the "dev" variants for your runtime binaries...<br> <br></div><br><br>... I had to manually do this to get it to work after installing the mesa stuff above:<br><br><pre class=code>
apt-get install libosmesa6
apt-get install libosmesa6-dev
</pre><br><br>Brilliant! God. Among. Men. <br><br></td></tr></table><br>
<a name="1097070"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#8">[#8]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hmmm, still a god among men, but I can't seem to make use of LoadPixmap, SetClsColor, etc -- just getting segfaults. I'm clutching at straws now, in terms of imports. As far as I can tell, LoadPixmap doesn't depend on Graphics, but I don't think I'm getting what exactly I should be importing.<br><br>Also, will the DrawImage, etc, commands be possible, or just Pixmap stuff? I don't mind creating a separate set of customised BRL modules, but I think I'm failing on "If you need BRL.Graphics, or something which imports BRL.Graphics, you cannot use these modules.. or, you change your dependencies to use BaH.OSMesaGraphicsBase instead of BRL.Graphics... etc"... embarrasingly.<br><br>Could you show the dependencies required for the below, or for a DrawImage version, if that's even possible?<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'
' OSMesa drawing example
'
'
SuperStrict

Framework BaH.OSMesaMax2D
Import BRL.PNGLoader
Import BRL.StandardIO

' create a pixmap
Local pixmap:TPixmap = LoadPixmap ("boing.png") ' CreatePixmap(400, 400, PF_RGBA8888)

'
' Note: To remain compatible with the Graphics framework, we must pass the pixel buffer
'       as an address (integer).
'
Local g:TGraphics = CreateGraphics(400, 400, 32, Int Ptr(Varptr pixmap.pixels)[0], 0)

' Set the current graphics context, so that we can do stuff.
SetGraphics(g)

' do some drawing

SetClsColor 32, 64, 128
Cls


DrawText "OSMesa Drawing!!", 100, 200

SetColor 255, 0, 0

DrawLine 50, 50, 100, 100

SetColor 0, 255, 0
DrawLine 50, 60, 100, 110

SetColor 0, 0, 255
DrawLine 50, 70, 100, 120

SetColor 255, 255, 255
DrawPixmap pixmap, 0, 0

' forces any buffered drawing commands to finish
Flip


' Save our pixmap!
' Graphics are drawn on an inverted Y axis, so perform a y-flip when you want to see it properly.
'
SavePixmapPNG(YFlipPixmap(pixmap), "test.png")
</textarea> <br><br></td></tr></table><br>
<a name="1097072"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#9">[#9]</a></td></tr></table></td></tr><tr ><td class="posttext"> OK, so after a load of local edit/FTP/PuTTY action, I can confirm it's simply failing to load the pixmap (it's coming back Null), which is a PNG in the same directory as the source file (in BlitzMax/bin for now), both with basic filename and full path, yet BRL.PNGLoader imports BRL.Pixmap, where LoadPixmap is, so I'm not sure why it's failing there... gah!<br><br>I get the same whether root or myself... any ideas? As you can see, the files are all in place:<br><br><pre class=code>
CENSORED@MACHINE:~/blitz/BlitzMax/bin$ ls
bcc  bmk  boing.png  docmods  fasm  makedocs  osmesa  osmesa.bmx  test.png
CENSORED@MACHINE:~/blitz/BlitzMax/bin$
</pre><br><br>Anyway, enough for tonight... fun, though! <br><br></td></tr></table><br>
<a name="1097077"></a>

<a name="1097093"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#10">[#10]</a></td></tr></table></td></tr><tr ><td class="posttext"> Very strange...<br><br>I've added another example, which loads a png and draws it with DrawImage... and also sets the background colour with SetClsColor.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

'
' OSMesa drawing example 2
'
'
SuperStrict

Framework BaH.OSMesaMax2D
Import BRL.PNGLoader
Import BRL.StandardIO

Local pix:TPixmap = LoadPixmap("star.png")
If Not pix Then
	Print "Error loading image"
	End
End If

' create a pixmap
Local pixmap:TPixmap = CreatePixmap(400, 400, PF_RGBA8888)


'
' Note: To remain compatible with the Graphics framework, we must pass the pixel buffer
'       as an address (integer).
'
Local g:TGraphics = CreateGraphics(400, 400, 32, Int Ptr(Varptr pixmap.pixels)[0], 0)

' Set the current graphics context, so that we can do stuff.
SetGraphics(g)

SetBlend ALPHABLEND
Local image:TImage = LoadImage(pix)

SetClsColor 50, 50, 50

' do some drawing
Cls


DrawText "OSMesa Drawing!!", 100, 200

SetColor 255, 0, 0

DrawLine 50, 50, 100, 100

SetColor 0, 255, 0
DrawLine 50, 60, 100, 110

SetColor 0, 0, 255
DrawLine 50, 70, 100, 120

SetColor 255, 255, 255
DrawImage image, 200, 50

' forces any buffered drawing commands to finish
Flip


' Save our pixmap!
' Graphics are drawn on an inverted Y axis, so perform a y-flip when you want to see it properly.
'
SavePixmapPNG(YFlipPixmap(pixmap), "test.png")
</textarea><br><br>Here's the output :<br><img src="http://brucey.net/programming/blitz/misc/screenshots/osmesamax2d_example3.png"><br><br><font class="tiny">Last edited 2011</font> <br><br></td></tr></table><br>
<a name="1097078"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#11">[#11]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm not sure it's a good idea to be using the pixmap you are loading, to also be the one you are going to be drawing into... especially if it is not the expected format - size/bits per pixel.<br><br>It's important that the output buffer memory size is the right size for the graphics context you are creating, otherwise you will have no end of issues when mesa is trying to write to data that it shouldn't be (because your actual buffer memory is less that it thinks it has access to)<br><br>You can also enable MESA_DEBUG before running your app. If there are any problems with the graphics context, it will show up on the console :<br><pre class=code>
export MESA_DEBUG=1
</pre><br><br>:o) <br><br></td></tr></table><br>
<a name="1097080"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#12">[#12]</a></td></tr></table></td></tr><tr ><td class="posttext"> I should also add that using a Pixmap is not required for your memory output buffer. You could just as well use a MemAlloc or Bank, as long as you create a block of memory large enough to hold the image data.<br><br>One other thing, I believe the default MAX size of an OSMesa context is about 1280 x 1024.<br>You can, apparently, increase this by changing a .h file somewhere in the system, but I haven't looked into that. (Google is your friend, for more information ;-)<br><br><br>The absolute most important thing in all this appears to be the link order of libOSMesa and libGL. OSMesa lib needs to come first. If you get lots of errors when MESA_DEBUG is enabled referring to trying to perform gl commands on no context, it's likely that the link order may be broken. You can check the order by running the 'ldd' command on your executable.<br>However, on Ubuntu at least, the default order of Module importing that I've created should be sufficient. Your mileage on other distros may vary, if said libraries are not located in /usr/lib.<br><br>Happy coding :o) <br><br></td></tr></table><br>
<a name="1097092"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#13">[#13]</a></td></tr></table></td></tr><tr ><td class="posttext"> <div class="quote"> I guess the main advantage to offscreen (direct-to-memory) drawing is speed? Or are there other, less obvious advantages? <br></div><br>I don't think speed will improve rendering into main memory, since the cpu will be doing all the work.<br><br>The main advantage I see is that fact that you don't need a gfx card to do the rendering. Well, this set of modules is not intended for displaying anything on the screen, since I've gone to so much length to exclude the X11 subsystem from it.<br>The main purpose of these modules is to be able to create gl-rendered images on a system where you'll only be creating image files, or perhaps sending the data directly to a browser over the web...<br><br>Just another tool in the arsenal, really :-)<br><br><div class="quote"> you're a programmer, right? <br></div><br>Yeah... sad, isn't it? Today I'm working on GWT/JBoss/Oracle. Who knows what surprise is in store for tomorrow!... <br><br></td></tr></table><br>
<a name="1097103"></a>

<a name="1097104"></a>

<a name="1178481"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#14">[#14]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ah, <b><a href="http://www.hi-toro.com/test.png" target="_blank">ya beauty</a></b>! Didn't realise I was trying to use the PNG as a graphics buffer (I usually just call Graphics, me!), so that was silly. I'll have to play around with that side of things a bit, and also play with this new toy and get a little server app going!<br><br>One thing I didn't get was how this part of the CreateGraphics call works, as it represents the hertz parameter:<br><br><pre class=code>
Int Ptr(Varptr pixmap.pixels)[0]
</pre><br><br>What's going on there? <br><br></td></tr></table><br>
<a name="1097105"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#15">[#15]</a></td></tr></table></td></tr><tr ><td class="posttext"> That would be from this bit of the original post :<br><div class="quote"> <br>As you can see, everything is fairly standard, apart from the CreateGraphics call, where we also pass in the address to our image buffer. To keep it the same API, we use an address, rather than a Byte Ptr to the data itself...<br> <br></div><br><br>To pass in a pixel buffer instead, we would need a new function. But this way, you get to use the standard API functions. It's a bit of a hack to pass the address in as an Int, but it works, and it keeps things nice and tidy.<br>I'm a lazy bugger, so the less I have to change, the better ;-)<br><br>Glad you're up and running! <br><br></td></tr></table><br>
<a name="1097106"></a>

<a name="1097117"></a>

<a name="1178482"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#16">[#16]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ohhh... you're just borrowing the hertz field since it's not needed. Nice/nasty! I like.<br><br>Thanks again -- I'll post anything interesting I manage to do with it.<br><br>EDIT: Stupid life getting in the way! Still not set up a 'live' server, but had it render <a href="http://www.hi-toro.com/test.png" target="_blank">this, from the code below</a>, on the server, which is cool.<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework BaH.OSMesaMax2D
Import BRL.PNGLoader
Import BRL.StandardIO
Import BRL.Random

Local pixmap:TPixmap = CreatePixmap (640, 480, PF_RGBA8888)

Local g:TGraphics = CreateGraphics (640, 480, 32, Int Ptr (Varptr pixmap.pixels)[0], 0)

SetGraphics(g)

SetBlend ALPHABLEND
SetMaskColor 255, 0, 255

Local rocket:TImage = LoadImage ("boing.png") ' From samples/hitoro/rockout...

SetClsColor 32, 64, 96
Cls

For Local loop:Int = 0 Until 100
	Local scale:Float = Rnd (0.1, 0.5)
	SetScale scale, scale
	SetRotation Rnd (360)
	DrawImage rocket, Rnd (GraphicsWidth ()), Rnd (GraphicsHeight ())
Next

Flip

SavePixmapPNG (YFlipPixmap (pixmap), "test.png")

</textarea> <br><br></td></tr></table><br>
<a name="1097442"></a>

<a name="1097443"></a>

<a name="1097446"></a>

<a name="1097461"></a>

<a name="1097462"></a>

<a name="1178483"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#17">[#17]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi Brucey,<br><br>Did you get to try minib3d at all? I had a quick go but got stuck...<br><br>I have done this, though, which is pretty cool, running "live" at 15 FPS in the background, using only 2.5% CPU! Refresh a few times to see it move...<br><br>http://www.hi-toro.com:9080/boing.png<br><br>(Has to be copied and pasted as the forum doesn't like colons in URLs!)<br><br>You can refresh after every second (it'll deliberately disconnect if hit more than this).<br><br>This is the code, brutally hacked into my existing <a href="/codearcs.php?code=2482" target="_blank">web server</a>... needs boing.png from /samples/hitoro/rockout and ability to write to its local folder, where it'll dump a PNG for each IP address that connects and not tidy up after itself...<br><br>Working with this on VirtualBox/Ubuntu makes things pretty easy!<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">

' IMPORTANT: Make sure "Threaded Build" is enabled in the Program -&gt; Build Options menu!

' -----------------------------------------------------------------------------
' BlitzServe 2 -- a very crude multithreaded HTTP server...
' -----------------------------------------------------------------------------

SuperStrict

Framework BaH.OSMesaMax2D

Import BRL.PNGLoader
Import BRL.StandardIO

Import BRL.FileSystem
Import BRL.Retro
Import BRL.Bank
Import BRL.Threads
Import BRL.Stream
Import BRL.SocketStream

' -----------------------------------------------------------------------------
' Set this to a folder on your hard drive containing the files to be served:
' -----------------------------------------------------------------------------

Global folder:String = CurrentDir ()

' The value below is derived from Win32 -&gt; GetSystemInfo (info:SYSTEM_INFO) -&gt;
' info.dwAllocationGranularity, the 'ideal' size for optimum read/write speeds
' in Windows. Later version might report bigger numbers, but this will be fine:

Const OPTIMUM_IO:Int = 65536

' -----------------------------------------------------------------------------
' To test...
' -----------------------------------------------------------------------------

' 1) Run the program and launch a web browser;

' 2) In the browser's address bar, type 127.0.0.1 plus the file name, eg.

'		<a href="http://127.0.0.1/index.html" target="_blank">http://127.0.0.1/index.html</a>
'		<img src="http://127.0.0.1/h0tchix.jpg">

'	Don't type the folder name!

' 3) If your firewall complains, you need to unblock/allow this program!

' 4) Repeatedly hit F5/Refresh in your browser to see the server handle
'    cut-off requests (you'll see ERROR: xyz messages).

' -----------------------------------------------------------------------------

' -----------------------------------------------------------------------------
' OK...
' -----------------------------------------------------------------------------

AppTitle = "BlitzServe 2..."

' -----------------------------------------------------------------------------
' Remove trailing slash from server's folder (HTTP GET command prefixes file
' name with a forward slash)...
' -----------------------------------------------------------------------------

folder = Replace (folder, "\", "/")

If (Right (folder, 1) = "/")
	folder = Left (folder, Len (folder) - 1)
EndIf

If FileType (folder) = 0
	RuntimeError "Set folder:String to a folder on your computer!"
EndIf

' -----------------------------------------------------------------------------
' Print queue for threads -- multiple threads running at the same time will
' cause corrupted output for Print, so queue within the thread and print all
' the thread's messages when ready...
' -----------------------------------------------------------------------------

Type PrintList

	Field list:TList = CreateList ()

	Method Add (info:String)
		ListAddLast list, info
	End Method

	Method PrintAll ()
		For Local i:String = EachIn list
			Print i
			ListRemove list, i
		Next
	End Method

End Type

' -----------------------------------------------------------------------------
' Each connection has a socket and an associated stream to read from...
' -----------------------------------------------------------------------------

Type Connection
	Field socket:TSocket
	Field stream:TSocketStream
End Type

' -----------------------------------------------------------------------------
' Thread list and mutex for safe access...
' -----------------------------------------------------------------------------

Global ThreadListMutex:TMutex = CreateMutex ()
Global ThreadList:TList = CreateList ()

' -----------------------------------------------------------------------------
' Temporary graphics window...
' -----------------------------------------------------------------------------

' This is just to allow keyboard input to be captured. Don't press ESC on
' the IDE output, as that just terminates the program. Press ESC with the
' graphics window highlighted so the program can close all connections. It won't
' cause any harm if you don't -- this is just for completeness' sake...

' Note that in real life, this would just be running in an infinite loop
' so there would be no need to test for ESC!

'Graphics 320, 200

' -----------------------------------------------------------------------------
' Just a local pointer
' -----------------------------------------------------------------------------

Local thread:TThread

Local threads:Int = 0

' -----------------------------------------------------------------------------
' To allow program to exit...
' -----------------------------------------------------------------------------

Global Quit:Int = False

' -----------------------------------------------------------------------------
' Display access mutex...
' -----------------------------------------------------------------------------

Global DisplayAccess:TMutex = CreateMutex ()

' -----------------------------------------------------------------------------
' Display misc...
' -----------------------------------------------------------------------------

' -----------------------------------------------------------------------------
' Port to use...
' -----------------------------------------------------------------------------

Local port:Int

port = 9080
'port = Int (Input ("Run on port: "))

Global Rocket:TImage
Global Display:TPixmap

Function StartDrawing:Int ()
	LockMutex (DisplayAccess)
End Function

Function StopDrawing:Int ()
	UnlockMutex	DisplayAccess
End Function

Type ConnectionAttempt
	Field remoteip:Int
	Field remotetime:Int
End Type

Local remotelist:TList = CreateList ()

' -----------------------------------------------------------------------------
' Create HTTP server...
' -----------------------------------------------------------------------------

Local server:TSocket = CreateTCPSocket ()

If server

	' Bind to port...
	
	If BindSocket (server, port)

		' Start listening for incoming connections...
		
		' NOTE: Don't use default 0 for backlog parameter, as this will cause
		' web pages to fail occasionally. The web browser will be requesting
		' all the files in any HTML page you request, and a 0-sized backlog
		' won't process the requests quickly enough. If the backlog isn't
		' cleared between SocketListen and the call to SocketAccept, the
		' request will fail, meaning images, etc, fail to display.
		
		' 5 is an old Windows standard, but there is lots of disagreement
		' as to what it should be, and different use scenarios. A server
		' expecting to be Slashdotted will require a much larger amount,
		' while a lowly desktop serving local files like this won't need
		' as much...
		
		' More information here:
		
		' <a href="http://tangentsoft.net/wskfaq/advanced.html#backlog" target="_blank">http://tangentsoft.net/wskfaq/advanced.html#backlog</a>
		' <a href="http://stackoverflow.com/questions/114874/socket-listen-backlog-parameter-how-to-determine-this-value" target="_blank">http://stackoverflow.com/questions/114874/socket-listen-backlog-parameter-how-to-determine-this-value</a>
		' <a href="http://patchwork.kernel.org/patch/2297/" target="_blank">http://patchwork.kernel.org/patch/2297/</a>
		
		SocketListen server, 5
		
'		Print
'		Print "BlitzServe 2: awaiting incoming connections..."
'		Print
'		Print "Launch your web browser and direct it to http://127.0.0.1:" + port + "/myfilename.html"
'		Print "where myfilename.html is a file inside the folder you specified..."
'		Print ""
	
		' Size of display...
		
		Local width:Int = 640
		Local height:Int = 480
		
		' Create display...
		
		Display = CreatePixmap (width, height, PF_RGBA8888)
		Local g:TGraphics = CreateGraphics (width, height, 32, Int Ptr (Varptr Display.pixels)[0], 0)
		SetGraphics (g)

		SetBlend ALPHABLEND
		SetMaskColor 255, 0, 255

		AutoMidHandle True

		Rocket = LoadImage ("boing.png")

		SetClsColor 128, 180, 200
		
		Local x:Float, y:Float
		Local xs:Float, ys:Float
		Local ang:Float
		Local sine:Float
		
		x = Rand (GraphicsWidth ())
		y = Rand (GraphicsHeight ())

		xs = Rand (2, 4)
		ys = Rand (2, 4)
		
		Cls
		
		Repeat

'			Cls
			
			x = x + xs
			y = y + ys
			
			If x &lt; 0 Or x &gt; GraphicsWidth () Then xs = -xs; x = x + xs
			If y &lt; 0 Or y &gt; GraphicsHeight () Then ys = -ys; y = y + ys

			ang = ang + 0.5
			SetRotation ang
			
			sine = (Sin (ang) + 0.2) * 0.5
			SetScale sine, sine
			
			DrawImage Rocket, x, y
		
'			Flip
			
			Local ticks:Int = MilliSecs ()
			
			' -------------------------------------------------------------------------
			' See if there's been an incoming connection attempt...
			' -------------------------------------------------------------------------
		
			Local remote:TSocket = SocketAccept (server)

			If remote
				
				Local inlist:Int = False
				
				For Local ca:ConnectionAttempt = EachIn remotelist

					If ca.remoteip = SocketRemoteIP (remote)

						inlist = True

						If ca.remotetime + 1000 &lt; MilliSecs ()
							ListRemove remotelist, ca
							inlist = False
						EndIf

						Exit

					EndIf

				Next

				If Not inlist
					
					Local ca:ConnectionAttempt = New ConnectionAttempt
					ca.remoteip = SocketRemoteIP (remote)
					ca.remotetime = MilliSecs ()
					ListAddLast remotelist, ca
	
					thread = CreateThread (ProcessConnection, remote)
					ListAddLast ThreadList, thread
					threads = threads + 1

				Else
					If SocketConnected (remote) Then CloseSocket remote
				EndIf
				
			EndIf
			
			For thread = EachIn ThreadList
				If Not ThreadRunning (thread)
					ListRemove ThreadList, thread
					threads = threads - 1
				EndIf
			Next

			' -------------------------------------------------------------------------
			' Don't wanna hog CPU...
			' -------------------------------------------------------------------------
			
			Delay 66.66 - (MilliSecs () - ticks) ' 15 FPS
			
		Until Quit
		
'		Print ""
'		Print "Waiting for connections to close..."
		
		' -----------------------------------------------------------------------------
		' Free any open TCP streams...
		' -----------------------------------------------------------------------------

		' Wait for each thread to finish its current job and close its own stream/socket...

		LockMutex ThreadListMutex
			For thread = EachIn ThreadList
				WaitThread thread
			Next
		UnlockMutex ThreadListMutex

		' -----------------------------------------------------------------------------
		' All done!
		' -----------------------------------------------------------------------------
		
		CloseSocket server

	Else
		Print "Couldn't bind to port " + port + "!"
	EndIf
	
Else

	Print "Couldn't create server!..."
	
EndIf

End

' -----------------------------------------------------------------------------
' Threaded file serve function...
' -----------------------------------------------------------------------------

Function ProcessConnection:Object (obj:Object)

	Local p:PrintList = New PrintList

	Local c:Connection = New Connection
	c.socket = TSocket (obj)
	
	If SocketConnected (c.socket)

		'p.Add ""
		'p.Add "Request from " + DottedIP (SocketRemoteIP (c.socket))
		
		c.stream = CreateSocketStream (c.socket)

	Else
		
		'p.Add "ERROR: No stream created from socket!"
		
		' No stream was created -- this can happen!

		KillConnection c
		'p.PrintAll
		
		Return Null

	EndIf

	' ---------------------------------------------------------------------
	' Some variables (see further down for meanings)...
	' ---------------------------------------------------------------------
	
	Local eoc:Int
	Local eop:Int

	Local command:String
	Local parameter:String
	Local file:String
	Local http:String
	Local program:String
	Local incoming:String
	
	' ---------------------------------------------------------------------
	' HTTP requests end with a blank line, so we read until we get that...
	' ---------------------------------------------------------------------

	Repeat
	
		' -----------------------------------------------------------------
		' Read a line from an incoming HTTP request...
		' -----------------------------------------------------------------

		' The format of an incoming request line is:
		
		'		"Command" [space] "parameters"

		' Examples...
						
		'		"GET /thisfile.txt"
		'		"User-Agent; AcmeBrowse"
		
		If SocketConnected (c.socket)
		
			incoming = ReadLine (c.stream)
			
		Else
		
			KillConnection c
			'p.PrintAll
			
			Return Null
			
		EndIf

		If incoming &lt;&gt; ""

			' -------------------------------------------------------------
			' Got a line? Let's parse! Split command and parameter(s)...
			' -------------------------------------------------------------

			eoc = Instr (incoming, " ")				' End of command part of incoming
			command = Lower (Left (incoming, eoc))		' Command part of incoming
			parameter = Mid (incoming, eoc + 1)		' Parameter part of incoming

		EndIf
			
		' -----------------------------------------------------------------
		' Let's see what command we've got...
		' -----------------------------------------------------------------

		Select command$
		
			Case "get "

				' ---------------------------------------------------------
				' Got a HTTP file request!
				' ---------------------------------------------------------

				' Format of GET is: "GET /thisfile.txt"
				
				eop = Instr (parameter, " ")			' End of first parameter ("GET")
				file = Mid (parameter, 1, eop - 1)		' First parameter ("GET")
				http = Mid (parameter, eop + 1)		' Second parameter ("/thisfile.txt")

				' ---------------------------------------------------------
				' Requesting program's name/identifier...
				' ---------------------------------------------------------

			Case "user-agent: "

				program = Mid (incoming, eoc + 1)
				
		End Select
		
	Until incoming = "" ' Got blank line after headers, so all done here...

	file = Replace (file, "\", "/")

	' -------------------------------------------------------------
	' Remove \ from end of filename (used for folder redirection)...
	' -------------------------------------------------------------

	If Right (file, 1) = "/" Then file = Left (file, Len (file) - 1)
	
	' ---------------------------------------------------------------------
	' Lessee what we've got...
	' ---------------------------------------------------------------------
	
	'p.Add "Requested file: " + file
	'p.Add "Requested by: " + program
	'p.Add "Requested HTTP version: " + http
	'p.PrintAll

	' ---------------------------------------------------------------------
	' OK, we barely know what we're doing, so only accept HTTP 1.1...
	' ---------------------------------------------------------------------
	
	If http$ &lt;&gt; "HTTP/1.1"

		' Very wary of streams now!
		
		Try
		
			If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, "HTTP/1.1 505 This server only accepts HTTP version 1.1"
			If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, ""

		Catch error:Object
		
			'p.Add "ERROR (" + file + "): Received non-HTTP 1.1 request, but stream failed outside error-checking!"
			
			KillConnection c
			'p.PrintAll
			
			Return Null
			
		End Try
		
	Else

		' -----------------------------------------------------------------
		' It was a HTTP 1.1 request...
		' -----------------------------------------------------------------

		' Convert any %xx (Hex) codes in URL to Chr (ascii) character...
		' (Eg. %20 is ascii 57, ie. Chr (57), ie. a Space.)
		
		file = UnHexURL (file)

		If file
			If Left (file, 1) &lt;&gt; "/"
				file = "/" + file ' Add leading "/" if not found...
			EndIf
		EndIf
		
		' -------------------------------------------------------------
		' Does the requested file exist in our 'site' folder?
		' -------------------------------------------------------------

		Local file_type:Int = FileType (folder + file)
		
		If file = "" Then file_type = 2
		
		Select file_type
		
			Case 0 ' File does not exist...

				Try
				
					If file = "[TOP_SECRET_RETRACTED_URL!]"
						If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, "&lt;HTML&gt;&lt;TITLE&gt;Bye!&lt;/TITLE&gt;&lt;BODY&gt;Server shutting down...&lt;/BODY&gt;&lt;/HTML&gt;"
						If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, ""
						Quit = True
						KillConnection c
						Return Null
					EndIf
				
				Catch error:Object
				
					'p.Add "ERROR (" + file + "): Stream failed during exit!"
					
					KillConnection c
					'p.PrintAll
					
					Return Null
					
				End Try
				
				'p.Add "404: File not found (" + file + ")"

				Try
				
					If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, "HTTP/1.1 404 Not Found"
					If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, ""

				Catch error:Object
				
					'p.Add "ERROR (" + file + "): Stream failed outside error-checking!"
					
					KillConnection c
					'p.PrintAll
					
					Return Null
					
				End Try
				
			Case 1 ' File exists!

				Try
				
					If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, "HTTP/1.1 200 OK"
					If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, ""

				Catch error:Object
				
					'p.Add "ERROR (" + file + "): Stream failed outside error-checking!"
					
					KillConnection c
					'p.PrintAll
					
					Return Null
					
				End Try

				Local requested:TStream
				
				If file = "/boing.png"

					'p.Add "Got PNG..."
					
					StartDrawing
					
						'p.Add "Started drawing..."
							
						'SetClsColor Rand (100, 255), Rand (100, 255), Rand (100, 255)
						'Cls
						
						'p.Add "Drawing..."

						'Local scale:Float = Rnd (0.1, 2.0)
						'SetScale scale, scale

						'SetRotation Rnd (360)
						
						'DrawImage Rocket, Rnd (GraphicsWidth ()), Rnd (GraphicsHeight ())
						'Flip
						
						'p.Add "Saving..."

						If TPixmap (Display) Then SavePixmapPNG (YFlipPixmap (Display), "test" + SocketRemoteIP (c.socket) + ".png")
						
						'p.Add "Saved..."
						
						requested = ReadFile ("test" + SocketRemoteIP (c.socket) + ".png")

						'p.Add "Stopping drawing..."

					StopDrawing

					'p.Add "Stopped drawing..."

				Else
					requested = ReadFile (folder + file)
				EndIf

				If requested

					Try

						While Not Eof (requested)
						
							If SocketConnected (c.socket) And Not Eof (c.stream)
							
								Local buffer:TBank = CreateBank (OPTIMUM_IO)
								Local bytesread:Int = 0
								Local offset:Int = 0
								
								If buffer

									Repeat

										bytesread = ReadBank (buffer, requested, 0, OPTIMUM_IO)

										' This line may trigger Try/Catch error (write-to-stream)...

										WriteBank buffer, c.stream, 0, bytesread
										offset = offset + bytesread

									Until Eof (requested)

								EndIf
								
								buffer = Null
								
							Else
						
								'p.Add "ERROR (" + file + "): Socket lost while writing file"
								Exit
						
							EndIf
						
						Wend
						
						If requested
							CloseFile requested
						EndIf

						If SocketConnected (c.socket)

							If Not Eof (c.stream)
								WriteLine c.stream, ""
							EndIf

						Else

							'p.Add "ERROR (" + file + "): No socket for sending blank line after file"
							
							KillConnection c
							'p.PrintAll

							Return Null

						EndIf

					Catch error:Object
				
						' This can be triggered during file read/write While/Wend loop,
						' for reasons unknown (socket lost just after socket and stream
						' checked valid?)...
						
						'p.Add "ERROR (" + file + "): Error while writing file to stream"
						
						KillConnection c
						'p.PrintAll
						
						Return Null
					
					End Try
			
				EndIf

			Case 2 ' Folder...

				' Try index.htm and index.html...
				
				Local redirect:String = file + "\index.htm"
				
				If FileType (folder + redirect) = 0 ' Nope!
					redirect = file + "\index.html" ' We'll see...
					If FileType (folder + redirect) = 0
						redirect = ""
					EndIf
				EndIf
				
				Try

					If redirect

						' Re-direct browser to index.htm or index.html if present (browser will make new request, ie. new thread will kick in)...
						
						If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, "HTTP/1.1 307 Temporary Redirect"
						If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, "location: " + redirect			
						If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, ""

					Else
						
						' No index.htm or index.html found, so show folder listing message...

						Local html:String = "&lt;HTML&gt;&lt;TITLE&gt;Folder request&lt;/TITLE&gt;&lt;BODY&gt;Folder listings not allowed!&lt;/BODY&gt;&lt;/HTML&gt;"

						If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, "HTTP/1.1 200 OK"
						If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, ""
						If SocketConnected (c.socket) And Not Eof (c.stream) Then WriteLine c.stream, html

					EndIf
					
				Catch error:Object
				
					'p.Add "ERROR (" + file + "): Stream failed outside error-checking!"
					
					KillConnection c
					'p.PrintAll
					
					Return Null
					
				End Try

		End Select

	EndIf

	'p.PrintAll
	KillConnection c
	
	Return Null
	
End Function

' Made separate as it's referenced a lot...

Function KillConnection (c:Connection)
	If SocketConnected (c.socket) Then CloseSocket c.socket
	If c.stream And Not Eof (c.stream) Then CloseStream c.stream
End Function

' Helper functions...

Function UnHexURL:String (url:String)
	Local pos:Int
	Repeat
		pos = Instr (url, "%")
		If pos
			Local hexx:String = Mid (url$, pos, 3)
			url = Replace (url, hexx, Chr (HexToDec (hexx)))
		EndIf
	Until pos = 0
	Return url
End Function

Function HexToDec:Int (h:String)

	' From PureBasic code by 'PB'...

	If Left (h, 1) = "%" Then h = Right (h, Len (h) - 1)
	h = Upper (h)

	Local a:String
	Local d:Int

	For Local r:Int = 1 To Len (h)
		d = d Shl 4; a = Mid (h, r, 1)
		If Asc (a) &gt; 60
			d = d + Asc (a) - 55
		Else
			d = d + Asc (a) - 48
		EndIf
	Next
	
	Return d
	
End Function
</textarea> <br><br></td></tr></table><br>
<a name="1097448"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#18">[#18]</a></td></tr></table></td></tr><tr ><td class="posttext"> That's cool. It moves! :-)<br><br><br>As for minib3d. I have got it running finally, without crashing, but the fog.bmx example only returns a blank image. Perhaps that's not a good example to try, of course....<br>I had to disable the call to glew init, because that does X stuff, and the ARB/VBO stuff crashes it too...<br>otherwise, I really dunno how it works to know what to look for next :-p <br><br></td></tr></table><br>
<a name="1097460"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >BlitzSupport</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#19">[#19]</a></td></tr></table></td></tr><tr ><td class="posttext"> I might have a go with a really early BMX-only version of minib3d and see if that gets me anywhere -- and I think VBOs came much later. Even just having this 2D ability is a really good feature though. <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
