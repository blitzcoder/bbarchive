<!DOCTYPE html><html lang="en" ><head ><title >Box2D SetSolid() - Contact Patch</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >Box2D SetSolid() - Contact Patch</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=124" >Brucey's Modules</a>/<a href="#bottom" >Box2D SetSolid() - Contact Patch</a><br><br>
<a name="1117318"></a>

<a name="1117319"></a>

<a name="1117320"></a>

<a name="1117321"></a>

<a name="1117322"></a>

<a name="1117324"></a>

<a name="1117326"></a>

<a name="1124754"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Armitage 1982</td><td align="right"><font class=tiny>(Posted 2011)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I have a little something I would like to share with you.<br>This patch works with the last v1.0.4 of the bah.Box2D (official Box2d rev 207). I did it because this version of the Box2d module will probably never be updated anymore and is way too much different from the current Box2d version (please, someone port it :p).<br><br><b>With this add-on you'll be able to switch on/off any Collision Responses while keeping Contact Points available.</b><br>This is a main feature to develop <i>One Sided Platform</i> or <i>One Way Door</i> thanks to Box2D.<br><br>All it does is :<br> - Adding a <b>void b2Contact::SetSolid(bool value)</b><br> - Modify each virtual b2ContactListener to include b2Contact when Add/Persist/Remove/Result is callback for a b2ContactPoint.<br><br>This old idea comes from Chriswa on the official Box2d forum when preSolve() didn't exist yet to create One Side Platform (http://box2d.org/forum/viewtopic.php?f=8&amp;t=1748&amp;hilit=mario),<br>which is the case of the only BlitzMax Box2d available to date.<br><br><br><b>You can find the modified sources here:</b><br><a href="http://www.filefactory.com/file/cf965e1/n/Box2d_SetSolid_Contact_Patch.zip" target="_blank">http://www.filefactory.com/file/cf965e1/n/Box2d_SetSolid_Contact_Patch.zip</a><br><br>Just drop these files on a fresh bah.box2d.mod v1.0.4 (don't forget a precious backup if you try this on your working version).<br><br>Then all you have to do is to implement yourself the <b>b2ContactListener</b> in order to change the solidity of your shapes while collecting contact Point for processing them later.<br><br>To give you an idea, this is a rough copy of my b2ContactListener implementation.<br>I've added plenty of comments a few minutes ago so excuse me if there is typo or errors...<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' The maximum number of contact point this code will process
Const K_MAXCONTACTPOINTS:Int = 2048

' Each Contact Points are stored in a array
Global m_points:ContactPoint[]

' This variable count the number of Contact Point available in the ContactPoint pool
Global m_pointCount:Int

Rem
bbdoc: Every game objects must be extend from this Type
about: When a box2d body/shape is created in relation with a game object. 
			Store a reference of it thanks to SetUserData() of body/shape definition.
			Later, this TB2Object will be requested thanks to GetUserData() by 
			ContactPoint::releaseOneSide()
			ContactPoint::checkOneSide()
			ContactPoint::processContactPoint()
endrem
Type TB2Object

	' Each object have an angle updated once every frame and used to checkOneSide()
	Field angle:Float = 0.0
	
	' A flag I use to know if the object currently support One Side Platform
	Field oneSide:Int = False
	
	' This method is called by the ContactListener each time one of the shape of this object have a Contact Point
	' I use it to resolve customized collision between objects.
	Method processContactPoint(cp:b2ContactPoint)
		
	End Method
	
EndType

Rem
bbdoc: Customized Contact Point
about: Informations used to resolve Collision Between objects
endrem
Type ContactPoint

	Const e_contactAdded:Int = 0
	Const e_contactPersisted:Int = 1
	Const e_contactRemoved:Int = 2

	Field shape1:b2Shape
	Field shape2:b2Shape
	Field normal:b2Vec2 
	Field position:b2Vec2 
	Field velocity:b2Vec2
	Field isSolid:Int
	Field manifoldCount:Int
	Field friction:Float
	Field restitution:Float
	Field separation:Float
	Field State:Int
	
	Method Delete()
		shape1 = Null
		shape2 = Null
		normal = Null
		Position = Null
		velocity = Null
		isSolid = Null
		manifoldCount = Null
		friction = Null
		restitution = Null
		separation = Null
		State = Null
	End Method
	
End Type

Type ContactListener Extends b2ContactListener


	Method Add(point:b2ContactPoint, contact:b2Contact)
	
		' No more room for new Contact Points
		If m_pointCount = K_MAXCONTACTPOINTS Return
	
		Local cp:ContactPoint = m_points[m_pointCount]
		If Not cp Then
			cp = New ContactPoint
			m_points[m_pointCount] = cp
		End If

		' Collect Contact Point informations
		cp.shape1 = point.GetShape1()
		cp.shape2 = point.GetShape2()
		cp.position = point.GetPosition()
		cp.normal = point.GetNormal()
		cp.velocity = point.GetVelocity()
		cp.isSolid = contact.IsSolid()
		cp.manifoldCount = contact.GetManifoldCount()
		cp.friction = point.GetFriction()
		cp.restitution = point.GetRestitution()
		cp.separation = point.GetSeparation()
		cp.State = ContactPoint.e_contactAdded		

		m_pointCount:+1
		
		checkOneSide(cp, contact)
		
	End Method
	
	Method Persist(point:b2ContactPoint, contact:b2Contact)
	
		' No more room for new Contact Points
		If m_pointCount = K_MAXCONTACTPOINTS Then Return
	
		Local cp:ContactPoint = m_points[m_pointCount]
		If Not cp Then
			cp = New ContactPoint
			m_points[m_pointCount] = cp
		End If

		' Collect Contact Point informations
		cp.shape1 = point.GetShape1()
		cp.shape2 = point.GetShape2()
		cp.position = point.GetPosition()
		cp.normal = point.GetNormal()
		cp.velocity = point.GetVelocity()
		cp.isSolid = contact.IsSolid()
		cp.manifoldCount = contact.GetManifoldCount()
		cp.friction = point.GetFriction()
		cp.restitution = point.GetRestitution()
		cp.separation = point.GetSeparation()
		cp.State = ContactPoint.e_contactPersisted

		m_pointCount:+1
		
		checkOneSide(cp, contact)
		
	End Method

	
	Method Remove(point:b2ContactPoint, contact:b2Contact)

		' No more room for new Contact Points
		If m_pointCount = K_MAXCONTACTPOINTS Then Return
	
		Local cp:ContactPoint = m_points[m_pointCount]
		If Not cp Then
			cp = New ContactPoint
			m_points[m_pointCount] = cp
		End If
		
		' Collect Contact Point informations
		cp.shape1 = point.GetShape1()
		cp.shape2 = point.GetShape2()
		cp.position = point.GetPosition()
		cp.normal = point.GetNormal()
		cp.velocity = point.GetVelocity()
		cp.manifoldCount = contact.GetManifoldCount()
		cp.friction = point.GetFriction()
		cp.restitution = point.GetRestitution()
		cp.separation = point.GetSeparation()
		cp.State = ContactPoint.e_contactRemoved
				
		m_pointCount:+1
		
		releaseOneSide(cp, contact)
		
	End Method
		
	Rem
	bbdoc: This function disable edge solidity if the Contact Point's normal is not within solidity Range degres AND direction face down
	about: This create the magic "cloud" one-sided platform and was ported to bmax from the Box2D Chriswa's Patch.
	endrem
	Method disableEdgeSolidity(point:ContactPoint, contact:b2Contact, reverse:Float, solidityAng:Float = 90.0, solidityRange:Float = 60.0)
		
		Local normalAng:Float = ATan2(point.normal.Y() * reverse, point.normal.X() * reverse)
		Local deltaAng:Float = (normalAng - solidityAng) * Pi / 180.0

		' Normalize angle
		deltaAng = deltaAng Mod Pi * 2
		If deltaAng &lt; - Pi Then deltaAng:+Pi * 2
		If deltaAng &gt; Pi Then deltaAng:-Pi * 2
		
		Local edgeIsBelowObject:Int = Abs(deltaAng) &lt; (solidityRange * Pi / 180.0)
		
		If Not edgeIsBelowObject Then contact.SetSolid(False)
	
		' Update the Contact Point informations
		cp.isSolid = contact.IsSolid()

	End Method
	
	Rem
	bbdoc: This method process every interaction bewteen objects by contact point
	about: you have to call it yourself somewhere in your main loop
	endrem
	Method processContactPoint()

		For Local i:Int = 0 To m_pointCount - 1
			Local cp:ContactPoint = m_points[i]
			
			' Only process solid object
			If Not cp.isSolid
				If Not (cp.shape1.IsSensor() Or cp.shape2.IsSensor()) Then Continue
			EndIf
			
			Local obj1:TB2Object = TB2Object(cp.shape1.GetUserData())
			Local obj2:TB2Object = TB2Object(cp.shape2.GetUserData())

			If obj1 Then obj1.processContactPoint(cp)
			If obj2
			
				' Reverse normal and swap cp.shape because the Contact Point is send by the other object
				' So I don't have to check this in every object::processContactPoint()
				cp.normal.Multiply(-1.0)

				Local oldShape:b2Shape = cp.shape1
				cp.shape1 = cp.shape2
				cp.shape2 = oldShape
				
				obj2.processContactPoint(cp)
				
			EndIf
			
		Next
		
	End Method
	
	Rem
	bbdoc: Will check the two object and setSolid() to create one Sided Platform
	endrem
	Method checkOneSide(cp:ContactPoint Var, contact:b2Contact Var)
		
			' Do not check sensor since they aren't solid
			If cp.shape1.IsSensor() Or cp.shape2.IsSensor() Then Return

			' Retrieve a copy of both game object colliding from shape UserData
			Local userData1:Object = cp.shape1.GetUserData()
			Local userData2:Object = cp.shape2.GetUserData()
			Local TBO:TB2Object
			
			' if object1 send the contact point then don't reverse the normal
			If userData1
				TBO = TB2Object(userData1)
				If TBO
					If TBO.oneSide Then disableEdgeSolidity(cp, contact, 1, TBO.angle + 90.0, 90.0)
				EndIf
			EndIf
			
			' If object2 send the contact point then we need to reverse the normal
			If userData2
				TBO = TB2Object(userData2)
				If TBO
					If TBO.oneSide Then disableEdgeSolidity(cp, contact, -1, TBO.angle + 90.0, 90.0)
				EndIf
			EndIf
						
	End Method

	Rem
	bbdoc: When the last contact point is removed, reset solidity (needed because AABBs will keep a b2Contact alive even when there are no b2ContactPoints)
	about: This simple approach lacks the smarts needed to behave properly with sensors!
	endrem
	Method releaseOneSide(cp:ContactPoint Var, contact:b2Contact Var)

			' Do not check sensor since they aren't solid
			If cp.shape1.IsSensor() Or cp.shape2.IsSensor() Then Return
			
			' Retrieve a copy of both game object colliding from shape UserData
			Local userData1:Object = cp.shape1.GetUserData()
			Local userData2:Object = cp.shape2.GetUserData()
			Local TBO:TB2Object
			
			' if object1 send the contact point the we consider the normal good
			If userData1
				TBO = TB2Object(userData1)
				If TBO
					If TBO.oneSide
						' if no more contact point for this object in his manifold then return to solid state
						If contact.GetManifoldCount() = 0 Then contact.SetSolid(True)
					End If
				EndIf
			EndIf
						
			' if object1 send the contact point the we consider the normal good
			If userData2
				TBO = TB2Object(userData2)
				If TBO
					If TBO.oneSide
						' if no more contact point for this object in his manifold then return to solid state
						If contact.GetManifoldCount() = 0 Then contact.SetSolid(True)
					End If
				EndIf
			EndIf

	End Method
	
End Type
</textarea><br><br>NOTE: This patch does not work correctly with <b>b2EdgeShape</b> ! Unfortunately, <b>b2EdgeShape</b> use his own math to resolve wrong side collision. However, a solution should exist I guess.<br><br>Hope this help :)<br><br><font class="tiny">Last edited 2011</font><br><br><font class="tiny">Last edited 2012</font> <br><br></td></tr></table><br>
<a name="1124755"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Armitage 1982</td><td align="right"><font class=tiny>(Posted 2012)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> <b>Changed</b><br><pre class=code>Local deltaAng:Float = normalAng - (solidityAng * Pi / 180.0)</pre><br>by<br><pre class=code>Local deltaAng:Float = (normalAng - solidityAng) * Pi / 180.0</pre><br><br>This will fix the mess of the resolution method!<br>I was actually mixing Radians and Degrees... ahhh, trigonometry ! <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
