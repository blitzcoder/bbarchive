<!DOCTYPE html><html lang="en" ><head ><title >wxMax multiple graphics contexts</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >wxMax multiple graphics contexts</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=124" >Brucey's Modules</a>/<a href="#bottom" >wxMax multiple graphics contexts</a><br><br>
<a name="1037227"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Rigz</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> I'm (still!) trying to improve compatibility on the dreaded Intel gfx chipsets by trying to only have 1 graphics context at anyone time. The only time there's 2 is when an animation properties dialogue is open. So what I've tried doing is destroying the preview canvas I have, and then creating the dialogue so only 1 context is ever open. This seems ok, except that I'm getting random crashes in release mode when opening the dialogue. Just "TimelineFX has stopped working" so not much in the way of clues.<br><br>Strange that debug works fine. Anyway, just wondering if anyone's got any ideas what might be causing it.<br><br>The other idea I'd like to try is this: would it be possible to make the the dialogue use the graphics context of the preview canvas so that an extra context doesn't have to be created? I don't mean just draw to the preview canvas in the background, but somehow move it temporarily into the animation dialogue.<br><br>Any other ideas welcome! <br><br></td></tr></table><br>
<a name="1037320"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Rigz</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Ok, I think I have the animation dialogue using the same graphics context now, I just did:<br><pre class=code>
dialog.canvas._graphics = win.PreviewPane._graphics
TwxGLGraphics(TMax2DGraphics(dialog.canvas._graphics)._graphics)._context = dialog.canvas
</pre><br>and then switch it back again after the dialog closes. That seems to work ok, with the exception that sometimes when the editor has just been opened the dialogue won't appear after the first few attempts, then it does. It may have always been like that though, I just never noticed... a bit strange.<br><br>Anyway, we'll shall see what the intel users say about it :)<br><br>Another thing while I was doing this, I updated wxGLMax2D as it was a bit out of date compared to the current BRL version. There were some changes to the way textures are removed from the card which were making a difference if you were trying to recreate graphics, it was causing white textures to appear. Here's an updated version:<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
' Copyright (c) 2007-2009 Bruce A Henderson
' 
' Permission is hereby granted, free of charge, to any person obtaining a copy
' of this software and associated documentation files (the "Software"), to deal
' in the Software without restriction, including without limitation the rights
' to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
' copies of the Software, and to permit persons to whom the Software is
' furnished to do so, subject to the following conditions:
' 
' The above copyright notice and this permission notice shall be included in
' all copies or substantial portions of the Software.
' 
' THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
' IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
' FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
' AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
' LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
' OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
' THE SOFTWARE.
' 
SuperStrict

Module wx.wxGLMax2D

ModuleInfo "Version: 1.00"
ModuleInfo "License: MIT"
ModuleInfo "Author: Bruce A Henderson"
ModuleInfo "Copyright: (c) 2007-2009 Bruce A Henderson"


?linux
ModuleInfo "CC_OPTS: -D__WXGTK__"
ModuleInfo "CC_OPTS: -D_FILE_OFFSET_BITS=64"
ModuleInfo "CC_OPTS: -D_LARGE_FILES"
ModuleInfo "CC_OPTS: -DWX_PRECOMP"
?win32
ModuleInfo "CC_OPTS: -DHAVE_W32API_H"
ModuleInfo "CC_OPTS: -D__WXMSW__"
ModuleInfo "CC_OPTS: -D_UNICODE"
ModuleInfo "CC_OPTS: -DUNICODE"
?macos
ModuleInfo "CC_OPTS: -D__WXMAC__"
ModuleInfo "CC_OPTS: -D_FILE_OFFSET_BITS=64"
ModuleInfo "CC_OPTS: -D_LARGE_FILES"
ModuleInfo "CC_OPTS: -DWX_PRECOMP"
?

Import "common.bmx"

Import BRL.Max2D
Import wx.wxGLCanvas

Private

Global GLGraphics:TwxGLGraphics

'Naughty!
Const GL_BGR:Int=$80E0
Const GL_BGRA:Int=$80E1
Const GL_CLAMP_TO_EDGE:Int=$812F
Const GL_CLAMP_TO_BORDER:Int=$812D

Global ix#,iy#,jx#,jy#
Global color4ub:Byte[4]

Global state_blend:Int
Global state_boundtex:Int
Global state_texenabled:Int

Function BindTex( name:Int )
	If name=state_boundtex Return
	glBindTexture GL_TEXTURE_2D,name
	state_boundtex=name
End Function

Function EnableTex( name:Int )
	BindTex name
	If state_texenabled Return
	glEnable GL_TEXTURE_2D
	state_texenabled=True
End Function

Function DisableTex()
	If Not state_texenabled Return
	glDisable GL_TEXTURE_2D
	state_texenabled=False
End Function

Global dead_texs:Int[], n_dead_texs:Int, dead_tex_seq:Int

'Enqueues a texture for deletion, to prevent release textures on wrong thread.

'Not thread safe, but that's OK because all threads are stopped when TGLImageFrame.Delete()
'is called, which is what calls us.

Function DeleteTex(name:Int, seq:Int)
	If seq &lt;&gt; dead_tex_seq Return

	'add tex to queue
	If dead_texs.length=n_dead_texs
		dead_texs=dead_texs[..n_dead_texs+10]
	EndIf
	dead_texs[n_dead_texs]=name
	n_dead_texs:+1
	'
End Function

Function CreateTex:Int(width:Int, height:Int, flags:Int)

	'flush dead texs
	If dead_tex_seq = GraphicsSeq
		For Local i:Int = 0 Until n_dead_texs
			glDeleteTextures 1,Varptr dead_texs[i]
		Next
	EndIf
	n_dead_texs=0
	dead_tex_seq=GraphicsSeq
	'
	'alloc new tex
	Local name:Int
	glGenTextures 1,Varptr name
	
	BindTex name
	
	'set texture parameters
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_WRAP_S,GL_CLAMP_TO_EDGE
	glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_WRAP_T,GL_CLAMP_TO_EDGE
	
	If flags &amp; FILTEREDIMAGE
		glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_LINEAR
		If flags &amp; MIPMAPPEDIMAGE
			glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_LINEAR_MIPMAP_LINEAR
		Else
			glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_LINEAR
		EndIf
	Else
		glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MAG_FILTER,GL_NEAREST
		If flags &amp; MIPMAPPEDIMAGE
			glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_NEAREST_MIPMAP_NEAREST
		Else
			glTexParameteri GL_TEXTURE_2D,GL_TEXTURE_MIN_FILTER,GL_NEAREST
		EndIf
	EndIf

	Local mip_level:Int

	Repeat
		glTexImage2D GL_TEXTURE_2D,mip_level,GL_RGBA8,width,height,0,GL_RGBA,GL_UNSIGNED_BYTE,Null
		If Not (flags &amp; MIPMAPPEDIMAGE) Exit
		If width=1 And height=1 Exit
		If width&gt;1 width:/2
		If height&gt;1 height:/2
		mip_level:+1
	Forever

	Return name
End Function

Function UploadTex( pixmap:TPixmap,flags:Int )
	Local mip_level:Int
	Repeat
		glPixelStorei GL_UNPACK_ROW_LENGTH,pixmap.pitch/BytesPerPixel[pixmap.format]
		glTexSubImage2D GL_TEXTURE_2D,mip_level,0,0,pixmap.width,pixmap.height,GL_RGBA,GL_UNSIGNED_BYTE,pixmap.pixels
		If Not (flags &amp; MIPMAPPEDIMAGE) Exit
		If pixmap.width&gt;1 And pixmap.height&gt;1
			pixmap=ResizePixmap( pixmap,pixmap.width/2,pixmap.height/2 )
		Else If pixmap.width&gt;1
			pixmap=ResizePixmap( pixmap,pixmap.width/2,pixmap.height )
		Else If pixmap.height&gt;1
			pixmap=ResizePixmap( pixmap,pixmap.width,pixmap.height/2 )
		Else
			Exit
		EndIf
		mip_level:+1
	Forever
	glPixelStorei GL_UNPACK_ROW_LENGTH,0
End Function

Function AdjustTexSize( width:Int Var, height:Int Var )
	Global maxTexSize:Int

	If maxTexSize = 0 Then
		glGetIntegerv GL_MAX_TEXTURE_SIZE, Varptr maxTexSize
	End If
		
	width = Min(Max(1, Pow2Size(width)), maxTexSize)
	height = Min(Max(1, Pow2Size(height)), maxTexSize)

	Function Pow2Size:Int(n:Int)
		Local size:Int = 1
		
		While size &lt; N
			size :Shl 1
		Wend

		Return size
	End Function

End Function

Public

Function CanvasGraphics2D:TGraphics(canvas:wxGLCanvas)
	If Not canvas._graphics Then
		canvas._graphics = GLMax2DDriver().AttachwxGraphics(canvas, 0)
	End If
	Return canvas._graphics
End Function

Type TGLImageFrame Extends TImageFrame

	Field u0#,v0#,u1#,v1#,uscale#,vscale#

	Field name:Int,seq:Int
	
	Method New()
		seq=GraphicsSeq
	End Method
	
	Method Delete()
		If Not seq Return
		DeleteTex name,seq
		seq=0
	End Method
	
	Method Draw( x0#,y0#,x1#,y1#,tx#,ty#,sx#,sy#,sw#,sh# )
		Assert seq=GraphicsSeq Else "Image does not exist"

		Local u0#=sx * uscale
		Local v0#=sy * vscale
		Local u1#=(sx+sw) * uscale
		Local v1#=(sy+sh) * vscale
		
		EnableTex name
		glBegin GL_QUADS
		glTexCoord2f u0,v0
		glVertex2f x0*ix+y0*iy+tx,x0*jx+y0*jy+ty
		glTexCoord2f u1,v0
		glVertex2f x1*ix+y0*iy+tx,x1*jx+y0*jy+ty
		glTexCoord2f u1,v1
		glVertex2f x1*ix+y1*iy+tx,x1*jx+y1*jy+ty
		glTexCoord2f u0,v1
		glVertex2f x0*ix+y1*iy+tx,x0*jx+y1*jy+ty
		glEnd
	End Method
	
	Function CreateFromPixmap:TGLImageFrame( src:TPixmap,flags:Int )
	
		'determine tex size
		Local tex_w:Int=src.width
		Local tex_h:Int=src.height
		AdjustTexSize tex_w,tex_h
		
		'make sure pixmap fits texture
		Local width:Int=Min( src.width,tex_w )
		Local height:Int=Min( src.height,tex_h )
		If src.width&lt;&gt;width Or src.height&lt;&gt;height src=ResizePixmap( src,width,height )

		'create texture pixmap
		Local tex:TPixmap=src
		
		'"smear" right/bottom edges if necessary
		If width&lt;tex_w Or height&lt;tex_h
			tex=TPixmap.Create( tex_w,tex_h,PF_RGBA8888 )
			tex.Paste src,0,0
			If width&lt;tex_w
				tex.Paste src.Window( width-1,0,1,height ),width,0
			EndIf
			If height&lt;tex_h
				tex.Paste src.Window( 0,height-1,width,1 ),0,height
				If width&lt;tex_w 
					tex.Paste src.Window( width-1,height-1,1,1 ),width,height
				EndIf
			EndIf
		Else
			If tex.format&lt;&gt;PF_RGBA8888 tex=tex.Convert( PF_RGBA8888 )
		EndIf
		
		'create tex
		Local name:Int=CreateTex( tex_w,tex_h,flags )
		
		'upload it
		UploadTex tex,flags

		'done!
		Local frame:TGLImageFrame=New TGLImageFrame
		frame.name=name
		frame.uscale=1.0/tex_w
		frame.vscale=1.0/tex_h
		frame.u1=width * frame.uscale
		frame.v1=height * frame.vscale
		Return frame

	End Function

End Type

Type TGLMax2DDriver Extends TMax2DDriver

	'graphics driver overrides
	Method GraphicsModes:TGraphicsMode[]()
		Return wxGLGraphicsDriver().GraphicsModes()
	End Method
	
	Method AttachGraphics:TMax2DGraphics( widget:Int,flags:Int )
	'	Local g:TGLGraphics=wxGLGraphicsDriver().AttachGraphics( widget,flags )
	'	If g Return TMax2DGraphics.Create( g,Self )
	End Method

	Method AttachwxGraphics:TMax2DGraphics( widget:wxGLCanvas, flags:Int )
		Local g:TwxGLGraphics = New TwxGLGraphics
		g._context = widget
		If g Return TMax2DGraphics.Create(g, Self)
	End Method
	
	Method CreateGraphics:TMax2DGraphics( width:Int,height:Int,depth:Int,hertz:Int,flags:Int )
		Local g:TwxGLGraphics=wxGLGraphicsDriver().CreateGraphics( width,height,depth,hertz,flags )
		If g Return TMax2DGraphics.Create( g,Self )
	End Method
	
	Method SetGraphics( g:TGraphics )
		If Not g
			TMax2DGraphics.ClearCurrent
			wxGLGraphicsDriver().SetGraphics Null
			Return
		EndIf
	
		Local t:TMax2DGraphics=TMax2DGraphics(g)
		Assert t And TwxGLGraphics( t._graphics )

		wxGLGraphicsDriver().SetGraphics t._graphics

		ResetGLContext t
		
		t.MakeCurrent
	End Method
	
	Method ResetGLContext( g:TGraphics )
		Local gw:Int,gh:Int,gd:Int,gr:Int,gf:Int
		g.GetSettings gw,gh,gd,gr,gf
		state_blend=0
		state_boundtex=0
		state_texenabled=0
		glDisable GL_TEXTURE_2D
		glMatrixMode GL_PROJECTION
		glLoadIdentity
		glOrtho 0,gw,gh,0,-1,1
		glMatrixMode GL_MODELVIEW
		glViewport 0,0,gw,gh
	End Method
	
	Method Flip( sync:Int )
		wxGLGraphicsDriver().Flip sync
	End Method
	
	Method ToString$()
		Return "OpenGL"
	End Method

	Method CreateFrameFromPixmap:TGLImageFrame( pixmap:TPixmap,flags:Int )
		Local frame:TGLImageFrame
		frame=TGLImageFrame.CreateFromPixmap( pixmap,flags )
		Return frame
	End Method

	Method SetBlend( blend:Int )
		If blend=state_blend Return
		state_blend=blend
		Select blend
		Case MASKBLEND
			glDisable GL_BLEND
			glEnable GL_ALPHA_TEST
			glAlphaFunc GL_GEQUAL,.5
		Case SOLIDBLEND
			glDisable GL_BLEND
			glDisable GL_ALPHA_TEST
		Case ALPHABLEND
			glEnable GL_BLEND
			glBlendFunc GL_SRC_ALPHA,GL_ONE_MINUS_SRC_ALPHA
			glDisable GL_ALPHA_TEST
		Case LIGHTBLEND
			glEnable GL_BLEND
			glBlendFunc GL_SRC_ALPHA,GL_ONE
			glDisable GL_ALPHA_TEST
		Case SHADEBLEND
			glEnable GL_BLEND
			glBlendFunc GL_DST_COLOR,GL_ZERO
			glDisable GL_ALPHA_TEST
		Default
			glDisable GL_BLEND
			glDisable GL_ALPHA_TEST
		End Select
	End Method

	Method SetAlpha( alpha# )
		If alpha&gt;1.0 alpha=1.0
		If alpha&lt;0.0 alpha=0.0
		color4ub[3]=alpha*255
		glColor4ubv color4ub
	End Method

	Method SetLineWidth( width# )
		glLineWidth width
	End Method
	
	Method SetColor( red:Int,green:Int,blue:Int )
		color4ub[0]=Min(Max(red,0),255)
		color4ub[1]=Min(Max(green,0),255)
		color4ub[2]=Min(Max(blue,0),255)
		glColor4ubv color4ub
	End Method

	Method SetClsColor( red:Int,green:Int,blue:Int )
		red=Min(Max(red,0),255)
		green=Min(Max(green,0),255)
		blue=Min(Max(blue,0),255)
		glClearColor red/255.0,green/255.0,blue/255.0,1.0
	End Method
	
	Method SetViewport( x:Int,y:Int,w:Int,h:Int )
		If x=0 And y=0 And w=GraphicsWidth() And h=GraphicsHeight()
			glDisable GL_SCISSOR_TEST
		Else
			glEnable GL_SCISSOR_TEST
			glScissor x,GraphicsHeight()-y-h,w,h
		EndIf
	End Method

	Method SetTransform( xx#,xy#,yx#,yy# )
		ix=xx
		iy=xy
		jx=yx
		jy=yy
	End Method

	Method Cls()
		glClear GL_COLOR_BUFFER_BIT
	End Method

	Method Plot( x#,y# )
		DisableTex
		glBegin GL_POINTS
		glVertex2f x+.5,y+.5
		glEnd
	End Method

	Method DrawLine( x0#,y0#,x1#,y1#,tx#,ty# )
		DisableTex
		glBegin GL_LINES
		glVertex2f x0*ix+y0*iy+tx+.5,x0*jx+y0*jy+ty+.5
		glVertex2f x1*ix+y1*iy+tx+.5,x1*jx+y1*jy+ty+.5
		glEnd
	End Method

	Method DrawRect( x0#,y0#,x1#,y1#,tx#,ty# )
		DisableTex
		glBegin GL_QUADS
		glVertex2f x0*ix+y0*iy+tx,x0*jx+y0*jy+ty
		glVertex2f x1*ix+y0*iy+tx,x1*jx+y0*jy+ty
		glVertex2f x1*ix+y1*iy+tx,x1*jx+y1*jy+ty
		glVertex2f x0*ix+y1*iy+tx,x0*jx+y1*jy+ty
		glEnd
	End Method
	
	Method DrawOval( x0#,y0#,x1#,y1#,tx#,ty# )
	
		Local xr#=(x1-x0)*.5
		Local yr#=(y1-y0)*.5
		Local segs:Int=Abs(xr)+Abs(yr)
		
		segs=Max(segs,12)&amp;~3

		x0:+xr
		y0:+yr
		
		DisableTex
		glBegin GL_POLYGON
		For Local i:Int=0 Until segs
			Local th#=i*360#/segs
			Local x#=x0+Cos(th)*xr
			Local y#=y0-Sin(th)*yr
			glVertex2f x*ix+y*iy+tx,x*jx+y*jy+ty
		Next
		glEnd
		
	End Method
	
	Method DrawPoly( xy#[],handle_x#,handle_y#,origin_x#,origin_y# )
		If xy.length&lt;6 Or (xy.length&amp;1) Return
		
		DisableTex
		glBegin GL_POLYGON
		For Local i:Int=0 Until Len xy Step 2
			Local x#=xy[i+0]+handle_x
			Local y#=xy[i+1]+handle_y
			glVertex2f x*ix+y*iy+origin_x,x*jx+y*jy+origin_y
		Next
		glEnd
	End Method
		
	Method DrawPixmap( p:TPixmap,x:Int,y:Int )
		Local blend:Int=state_blend
		DisableTex
		SetBlend SOLIDBLEND
		
		Local t:TPixmap=YFlipPixmap(p)
		If t.format&lt;&gt;PF_RGBA8888 t=ConvertPixmap( t,PF_RGBA8888 )
		glRasterPos2i 0,0
		glBitmap 0,0,0,0,x,-y-t.height,Null
		glDrawPixels t.width,t.height,GL_RGBA,GL_UNSIGNED_BYTE,t.pixels
		
		SetBlend blend
	End Method

	Method GrabPixmap:TPixmap( x:Int,y:Int,w:Int,h:Int )
		Local blend:Int=state_blend
		SetBlend SOLIDBLEND
	
		Local p:TPixmap=CreatePixmap( w,h,PF_RGBA8888 )
		glReadPixels x,GraphicsHeight()-h-y,w,h,GL_RGBA,GL_UNSIGNED_BYTE,p.pixels
		p=YFlipPixmap(p)

		SetBlend blend
		Return p
	End Method

	Method SetResolution(width:Float, height:Float)
		glMatrixMode GL_PROJECTION
		glLoadIdentity
		glOrtho 0,width,height,0,-1,1
		glMatrixMode GL_MODELVIEW
	End Method
	
End Type

Rem
bbdoc: Get OpenGL Max2D Driver
about:
The returned driver can be used with #SetGraphicsDriver to enable OpenGL Max2D 
rendering.
End Rem
Function GLMax2DDriver:TGLMax2DDriver()
	Global _driver:TGLMax2DDriver=New TGLMax2DDriver
	Return _driver
End Function

SetGraphicsDriver GLMax2DDriver()


</textarea> <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
