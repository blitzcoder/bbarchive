<!DOCTYPE html><html lang="en" ><head ><title >changing tree ctrl for a window</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >changing tree ctrl for a window</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=124" >Brucey's Modules</a>/<a href="#bottom" >changing tree ctrl for a window</a><br><br>
<a name="1204699"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >UNZ</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> Hi folks,<br><br>I try to create multiple wxTreeCtrl to store data. But only one of them should be visible.<br>My thought is to hide the unused trees and only show the active. Problem is that the active tree is not resized, unless you resize the window.<br><br>It seems like Refresh() and Update() do nothing...<br><br>example:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework wx.wxApp
Import wx.wxPanel
Import wx.wxFrame
Import wx.wxButton
Import wx.wxTreeCtrl

Import brl.map
Import brl.random

New MyApp.Run()

Type MyApp Extends wxApp

	Field frame:MyFrame

	'-----------------------------------------------------
	Method OnInit:Int()
		frame = MyFrame(New MyFrame.Create(Null, wxID_ANY) )
		frame.Show()
		Return True
	End Method
End Type

Type MyFrame Extends wxFrame

	Field map:TMap = New TMap
	Field tree:wxTreeCtrl
	Field sizer:wxSizer
	Field id:Int
	
	Const MAX_TREES:Int= 5
	Const ID_NEXT:Int = 1
	
	'-----------------------------------------------------
	Method OnInit()
		Local menubar:wxMenuBar = New wxMenuBar.Create()
		SetMenuBar(menuBar)
		Local menu:wxMenu = New wxMenu.Create()
		menubar.Append(menu, "file")
		
		menu.Append(ID_NEXT, "next")
		
		Connect(ID_NEXT, wxEVT_COMMAND_MENU_SELECTED, OnNext, Self)
		
		For Local i:Int = 0 To MAX_TREES - 1
			AddTree(String(i) )
		Next
	End Method
	
	'-----------------------------------------------------
	Rem
		bbdoc: set next tree to active
	End Rem
	Function OnNext(event:wxEvent)
		Local f:MyFrame = MyFrame(event.userData)
		
		f.id :+ 1
		f.id = f.id Mod MAX_TREES
		Local t:wxTreeCtrl = wxTreeCtrl(f.map.ValueForKey(String(f.id) ) )
		f.SetTree(t)
	End Function
	
	'-----------------------------------------------------
	Rem
		bbdoc: create and add a new tree to the map
	End Rem
	Method AddTree(key$)
		Local t:wxTreeCtrl = wxTreeCtrl(map.ValueForKey(key) )
		If Not t
			t = CreateTree(key)
			map.Insert(key, t)
		EndIf
		SetTree(t)
	End Method
	
	'-----------------------------------------------------
	Rem
		bbdoc: change the active tree
	End Rem
	Method SetTree(t:wxTreeCtrl)
		If Not t Then DebugLog "no tree to set"
		If t And t &lt;&gt; tree
			sizer = New wxBoxSizer.Create(wxHORIZONTAL)
			
			'hide old tree
			If tree Then tree.Hide()
			
			'remember new tree
			tree = t
			
			'set size for new tree
			sizer.Add(t, 1, wxEXPAND)
			SetSizer(sizer, True)
			
			Refresh(True) 'nothing happens?
			Update() 'nothing happens?
			
			'show new
			t.Show()
		EndIf
	End Method
	
	'-----------------------------------------------------
	Rem
		bbdoc: create a tree with some content
	End Rem
	Method CreateTree:wxTreeCtrl(key$)
		Local t:wxTreeCtrl = New wxTreeCtrl.Create(Self, wxID_ANY)
		Local root:wxTreeItemId = t.AddRoot("root")
		t.AppendItem(root, key + "_a")
		t.AppendItem(root, key + "_b")
		t.AppendItem(root, key + "_b")
		t.ExpandAll()
		
		Return t
	End Method
End Type
</textarea> <br><br></td></tr></table><br>
<a name="1204724"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> What happens if you use a parent panel for each treectrl?<br><br>Then just resize that parent panels according to your whishes (or hide them or ...).<br><br>As this would only be hacking I assume there is just something missing in the code sending out the correct event...<br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="1204878"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >UNZ</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> I found a solution.<br>Looks like the problem was that the sizer did not update or whatever.<br><br>Sizers have their own Show() method. And a method Layout() to apply such changes. After I hide the old ctrl and show the new ctrl both with the sizer's method everything works.<br><br>example:<br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework wx.wxApp
Import wx.wxPanel
Import wx.wxFrame
Import wx.wxButton
Import wx.wxTreeCtrl

Import brl.map
Import brl.random

New MyApp.Run()

Type MyApp Extends wxApp

	Field frame:MyFrame

	'-----------------------------------------------------
	Method OnInit:Int()
		frame = MyFrame(New MyFrame.Create(Null, wxID_ANY) )
		frame.Show()
		Return True
	End Method
End Type

Type MyFrame Extends wxFrame

	Field map:TMap = New TMap
	Field tree:wxTreeCtrl
	Field sizer:wxSizer
	Field id:Int
	
	Const MAX_TREES:Int= 5
	Const ID_NEXT:Int = 1
	
	'-----------------------------------------------------
	Method OnInit()
		Local menubar:wxMenuBar = New wxMenuBar.Create()
		SetMenuBar(menuBar)
		Local menu:wxMenu = New wxMenu.Create()
		menubar.Append(menu, "file")
		
		menu.Append(ID_NEXT, "next")
		
		Connect(ID_NEXT, wxEVT_COMMAND_MENU_SELECTED, OnNext, Self)
		
		sizer = New wxBoxSizer.Create(wxHORIZONTAL)
		SetSizer(sizer)
		For Local i:Int = 0 To MAX_TREES - 1
			tree = AddTree(String(i) )
		Next
	End Method
	
	'-----------------------------------------------------
	Rem
		bbdoc: set next tree to active
	End Rem
	Function OnNext(event:wxEvent)
		Local f:MyFrame = MyFrame(event.userData)
		
		f.id :+ 1
		f.id = f.id Mod MAX_TREES
		Local t:wxTreeCtrl = wxTreeCtrl(f.map.ValueForKey(String(f.id) ) )
		f.SetTree(t)
	End Function
	
	'-----------------------------------------------------
	Rem
		bbdoc: create and add a new tree to the map
	End Rem
	Method AddTree:wxTreeCtrl(key$)
		Local t:wxTreeCtrl = wxTreeCtrl(map.ValueForKey(key) )
		If Not t
			t = CreateTree(key)
			map.Insert(key, t)
		EndIf
		SetTree(t)
		Return t
	End Method
	
	'-----------------------------------------------------
	Rem
		bbdoc: change the active tree
	End Rem
	Method SetTree(t:wxTreeCtrl)
		If Not t Then DebugLog "no tree to set"
		If t And t &lt;&gt; tree
			'hide old tree
			If tree Then sizer.Show(tree, False)
			
			'remember new tree
			tree = t
			
			'show new
			sizer.Show(t, True)
			
			'set sizer for new tree
			sizer.Layout()
		EndIf
	End Method
	
	'-----------------------------------------------------
	Rem
		bbdoc: create a tree with some content
	End Rem
	Method CreateTree:wxTreeCtrl(key$)
		Local t:wxTreeCtrl = New wxTreeCtrl.Create(Self, wxID_ANY)
		sizer.Add(t, 1, wxEXPAND)
		sizer.Show(t, False)
		Local root:wxTreeItemId = t.AddRoot("root")
		For Local i:Int = 0 To 30
			t.AppendItem(root, key + "_" + i)
		Next
		t.ExpandAll()
		
		Return t
	End Method
End Type
</textarea> <br><br></td></tr></table><br>
<a name="1204976"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Derron</td><td align="right"><font class=tiny>(Posted 2013)</font>&nbsp;<a href="#4">[#4]</a></td></tr></table></td></tr><tr ><td class="posttext"> Think the sizers are something like a dynamically sized panel (or "rectangle widget"). So they need to get resized to adjust their children. Maybe they do something like "auto full size / extend" for their children (think Delphi had such behaviour for some of its components). <br>If not, they should not care about their children. You then would have a hidden Tree but an still expanded sizer.<br><br>But glad you found a workaround (people are awaiting next release :D).<br><br><br>bye<br>Ron <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
