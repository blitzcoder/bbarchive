<!DOCTYPE html><html lang="en" ><head ><title >wx under 1.36 not drawing images</title><meta name='viewport' content='width=device-width, initial-scale=0.66'><meta name='description' content='language:basic, target:desktop, category:game development'><link rel="styleSheet" href="../skins/granite/style.css" type="text/css"><style type="text/css">div.bbcode {padding: 8px;background: #E5E5E5;color: #000000;border: 1px dashed #B4B4BE;}</style></head><body ><table width=100% cellspacing=0 cellpadding=0><tr ><td class="menubarleft"></td><td class="menubar"><table cellspacing="0" cellpadding="0"><tr ><td >&nbsp;</td><td class="menuitemleft"></td><td class="menuitem"><a href="/" class="menuitem">News</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/forums.php" class="menuitem">Forums</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/codearcs.php" class="menuitem">Code</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/logs.php" class="menuitem">Logs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/gallery.php" class="menuitem">Gallery</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/sdkspecs.php" class="menuitem">Specs</a></td><td class="menuitemright"></td><td class="menuitemleft"></td><td class="menuitem"><a href="/search.php" class="menuitem">Search</a></td><td class="menuitemright"></td></tr></table></td><td class="menubarright"></td></tr></table><div class="main"><h1 >wx under 1.36 not drawing images</h1><a href="forums.php" >BlitzMax Forums</a>/<a href="topics.php?forum=124" >Brucey's Modules</a>/<a href="#bottom" >wx under 1.36 not drawing images</a><br><br>
<a name="1005455"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Rigz</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#1">[#1]</a></td></tr></table></td></tr><tr ><td class="posttext"> For some reason images are not being drawn since updating to 1.36, and the latest rev 667 of wxmax. here's an example (the wxglmax2d sample modified to draw an image from media folder):<br><br><textarea class=codebox name="code" wrap=OFF readonly="true" rows="15" cols="80">
SuperStrict

Framework wx.wxApp
Import wx.wxFrame
Import wx.wxglmax2d
Import wx.wxTimer
Import brl.pngloader

Global image:TImage = LoadImage("media/artprovider/stock_folder-16.png")

New MyApp.run()


Type MyApp Extends wxApp

	Field frame:MyFrame

	Method OnInit:Int()

		' Create the main application windowType MyFrame Extends wxFrame

		frame = MyFrame(New MyFrame.Create(,,"", , , 640, 480))
		
		' Show it and tell the application that it's our main window
		frame.show(True)
		SetTopWindow(frame)

		Return True
	End Method

End Type

Type MyFrame Extends wxFrame

	Field canvas:MyCanvas

	Method OnInit()

		canvas = MyCanvas(New MyCanvas.Create(Self, -1, GRAPHICS_BACKBUFFER|GRAPHICS_DEPTHBUFFER))
	
		ConnectAny(wxEVT_CLOSE, OnClose)
	End Method
	
	Function OnClose(event:wxEvent)
		MyFrame(event.parent).canvas.timer.Stop() ' we really need to stop the timer on Mac...
		event.Skip()
	End Function

End Type


Type MyCanvas Extends wxGLCanvas

	Field timer:wxTimer

	Method OnInit()
		SetBackgroundStyle(wxBG_STYLE_CUSTOM)
	
		timer = New wxTimer.Create(Self)


		ConnectAny(wxEVT_TIMER, OnTick)

		timer.Start(100)
	End Method

	Method OnPaint(event:wxPaintEvent)
		Render()
	End Method

	Method Render()

		SetGraphics CanvasGraphics2D( Self )

		SetClsColor(0, 0, 0)
		SetColor(255, 255, 255)
		
		Cls
		
		'DebugStop
		DrawImage image, 10, 10

		drawClock()
		
		Flip

	End Method

	Function OnTick(event:wxEvent)
		wxWindow(event.parent).Refresh()
	End Function
	
	
	Method drawClock()
	
		SetLineWidth(2)

		Local x:Int = 200, y:Int = 200
		Local radius:Int = 100
		' clock ticks
		For Local i:Int = 0 Until 12
			Local inset:Double = 0
		
			If i Mod 3 = 0 Then
				SetLineWidth(2)
				inset = 0.2 * radius
			Else
				inset = 0.1 * radius
				SetLineWidth(1)
			End If
			
			DrawLine x + (radius - inset) * Cos(i * 30), y + (radius - inset) * Sin(i * 30), ..
				x + radius * Cos(i * 30), y + radius * Sin (i * 30)
			
			' draw numbers
			inset = 0.3 * radius
			
			Local t:Int = (i + 2) Mod 12 + 1
			DrawText romanNumerals(t), x + (radius - inset) * Cos(i * 30) - 7, y + (radius - inset) * Sin(i * 30) - 6
		Next


		' clock hands
		Local hours:Int = CurrentTime()[0..2].toInt() Mod 12
		Local minutes:Int = CurrentTime()[3..5].toInt()
		Local seconds:Int = CurrentTime()[6..8].toInt()
		
		' hour hand:
		' the hour hand is rotated 30 degrees  per hour + 1/2 a degree per minute
		'
		SetLineWidth(4)

		DrawLine x, y, x + radius / 2 * Sin(30 * hours + minutes / 2.0), ..
				   y + radius / 2 * -Cos(30 * hours + minutes / 2.0)
		
		
		' minute hand:
		' the minute hand is rotated 6 degrees per minute + 1/10 a degree per second
		'
		SetLineWidth(2)
		DrawLine x, y, x + radius * 0.75 * Sin(6 * minutes + seconds / 10.0), ..
				   y + radius * 0.75 * -Cos(6 * minutes + seconds / 10.0)
		
		' seconds hand:
		' the second hand is rotated 6 degrees per second
		'
		SetColor(255, 0, 0)

		DrawLine x, y, x + radius * 0.7 * Sin(6 * seconds), ..
				   y + radius * 0.7 * -Cos(6 * seconds)


	End Method

	Method romanNumerals:String(value:Int)
		Local rn:String = ""
		
		While value &gt; 9
			rn:+ "X"
			value:- 10
		Wend
	
		If value &gt; 8 Then
			rn:+ "IX"
			value:- 9
		End If
		
		If value &gt; 4 Then
			rn:+ "V"
			value:- 5
		End If
		
		If value &gt; 3 Then
			rn:+ "IV"
			value:- 4
		End If
		
		While value &gt; 0
			rn:+ "I"
			value:- 1
		Wend
		
		Return rn
	End Method

End Type

</textarea><br><br>Works fine under 1.35 rev 666. Can't see any obvious reasons why... Any ideas? <br><br></td></tr></table><br>
<a name="1005582"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Pete Rigz</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#2">[#2]</a></td></tr></table></td></tr><tr ><td class="posttext"> Amazing what a good nights sleep can do for you, it was quite obvious after all :)<br><br>the bug is in wxglmax2d, in Createfrompixmap method of TGLImageFrame, from line 258 it looks like:<br><br><pre class=code>
		'done!
		Local frame:TGLImageFrame=New TGLImageFrame
		frame.name=name
		frame.u1=Float(width)/Float(tex_w)
		frame.v1=Float(height)/Float(tex_h)
		Return frame
</pre><br><br>but copying from glmax2d it should be:<br><br><pre class=code>
		'done!
		Local frame:TGLImageFrame=New TGLImageFrame
		frame.name=name
		frame.uscale=1.0/tex_w
		frame.vscale=1.0/tex_h
		frame.u1=width * frame.uscale
		frame.v1=height * frame.vscale
		Return frame
</pre><br><br>without that the uv coords were all being set to 0. <br><br></td></tr></table><br>
<a name="1006449"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td class="posthead"><table width=100% cellspacing=0 cellpadding=0><tr ><td >Brucey</td><td align="right"><font class=tiny>(Posted 2010)</font>&nbsp;<a href="#3">[#3]</a></td></tr></table></td></tr><tr ><td class="posttext"> Updated. Thanks :-) <br><br></td></tr></table><br>
<a name="bottom"></a>
<table width=100% cellspacing=0 cellpadding=0><tr ><td align="right"><a href="http://monkeycoder.co.nz" target="_blank"><img src="/img/monkey2.svg" ></a> <a href="https://github.com/blitz-research" target="_blank"><img src="/img/github.svg" ></a> <a href="https://discord.gg/qJccbSp" target="_blank"><img src="/img/discord.svg" ></a></td></tr></table><br></div><br><table width="100%"><tr><td></body></html lang="en">
